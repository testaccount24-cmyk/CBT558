         TITLE    'COMPARE COBOL SOURCE PROGRAMS'
COMPCBL  SVLNK R3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   TWO COBOL SOURCE PROGRAM FILES ARE COMPARED. THE FILES MUST BE
* FIXED LENGTH WITH 80 BYTE RECORDS. RECORDS ARE READ AND COMPARED
* UNTIL A MISMATCH IS FOUND. COMPARISON IS MADE ON COLUMNS 7-72. WHEN
* RECORDS DO NOT MATCH, A FOUR-LINE MESSAGE IS WRITTEN, SHOWING BOTH
* RECORDS, AND THEIR SEQUENCE NUMBERS. A READ IS THEN ISSUED TO THE
* TERMINAL. THE USER MUST RESPOND WITH "1" IF THE INPUT1 FILE IS TO
* BE READ, A "2" IF THE INPUT2 FILE IS TO BE READ, AN "E" IF THE
* EXECUTION IS TO BE TERMINATED. ANY OTHER REPLY WILL RESULT IN BOTH
* INPUT FILES BEING READ.
*
* AUTHOR R. THORNTON JUNE 1979
*
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         OPEN  (INPUT1,,INPUT2,,SYSIN,,SYSPRINT,OUTPUT)
*
*
*
GETS     NOP   GET2                    BRANCH IF INPUT1 AT EOF
         GET   INPUT1                  READ INPUT1
         LR    R5,R1                   GET RECORD ADDRESS
GET2     NOP   CPR                     BRANCH IF INPUT2 AT EOF
         GET   INPUT2                  READ INPUT2
         LR    R6,R1                   GET RECORD ADDRESS
CPR      CLC   6(72,R5),6(R6)          ARE THE CARDS IDENTICAL?
         BE    GETS                    YES
         MVC   PRT+10(23),=C'INPUT1 RECORD NUMBER IS'
         MVC   PRT+35(6),0(R5)         INPUT 1 CARD NUMBER TO PRINT
         PUT   SYSPRINT,PRT            WRITE TO USER
         MVC   PRT,BLANX               CLEAR PRINT LINE
         MVC   PRT(72),0(R5)           INPUT1 RECORD TO PRINT
         PUT   SYSPRINT,PRT            WRITE TO USER
         MVC   PRT,BLANX               CLEAR PRINT LINE
         MVC   PRT+10(23),=C'INPUT2 RECORD NUMBER IS'
         MVC   PRT+35(6),0(R6)         INPUT 2 CARD NUMBER TO PRINT
         PUT   SYSPRINT,PRT            WRITE TO USER
         MVC   PRT,BLANX               CLEAR PRINT LINE
         MVC   PRT(72),0(R6)           INPUT1 RECORD TO PRINT
         PUT   SYSPRINT,PRT            WRITE TO USER
         MVC   PRT,BLANX               CLEAR PRINT LINE
         GET   SYSIN                   READ USER'S REPLY
         LR    R7,R1                   COPY REPLY ADDRESS
         CLI   0(R7),C'1'              REQUEST INPUT1 READ?
         BE    GET1                    YES
         CLI   0(R7),C'2'              REQUEST INPUT2 READ?
         BE    GET2                    YES
         CLI   0(R7),C'E'              REQUEST FOR EOJ?
         BE    EOJ                     YES
         B     GETS                    NONE OF THESE, READ BOTH FILES
GET1     NOP   CPR                     BRANCH IF EOF ON INPUT1
         GET   INPUT1                  READ INPUT1 FILE
         LR    R5,R1                   COPY RECORD ADDRESS
         B     CPR                     GO COMPARE RECORDS
*
*
*
EOF1     LA    R5,EOFMSG               GET EOF MESSAGE ADDRESS
         CR    R5,R6                   BOTH FILES AT END?
         BE    EOJ                     YES
         MVI   GET1+1,C'0'             NO, PROHIBIT INPUT1 READS
         MVI   GETS+1,C'0'             IN BOTH PLACES
         B     GET2                    AND GO READ INPUT2
*
*
*
EOF2     LA    R6,EOFMSG               GET EOF MESSAGE ADDRESS
         CR    R5,R6                   BOTH FILES AT END?
         BE    EOJ                     YES
         MVI   GET2+1,C'0'             NO, PROHIBIT INPUT2 READS
         B     GET1                    AND GO READ INPUT1
*
*
*
EOJ      CLOSE (INPUT1,,INPUT2,,SYSPRINT)
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
BLANX    DC    CL80' '
PRT      DC    CL80' '
EOFMSG   DC    X'FF'
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,                        X
               RECFM=FB,LRECL=80,EODAD=EOF1
INPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT2,                        X
               RECFM=FB,LRECL=80,EODAD=EOF2
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         END

         TITLE 'PDSDIRC - PRINT LOAD MODULE DIRECTORY LISTING'
PDSDIRC  SVLNK R3
**********************************************************************
* PROGRAM TO READ THE DIRECTORY IN AN INPUT PDS AND PRINT A LISTING  *
* CONTAINING THE MEMBER NAME, TTR, MODULE SIZE, AMODE, RMODE, AND    *
* LINKEDIT ATTRIBUTES.                                               *
*                                                                    *
* AUTHOR R. THORNTON   FEB 1992                                      *
**********************************************************************
         OPEN  (SYSPRINT,OUTPUT,SYSLIB) OPEN FILES
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BAS   R9,HEADING              PRINT FIRST HEADER
*
*
*
MAINLINE LH    R12,REMBYTS             GET NBR BYTES REMAINING IN BLOCK
         CH    R12,ELEVEN              ANY MORE ENTRIES IN BLOCK
         BNL   GETADR                  YES
NEXREC   GET   SYSLIB,DIRBLK           READ DIRECTORY BLOCK
         LR    R12,R1                  GET BLOCK ADDRESS
         MVC   REMBYTS,0(R12)          MOVE NBR BYTES IN BLOCK
         CLC   REMBYTS,ZERO            ANY BYTES IN BLOCK
         BE    EOJ                     NO, EOF AND THEREFORE EOJ
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         CLI   0(R12),X'F9'            END OF DIRECTORY?
         BH    EOJ                     YES
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         MVC   PRTNAME,0(R12)          MOVE MEMBER NAME TO PRINT
         MVC   HEX+1(3),8(R12)         MOVE TTR FOR CONVERSION
         BAS   R9,HEX2PRT              GO MAKE IT PRINTABLE
         MVC   PRTTTR,CHARS+2          AND MOVE TO PRINT
         TM    11(R12),X'80'           IS THIS AN ALIAS?
         BZ    ATTR180                 NO
         TM    20(R12),X'04'           SCATTER FORMAT?
         BZ    NOSCTR                  NO
         MVC   PRTALIAS,44(R12)        YES, MOVE ALIAS NAME
         B     ATTR180                 CONTINUE
NOSCTR   MVC   PRTALIAS,36(R12)        MOVE ALIAS NAME
ATTR180  TM    20(R12),X'80'           RENT?
         BZ    ATTR140                 NO
         MVC   PRTRENT,=C'RENT'        YES
ATTR140  TM    20(R12),X'40'           REUS?
         BZ    ATTR120                 NO
         MVC   PRTREUS,=CL4'REUS'      YES
ATTR120  TM    20(R12),X'20'           OVLY?
         BZ    ATTR110                 NO
         MVC   PRTOVLY,=CL4'OVLY'      YES
ATTR110  TM    20(R12),X'10'           TEST?
         BZ    ATTR108                 NO
         MVC   PRTTEST,=CL4'TEST'      YES
ATTR108  TM    20(R12),X'08'           ONLY LOADABLE?
         BZ    ATTR104                 NO
         MVC   PRTLOAD,=CL4'LOAD'      YES
ATTR104  TM    20(R12),X'04'           SCTR?
         BZ    ATTR102                 NO
         MVC   PRTSCTR,=CL4'SCTR'      YES
ATTR102  TM    20(R12),X'02'           EXEC?
         BZ    ATTR201                 NO
         MVC   PRTEXEC,=CL4'EXEC'      YES
ATTR201  TM    21(R12),X'01'           REFR?
         BZ    TOTLENG                 NO
         MVC   PRTREFR,=CL4'REFR'      YES
TOTLENG  MVC   HEX+1(3),22(R12)        CONVERT
         BAS   R9,HEX2PRT                 TOTAL SIZE
         MVC   PRTSIZE,CHARS+2               TO PRINTABLE
         MVC   HEX+1(3),27(R12)        CONVERT
         BAS   R9,HEX2PRT                 ENTRY POINT
         MVC   PRTEPA,CHARS+2                 ADDRESS TO PRINTABLE
         TM    30(R12),X'20'           PAGE ALIGN?
         BZ    CHKSSI                  NO
         MVC   PRTPAGE,=CL4'PAGE'      YES
CHKSSI   TM    30(R12),X'10'           SSI PRESENT?
         BZ    GETAUTH                 NO
         LA    R1,33(R12)              POINT PAST BASE AREA
         TM    20(R12),X'04'           SCTR?
         BZ    TSTALIAS                NO
         LA    R1,8(R1)                YES, PASS SCTR DATA
TSTALIAS TM    11(R12),X'80'           THIS AN ALIAS?
         BZ    ATSSI                   NO
         LA    R1,11(R1)               YES, PASS OVER ALIAS DATA
ATSSI    LA    R1,1(R1)                ROUND TO
         N     R1,KILLZERO                NEXT HALFWORD
         MVC   HEX,0(R1)               CONVERT
         BAS   R9,HEX2PRT                 SSI DATA
         MVC   PRTSSI,CHARS                  TO PRINTABLE
GETAUTH  LA    R1,33                   OFFSET TO END OF BASE
         TM    20(R12),X'04'           SCTR?
         BZ    NOSCTR2                 NO
         LA    R1,8(R1)                YES, ADD FOR SCTR
NOSCTR2  TM    11(R12),X'80'           THIS AN ALIAS?
         BZ    NOALIAS                 NO
         LA    R1,11(R1)               YES, ADD FOR ALIAS DATA
NOALIAS  TM    30(R12),X'10'           SSI PRESENT?
         BZ    ATAC                    NO
         LA    R1,1(R1)                ROUND TO
         N     R1,KILLZERO                NEXT HALFWORD
         LA    R1,4(R1)                ADD FOR SSI
ATAC     AR    R1,R12                  POINT TO AUTH CODE FIELD
         CLI   0(R1),X'01'             AUTH CODE LENGTH = 1?
         BNE   DOMODES                 NO
         CLI   1(R1),X'01'             AC=1?
         BNE   DOMODES                 NO
         MVC   PRTAC,=CL4'AC=1'        YES
DOMODES  TM    31(R12),X'10'           RMODE=ANY?
         BZ    RM24                    NO
         MVC   PRTRMODE,=CL3'ANY'      YES
         B     TSTAMODE                CONTINUE
RM24     MVC   PRTRMODE,=CL3' 24'      NO, SHOW RMODE=24
TSTAMODE TM    31(R12),X'02'           AMODE=24?
         BZ    AM24                    YES
         TM    31(R12),X'01'           AMODE=ANY?
         BO    AMANY                   YES
         MVC   PRTAMODE,=CL3' 31'      NO, SHOW AMODE=31
         B     PRTALINE                CONTINUE
AMANY    MVC   PRTAMODE,=CL3'ANY'      SHOW AMODE=ANY
         B     PRTALINE                CONTINUE
AM24     MVC   PRTAMODE,=CL3' 24'      SHOW AMODE=24
PRTALINE PUT   SYSPRINT,PRTLINE        PRINT THE LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         AP    LINECT,=P'1'            INCREMENT LINE COUNTER
         CP    LINECT,=P'60'           TIME FOR A NEW PAGE?
         BNH   POINTNXT                NO
         BAS   R9,HEADING              YES, PRINT HEADING
POINTNXT NI    11(R12),CLRBITS         CLEAR ALIAS, TTR BITS
         SR    R11,R11                 CLEAR WORK REG
         IC    R11,11(R12)             GET NBR USER HALFWORDS
         SLL   R11,1                   MULTIPLY BY 2
         LA    R11,12(R11)             CALCULATE RECORD LENGTH
         LH    R12,REMBYTS             GET NBR BYTES REMAINING
         SR    R12,R11                 SUBTRACT THIS ENTRY LENGTH
         BP    STREM                   ANY LEFT
         SR    R12,R12                 NO, SET TO ZERO
STREM    STH   R12,REMBYTS             STORE REMAINING BYTES
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         B     MAINLINE                GO GET NEXT ENTRY
***********************************************************************
*        PRINT HEADING                                                *
***********************************************************************
HEADING  PUT   SYSPRINT,HEADER         WRITE HEADER LINE
         ZAP   LINECT,=P'2'            RESET LINE COUNTER
         MVI   PCC,C'0'                DOUBLE SPACE NEXT LINE
         BR    R9                      RETURN
***********************************************************************
*        CONVERT HEX TO PRINTABLE CHARACTERS                          *
***********************************************************************
HEX2PRT  UNPK  CHARS(9),HEX(5)         UNPACK TO MAKE PRINTABLE
         TR    CHARS,TRPRT-C'0'        MAKE A-F PRINTABLE
         BR    R9
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      CLOSE (SYSPRINT,,SYSLIB)      CLOSE FILES
         L     R13,4(R13)              POINT TO CALLERS SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        DATA AND FILE AREAS                                          *
***********************************************************************
*
REMBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ELEVEN   DC    H'11'
ZERO     DC    H'0'
RECADDR  DS    F
KILLZERO DC    X'FFFFFFFE'             USED TO CLEAR LOW BIT VALUE
HEX      DC    XL4'00',XL1'00'         HEX FIELD
CHARS    DC    CL8' ',CL1' '           CHARACTER FIELD
TRPRT    DC    CL16'0123456789ABCDEF'  TRANSLATE FOR A-F
CLRBITS  EQU   X'1F'
DIRBLK   DS    CL256
BLANKS   DC    CL133' '
LINECT   DC    PL2'0'
*
PRTLINE  DS    0CL133
PCC      DS    CL1
PRTNAME  DS    CL8
         DS    CL2
PRTSIZE  DS    CL6
         DS    CL2
PRTEPA   DS    CL6
         DS    CL2
PRTTTR   DS    CL6
         DS    CL2
PRTALIAS DS    CL8
         DS    CL2
PRTAMODE DS    CL3
         DS    CL2
PRTRMODE DS    CL3
         DS    CL2
PRTRENT  DS    CL4
         DS    CL2
PRTREFR  DS    CL4
         DS    CL2
PRTREUS  DS    CL4
         DS    CL2
PRTOVLY  DS    CL4
         DS    CL2
PRTTEST  DS    CL4
         DS    CL2
PRTLOAD  DS    CL4
         DS    CL2
PRTSCTR  DS    CL4
         DS    CL2
PRTEXEC  DS    CL4
         DS    CL2
PRTPAGE  DS    CL4
         DS    CL2
PRTAC    DS    CL4
         DS    CL2
PRTSSI   DS    CL8
         DS    CL11
*
HEADER   DS    0CL133
         DC    CL1'1'
         DC    CL8'MEM-NAME'
         DC    CL2' '
         DC    CL6'LENGTH'
         DC    CL2' '
         DC    CL6'ENT-PT'
         DC    CL2' '
         DC    CL6'---TTR'
         DC    CL2' '
         DC    CL8'ALIAS-OF'
         DC    CL2' '
         DC    CL3'A-M'
         DC    CL2' '
         DC    CL3'R-M'
         DC    CL2' '
         DC    CL4'RENT'
         DC    CL2' '
         DC    CL4'REFR'
         DC    CL2' '
         DC    CL4'REUS'
         DC    CL2' '
         DC    CL4'OVLY'
         DC    CL2' '
         DC    CL4'TEST'
         DC    CL2' '
         DC    CL4'LOAD'
         DC    CL2' '
         DC    CL4'SCTR'
         DC    CL2' '
         DC    CL4'EXEC'
         DC    CL2' '
         DC    CL4'PAGE'
         DC    CL2' '
         DC    CL4'AC= '
         DC    CL2' '
         DC    CL8'SSI-DATA'
         DS    CL11
*
*
*
SYSLIB   DCB   DSORG=PS,MACRF=GM,DDNAME=SYSLIB,RECFM=F,                X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,            X
               LRECL=133
         END

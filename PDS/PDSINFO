         TITLE 'PDSINFO: PRINTS DIRECTORY LISTING FOR A LOAD LIBRARY'
PDSINFO  SVLNK R3
***********************************************************************
* PROGRAM TO READ THE DIRECTORY OF A LOAD MODULE LIBRARY PDS AND WRITE*
* A LISTING SHOWING VARIOUS INFORMATION ABOUT THE MEMBERS INCLUDING   *
* MEMBER NAME, LENGTH, ATTRIBUTES, ALIAS-OF NAME (IF MEMBER IS AN     *
* ALIAS)                                                              *
*                                                                     *
* AUTHOR R. THORNTON   JAN 1993 USING MODULENG AND IHAPDS             *
***********************************************************************
*
         OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
         BAS   R9,PRINT                PRINT HEADER LINE
*
*
*
READDIR  LH    R12,REMBYTS             GET NBR BYTES REMAINING IN BLOCK
         CH    R12,=H'35'              ANY MORE ENTRIES IN BLOCK
         BH    GETADR                  YES
NEXREC   GET   INPUT1                  READ DIRECTORY BLOCK
         ST    R1,BLOKSTRT             SAVE BLOCK START ADDRESS
         LR    R12,R1                  GET BLOCK ADDRESS
         MVC   REMBYTS,0(R12)          MOVE NBR BYTES IN BLOCK
         CLC   REMBYTS,ZERO            ANY BYTES IN BLOCK
         BE    EOJ                     NO, EOF
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         USING PDS2,R12
         CLI   PDS2NAME,X'FF'          END OF BLOCK?
         BE    NEXREC                  YES
         MVC   PNAME,PDS2NAME          MOVE MEMBER NAME TO PRINT
         CLI   PNAME,C'V'
         BNE   UNPKTTR
         B     UNPKTTR
UNPKTTR  UNPK  WORK(7),PDS2TTRP(4)     TTR TO
         TR    WORK(6),TRTBL-C'0'        PRINT
         MVC   PTTR,WORK                    LINE
         UNPK  WORK(7),PDS2STOR(4)     MODULE
         TR    WORK(6),TRTBL-C'0'         LENGTH TO
         MVC   PLENG,WORK                    PRINT LINE
         TM    PDS2INDC,PDS2ALIS       THIS MEMBER AN ALIAS?
         BZ    TRYREFR                 NO
         LA    R11,PDSBCEND            POINT TO END OF BASIC SECTION
         TM    PDS2ATR1,PDS2SCTR       SCATTER SECTION PRESENT?
         BZ    MOVALIAS                NO
         LA    R11,PDSS01LN(R11)       YES, STEP PAST SCTR SECTION
         USING PDSS02,R11
MOVALIAS MVC   PALIAS,PDS2MNM          MOVE ALIAS-OF NAME TO PRINT
         DROP  R11
TRYREFR  TM    PDS2ATR2,PDS2REFR       IS IT REFR?
         BZ    TRYRENT                 NO
         MVC   PREFR,=C'REFR'          YES
TRYRENT  TM    PDS2ATR1,PDS2RENT       IS IT RENT?
         BZ    TRYREUS                 NO
         MVC   PRENT,=C'RENT'          YES
TRYREUS  TM    PDS2ATR1,PDS2REUS       IS IT REUS?
         BZ    TRYEXEC                 NO
         MVC   PREUS,=C'REUS'          YES
TRYEXEC  TM    PDS2ATR1,PDS2EXEC       IS IT EXECUTABLE?
         BZ    RDYPRNT                 NO
         MVC   PEXEC,=C'EXEC'          YES
RDYPRNT  BAL   R9,PRINT                LINE FULL, PRINT IT
NEXDNT   NI    PDS2INDC,B'00011111'    CLEAR ALIAS, TTR BITS
         SR    R11,R11                 CLEAR R11 FOR INSERT
         IC    R11,PDS2INDC            PICK UP NBR USER HALFWORDS
         SLL   R11,1                   X2 = NUMBER USER BYTES
         LA    R11,PDS2USRD-PDS2(R11)  ENTRY LENGTH (BASIC+USER)
CALCREM  LH    R12,REMBYTS             GET NBR BYTES REMAINING
         SR    R12,R11                 SUBTRACT THIS ENTRY LENGTH
         BP    STREM                   ANY LEFT
         SR    R12,R12                 NO, SET TO ZERO
STREM    STH   R12,REMBYTS             STORE REMAINING BYTES
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         CLI   0(R11),X'FF'            END OF DIRECTORY?
         BE    EOJ                     YES
         B     READDIR                 GO READ AGAIN
*
*
*
PRINT    PUT   PRINT1,PRT              PRINT A LINE
         MVI   PRT,C' '                BEGIN CLEAR
         MVC   PRT+1(79),PRT           FINISH CLEAR
         BR    R9                      RETURN
*
*
*
EOJ      CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
BLOKSTRT DC    A(0)                    ADDRESS OF START OF BLOCK
TRTBL    DC    C'0123456789ABCDEF'
REMBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ZERO     DC    H'0'
RECADDR  DS    F
BLANKS   DC    CL80' '                 CONSTANT BLANKS
WORK     DS    CL9                     HEX <-> CHAR WORK
*
PRT      DS    0CL80
PNAME    DS    CL8                     MODULE NAME
         DS    CL1
PLENG    DS    CL6                     MODULE LENGTH
         DS    CL1
PTTR     DS    CL6                     TTR TO MODULE
         DS    CL1
PALIAS   DS    CL8                     ALIAS-OF NAME
         DS    CL1
PREFR    DS    CL4                     REFRESHABLE
         DS    CL1
PRENT    DS    CL4                     REENTRANT
         DS    CL1
PREUS    DS    CL4                     REUSABLE
         DS    CL1
PEXEC    DS    CL4                     EXECUTABLE
         DS    CL29
*
         ORG   PRT
HEADLINE DS    0CL80
         DC    CL9'__NAME__ '          MODULE NAME
         DC    CL7'LENGTH '            MODULE LENGTH
         DC    CL7'__TTR_ '            TTR TO MODULE
         DC    CL9'ALIAS-OF '          ALIAS-OF NAME
         DC    CL5'REFR '              REFRESHABLE
         DC    CL5'RENT '              REENTRANT
         DC    CL5'REUS '              REUSABLE
         DC    CL4'EXEC'               EXECUTABLE
         DC    CL29' '
         ORG
*
         LTORG
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,RECFM=F,                X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,RECFM=FB,               X
               LRECL=80
*
*
*
         IHAPDS PDSBLDL=NO
PDSINFO  CSECT
         END

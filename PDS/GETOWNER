         TITLE '*** GETOWNER: FIND SYSTEM OWNER FOR PROGRAMS ***'
         PRINT NOGEN
***********************************************************************
* USE THE PROD.OWNER.DATA AND EMVSP.OUTPUT.PROCLIB.JES2 DATASETS TO   *
* CREATE A FILE OF PROGRAMS AND THIER OWNERS FOR USE WITH THE COBSUBEM*
* PROGRAM IN PRODUCING A LISTING OF SUBROUTINES, THEIR OWNERS AND     *
* USING PROGRAMS.                                                     *
*                                                                     *
* AUTHOR: R. THORNTON NOV 1993                                        *
***********************************************************************
*
GETOWNER SVLNK R3
***********************************************************************
*        GET PARM FIELD LETTER AND OPEN FILES                         *
***********************************************************************
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),=H'0'           ANY PARM DATA?
         BE    OPENS                   NO, CONTINUE
PARMOK   MVC   PARMLTTR(1),2(R2)       SAVE PARM DATA
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
         CALL  READMEM,OPEN            OPEN MEMBERS FOR INPUT
***********************************************************************
*        LOAD THE ENDEVOR MEMBER-TO-SYSTEM ID TABLE                   *
***********************************************************************
         L     R12,ENDEVTBA            POINT TO THE TABLE
ENDEVLD  GET   INPUT2,ENDEVREC         READ A TABLE RECORD
         MVC   0(8,R12),TBLMEMBR       MOVE MEMBER NAME
         MVC   8(8,R12),TBLSYS         MOVE SYSTEM ID
         LA    R12,16(R12)             STEP TO NEXT ENTRY
         C     R12,ENDEVNDA            PAST END OF TABLE?
         BL    ENDEVLD                 NO
         WTO   'ENDEVOR MEMBER-TO-SYSTEM ID TABLE FULL',ROUTCDE=11
         ABEND 111,DUMP
EOT      ST    R12,ENDEVNDA            UPDATE TABLE END ADDRESS
         CLOSE INPUT2                  CLOSE ENDEVOR TABLE FILE
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE BAS   R9,GETIN                READ INPUT RECORD
         BAS   R9,GETOWNR              GET PROC OWNER
         CLI   OWNER,C' '              VALID OWNER RECORD?
         BE    MAINLINE                NO
         BAS   R9,PROCPROC             YES, PROCESS AGAINST PROCLIB
         B     MAINLINE                LOOP THROUGH INPUT FILE
***********************************************************************
*             GET OWNER NAME/TSO-ID FROM INPUT RECORD                 *
***********************************************************************
GETOWNR  ST    R9,GO9                  SAVE RETURN REG
         MVC   OWNER,BLANKS            CLEAR OWNER NAME FIELD
         CLI   PROCNAME,C'A'           VALID PROCNAME?
         BL    GOXIT                   NO
         MVC   PROC,PROCNAME           YES, PROC NAME TO OUTPUT REC
         CLI   TMLDRNM,C'A'            GOT TEAM LEADER NAME?
         BL    CHK1                    NO
         MVC   OWNER(6),TMLDRNM        YES, MOVE IT
         B     FORMOUT                 QUIT
CHK1     CLI   TMLDRID,C'A'            GOT TEAM LEADER TSO ID?
         BL    CHK2                    NO
         MVC   OWNER(8),TMLDRID        YES, MOVE IT
         B     FORMOUT                 QUIT
CHK2     CLI   SYSMGRNM,C'A'           GOT SYSTEM MANAGER NAME?
         BL    CHK3                    NO
         MVC   OWNER(6),SYSMGRNM       YES, MOVE IT
         B     FORMOUT                 QUIT
CHK3     CLI   SYSMGRID,C'A'           GOT SYSTEM MANAGER TSO ID?
         BL    CHK4                    NO
         MVC   OWNER(8),SYSMGRID       YES, MOVE IT
         B     FORMOUT                 QUIT
CHK4     CLI   SYSBKUNM,C'A'           GOT SYSTEM BACKUP NAME?
         BL    CHK5                    NO
         MVC   OWNER(6),SYSBKUNM       YES, MOVE IT
         B     FORMOUT                 QUIT
CHK5     CLI   TMLDRID,C'A'            GOT SYSTEM BACKUP TSO ID?
         BL    FORMOUT                 NO
         MVC   OWNER(8),SYSBKUID       YES, MOVE IT
FORMOUT  MVC   PGMNAME,BLANKS          CLEAR PROGRAM NAME FIELD
         MVC   PROC,PROCNAME           MOVE IN PROC NAME
         MVC   OWNERLST,SYSMGRID       MOVE IN "OWNER" LIST
GOXIT    L     R9,GO9                  RETURN TO CALLER
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             PROCESS PROC IDENTIFIED BY INPUT RECORD                 *
***********************************************************************
PROCPROC ST    R9,PP9                  SAVE RETURN REG
         MVC   FINDNAME,PROCNAME       PROC NAME FOR FIND
         CALL  READMEM,FIND            GO FIND MEMBER
         CLI   FINDNAME,0              WAS MEMBER FOUND?
         BNE   NEXREC                  YES
         MVC   PRT(8),PROCNAME         PROC NAME TO PRINT
         MVC   PRT+9(18),=CL18'PROC WAS NOT FOUND'
         BAS   R9,PRINT                PRINT ERROR MESSAGE
         B     PPXIT                   AND EXIT
NEXREC   CALL  READMEM,(READ,RECORD)   READ NEXT RECORD
         CLI   RECORD,X'FF'            END OF MEMBER?
         BE    PPXIT                   YES, ALL THROUGH
         CLC   RECORD(2),=C'//'        COL 1-2 = //
         BNE   NEXREC                  NO, NOT JCL
         CLI   RECORD+2,C'*'           IS IT A COMMENT?
         BE    NEXREC                  YES
         LA    R1,RECORD+2             POINT TO COL 3
         LA    R2,9                    NUMBER BYTES TO SEARCH
FINDBLNK CLI   0(R1),C' '              BLANK FOUND?
         BE    FNDNONBK                YES
         LA    R1,1(R1)                NO, STEP OVER 1 BYTE
         BCT   R2,FINDBLNK             NO, CONTINUE SEARCH
         B     NEXREC                  NO BLANK IN 3-12, SKIP RECORD
FNDNONBK LA    R2,RECORD+71            POINT TO END OF RECORD
         SR    R2,R1                   NBR BYTES LEFT IN RECORD
CKSOLID  CLI   0(R1),C' '              FOUND SOLID CHARACTER?
         BNE   CHKOPER                 YES
         LA    R1,1(R1)                NO, STEP TO NEXT BYTE
         BCT   R2,CKSOLID              AND CONTINUE SEARCH
         B     NEXREC                  NOT FOUND, SKIP RECORD
CHKOPER  CLC   0(5,R1),=C'EXEC '       EXEC CARD?
         BNE   NEXREC                  NO, SKIP THE RECORD
EXECARD  LA    R1,5(R1)                STEP PAST EXEC
EXBLNK   CLI   0(R1),C' '              FOUND NON-BLANK?
         BNE   CKPGMEQ                 YES, CHECK FOR PGM=
         LA    R1,1(R1)                NO, STEP TO NEXT BYTE
         BCT   R2,EXBLNK               CONTINUE
         B     NEXREC                  NOT FOUND, SKIP RECORD
CKPGMEQ  CLC   0(4,R1),=C'PGM='        PGM=?
         BNE   NEXREC                  NO
         LA    R1,4(R1)                STEP PAST THE PGM=
PGMSTRT  MVC   PGMNAME,BLANKS          CLEAR PROGRAM NAME
         LA    R12,PGMNAME             POINT TO PROGRAM NAME FIELD
         LA    R2,9                    MAX PGN SIZE+1
CKPGMEND CLI   0(R1),C' '              END OF NAME?
         BE    GOTPGM                  YES
         CLI   0(R1),C','              END OF NAME?
         BE    GOTPGM                  YES
         MVC   0(1,R12),0(R1)          MOVE NAME BYTE
         LA    R1,1(R1)                TO NEXT RECORD BYTE
         LA    R12,1(R12)              TO NEXT NAME BYTE
         BCT   R2,CKPGMEND             LOOP TO MOVE DSN NAME
         B     NEXREC                  TOO LONG, SKIP TO NEXT REC
GOTPGM   LA    R1,SKIPTABL             POINT TO TABLE
CKSKPND  CLI   0(R1),X'FF'             END OF TABLE?
         BE    SCHNDVR                 YES
         CLC   PGMNAME,0(R1)           PROGRAM TO BE SKIPPED?
         BE    PPXIT                   YES
         LA    R1,8(R1)                ADVANCE ONE ENTRY
         B     CKSKPND                 CONTINUE
SCHNDVR  L     R12,ENDEVTBA            POINT TO ENDEVOR TABLE
         MVC   ENDVSYS,BLANKS          CLEAR ENDEVOR SYSTEM NAME
TBSRCH   C     R12,ENDEVNDA            PAST END OF TABLE?
         BH    GOODPGM                 YES, NOT FOUND
         CLC   PGMNAME,0(R12)          FOUND MEMBER ENTRY?
         BE    ENDEVFND                YES
         LA    R12,16(R12)             NO, ADVANCE TO NEXT ENTRY
         B     TBSRCH                  AND CONTINUE SEARCH
ENDEVFND MVC   ENDVSYS,8(R12)          SYSTEM ID TO PRINT
GOODPGM  BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         B     NEXREC                  CONTINUE PROC PROCESSING
PPXIT    L     R9,PP9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PUTOUT   PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,PRINT1,,INPUT1) CLOSE FILES
         CALL  READMEM,CLOSE           CLOSE MEMBER FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
*
OPEN     DC    CL9'OINPUT3'            OPEN PARAMETER
READ     DC    C'R'                    READ PARAMETER
CLOSE    DC    C'C'                    CLOSE PARAMETER
*
FIND     DS    0CL5                    FIND PARAMETER
         DC    C'FN'                   FIND BY NAME
FINDNAME DS    CL8                     PROCNAME TO FIND
*
ENDEVTBA DC    A(ENDEVOR)              ENDEVOR SYSTEM ID TBL ADDR
ENDEVNDA DC    A(ENDTBL)               ENDEVOR TABLE END ADDRESS
GO9      DS    A                       RETURN ADDR FOR GETOWNR
PP9      DS    A                       RETURN ADDR FOR PROCPROC
RECORD   DS    CL80                    RECORD RETURNED BY READMEM
BLANKS   DC    CL133' '                BLANKS
*
ENDEVREC DS    0CL133                  ENDEVOR MEMBER-TO-SYSTEM RECD
         DS    CL1
TBLMEMBR DS    CL8                     MEMBER NAME
         DS    CL14
TBLSYS   DS    CL8
         DS    CL102
*
INREC    DS    0CL150                   INPUT RECORD
PROCNAME DS    CL8                        PROC NAME
         DS    CL2
DESCRIPT DS    CL35
         DS    CL1
SYSMGRID DS    CL8
         DS    CL1
SYSMGRNM DS    CL6
         DS    CL1
TMLDRID  DS    CL8
         DS    CL1
TMLDRNM  DS    CL6
         DS    CL1
SYSBKUID DS    CL8
         DS    CL1
SYSBKUNM DS    CL6
         DS    CL67
*
OUTREC   DS    0CL133                   OUTPUT RECORD
         DC    CL1' '
PGMNAME  DC    CL8' '                     PROGRAM NAME
         DC    CL4' '
ENDVSYS  DC    CL8' '                     ENDEVOR SYSTEM NAME
         DC    CL2' '
OWNER    DC    CL8' '                     PROGRAM OWNER
         DC    CL1' '
PARMLTTR DC    CL6' PROC='
PROC     DC    CL8' '
         DC    CL10'  OWNERS: '
OWNERLST DC    CL47' '
         DC    CL30' '
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DC    C' '                      CARRIAGE CONTROL BYTE
PRT      DC    CL132' '                  PRINT DATA
*
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FBA,LRECL=133
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=150
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOT,              X
               RECFM=FBA,LRECL=133
         LTORG
SKIPTABL DS    0CL8                    TABLE OF PROGRAMS TO SKIP
         DC    CL8'DFSERAI0'
         DC    CL8'DFSRRC00'
         DC    CL8'DFSUARC0'
         DC    CL8'DFSUDMP0'
         DC    CL8'DFSURG10'
         DC    CL8'DSNUTILB'
         DC    CL8'EASYTREV'
         DC    CL8'FOCUS   '
         DC    CL8'ICEMAN  '
         DC    CL8'IDCAMS  '
         DC    CL8'IEBCOPY '
         DC    CL8'IEBGENER'
         DC    CL8'IEBPTPCH'
         DC    CL8'IEFBR14 '
         DC    CL8'IEHPROGM'
         DC    CL8'IFASMPDP'
         DC    CL8'IGHRCO00'
         DC    CL8'IKJEFT01'
         DC    CL8'IKJEFT1A'
         DC    CL8'SAS     '
         DC    CL8'SASHOST '
         DC    CL8'SASLPA  '
         DC    CL8'SMFDUMP '
         DC    CL8'SORT    '
         DC    CL8'SORT33  '
         DC    CL8'SYNCSORT'
         DC    8X'FF'
GETOWNER CSECT
         LTORG
ENDEVOR  DS    7000CL16        ENDEVOR MEMBER-TO-SYSTEM ID TABLE
ENDTBL   EQU   *
         END

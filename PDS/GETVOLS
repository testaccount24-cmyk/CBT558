         TITLE '*** GET VOLUME SERIAL NUMBERS FOR A DATASET ***'
GETVOLS SVLNK R3
GETVOLS AMODE 31
GETVOLS RMODE ANY
**************************************************************
* THIS SUBROUTINE USES AN OPEN DCB PASSED BY THE CALLER TO   *
* FIND THE TIOT ENTRY FOR THE DD STATEMENT, THEN ACCESSES    *
* THE JFCB USING THE TIOEJFCB FIELD. THE 30 CHARACTER FIELD  *
* NAMED JFCBVOLS IS PASSED BACK TO THE CALLER. THIS FIELD    *
* CONTAINS THE VOLUME SERIAL NUMBERS OF THE FIRST FIVE       *
* VOLUMES ALLOCATED TO THE DATASET. IF FEWER THAN 5 VOLUMES  *
* ARE ASSIGNED, THE REMAINING FIELDS WILL BE SET TO BLANKS.  *
*                                                            *
* COBOL CALLING EXAMPLE:                                     *
*            .                                               *
*            .                                               *
*            .                                               *
*            FILE SECTION.                                   *
*            FD  TRAN-FILE                                   *
*                RECORDING MODE IS ...                       *
*            .                                               *
*            .                                               *
*            .                                               *
*            WORKING-STORAGE SECTION.                        *
*            .                                               *
*                05  VOLUME-SERIAL-NOS  PIC X(6)             *
*            .                          OCCURS 5 TIMES.      *
*            .                                               *
*            .                                               *
*            OPEN INPUT (OR OUTPUT) TRAN-FILE                *
*            .                                               *
*            .                                               *
*            .                                               *
*            CALL 'GETVOLS' USING TRAN-FILE,                 *
*                                  VOLUME-SERIAL-NO.         *
*                                                            *
*                                                            *
* AUTHOR: R. THORNTON 08/28/92  FOR DEBBIE SEITZ             *
**************************************************************
*                                                            *
***********************************************************************
*        PRE-CLEAR CALLER'S VOLUME SERIAL AREA TO BLANKS              *
***********************************************************************
         L     R11,4(R1)               POINT TO USER'S VOLSER FIELD
         MVC   0(30,R11),BLANKS        CLEAR IT TO BLANKS
***********************************************************************
*        CHECKS: (1) IS IT AN OPEN DCB?, (2) FOR TAPE OR DISK?        *
*        IF NOT, RETURN WITH BLANK VOLUME SERIAL FIELD                *
***********************************************************************
         L     R12,0(R1)               POINT TO DCB
         TM    48(R12),X'10'           DCBOFLGS: IS FILE OPEN?
         BZ    RETURN                  NO, EXIT WITH ERROR
         SR    R10,R10                 CLEAR FOR INSERT
         ICM   R10,7,45(R12)           DCBDEBAD: POINT TO THE DEB
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         CLM   R12,7,25(R10)           DEBDCBAD POINTS BACK TO THE DCB?
         BNE   RETURN                  NO, INVALID
         ICM   R10,7,33(R10)           DEBUCBAD: POINT TO THE UCB
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         CLI   2(R10),X'FF'            UCBID = X'FF' IF VALID UCB
         BNE   RETURN                  NO, INVALID
         CLI   18(R10),X'80'           UCBTBYT3: IS THIS A TAPE DEVICE?
         BE    FINDJFCB                YES
         CLI   18(R10),X'20'           UCBTBYT3: IS THIS A DASD DEVICE?
         BNE   RETURN                  NO, INVALID
***********************************************************************
*        LOCATE THE JFCB AND MOVE THE VOLUME SERIAL NUMBERS BACK      *
***********************************************************************
FINDJFCB ICM   R10,7,17                FLCCVT: POINT TO THE CVT
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         ICM   R10,7,1(R10)            CVTTCBP: POINT TO TCB WORDS
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         ICM   R10,7,1(R10)            GET THE TCB ADDRESS
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         ICM   R10,7,13(R10)           TCBTIO: POINT TO THE TIOT
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         AH    R10,40(R12)             +DCBTIOT = TIOT TIOENTRY ADDRESS
         ICM   R10,7,12(R10)           TIOEJFCB: POINT TO THE JFCB-16
         BZ    RETURN                  RETURN IF NO VALID ADDRESS
         SR    R1,R1                   CLEAR FOR INSERT
         IC    R1,133(R10)             JFCBNVOL: GET NUMBER VOLUMES
         MH    R1,H6                   MOVE LENGTH = 6 * NBR VOLS
         BCTR  R1,0                    DEDUCT 1 FOR MVC LENGTH CODE
         EX    R1,MOVEVOLS             MOVE VOLUME SERIALS TO CALLER
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
RETURN   UNLNK 0                       RETURN TO CALLER
***********************************************************************
*        EXECUTED MOVE INSTRUCTION                                    *
***********************************************************************
MOVEVOLS MVC   0(1,R11),134(R10)       MOVE JFCBVOLS TO CALLER AREA
***********************************************************************
*        PROGRAM CONSTANTS                                            *
***********************************************************************
H6       DC    H'6'                    CONSTANT 6
BLANKS   DC    CL30' '                 CONSTANT BLANKS
         END

         TITLE '*** GET DSNAME, VOLSERS AND DCB INFO ***'
         PRINT NOGEN
GETDCB SVLNK R3,,,SUB
**************************************************************
* THIS ACCEPTS THE ADDRESS OF AN OPEN DCB (COBOL FD) AND     *
* USES IT TO LOCATE THE JFCB. THE DSNAME, VOLUME SERIAL      *
* NUMBER FIELD, RECFM, LRECL, AND BLKSIZE ARE RETURNED TO    *
* THE CALLER. FOR SUCCESSFUL OPERATION, THE FILE MUST BE OPEN*
* AND MUST HAVE DSORG=PS.                                    *
*                                                            *
* COBOL CALLING EXAMPLE:                                     *
*            .                                               *
*            .                                               *
*      FILE-CONTROL.                                         *
*          SELECT SAMPLE-FILE ASSIGN TO INPUT1.              *
*      DATA DIVISION.                                        *
*      FILE SECTION.                                         *
*      FD  INPUT-FILE                                        *
*            .                                               *
*            .                                               *
*            WORKING-STORAGE SECTION.                        *
*            .                                               *
*            01  GETDCB-PARAMETERS.                          *
*                05  GETDCB-STATUS         PIC X.            *
*                    88 GETDCB-SUCCESSFUL  VALUE ' '.        *
*                    88 DSORG-NOT-PS       VALUE 'X'.        *
*                    88 FILE-NOT-OPEN      VALUE 'N'.        *
*                05  DATASET-NAME          PIC X(44).        *
*                05  VOLUME-SERIAL         PIC X(6)          *
*                                          OCCURS 5 TIMES.   *
*                05  RECORDING-FORMAT      PIC X(4).         *
*                05  LOGICAL-RECORD-LENGTH PIC 9(9).         *
*                05  BLOCK-SIZE            PIC 9(9).         *
*                                                            *
*                .                                           *
*                .                                           *
*                .                                           *
*            PROCEDURE DIVISION.                             *
*                .                                           *
*                .                                           *
*            OPEN INPUT SAMPLE-FILE.                         *
*                .                                           *
*                .                                           *
*                .                                           *
*                CALL 'GETDCB' USING SAMPLE-FILE,            *
*                                    GETDCB-PARAMETERS.      *
*                IF GETDCB-SUCCESSFUL                        *
*                .                                           *
*                .                                           *
*                                                            *
*                                                            *
*                                                            *
* AUTHOR: R. THORNTON 08/18/94  FOR PATRICIA SMITH           *
**************************************************************
*                                                            *
***********************************************************************
*        POINT TO PARAMETER AREA                                      *
***********************************************************************
         L     R6,0(R1)                POINT TO THE DCB
         USING IHADCB,R6
         L     R7,4(R1)                POINT TO PARAMETER AREA
         USING PARAMS,R7
***********************************************************************
*        CHECK IF FILE IS OPEN AND DSORG=PS                           *
***********************************************************************
         TM    DCBOFLGS,X'10'          IS IT OPEN?
         BO    CHKDSRG                 YES
         MVI   PSTATUS,C'X'            NO, SHOW FILE NOT OPEN
         B     RETURN                  RETURN TO CALLER
CHKDSRG  CLI   DCBDSORG,DCBDSGPS       IS DSORG=PS?
         BE    SETOK                   YES
         MVI   PSTATUS,C'N'            NO, SHOW DSORG NOT PS
         B     RETURN                  RETURN TO CALLER
SETOK    MVI   PSTATUS,C' '            SET GOOD RETURN CODE
***********************************************************************
*        LOCATE THE JFCB AND SEND INFORMATION TO CALLER               *
***********************************************************************
         L     R1,16                   GET CVT POINTER
         L     R1,0(R1)                POINT TO NEW-OLD WORDS
         L     R1,0(R1)                POINT TO MY TCB
         L     R1,12(R1)               POINT TO MY TIOT
         AH    R1,DCBTIOT              POINT TO TIOT DD ENTRY
         ICM   R1,7,12(R1)             POINT TO JFCB
         LA    R1,16(R1)               STEP PAST PREFIX
         USING JFCB,R1                 MOVE JFCB TO WORK AREA
         MVC   PDSNAME,JFCBDSNM        SEND BACK DSNAME
         MVC   PVOLSER,JFCBVOLS        SEND BACK VOLUME SERIAL NBRS
         DROP  R1
         MVI   PRECFM,C'U'             ASSUME RECFM=U
         TM    DCBRECFM,DCBRECU        IS RECFM=U?
         BO    CLEAREST                YES
         MVI   PRECFM,C'V'             ASSUME RECFM=V
         TM    DCBRECFM,DCBRECV        IS RECFM=V?
         BO    CLEAREST                YES
         MVI   PRECFM,C'F'             NO, SET RECFM=F
CLEAREST MVC   PRECFM+1(3),=C'   '     CLEAR REST OF RECFM
         LA    R1,PRECFM+1             POINT TO RECFM 2ND BYTE
         TM    DCBRECFM,X'10'          IS IT A BLOCKED FILE?
         BZ    CHKSP                   NO
         MVI   0(R1),C'B'              YES, SHOW BLOCKED FILE
         LA    R1,1(R1)                BUMP POINTER
CHKSP    TM    DCBRECFM,DCBRECSB       IS IT SPANNED/STANDARD?
         BZ    CHKCTL                  NO
         MVI   0(R1),C'S'              YES, SHOW SPANNED/STANDARD
         LA    R1,1(R1)                BUMP POINTER
CHKCTL   TM    DCBRECFM,DCBRECCA       HAS IT ASA CONTROL CHARS?
         BZ    CHKMCH                  NO
         MVI   0(R1),C'A'              YES, SHOW ASA CTL CHARS
         B     DOLRECL                 CONTINUE
CHKMCH   TM    DCBRECFM,DCBRECCM       HAS IT MACHINE CTL CHARS?
         BZ    DOLRECL                 NO
         MVI   0(R1),C'M'              YES, SHOW MACHINE CTL CHARS
DOLRECL  LH    R1,DCBLRECL             GET LRECL
         CVD   R1,DBLWD                CONVERT IT TO PACKED DECIMAL
         OI    DBLWD+7,X'0F'           MAKE IT UNSIGNED
         UNPK  PLRECL,DBLWD            UNPACK TO CALLER'S AREA
         LH    R1,DCBBLKSI             GET BLKSIZE
         CVD   R1,DBLWD                CONVERT IT TO PACKED DECIMAL
         OI    DBLWD+7,X'0F'           MAKE IT UNSIGNED
         UNPK  PBLKSIZE,DBLWD          UNPACK TO CALLER'S AREA
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
RETURN   L     R13,4(R13)              POINT TO CALLER'S SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        PROGRAM CONSTANTS                                            *
***********************************************************************
         PRINT GEN
DBLWD    DS    D                       DOUBLEWORD WORK AREA
PARAMS   DSECT ,                       CALLER'S PARAMETER AREA
PSTATUS  DS    CL1                       RETURN STATUS: BLANK=O.K.,
*                                          X = FILE NOT OPEN
*                                          N = DSORG NOT PS
PDSNAME  DS    CL44                      DATASET NAME
PVOLSER  DS    CL30                      VOLUME SERIALS 1-5
PRECFM   DS    CL4                       RECFM
PLRECL   DS    CL9                       LRECL
PBLKSIZE DS    CL9                       BLKSIZE
JFCB     DSECT
         IEFJFCBN LIST=YES
         DCBD   DSORG=PS
GETDCB CSECT
         END

         PRINT NOGEN
PDSDIR   SVLNK R3
********************************************************************
* READS THE DIRECTORY OF THE PDS GIVEN BY THE INPUT1 DD CARD AND   *
* WRITES AN OUTPUT FILE CONTAINING ONE RECORD PER MEMBER WITH      *
* MEMBER NAME IN COLUMNS 1-8 AND BLANKS IN COLUMNS 9-80. THE OUTPUT*
* FILE IS SPECIFIED BY THE OUTPUT1 DD CARD. RECFM=FB,LRECL=80 IS   *
* HARD-CODED FOR BOTH INPUT1 AND OUTPUT1.                          *
* AUTHOR R THORNTON 12/19/90                                       *
********************************************************************
         L     R14,0(R1)               GET PARM AREA ADDRESS
         ICM   R15,3,0(R14)            GET PARM LENGTH
         BZ    OPENS                   BRANCH IF NO PARM DATA
         CH    R15,H48                 OVER 48 BYTES?
         BNH   EXMVC                   NO
         LH    R15,H48                 YES, SET LENGTH TO MAX
EXMVC    BCTR  R15,0                   COMPUTE LENGTH CODE FOR MVC
         EX    R15,MVCPARM             MOVE PARM DATA TO SAVE
         B     OPENS                   CONTINUE
MVCPARM  MVC   PARMDATA(1),2(R14)      SAVE PARM DATA (EXECUTED)
OPENS    OPEN  (OUTPUT1,OUTPUT)        OPEN OUTPUT FILE
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
RDIRCTY  CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF DIRECTORY
         BH    EOJ                     YES, END OF JOB
         MVC   OUTNAME,MEMNAME         NAME TO OUTPUT RECORD
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         B     RDIRCTY                 CONTINUE
EOJ      L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*                      *** STORAGE AND FILES ***
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
H48      DC    H'48'                   CONSTANT 48
OUTREC   DS    0CL80                   OUTPUT RECORD
OUTNAME  DS    CL8                       MEMBER NAME COL 1-8
         DC    CL15' '                   BLANKS COL 9-23
PARMDATA DC    CL48' '                   PARM DATA COL 24-71
         DC    CL9' '                    BLANKS COL 72-80
EOF      EQU   C'Z'
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         END

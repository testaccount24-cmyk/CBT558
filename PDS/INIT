         MACRO
&L       INIT  &SAVE=(14,12),&BASE=12,&LENGTH=72,&RENT=N,&REGS=N,      *
               &BASES=1,&ID=,&RSV=,&LOC=BELOW
.*
.*      INIT - ASSEMBLER PROGRAM PROLOGUE - REENTRANT/NON-REENTRANT
.*
.*      MACRO BY DAVID R. BIVENS - 04/17/79
         GBLA  &ENTER
         LCLA  &TEMP,&INDEX,&WORK,&R1,&R2,&IDLEN
&INDEX   SETA  &SYSNDX
&ENTER   SETA  2
         AIF   ('&BASE' NE '&ENTER').USE2
&ENTER   SETA  3
.USE2    ANOP
         AIF   (0 EQ K'&L).NOLBL
&L       CSECT
.NOLBL   ANOP
         AIF   (0 EQ K'&ID).IDOUT
         AGO   .IDOK -WAS AIF ('U' EQ T'&ID).IDOK-
.BADID   ANOP
         MNOTE 4,'INVALID CSECT IDENTIFIER SPECIFIED'
         AGO   .IDOUT
.IDOK    ANOP
         AIF   (255 LT K'&ID).BADID
         AIF   ('*' EQ '&ID').SPCLID
&WORK    SETA  ((K'&ID+2)/2)*2+4
&TEMP    SETA  K'&ID
         B     &WORK.(15)              BRANCH AROUND ID.
         DC    AL1(&TEMP)          ID LENGTH
         DC    C'&ID'
&IDLEN   SETA  &WORK
         AGO   .IDOUT
.SPCLID  ANOP
         B     14(15)
         DC    AL1(8)
         AIF   (0 NE K'&L).NEWSECT
         DC    CL8'&SYSECT'
&IDLEN   SETA  14
         AGO   .IDOUT
.NEWSECT ANOP
         DC    CL8'&L'
&IDLEN   SETA  14
.IDOUT   ANOP
         AIF   (2 EQ N'&SAVE).REGOK1
         MNOTE 4,'INVALID SPECIFICATION OF SAVE REGISTERS - &SAVE - (14,
               2 USED'
.REGOK1  ANOP
&R1      SETA  &SAVE(1)
&R2      SETA  &SAVE(2)
         STM   &R1,&R2,12(13)
         LR    &ENTER,1            SAVE REG1 PARM ADDRESS.
         LR    &BASE,15            LOAD BASE REGISTER.
&WORK    SETA  8
&WORK    SETA  &WORK+&IDLEN
         USING *-&WORK,&BASE     ESTABLISH ADDRESSABILITY.
         LCLA  &BASEN,&BASEO,&BASNO,&DISP
&DISP    SETA  &BASE-&BASES
         AIF   (&DISP GE 2).BASNOK
         MNOTE 8,'INVALID BASE= / BASES= COMBINATION, RESULTS WOULD'
         MNOTE 8,'CREATE BASE REGISTERS LESS THAN R2.'
         MEXIT
.BASNOK  ANOP
&BASEN   SETA  &BASE
&BASEO   SETA  &BASEN
&BASNO   SETA  &BASES
&DISP    SETA  4095
&WORK    SETA  &WORK+4
.NXTBAS  ANOP
&BASNO   SETA  &BASNO-1
         AIF   (0 GE &BASNO).BASEND
&BASEN   SETA  &BASEN-1
&BASEO   SETA  &BASEN+1
         AIF   (&BASEN LT 2).BASERR
         LA    &BASEN,4095(,&BASEO)
         USING *-&WORK+&DISP,&BASEN
&DISP    SETA  &DISP+4096
&WORK    SETA  &WORK+4
         AGO   .NXTBAS
.BASERR  MNOTE 8,'INVALID BASE REGISTER COMBINATION SPECIFIED'
         MEXIT
.BASEND  ANOP
         AIF   ('Y' EQ '&RENT').YRENT
         AIF   (0 NE K'&RSV).HISAVE
.MYSAVE  ANOP
         B     PRSV&INDEX          BRANCH AROUND SAVE AREA.
RSVA&INDEX DS  18F                 SAVE AREA.
PRSV&INDEX DS  0H                  AROUND SAVE AREA.
         MNOTE *,'SAVE AREA GENERATED'
         LA    1,RSVA&INDEX
         AGO   .CHAIN
.HISAVE  ANOP
         AIF   ('F' NE T'&RSV AND 'A' NE T'&RSV).BADRSV
         LA    1,&RSV              USER-PROVIDED SAVE AREA ADDRESS.
         AGO   .CHAIN
.BADRSV  ANOP
         MNOTE 4,'INCORRECT SAVE AREA ADDRESS SPECIFIED'
         AGO   .MYSAVE
.YRENT   ANOP
         SR    0,0
         AIF   ('F' NE T'&LENGTH).TRYH
         L     0,&LENGTH
         AGO   .GOTLEN
.TRYH    AIF   ('H' NE T'&LENGTH).TRYA
         LH    0,&LENGTH
         AGO   .GOTLEN
.TRYA    AIF   ('A' NE T'&LENGTH).TRYU
         L     0,&LENGTH
         AGO   .GOTLEN
.TRYU    AIF   ('U' NE T'&LENGTH).TRYN
         LA    0,&LENGTH
         AGO   .GOTLEN
.TRYN    AIF   ('N' NE T'&LENGTH).TRYZ
         LA    0,&LENGTH
         AGO   .GOTLEN
.TRYZ    ANOP
         MNOTE 8,'UNSUPPORTED LENGTH CONSTANT - &LENGTH - 72 ASSUMED'
         LA    0,72                **** ASSUMED SUBPOOL 0 LENGTH ****
.GOTLEN  ANOP
*        GETMAIN R,LV=(0),LOC=(&LOC,ANY)
         GETMAIN R,LV=(0),LOC=(&LOC,ANY)
.CHAIN   ANOP
         ST    13,4(1)             SAVE AREA FORWARD CHAIN.
         ST    1,8(13)             SAVE AREA BACKWARD CHAIN.
         LM    14,0,12(13)    RESTORE R0, R14, R15...
         LR    13,1                OUR SAVE AREA ADDRESS.
         LR    1,&ENTER            RESTORE REG 1 ADDRESS.
         AIF   ('Y' NE '&REGS').NOREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FR0      EQU   0
FR2      EQU   2
FR4      EQU   4
FR6      EQU   6
.NOREGS  ANOP
         MEND

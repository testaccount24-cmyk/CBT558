         TITLE '***** MODSCB2: CHANGE FILE EXPIRATION DATE *****'
         PRINT NOGEN
MODSCB2  CSECT
MODSCB2  AMODE 31
MODSCB2  RMODE 24
**********************************************************************
*  A CONTROL CARD FILE IS READ TO SPECIFY WHICH FILES ARE TO BE      *
*  PROCESSED, WHERE THEY ARE TO BE FOUND, AND WHETHER THE EXPIRATION *
*  DATE IS TO BE SET TO 99/365 (HI) OR TO 00/000 (LOW). CONTROL CARDS*
*  ARE ENTERED FOLLOWING A SYSIN DD CARD AND HAVE THE FOLLOWING FORM:*
*                                                                    *
*  COLUMN    DESCRIPTION                                             *
*       1    H=SET EXPIRATION TO 99/365, L=SET EXPIRATION TO 00/000  *
*     2-7    VOLUME SERIAL OF THE DISK CONTAINING THE FILE. IF THIS  *
*            FIELD IS BLANK, THE CATALOG WILL BE USED TO FIND THE    *
*            VOLUME SERIAL NUMBER.                                   *
*    8-51    NAME OF THE DATASET WHOSE EXPIRATION DATE IS TO BE      *
*            CHANGED.                                                *
*   52-72    RESERVED FOR FUTURE EXPANSION.                          *
*   73-80    OPTIONAL CARD NUMBER FIELD. NOT USED BY THE PROGRAM.    *
*                                                                    *
*  AN AUDIT TRAIL/ERROR REPORT IS WRITTEN TO THE SYSPRINT FILE. MOST *
*  ERRORS WILL RESULT IN A MESSAGE, BUT PROCESSING WILL CONTINUE.    *
*                                                                    *
*  NOTE: ALL THIS PROGRAM CHANGES IS THE VTOC ENTRY. THE EXPIRATION  *
*        IN THE CATALOG WILL NOT BE CHANGED. IF THIS IS DESIRED, A   *
*        SEPARATE IDCAMS ALTER RUN CAN BE MADE TO SET THE CATALOG'S  *
*        EXPIRATION DATE ALSO.                                       *
*                                                                    *
*  AUTHOR R. THORNTON REWRITTEN AUG 1987                             *
**********************************************************************
* MAINTENANCE CHANGES
**********************************************************************
* APR 2002 RNT CHANGED UCB LOOKUP METHOD TO USE THE UCBSCAN MACRO SO *
*              UCB'S ABOVE THE LINE AND DYNAMIC UCB'S CAN BE FOUND.  *
*              ALSO REMOVED THE SVLNK MACRO AND HARD-CODED THE ENTRY *
*              POINT SETUP AND SPECIFIED AMODE=31,RMODE=24.          *
**********************************************************************
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS
         LR    R3,R15                  COPY PROGRAM BASE REGISTER
         USING MODSCB2,R3
         LA    R15,MYSAVE              POINT TO MY SAVE AREA
         ST    R15,8(R13)              SET UP LSA POINTER
         ST    R13,4(R15)              SET UP HSA POINTER
         LR    R13,R15                 POINT R13 TO MY SAVE AREA
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'MODSCB2'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
**********************************************************************
*        OPEN THE CONTROL CARD AND LISTING FILES.                    *
**********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT,SYSIN)  OPEN CTL CARD, LISTING
         PUT   SYSPRINT,HEADER         WRITE PRINT HEADER
***********************************************************************
* ALLOCATE A DD STATEMENT FOR USE BY EXCP AND OPEN THE DCB USED FOR   *
* EXCP. IF EITHER THE ALLOCATE OR OPEN FAILS, A U0333 ABEND WILL BE   *
* PRODUCED WITH AN EXPLANATORY MESSAGE.                               *
***********************************************************************
ALLOCDYN CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE A TEMPORARY DATASET
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    SETDDNM                 NO
ALLOCERR WTO   'ERROR: DYNAMIC ALLOC FAILED FOR WORK FILE',ROUTCDE=11
         ABEND 888,DUMP                TERMINATE WITH ERROR
SETDDNM  MVC   VTOC+40(8),DDN          MOVE GENERATED DDNAME
         OPEN  (VTOC,OUTPUT)           OPEN FILE FOR EXCP
         TM    VTOC+48,X'10'           WAS OPEN SUCCESSFUL?
         BO    SAVEUE                  YES
OPENERR  WTO   'ERROR: UNABLE TO OPEN WORK FILE',ROUTCDE=11
         ABEND 888,DUMP                TERMINATE WITH ERROR
SAVEUE   L     R12,VTOC+44             @ DEB
         MVC   SAVUCB,33(R12)          SAVE DEB UCB ADDRESS
         MVC   SAVEXT,38(R12)          SAVE EXTENTS
**********************************************************************
*        READ THE NEXT SYSIN CONTROL CARD                            *
**********************************************************************
READCTL  GET   SYSIN,CTLCARD           READ NEXT CONTROL CARD
         MVC   PRTLOC,=C'    '         CLEAR PRINT FROM FIELD
         MVC   SETMESG,SETHIGH         ASSUME EXPDT SET TO 99/365
         MVC   USEXPDT,HIDATE          ASSUME EXPDT SET TO 99/365
         CLI   CTLHL,C'H'              IS IT HI REQUEST?
         BE    VSNLOCN                 YES
         MVC   SETMESG,SETLOW          ASSUME EXPDT SET TO 99/365
         MVC   USEXPDT,LODATE          ASSUME EXPDT SET TO 99/365
         CLI   CTLHL,C'L'              IS IT LO REQUEST?
         BE    VSNLOCN                 YES
         MVC   PRTRES,INVCTLHL         NO, ERROR MESSAGE
         B     PRTRSLT                 GO PRINT MESSAGE
VSNLOCN  MVC   PRTLOC,=C'CARD'         ASSUME CARD HAS VOLSER
         CLC   CTLVOL,BLANX            CARD HAS VOLSER?
         BNE   LOCUCB                  YES
         MVC   PRTLOC,=C'CTLG'         NO, CATALOG USED
***********************************************************************
*        SEARCH THE CATALOG FOR A DATASET, SAVE VOLUME SERIAL         *
***********************************************************************
READCAT  MVC   CATNAME,CTLDSN          DSNAME TO SEARCH PARMS
         LOCATE BYDSN                  READ CATALOG FOR DSNAME
         LTR   R15,R15                 SUCCESSFUL CATALOG READ?
         BZ    GOTCAT1                 YES
         MVC   PRTRES,NOCTLG           NO, MESSAGE TO PRINT
         B     PRTRSLT                 GO PRINT RESULT MESSAGE
GOTCAT1  SR    R11,R11                 CLEAR FOR INSERT
         ICM   R11,3,LOCAREA           GET VOLSER COUNT
         CH    R11,H1                  JUST ONE VOLUME?
         BE    SAVEVOL                 YES
         MVC   PRTRES,MULTIVOL         NO, MULTIVOLUME ERROR
         B     PRTRSLT                 GO PRINT RESULT MESSAGE
SAVEVOL  MVC   CTLVOL,LOCAREA+6        MOVE VOLSER FOR SEARCH
***********************************************************************
*        LOCATE THE UCB FOR THE VOLUME SERIAL IN CTLVOL               *
***********************************************************************
LOCUCB   XC    WORK100,WORK100         CLEAR FOR EACH NEW INVOCATION
VLSLOOP  SMODE KEY=0,STATE=S           KEY SUPVR STATE, KEY 0
         UCBSCAN ADDRESS,WORKAREA=WORK100,UCBPTR=UCBADDR,              X
               NOPIN,DYNAMIC=YES,RANGE=ALL,DEVCLASS=DASD
         ST    R15,SAV15               SAVE RETURN CODE
         SMODE KEY=NZERO,STATE=P       BACK TO PROGRAM STATE, KEY
         CLC   SAV15,FZERO             FOUND UCB?
         BE    GETUCBA                 YES
NOVOL    MVC   PRTRES,BADVOL           ERROR MESSAGE
         B     PRTRSLT                 GO PRINT RESULT
GETUCBA  L     R8,UCBADDR              GET THE UCB ADDRESS
         USING UCBOB,R8
         CLC   UCBVOLI,CTLVOL          FOUND UCB FOR THIS VOLSER?
         BNE   VLSLOOP                 NO, CONTINUE SEARCH
***********************************************************************
*        TEST FOR VALID ONLINE DASD UCB ADDRESS                       *
***********************************************************************
TESTUCB  TM    UCBSTAT,UCBONLI         IS IT ONLINE?
         BNZ   TSTUCB                  YES
         MVC   PRTRES,UCBOFLN          UCB OFFLINE MESSAGE
         B     PRTRSLT                 GO PRINT RESULT
TSTUCB   CLI   UCBID,X'FF'             THIS A GOOD UCB ADDRESS?
         BE    TSTCLASS                YES
         MVC   PRTRES,INVALUCB         INVALID UCB MESSAGE
         B     PRTRSLT                 GO PRINT RESULT
TSTCLASS CLI   UCBDVCLS,UCB3DACC       IS IT DIRECT ACCESS DEVICE?
         BE    VTOCSTRT                YES
         MVC   PRTRES,UCBNOTDA         INVALID DEVICE CLASS MSG
         B     PRTRSLT                 GO PRINT RESULT
***********************************************************************
*        COMPUTE VTOC START ADDRESS FROM UCB TTR                      *
***********************************************************************
VTOCSTRT SR    R14,R14                 CLEAR FOR DIVIDE
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R15,3,UCBVTOC           REL VTOC START TRACK
         D     R14,=F'15'              COMPUTE CYL-HD
         STH   R15,VTOCCYL             SAVE VTOC CYLINDER NBR
         STH   R14,VTOCHD              SAVE VTOC HEAD NUMBER
***********************************************************************
*        SET DEB UCB FOR THIS DEVICE, EXTENTS FOR VTOC                *
***********************************************************************
DEBUCBEX L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         STCM  R8,7,33(R12)            CHANGE DEB UCB ADDRESS
         MVC   38(10,R12),ALLCYL       SET DEB START/END EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
         DROP  R8
***********************************************************************
*        SET UP EXCP TO READ THE FORMAT 1 DSCB AND READ IT            *
***********************************************************************
READFMT1 MVC   VTSRCH(4),VTOCCYL       START AT VTOC BEGIN CCHH ADDR
         MVI   VTREC,1                 START WITH RECORD 1
         MVC   DSNAME,CTLDSN           MOVE DSNAME FOR SEARCH
         MVI   RWCCW,6                 SET CCW TO READ DATA
         EXCP  VTOCIOB                 LOCATE AND READ FORMAT 1 DSCB
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             NORMAL COMPLETION
         BE    REWFMT1                 YES
         MVC   PRTRES,VTOCRDER         NOT FOUND/IO ERROR MSG
         B     PRTRSLT                 GO PRINT RESULT
***********************************************************************
*        SET FORMAT 1 EXPIRATION DATE AND REWRITE FORMAT 1 DSCB       *
***********************************************************************
REWFMT1  MVC   DS1EXPDT,USEXPDT        SET EXPIRATION DATE
         MVI   RWCCW,5                 CHANGE READ COMMAND TO WRITE
         EXCP  VTOCIOB                 LOCATE AND REWRITE DATA PORTION
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             I/O COMPLETED NORMALLY
         BE    SUCCESS                 YES
         MVC   PRTRES,VTOCWTER         NOT FOUND/IO ERROR MSG
         B     PRTRSLT                 GO PRINT RESULT
SUCCESS  MVC   PRTRES,GOODUPDT         SUCCESSFUL UPDATE MSG
***********************************************************************
*        WRITE RESULTS, CONTINUE TO NEXT CARD                         *
***********************************************************************
PRTRSLT  MVC   PRTHL,CTLHL             HI/LO REQUEST TO PRINT
         MVC   PRTVOL,CTLVOL           VOLSER TO PRINT
         MVC   PRTDSN,CTLDSN           DSNAME TO PRINT
         PUT   SYSPRINT,PRTLINE        PRINT MESSAGE
         B     READCTL                 CONTINUE PROCESSING
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         MVC   33(3,R12),SAVUCB        RESTORE DEB UCB ADDRESS
         MVC   38(10,R12),SAVEXT       RESTORE DEB EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
         CLOSE (SYSPRINT,,VTOC,,SYSIN) CLOSE FILES USED
         L     R13,4(R13)              GET O/S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE O/S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO O/S
*
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
*
SAVUCB   DS    XL3                     SAVED DEB UCB ADDRESS
SAVEXT   DS    XL10                    SAVED DEB EXTENT
H1       DC    H'1'                    CONSTANT 1
TWO      DC    H'2'                    CONSTANT 2
USEXPDT  DC    AL3(00)                 EXPIRATION DATE TO USE
HIDATE   DC    AL1(99),AL2(365)        HIGH EXPIRATION DATE 99/365
LODATE   DC    XL3'00'                 LOW EXPIRATION DATE, ZEROS
ZEROS    DC    XL4'00'                 ZEROES
BLANX    DC    CL80' '                 CONSTANT BLANKS
FOUNDSW  DC    X'00'                   MODSCB DDNAMES FOUND
ALLCYL   DS    0H                      DEB EXTENTS
VTOCCYL  DS    H                       VTOC CYLINDER
VTOCHD   DS    H                       VTOC HEAD
         DC    H'32767',H'32767'       END DEB EXTENT
DSNAME   DS    CL44                    DATASET NAME
***********************************************************************
*        CATALOG SEARCH DATA FIELDS                                   *
***********************************************************************
BYDSN    CAMLST NAME,CATNAME,,LOCAREA READ CATALOG LIST
CATNAME  DC    CL44' '                 DSNAME FOR CAMLST MACRO
LOCAREA  DS    CL265                   CATALOG INFORMATION AREA
PRMLST   DS    0F                      PARMLIST FOR CVTUCBSC
         DC    A(WORK100)                 WORK AREA ADDRESS
         DC    A(DEVCLASS)                DEVICE CLASS TYPE BYTE
         DC    XL1'80',AL3(UCBADDR)       UCB ADDRESS RETURN FIELD
         DS    0D                      DOUBLEWORD ALIGN WORK100
WORK100  DC    XL100'00'               WORK AREA
DEVCLASS DC    AL1(UCB3DACC)           SEARCH DEVICE TYPE
UCBADDR  DC    A(0)                    UCB ADDRESS
SAV15    DC    F'0'                    SAVE AREA FOR RETURN CODE
FZERO    DC    F'0'                    CONSTANT ZERO
***********************************************************************
*        PRINT HEADER                                                 *
***********************************************************************
HEADER   DS    0CL133                  HEADER LINE
         DC    CL1'1'
         DC    CL4'H/L '
         DC    CL7'VOLSER '
         DC    CL5'FROM '
         DC    CL45'DATASET NAME'
         DC    CL71'UPDATE RESULTS'
***********************************************************************
*        PRINT LINE                                                   *
***********************************************************************
PRTLINE  DS    0CL133                  PRINT LINE
         DC    CL2' '                  CARRIAGE CONTROL BYTE
PRTHL    DS    CL1                     H OR L FROM CARD
         DC    CL2' '
PRTVOL   DS    CL6                     VOLUME SERIAL
         DC    CL1' '
PRTLOC   DS    CL4                     VOLSER SOURCE
         DC    CL1' '
PRTDSN   DS    CL44                    DATASET NAME
         DC    CL1' '
PRTRES   DS    CL49                    RESULT MESSAGE TEXT
         DC    CL22' '
***********************************************************************
*        PRINT RESULT MESSAGES                                        *
***********************************************************************
NOCTLG   DC    CL49'ABORTED: NO CATALOG ENTRY FOUND, NO VOLSER GIVEN '
MULTIVOL DC    CL49'ABORTED: CANT HANDLE MULTI-VOLUME FILES VIA CTLG '
BADVOL   DC    CL49'ABORTED: VOLSER GIVEN IS NOT IN ANY UCB          '
INVALUCB DC    CL49'ABORTED: INVALID UCB ADDRESS FROM SEARCH ROUTINE '
UCBOFLN  DC    CL49'ABORTED: UCB IS MARKED OFFLINE                   '
UCBNOTDA DC    CL49'ABORTED: DEVICE IS NOT DIRECT ACCESS             '
VTOCRDER DC    CL49'ABORTED: I/O ERROR READING VTOC OR FILE NOT FOUND'
VTOCWTER DC    CL49'ABORTED: I/O ERROR REWRITING FORMAT 1 DSCB       '
INVCTLHL DC    CL36'ABORTED: INVALID HI/LO INDICATOR IN CARD COL 1   '
GOODUPDT DC    CL32'SUCCESS: EXPIRATION DATE SET TO '
SETMESG  DC    CL6' '
         DC    CL11' '
SETLOW   DC    CL6'00/000'
SETHIGH  DC    CL6'99/365'
***********************************************************************
*        CONTROL CARD                                                 *
***********************************************************************
CTLCARD  DS    0CL80                   CONTROL CARD
CTLHL    DS    CL1                     H=SET EXPDT HI, L=SET IT LOW
CTLVOL   DS    CL6                     VOLSER. USE CTLG IF BLANK
CTLDSN   DS    CL44                    DATASET NAME
         DS    CL21                    FILLER
CTLNBR   DS    CL8                     OPTIONAL CARD NUMBER
***********************************************************************
*        DYNAMIC ALLOCATE PARMS                                       *
***********************************************************************
DAPARM1  DS    0F                      * 1ST PARAMETER FOR DYNALLOC *
DDN      DC    CL8'INPUT9'             DDNAME
         DC    CL44'&&MODSCB'          DATASET NAME
         DC    XL8'00'                 MEMBER NAME
DISPOS   DC    XL3'00'                 DISPOSITION:
*                                    BYTE1: 0=OLD,M=MOD,N=NEW,S=SHR
*                                    BYTE2: U=UNCATLG,C=CATLG,D=DELETE
*                                    K=KEEP; BYTE3: SAME AS BYTE2
UNNAME   DC    CL8'DISK'               UNIT NAME
         DC    XL2'00'                 UNIT COUNT
VOLUSRL  DC    XL6'00'                 VOLUME SERIAL NUMBER
         DC    XL1'00'                 LABEL TYPE:
*                                      N=NL, S=SL, B=BLP
         DC    XL2'00'                 DATASET SEQUENCE NBR (TAPE)
         DC    XL1'00'                 F: FREE=CLOSE
         DC    XL2'00'                 RETENTION PERIOD IN DAYS
         DC    XL3'00'                 RECORDING FORMAT:
*                                      BYTE1: F=FIXED, V=VARIABLE,
*                                      U=UNDEFINED; BYTE2: B=BLOCKED,
*                                      BYTE3: A=ASA CONTROL CHARACTERS
         DC    XL2'00'                 LOGICAL RECORD LENGTH
         DC    XL2'00'                 BLOCK SIZE
         DC    XL1'00'                 TAPE DENSITY: 1=1600,6=6250
         DC    XL2'00'                 BUFNO
         DC    XL2'00'                 KEY LENGTH
         DC    XL1'00'                 DATASET ORGANIZATION:
*                                      V=VSAM, P=PDS, D=DIRECT, S=SEQ
SPCTYPE  DC    CL1'T'                  SPACE TYPE:
*                                      C=CYL, T=TRK
SPCSIZE  DC    XL2'01'                 PRIMARY SPACE AMOUNT
         DC    XL2'00'                 SECONDARY SPACE AMOUNT
         DC    CL1'00'                 R=RLSE
         DC    XL2'00'                 NBR DIRECTORY BLOCKS
         DC    XL5'00'                 EXPIRATION DATE YYDDD
*
*
*
DAPARM2  DS    0F                      2ND PARAMETER FOR DYNALLOC
         DC    CL1'A'                  REQUEST CODE:
*                                      A=ALLOCATE A DATASET
*                                      V=VOLSER FEEDBACK
RETRNCD  DC    CL2' '                  DYNALLOC RETURN CODE
ERRRET   DC    CL4' '                  ERROR RETURN CODE
INFORET  DC    CL4' '                  INFO RETURN CODE
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
CMDCHN   DS    0D                      COMMAND CHAIN TO READ/WRITE
         CCW   X'1A',VTSRCH,X'70',5 READ HA TO POSITION TO INDEX PT
         CCW   X'A9',DSNAME,X'40',44 SEARCH DSCB'S FOR THAT NAMED
         CCW   8,*-8,0,0               TIC
RWCCW    CCW   6,DS1FMTID,0,96         READ FORMAT 1 DSCB
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,                      X
               RECFM=FBA,LRECL=133,BLKSIZE=133
VTOC     DCB   DDNAME=MODSCB,MACRF=E
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
         DSCBDEF  DSECT=NO,FMT2=NO,FMT3=NO,FMT4=NO,FMT5=NO,FMT6=NO
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
         PRINT NOGEN
         CVT   DSECT=YES               GENERATE CVT DSECT
         IEFUCBOB ,                    GENERATE UCB DSECT
MODSCB2  CSECT
*********************************************************************
*                REGISTER EQUATES                                   *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         PRINT NOGEN
         TITLE 'PRINT LOAD LIBRARY DIRECTORY WITH LINKEDIT DATES'
LMODHIST SVLNK R3
*
*******************************************************************
* THIS PROGRAM READS A CONTROL FILE TO OBTAIN THE MEMBER NAMES.   *
* FOR EACH MEMBER NAME SPECIFIED, A FIND MACRO IS ISSUED TO       *
* POSITION TO THE TEXT FOR THE MEMBER, AND THE MEMBER IS READ     *
* UNTIL A TYPE 2 IDR RECORD IS FOUND. THE LINK-EDIT DATE IS TAKEN *
* FROM THE IDR RECORD AND PRINTED WITH THE MEMBER NAME. IF NO TYPE*
* 2 IDR RECORD IS FOUND, THE MEMBER NAME IS NOT PRINTED.          *
* SEE PDSHIST FOR A FULL LIBRARY VERSION.                         *
*******************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,PDSMEM,,SYSIN) OPEN FILES
         GETMAIN R,LV=32768           PDS BLOCK AREA
         ST    R1,PDSAREA             SAVE THE ADDRESS
*
*******************************************************************
* MAINLINE ROUTINE: READ THE PDS DIRECTORY. FOR EACH DIRECTORY    *
* ENTRY, READ THE MEMBER TO LOCATE THE TYPE 2 ESD ENTRY WHICH     *
* CONTAINS THE LINK-EDIT DATE. WHEN FOUND, PRINT THE MEMBER NAME  *
* AND LINK-EDIT DATE.                                             *
*******************************************************************
*
NEWMEM   GET   SYSIN,MEMRECD           READ DIRECTORY RECORD
         BAL   R9,FINDMEM              LOCATE MEMBER
         CLI   MEMBER,0                MEMBER FOUND O.K.?
         BE    NEWMEM                  NO
NEXTREC  BAL   R9,READMEM              READ NEXT RECORD IN MEMBER
         CLI   MEMBER,0                IS IT EOF?
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         CLI   0(R12),X'80'            IS IT AN IDR RECORD?
         BNE   NEXTREC                 NO
         CLI   2(R12),X'02'            TYPE 2 IDR RECORD?
         BNE   NEXTREC                 NO
         MVC   PRTNAME,MEMBER          NAME TO PRINT
         MVC   PRTDATE,=X'402120202061202020' EDIT WORD
         ED    PRTDATE+1,14(R12)         EDIT LINKEDIT DATE
         MVC   PRTDATE(3),=C' 19'
         MVI   PRTDATE+9,C' '
         CLI   15(R12),X'64'
         BH    OLDCENT
         MVC   PRTDATE(3),=C' 20'
OLDCENT  DS    0H
         BAL   R9,PRINT                PRINT THE LINE
         B     NEWMEM                  GO GET ANOTHER DIRECTORY ENT
*
*******************************************************************
* PRINT SUBROUTINE                                                *
*******************************************************************
*
PRINT    PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
         MVI   PRTCC,C' '              SET SINGLE SPACE CONTROL
         MVC   PRT,PRTCC               CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
*
*******************************************************************
* ROUTINE TO LOCATE A PDS MEMBER BY NAME                          *
*******************************************************************
*
FINDMEM  FIND  PDSMEM,MEMBER,D         FIND MEMBER BY NAME
         LTR   R15,R15                 ANY ERROR
         BCR   8,R9                    NO, RETURN TO CALLER
         MVI   MEMBER,0                INDICATE NOT FOUND
         BR    R9                      GO BACK TO CALLER
*
*******************************************************************
* ROUTINE TO READ THE NEXT RECORD FROM THE MEMBER OF A PDS        *
*******************************************************************
*
READMEM  L     R12,PDSAREA             POINT TO INPUT BUFFER
         READ  INDECB,SF,PDSMEM,(12),'S'  READ NEXT BLOCK
         CHECK INDECB                  WAIT FOR COMPLETION
         BR    R9                      EXIT
MEMEOF   MVI   MEMBER,0                INDICATE END OF FILE
         BR    R9                      GO BACK TO CALLER
*
*******************************************************************
* END OF JOB - ALL DIRECTORY ENTRIES PROCESSED                    *
*******************************************************************
*
EOJ     CLOSE (SYSPRINT,,SYSIN,,PDSMEM) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*******************************************************************
* STORAGE AND FILE AREAS                                          *
*******************************************************************
*
PDSAREA  DS    A                       ADDRESS OF PDS BLOCK AREA
*
PRTLINE  DS    0CL133                  PRINT LINE
PRTCC    DC    C'1'                    CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT TEXT
PRTNAME  DS    CL8                     MEMBER NAME
         DC    CL4' '
PRTDATE  DC    CL9'  YY/DDD'           LINK-EDIT DATE
         DC    CL111' '
*
MEMRECD  DS    0CL80                   MEMBER NAME RECORD
MEMBER   DS    CL8                       MEMBER NAME
         DS    CL72
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,LRECL=81,BLKSIZE=81
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,RECFM=FB,                X
               LRECL=80,EODAD=EOJ
PDSMEM   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,EODAD=MEMEOF
         END

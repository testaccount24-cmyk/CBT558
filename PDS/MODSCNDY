        TITLE '***** MODSCNDY: SET SECONDARY SPACE ALLOC TO ZERO *****'
         PRINT NOGEN
MODSCNDY SVLNK R3,R4
*
**********************************************************************
*  1. A WORK DATASET IS ALLOCATED DYNAMICALLY, AND IS OPENED FOR USE *
*     BY THE EXCP ROUTINES. ABEND U0111 IF THIS FAILS.               *
*                                                                    *
*  2. THE TIOT IS SCANNED FOR A DDNAME OF "MODSCNDY". IF IT IS NOT   *
*     FOUND, OR DOES NOT POINT TO A DASD DEVICE, ABEND U0222.        *
*                                                                    *
*  3. COMPUTE THE VTOC START ADDRESS FROM THE UCB TTR. SET THE DEB   *
*     FOR THE EXCP WORK FILE TO POINT TO THE DEVICE CONTAINING THE   *
*     DATASET TO BE MODIFIED, AND SET DEB EXTENTS FOR THE VTOC.      *
*                                                                    *
*  4. SET UP EXCP TO READ THE FORMAT 1 DSCB AND READ IT. IF ANY ERROR*
*     OCCURS, ABEND WITH U0444.                                      *
*                                                                    *
*  5. SET FORMAT 1 EXPIRATION DATE AND REWRITE FORMAT 1 DSCB. IF ANY *
*     ERROR OCCURS ABEND WITH U0555.                                 *
*                                                                    *
*  6. END OF JOB. RESTORE EXCP WORK FILE AND TERMINATE NORMALLY.     *
*                                                                    *
*  AUTHOR R. THORNTON WRITTEN APR 2001 BASED ON PROGRAM MODSCB       *
**********************************************************************
*
***********************************************************************
* 1. ALLOCATE A DD STATEMENT FOR USE BY EXCP AND OPEN THE DCB USED    *
*    FOR EXCP. IF EITHER THE ALLOCATE OR OPEN FAILS, A U0333 ABEND    *
*    WILL BE PRODUCED WITH AN EXPLANATORY MESSAGE.                    *
***********************************************************************
ALLOCDYN CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE A TEMPORARY DATASET
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    SETDDNM                 NO
ALLOCERR WTO   'ERROR: DYNAMIC ALLOC FAILED FOR WORK FILE',ROUTCDE=11
         ABEND 111,DUMP                TERMINATE WITH ERROR
SETDDNM  MVC   VTOC+40(8),DDN          MOVE GENERATED DDNAME
         OPEN  (VTOC,OUTPUT)           OPEN FILE FOR EXCP
         TM    VTOC+48,X'10'           WAS OPEN SUCCESSFUL?
         BO    SAVEUE                  YES
OPENERR  WTO   'ERROR: UNABLE TO OPEN WORK FILE',ROUTCDE=11
         ABEND 111,DUMP                TERMINATE WITH ERROR
SAVEUE   L     R12,VTOC+44             @ DEB
         MVC   SAVUCB,33(R12)          SAVE DEB UCB ADDRESS
         MVC   SAVEXT,38(R12)          SAVE EXTENTS
**********************************************************************
* 2. LOCATE THE TIOT DD ENTRY FOR DDNAME MODSCNDY. IF NOT FOUND,     *
*    OR IF NOT FOR A DASD DEVICE, ABEND U0111.                       *
**********************************************************************
FINDTIOT L     R1,16                   GET CVT ADDR
         L     R1,0(R1)                GET TCB POINTER ADDR
         L     R1,0(R1)                GET TCB ADDR
         L     R1,12(R1)               GET TIOT ADDR
         LA    R8,24(R1)               GET 1ST DD ENTRY ADDR
         ST    R8,THISDD               SAVE DD ENTRY ADDR
         B     CKTEND                  GO BEGIN TIOT SEARCH
SCHTIOT  L     R8,THISDD               GET TIOT ADDR ENTRY
STEPTIOT SR    R2,R2                   CLEAR WORK REG
         IC    R2,0(R8)                GET DD ENTRY LENGTH
         AR    R8,R2                   GET NEXT DD ENTRY ADDR
         ST    R8,THISDD               SAVE IT'S ADDRESS
CKTEND   CLC   0(4,R8),ZEROS           END OF TIOT
         BE    NODDFND                 YES, NO MODSCNDY DD STATEMENT
         CLC   4(8,R8),MODNAM          DDNAME BEGINS MODSCNDY+
         BNE   STEPTIOT                NO, CONTINUE SEARCH
         B     TESTUCB                 YES, GO CHECK FOR DASD
NODDFND  WTO   'MODSCNDY: MISSING DD STATEMENT FOR MODSCNDY',ROUTCDE=11
         ABEND 222,DUMP
TESTUCB  L     R10,16(R8)              POINT TO UCB
         CLI   2(R10),X'FF'            THIS A GOOD UCB ADDRESS?
         BNE   INVUCB                  NO, ABORT
         TM    3(R10),X'80'            DEVICE IS ONLINE?
         BZ    INVUCB                  NO, ABORT
         CLI   18(R10),X'20'           IS IT DIRECT ACCESS DEVICE?
         BE    VTOCSTRT                YES, CONTINUE
INVUCB   WTO   'MODSCNDY: INVALID UCB OR DEVICE TYPE.',ROUTCDE=11
         ABEND 222,DUMP                TERMINATE IN ERROR
***********************************************************************
* 3. COMPUTE THE VTOC START ADDRESS FROM THE UCB TTR. SET THE DEB FOR *
*    THE EXCP WORK FILE TO POINT TO THE DEVICE CONTAINING THE DATASET *
*    TO BE MODIFIED, AND SET DEB EXTENTS FOR THE VTOC.                *
***********************************************************************
VTOCSTRT SR    R14,R14                 CLEAR FOR DIVIDE
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R15,3,24(R10)           PICK UP VTOC START TRACK
         D     R14,=F'15'              COMPUTE CYL-HD
         STH   R15,VTOCCYL             SAVE VTOC CYLINDER NBR
         STH   R14,VTOCHD              SAVE VTOC HEAD NUMBER
DEBUCBEX L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         STCM  R10,7,33(R12)           CHANGE DEB UCB ADDRESS
         MVC   38(10,R12),ALLCYL       SET DEB START/END EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
***********************************************************************
* 4. SET UP EXCP TO READ THE FORMAT 1 DSCB AND READ IT. IF ANY ERROR  *
*    OCCURS, ABEND WITH U0444.                                        *
***********************************************************************
READFMT1 MVC   VTSRCH(4),VTOCCYL       START AT VTOC BEGIN CCHH ADDR
         MVI   VTREC,1                 START WITH RECORD 1
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R15,7,12(R8)            POINT TO TIOEJFCB
         MVC   DSNAME,16(R15)          MOVE DSNAME FOR SEARCH
         MVI   RWCCW,6                 SET CCW TO READ DATA
         EXCP  VTOCIOB                 LOCATE AND READ FORMAT 1 DSCB
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             NORMAL COMPLETION
         BE    REWFMT1                 YES, CONTINUE
         WTO   'MODSCNDY: ERROR READING FORMAT1 LABEL',ROUTCDE=11
         BAL   R9,CLOSEXCP             GO CLOSE THE EXCP FILE
         ABEND 444,DUMP                TERMINATE IN ERROR
***********************************************************************
* 5. SET FORMAT 1 EXPIRATION DATE AND REWRITE FORMAT 1 DSCB. IF ANY   *
*    ERROR OCCURS ABEND WITH U0555.                                   *
***********************************************************************
REWFMT1  XC    DS1SCAL,DS1SCAL         SET SECONDARY ALLOC = 0
         MVI   RWCCW,5                 CHANGE READ COMMAND TO WRITE
         EXCP  VTOCIOB                 LOCATE AND REWRITE DATA PORTION
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             I/O COMPLETED NORMALLY
         BE    SUCCESS                 YES
         WTO   'MODSCNDY: ERROR REWRITING FORMAT1 LABEL',ROUTCDE=11
         BAL   R9,CLOSEXCP             GO CLOSE THE EXCP FILE
         ABEND 555,DUMP                TERMINATE IN ERROR
SUCCESS  WTO   'MODSCNDY: DATASET NOW HAS 0 SECONDARY ALLOC',ROUTCDE=11
***********************************************************************
* 6. END OF JOB. RESTORE EXCP WORK FILE AND TERMINATE NORMALLY.       *
***********************************************************************
EOJ      BAL   R9,CLOSEXCP             GO CLOSE THE EXCP FILE
         L     R13,4(R13)              GET O/S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE O/S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO O/S
***********************************************************************
* SUBROUTINE TO CLOSE DOWN THE EXCP WORK FILE.                        *
***********************************************************************
CLOSEXCP L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         MVC   33(3,R12),SAVUCB        RESTORE DEB UCB ADDRESS
         MVC   38(10,R12),SAVEXT       RESTORE DEB EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
         CLOSE VTOC                    CLOSE EXCP FILE.
         BR    R9                      RETURN TO CALLER
*
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
*
SAVUCB   DS    XL3                     SAVED DEB UCB ADDRESS
SAVEXT   DS    XL10                    SAVED DEB EXTENT
THISDD   DS    F                       CURRENT TIOT DD ENTRY ADDR
MODNAM   DC    CL8'MODSCNDY'           DDNAME TO FIND
ZEROS    DC    XL4'00'                 ZEROES
ALLCYL   DS    0H                      DEB EXTENTS
VTOCCYL  DS    H                       VTOC CYLINDER
VTOCHD   DS    H                       VTOC HEAD
         DC    H'32767',H'32767'       END DEB EXTENT
DSNAME   DS    CL44 DATASET NAME
*
DAPARM1  DS    0F                      * 1ST PARAMETER FOR DYNALLOC *
DDN      DC    CL8'INPUT9'             DDNAME
         DC    CL44'&&MODSCNDY'        DATASET NAME
         DC    XL8'00'                 MEMBER NAME
DISPOS   DC    XL3'00'                 DISPOSITION:
*                                    BYTE1: 0=OLD,M=MOD,N=NEW,S=SHR
*                                    BYTE2: U=UNCATLG,C=CATLG,D=DELETE
*                                    K=KEEP; BYTE3: SAME AS BYTE2
UNNAME   DC    CL8'DISK'               UNIT NAME
         DC    XL2'00'                 UNIT COUNT
VOLUSRL  DC    XL6'00'                 VOLUME SERIAL NUMBER
         DC    XL1'00'                 LABEL TYPE:
*                                      N=NL, S=SL, B=BLP
         DC    XL2'00'                 DATASET SEQUENCE NBR (TAPE)
         DC    XL1'00'                 F: FREE=CLOSE
         DC    XL2'00'                 RETENTION PERIOD IN DAYS
         DC    XL3'00'                 RECORDING FORMAT:
*                                      BYTE1: F=FIXED, V=VARIABLE,
*                                      U=UNDEFINED; BYTE2: B=BLOCKED,
*                                      BYTE3: A=ASA CONTROL CHARACTERS
         DC    XL2'00'                 LOGICAL RECORD LENGTH
         DC    XL2'00'                 BLOCK SIZE
         DC    XL1'00'                 TAPE DENSITY: 1=1600,6=6250
         DC    XL2'00'                 BUFNO
         DC    XL2'00'                 KEY LENGTH
         DC    XL1'00'                 DATASET ORGANIZATION:
*                                      V=VSAM, P=PDS, D=DIRECT, S=SEQ
SPCTYPE  DC    CL1'T'                  SPACE TYPE:
*                                      C=CYL, T=TRK
SPCSIZE  DC    XL2'01'                 PRIMARY SPACE AMOUNT
         DC    XL2'00'                 SECONDARY SPACE AMOUNT
         DC    CL1'00'                 R=RLSE
         DC    XL2'00'                 NBR DIRECTORY BLOCKS
         DC    XL5'00'                 EXPIRATION DATE YYDDD
*
*
*
DAPARM2  DS    0F                      2ND PARAMETER FOR DYNALLOC
         DC    CL1'A'                  REQUEST CODE:
*                                      A=ALLOCATE A DATASET
*                                      V=VOLSER FEEDBACK
RETRNCD  DC    CL2' '                  DYNALLOC RETURN CODE
ERRRET   DC    CL4' '                  ERROR RETURN CODE
INFORET  DC    CL4' '                  INFO RETURN CODE
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
CMDCHN   DS    0D                      COMMAND CHAIN TO READ/WRITE
         CCW   X'1A',VTSRCH,X'70',5 READ HA TO POSITION TO INDEX PT
         CCW   X'A9',DSNAME,X'40',44 SEARCH DSCB'S FOR THAT NAMED
         CCW   8,*-8,0,0               TIC
RWCCW    CCW   6,DS1FMTID,0,96         READ FORMAT 1 DSCB
VTOC     DCB   DDNAME=MODSCNDY,MACRF=E
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
         DSCBDEF  DSECT=NO,FMT2=NO,FMT3=NO,FMT4=NO,FMT5=NO,FMT6=NO
         END

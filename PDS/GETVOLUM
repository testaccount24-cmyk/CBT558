*          DATA SET GETVOLUM   AT LEVEL 001 AS OF 07/11/84
         TITLE '*** GET VOLUME SERIAL FOR A DATASET ***'
GETVOLUM AMODE 31
GETVOLUM RMODE ANY
GETVOLUM SVLNK R3,,,SUB
**************************************************************
* THIS SUBROUTINE LOCATES THE CURRENT VOLUME SERIAL NUMBER   *
* FOR AN OPEN DATASET AND PASSES IT TO THE CALLER. THE       *
* CALLER MUST PASS THE ADDRESS OF AN OPEN DCB (FD IN COBOL)  *
* AND THE ADDRESS OF A 6-BYTE AREA TO RECEIVE THE VOLUME     *
* SERIAL NUMBER ON RETURN. IF THE DCB IS NOT OPEN, THE       *
* CALLER'S VOLUME SERIAL NUMBER FIELD WILL BE SET TO BLANKS. *
*                                                            *
* FOR THE SUBROUTINE TO WORK PROPERLY, THE DATASET MUST BE   *
* OPEN, THE DD STATEMENT MUST CONTAIN DCB=BUFNO=1, AND THE   *
* DATASET MUST RESIDE ON TAPE OR DISK.                       *
*                                                            *
* COBOL CALLING EXAMPLE:                                     *
*            .                                               *
*            .                                               *
*            .                                               *
*            FILE SECTION.                                   *
*            FD  TRAN-FILE                                   *
*                RECORDING MODE IS ...                       *
*            .                                               *
*            .                                               *
*            .                                               *
*            WORKING-STORAGE SECTION.                        *
*            .                                               *
*                05  VOLUME-SERIAL-NO   PIC X(6).            *
*            .                                               *
*            .                                               *
*            .                                               *
*            OPEN INPUT (OR OUTPUT) TRAN-FILE                *
*            .                                               *
*            .                                               *
*            .                                               *
*            CALL 'GETVOLUM' USING TRAN-FILE,                *
*                                  VOLUME-SERIAL-NO.         *
*                                                            *
*                                                            *
* AUTHOR: R. THORNTON 07/13/84  FOR PROJECT 98-00036         *
*********************************************************************
*                  MAINTENANCE CHANGES                              *
*********************************************************************
* MADE CHANGES TO SUPPORT AMODE=31,RMODE=ANY APR 99 RNT             *
*********************************************************************
*   SUBROUTINE GETVOLUM    PROJECT  965294     4/28/97
*   THIS SUBROUTINE HAS BEEN ANALYZED BY CGA AND DETERMINED TO CONTAIN
*   NO DATES AND TO BE Y2000 COMPLIANT.  THEREFORE, THE CDC CORE TEAM
*   HAS CONCLUDED NO CHANGES ARE NECESSARY.  IT WILL REMAIN UNDER
*   BCBS01/COMMON/COMMON ON ENDEVOR.
**********************************************************************
         L     R12,0(R1)               POINT TO DCB
         L     R11,4(R1)               POINT TO USER'S VOLSER FIELD
         MVC   0(6,R11),BLANKS         CLEAR IT TO BLANKS
         TM    48(R12),X'10'           IS FILE OPEN?
         BZ    RETURN                  NO, EXIT WITH ERROR
         L     R12,DCBDEBAD-IHADCB(R12) POINT TO THE DEB
         N     R12,MASKHI              CLEAR HIGH ORDER BYTE
         ICM   R12,7,DEBSUCBB-DEBBASIC(R12) THEN POINT TO THE UCB
         CLI   2(R12),X'FF'            VALID UCB?
         BNE   RETURN                  NO, EXIT WITH ERROR
         TM    18(R12),X'A0'           IS IT TAPE OR DISK?
         BZ    RETURN                  NO, EXIT WITH ERROR
         MVC   0(6,R11),28(R12)        MOVE VOLSER TO CALLER
*
*
*
RETURN   L     R13,4(R13)              POINT TO CALLER'S SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
         DS    0F
MASKHI   DC    X'00FFFFFF'             TO CLEAR HIGH ORDER BYTE
BLANKS   DC    CL6' '                  CONSTANT BLANKS
         DCBD  DSORG=PS
         IEZDEB LIST=YES
         END

         TITLE '*** TRACEDT: PROCESS A GTF TRACE FILE ***'
***********************************************************************
* READ AND PROCESS A GTF TRACE FILE.                                  *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*
TRACEDT SVLNK R3
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT,INPUT1) OPEN FILES
         BAS   R9,HEADING              PRINT FIRST HEADING
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
         USING INREC,R6
MAINLINE BAS   R9,GETIN                READ INPUT RECORD
         CLC   INTYPE,=X'FF01'         IS THIS AN SVC TRACE RECORD?
         BE    DOSVCS                  YES
         B     MAINLINE                NONE OF THESE, GO READ AGAIN
DOSVCS   BAS   R9,SVCRECS              GO PROCESS SVC TRACE RECORDS
         B     MAINLINE                GO READ AGAIN
***********************************************************************
*             PROCESS SVC TRACE RECORDS                               *
***********************************************************************
SVCRECS  ST    R9,SVC9                 SAVE RETURN ADDRESS
         MVC   DBLWD,INTOD             CONVERT TOD CLOCK
         CALL  TIMECONV,(DBLWD,DATETIME)  TO PRINTABLE VALUE
         MVC   PRTJUL(2),JULYR         MOVE
         MVI   PRTJUL+2,C'/'             JULIAN
         MVC   PRTJUL+3(3),JULDAY          DATE TO PRINT
         MVC   PRTTIME(2),HOUR         MOVE
         MVI   PRTTIME+2,C':'            TIME
         MVC   PRTTIME+3(2),MINUTE         OF
         MVI   PRTTIME+5,C':'                DAY
         MVC   PRTTIME+6(2),SECOND             TO PRINT
         ZAP   CHGCOUNT,CHGCOUNT       TOD REVERSAL ALREADY DETECTED?
         BNZ   MOVEJOB                 YES
         CLC   INTOD,LASTOD            DID TIME REVERSE?
         BL    REVERSED                YES
         MVC   LASTOD,INTOD            NO, SAVE LAST TOD VALUE
         B     MOVEJOB                 CONTINUE
REVERSED ZAP   CHGCOUNT,INPCOUNT       TOD REVERSED, SET COUNT
MOVEJOB  MVC   PRTJOB,INJOB            JOB NAME
         SR    R1,R1                   CONVERT
         IC    R1,INSVCNO                SVC
         CVD   R1,DBLWD                    NUMBER
         MVC   PRTSVCNO,=X'202020'           TO
         ED    PRTSVCNO,DBLWD+6                PRINTABLE
         UNPK  PRTADDR(9),INSVCADR(5)  CONVERT
         TR    PRTADDR,PRTCHAR-C'0'      SVC ADDRESS
         MVI   PRTADDR+8,C' '              TO PRINTABLE
         MVC   PRTMODN,INMODN          MODULE NAME
         UNPK  PRTREG15(9),INREG15(5)  CONVERT
         TR    PRTREG15,PRTCHAR-C'0'     REG 15
         MVI   PRTREG15+8,C' '             TO PRINTABLE
         UNPK  PRTREG0(9),INREG0(5)    CONVERT
         TR    PRTREG0,PRTCHAR-C'0'      REG 0
         MVI   PRTREG0+8,C' '              TO PRINTABLE
         UNPK  PRTREG1(9),INREG1(5)   CONVERT
         TR    PRTREG1,PRTCHAR-C'0'      REG 1
         MVI   PRTREG1+8,C' '              TO PRINTABLE
         CLI   INSVCNO,4              IS THIS SVC 4 (GETMAIN)?
         BE    SVC004                 YES
         CLI   INSVCNO,5              IS THIS SVC 5 (FREEMAIN)?
         BE    SVC005                 YES
         CLI   INSVCNO,8              IS THIS SVC 8 (LOAD)?
         BE    SVC008                 YES
         CLI   INSVCNO,9              IS THIS SVC 9 (DELETE)?
         BE    SVC009                 YES
         CLI   INSVCNO,10             IS THIS SVC 10 (GET/FREEMAIN)?
         BE    SVC010                 YES
         CLI   INSVCNO,120            IS THIS SVC 120 (GET/FREEMAIN)?
         BE    SVC120                 YES
         B     PRNTLIN                NONE OF THESE
SVC004   UNPK  PRTREST(9),INREST+1(5) PLIST BYTES +0 TO +3
         TR    PRTREST(8),PRTCHAR-C'0'
         MVI   PRTREST+8,C' '
         UNPK  PRTREST+9(3),INREST+5(2) PLIST BYTE +4
         TR    PRTREST+9(2),PRTCHAR-C'0'
         MVI   PRTREST+11,C' '
         UNPK  PRTREST+12(7),INREST+6(4) PLIST BYTES +5 TO +7
         TR    PRTREST+12(6),PRTCHAR-C'0'
         MVI   PRTREST+18,C' '
         UNPK  PRTREST+19(3),INREST+9(2) PLIST BYTE +8
         TR    PRTREST+19(2),PRTCHAR-C'0'
         MVI   PRTREST+21,C' '
         UNPK  PRTREST+22(3),INREST+10(2) PLIST BYTE +9
         TR    PRTREST+22(2),PRTCHAR-C'0'
         MVI   PRTREST+24,C' '
         B     PRNTLIN                GO PRINT THE LINE
SVC005   B     SVC004                 HANDLE SVC005 SAME AS SVC004
SVC008   MVC   PRTREST(8),INREST      BYTES 1-8
         B     PRNTLIN                GO PRINT THE LINE
SVC009   B     SVC008                 HANDLE SVC009 SAME AS SVC008
SVC010   B     PRNTLIN                HANDLE SVC010
SVC120   B     PRNTLIN                HANDLE SVC120
PRNTLIN  BAS   R9,PRINT               WRITE PRINT RECORD
         L     R9,SVC9                 GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN    GET   INPUT1                  READ NEXT RECORD
         LR    R6,R1                   LOAD DSECT POINTER REG
         AP    INPCOUNT,P1             INCREMENT COUNTER
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             PRINT SUBROUTINE                                        *
***********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         AP    LINECT,P1               INCREMENT LINE COUNTER
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         CP    LINECT,ENDPAGE          PAST END OF PAGE?
         BNHR  R9                      NO, RETURN
***********************************************************************
*             HEADING SUBROUTINE                                      *
***********************************************************************
HEADING  ST    R9,H9                   SAVE RETURN ADDRESS
         ZAP   LINECT,P0               RESET LINE COUNTER
         AP    PAGECT,P1               INCREMENT PAGE COUNTER
         MVC   PAGENBR,PAGEDIT         PAGE NUMBER
         ED    PAGENBR,PAGECT              TO HEADER LINE
         MVC   PRTLINE,HEADER1         PRINT HEADER
         BAS   R9,PRINT                    LINE 1
         MVC   PRTLINE,HEADER2         PRINT HEADER
         BAS   R9,PRINT                    LINE 2
         MVI   PCC,C'0'                DOUBLE SPACE NEXT DATA LINE
         L     R9,H9                   GET RETURN ADDRESS
         BR    R9                      RETURN
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      MVC   PRT(12),TOTLEDIT        EDIT MASK
         ED    PRT(12),INPCOUNT        EDIT INPUT COUNTER
         MVC   PRT+13(12),=CL12'RECORDS READ' IDENTIFY COUNT
         BAS   R9,PRINT                PRINT COUNT
         ZAP   CHGCOUNT,CHGCOUNT       DID TIME REVERSE?
         BZ    CLOSES                  NO
         MVC   PRT(12),TOTLEDIT        EDIT MASK
         ED    PRT(12),CHGCOUNT        EDIT TOD CHANGE COUNTER
         MVC   PRT+13(23),=CL23'RECORDS AT TOD REVERSAL'
         BAS   R9,PRINT                PRINT COUNT
CLOSES   CLOSE (PRINT1,,INPUT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
LASTOD   DC    D'0'                    LAST TIME OF DAY
SVC9     DS    A                       RETURN ADDRESS FOR SVCRECS
ZEROS    DC    9C'0'
CONVWORK DC    XL9'00'
PRTCHAR  DC    C'0123456789ABCDEF'
DATETIME DS    0CL11                   DATE/TIME RETURNED BY TIMECONV
JULYR    DS    CL2                       JULIAN YEAR
JULDAY   DS    CL3                       JULIAN DAY
HOUR     DS    CL2                       HOUR OF DAY
MINUTE   DS    CL2                       MINUTES PAST HOUR
SECOND   DS    CL2                       FULL SECONDS PAST MINUTE
FRACTION DS    CL2                       HUNDREDTHS OF SECONDS
*
EDLENG   DC    XL6'20206B202020'       EDIT WORD FOR LENGTH
*
H9       DS    A                       HEADING RETURN ADDRESS
RETCODE  DS    F                       RETURN CODE
H0       DC    H'0'                    HEX ZERO
MAXPARM  DC    AL2(L'PARMDATA)         MAX PARM DATA LENGTH
P0       DC    P'0'                    PACKED 0
P1       DC    P'1'                    PACKED 1
LINECT   DS    PL2                     LINE COUNTER
PAGECT   DC    PL3'0'                  PAGE COUNTER
ENDPAGE  DC    P'57'                   MAX LINES PER PAGE
INPCOUNT DC    PL5'0'                  INPUT RECORD COUNTER
CHGCOUNT DC    PL5'0'                  RECORD NUMBER AT TOD CHANGE
PAGEDIT  DC    XL7'4020206B202020'     EDIT MASK FOR PAGE NBR
TOTLEDIT DC    XL12'402020206B2020206B202120' EDIT MASK FOR TOTAL
PARMDATA DS    CL8                     PARM DATA FROM EXEC
BLANKS   DC    CL133' '                BLANKS
*
HEADER1  DS    0CL133                  HEADER LINE 1
         DC    CL1'1'                  CARRIAGE CONTROL
         DC    CL47' '
         DC    CL37'LISTING OF GTF SVC TRACE RECORDS'
         DC    CL37' '
         DC    CL4'PAGE'
PAGENBR  DS    CL7                     PAGE NUMBER
*
HEADER2  DS    0CL133                  HEADER LINE 2
         DC    CL1'0'                  CARRIAGE CONTROL
HDGJUL   DC    CL6'YY/DDD'
         DC    CL1' '
HDGTIME  DC    CL8'HH:MM:SS'
         DC    CL1' '
HDGJOB   DC    CL8'JOB'
         DC    CL1' '
HDGSVCNO DC    CL3'SVC'
         DC    CL1' '
HDGADDR  DC    CL8'SVC ADDR'
         DC    CL1' '
HDGMODN  DC    CL8'MODNAME'
         DC    CL1' '
HDGREG15 DC    CL8'REG 15'
         DC    CL1' '
HDGREG0  DC    CL8'REG 0'
         DC    CL1' '
HDGREG1  DC    CL8'REG 1'
         DC    CL132' '
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
PRTJUL   DS    CL6
         DS    CL1
PRTTIME  DS    CL8
         DS    CL1
PRTJOB   DS    CL8
         DS    CL1
PRTSVCNO DS    CL3
         DS    CL1
PRTADDR  DS    CL8
         DS    CL1
PRTMODN  DS    CL8
         DS    CL1
PRTREG15 DS    CL8
         DS    CL1
PRTREG0  DS    CL8
         DS    CL1
PRTREG1  DS    CL8
         DS    CL1
PRTREST  DS    0C
         DS    CL132
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=VB
         LTORG
*
INREC    DSECT ,                        INPUT RECORD
INRDW    DS    H                       INPUT FILE RDW
         DS    H
INTYPE   DS    XL2                     TYPE: FF01 IS SVC RECORD(?)
INTOD    DS    XL8                     TOD CLOCK VALUE
         DS    XL2
INASCB   DS    XL4                     ASCB ADDRESS
INCPU    DS    XL2                     CPU NUMBER
INJOB    DS    CL8                     JOB NAME
INPSW    DS    0XL8                    OLD PSW
         DS    XL3
INSVCNO  DS    XL1                     SVC NUMBER
INSVCADR DS    XL4                     ADDRESS+2 OF SVC INSTRUCTION
INTCB    DS    XL4                     TCB ADDRESS
INMODN   DS    CL8                     MODULE NAME
INREG15  DS    XL4                     REGISTER 15
INREG0   DS    XL4                     REGISTER 0
INREG1   DS    XL4                     REGISTER 1
INREST   DS    0X
TRACEDT CSECT
         END

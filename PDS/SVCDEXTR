     TITLE '*** SVCDEXTR: GET SVCDUMP DATA FROM R99SYSAU/R99SYSBU ***'
         PRINT NOGEN
SVCDEXTR SVLNK R3
***********************************************************************
* READS THE OUTPUT PRODUCED BY JOBS R99SYSAU AND R99SYSBU AFTER THEY  *
* ARE WRITTEN TO DISK FILES FROM SAR AS FOLLOWS:                      *
*    1) GET INTO SAR (EP) IN TSO AND SELECT THE REPORTS DATABASE.     *
*    2) ENTER PROGRAM NAME R99SYSAU AND GENERATION NUMBER *           *
*    3) ENTER THE LETTER J AT THE LEFT OF THE DESIRED RUN             *
*    4) ENTER * AFTER THE SYSOUT (OVER THE A)                         *
*    5) PF3 SEVERAL TIMES UNTIL BACK AT THE INITIAL SCREEN, THEN      *
*       REPEAT STEPS 1-4 FOR R99SYSBU                                 *
*    6) PF3 BACK TO ISPF MENU: A JOB SHOULD BE SUBMITTED TO WRITE THE *
*       COMBINED OUTPUT OF THE TWO JOBS. CHECK IT IN SDSF.            *
*    7) IF OUTPUT IS O.K., WRITE IT TO A DISK FILE USING THE OUT      *
*       COMMAND, E.G.: TSO OUT USER02K PRINT(P)                       *
*    8) STEP 7 WOULD CREATE DISK DATASET USER02.P.OUTLIST IF PRINT(P) *
*       IS SELECTED. THIS IS THE FILE READ IN BY THIS PROGRAM. IT     *
*       CONTAINS ALL THE PRINTOUT FROM THE JOBS SELECTED ABOVE.       *
* ONE RECORD IS WRITTEN FOR EACH ARCHIVED DUMP, INCLUDING THE SYSTEM  *
* ID (A OR B), DATE, TIME, ARCHIVE DATASET NAME, AND DUMP TITLE.      *
*                                                                     *
* AUTHOR R THORNTON JUN 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
*
         USING IECNTRY,R12
INIT1    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
* LOOK FOR IECTMS9 MESSAGES AND WRITE THEM TO OUTPUT. WHEN THE FIRST  *
* COPYDUMP RECORD IS FOUND, THERE ARE NO MORE IECTMS9'S               *
***********************************************************************
GET1     GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         CLC   INRECD+5(16),=CL16'START *** R99SYS' GOT START RECORD?
         BNE   CHEKTMS9                NO
         BAS   R9,INITTABL             YES, INITIALIZE TABLE
         MVC   OUTMNTH,INRECD+58       SAVE DATE
         MVC   OUTDAY,INRECD+61           IN OUTPUT
         MVC   OUTYEAR,INRECD+64             RECORD AREA
         MVC   OUTHR,INRECD+67         SAVE TIME IN
         MVC   OUTMIN,INRECD+70           OUTPUT RECORD AREA
CHEKTMS9 CLC   INRECD+20(7),IECTMS9    IS IT AN IECTMS9 RECORD?
         BNE   CKCOPYDP                NO
CHECKIEC LA    R1,INRECD+48            POINT TO DUMP NUMBER FIELD
         BAS   R9,CVRTDUMP             GO CONVERT TO TABLE POINTER
         LTR   R12,R12                 VALID POINTER?
         BZ    GET1                    NO
         MVC   IECDSN,INRECD+68        MOVE LAST 17 CHAR OF DSNAME
         B     GET1                    CONTINUE
CKCOPYDP CLC   INRECD+2(8),COPYDUMP    FOUND A COPYDUMP RECORD?
         BNE   CKTITL                  NO
         LA    R1,INRECD+31            POINT TO DUMP FIELD
         BAS   R9,CVRTDUMP             GO CONVERT TO TABLE POINTER
         ST    R12,IECADDR             SAVE FOR LATER
         B     GET1                    AND GO READ AGAIN
CKTITL   CLC   INRECD+10(6),TITLE      FOUND TITLE= RECORD?
         BNE   GET1                    NO
         L     R12,IECADDR             GET IECTMS9 TABLE ENTRY PTR
         LTR   R12,R12                 VALID POINTER?
         BZ    GET1                    NO, EXIT
         MVC   OUTDSN,IECDSN           IECTMS9 DSNAME TO OTUPUT
         MVC   OUTITLE,INRECD+16       TITLE TO OUTPUT
         PUT   OUTPUT1,OUTRECD         COPY RECORD TO OUTPUT FILE
         B     GET1                    GO READ NEXT RECORD
***********************************************************************
* USE DUMP NUMBER TO COMPUTE TABLE ENTRY POINTER                      *
***********************************************************************
CVRTDUMP SR    R12,R12                 CLEAR POINTER REG
         CLC   0(4,R1),=CL4'DUMP'      VALID DATA?
         BNE   GET1                    NO
         CLI   6(R1),C' '              VALID DATA?
         BE    CKDGT1                  YES
         CLC   6(R1),C')'              VALID DATA?
         BNER  R9                      NO
CKDGT1   CLI   4(R1),C'0'              VALID DECIMAL DIGIT?
         BLR   R9                      NO
         CLI   4(R1),C'9'              VALID DECIMAL DIGIT?
         BHR   R9                      NO
         CLI   5(R1),C' '              SINGLE DIGIT DUMP NUMBER?
         BE    SNGLIEC                 YES
         CLI   5(R1),C')'              SINGLE DIGIT DUMP NUMBER?
         BE    SNGLIEC                 YES
         CLI   5(R1),C'0'              VALID TWO DIGIT NUMBER?
         BLR   R9                      NO
         CLI   5(R1),C'9'              VALID TWO DIGIT NUMBER?
         BHR   R9                      NO
         CLI   4(R1),C'1'              DUMP NUMBER < 20?
         BHR   R9                      NO
         PACK  DBLWD,4(2,R1)           PACK 2-DIGIT NUMBER
         B     CONVIEC                 CONTINUE
SNGLIEC  PACK  DBLWD,4(1,R1)           PACK SINGLE DIGIT NUMBER
CONVIEC  CVB   R12,DBLWD               CONVERT NUMBER TO BINARY
         MH    R12,IECTLENG            COMPUTE ENTRY OFFSET
         LA    R12,IECTABLE(R12)       POINT TO TABLE ENTRY
         BR    R9                      EXIT
***********************************************************************
* CLEAR THE IECTMS9 MESSAGE DATA TABLE                                *
***********************************************************************
INITTABL MVI   SWITCH,0                SHOW TABLE INITIALIZED
         LA    R12,IECTABLE            POINT TO THE TABLE
         LA    R11,10                  NUMBER ENTRIES TO CLEAR
CLEARENT XC    0(IECNTND-IECNTRY,R12),0(R12) CLEAR AN ENTRY
         LA    R12,IECNTND-IECNTRY(R12)      STEP TO NEXT ENTRY
         BCT   R11,CLEARENT            LOOP TO CLEAR TABLE
         BR    R9                      RETURN
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
IECADDR  DS    F                       ADDRESS OF CURRENT TABLE ENTRY
IECTLENG DC    AL2(IECNTND-IECNTRY)    TABLE ENTRY LENGTH
COPYDUMP DC    CL8'COPYDUMP'
TITLE    DC    XL6'E389A393857E'       LOWER CASE TITLE=
IECTMS9  DC    CL7'IECTMS9'
SWITCH   DC    X'00'                   SWITCH=1 AFTER COPYDUMP FOUND
*
INRECD   DS    CL132                   INPUT RECORD
*
OUTRECD  DS    0CL152                  OUTPUT RECORD
OUTYEAR  DS    CL2                        YEAR
OUTMNTH  DS    CL2                        MONTH
OUTDAY   DS    CL2                        DAY
OUTHR    DS    CL2                        HOUR
OUTMIN   DS    CL2                        MINUTE
         DC    CL9'SYSTEM.SY'             DSNAME PREFIX
OUTDSN   DS    CL17                       IECTMS9 DSNAME
OUTITLE  DS    CL116                      TITLE
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=132
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=152
         LTORG
IECTABLE DS    20CL36               IECTMS9 MESSAGE TABLE
*
IECNTRY  DSECT ,                    DATA FROM IECTMS9 MESSAGE
IECDSN   DS    CL17                    DSNAME (LAST 17 CHARACTERS)
IECNTND  EQU   *                       END OF TABLE ENTRY
         END

         TITLE '*** DSNVOL: GET VOLUME SERIALS FOR A DSNAME *****'
DSNVOLS  CSECT
DSNVOLS  AMODE 24
DSNVOLS  RMODE 24
*********************************************************************
* USES THE CALLTSSR MACRO TO INVOKE IKJEHCIR TO RETRIEVE SYSTEM     *
* CATALOG INFORMATION CONTAINING UP TO 255 VOLUME SERIAL NUMBERS    *
* FOR THE DSNAME PASSED BY THE CALLER.                              *
* STANDARD LINKAGE CONVENTIONS ARE USED. THE CALLER PASSES TWO      *
* PARAMETERS:                                                       *
*  1) THE FULLY QUALIFIED DSNAME WHOSE VOLUME SERIALS ARE DESIRED.  *
*     THE DSNAME MUST BE LEFT JUSTIFIED WITH TRAILING SPACES IN A   *
*     44-CHARACTER FIELD.                                           *
*  2) A 1536-BYTE AREA WHICH WILL CONTAIN 0-255 6-BYTE VOLUME SERIAL*
*     NUMBERS ON RETURN. A FIELD OF 6 HEX FF'S FOLLOWS THE LAST     *
*     VOLUME SERIAL IN THE LIST. IF AN ERROR OCCURS AND VOLUME      *
*     SERIALS WERE NOT OBTAINED, THE FIRST ENTRY WILL CONTAIN 6 HEX *
*     FF'S, THE NEXT 4 BYTES WILL CONTAIN THE VALUE OF R15 ON RETURN*
*     FROM THE IKJEHCIR ROUTINE, AND THE NEXT BYTE WILL CONTAIN THE *
*     ONE-BYTE RETURN CODE FROM THE LOCATE MACRO FOLLOWED BY A 120- *
*     BYTE ERROR DESCRIPTION.                                       *
* PROCESSING:                                                       *
*  1) PLACE THE ADDRESS OF THE DSNAME PASSED BY THE CALLER IN THE   *
*     CIRSRCH FIELD OF THE PARAMETER LIST FOR THE IKJEHCIR ROUTINE. *
*  2) USE THE CALLTSSR MACRO TO INVOKE THE IKJEHCIR ROUTINE, WHICH  *
*     SEARCHES THE CATALOG AND RETURNS UP TO 255 VOLUME SERIALS.    *
*  3) IF R15 CONTAINS 0 ON RETURN, IKJEHCIR WAS SUCCESSFUL, AND THE *
*     CIRWORK AREA CONTAINS A LIST OF 1-255 VOLUME ENTRIES. THE LIST*
*     IS PROCESSED TO MOVE THE VOLUME SERIALS IT CONTAINS TO THE    *
*     CALLER'S RETURN AREA. A 6-BYTE FIELD OS HEX FF'S IS MOVED TO  *
*     FOLLOW THE LAST VOLUME SERIAL.                                *
*  4) IF R15 IS NON-ZERO ON RETURN FROM IKJEHCIR, AN ERROR OCCURRED *
*     AND NO VOLUME SERIALS WERE RETURNED. A SIX-BYTE FIELD OF HEX  *
*     FF'S IS MOVED TO THE BEGINNING OF THE CALLER'S RETURN AREA    *
*     FOLLOWED BY THE R15 VALUE RETURNED BY IKJEHCIR, FOLLOWED BY   *
*     THE 1-BYTE LOCATE RETURN CODE RETURNED BY IKJEHCIR, WHICH IS  *
*     FOLLOWED BY A 120 BYTE TEXT FIELD CONTAINING A SHORT ERROR    *
*     DESCRIPTION.                                                  *
*                                                                   *
* AUTHOR R THORNTON DEC 1998                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING DSNVOLS,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     PARMS                   BRANCH AROUND CONSTANTS
         DC    CL8'DSNVOLS'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
PARMS    L     R5,0(R1)                POINT TO CALLER'S DSNAME FIELD
         L     R6,4(R1)                POINT TO CALLER'S RETURN AREA
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE ST    R5,CIRSRCH              READ INPUT RECORD
         CALLTSSR EP=IKJEHCIR,MF=(E,CIRPARM) EXECUTE IKJEHCIR
         LTR   R15,R15                 ANY ERROR?
         BZ    MLVOLS                  NO
         ST    R15,SAVER15             YES, SAVE R15
         MVC   0(6,R6),HIVAL           HEX FF'S TO CALLER AREA +0
         MVC   6(4,R6),SAVER15         R15 FROM IKJEHCIR TO AREA+6
         MVC   10(1,R6),CIRLOCRC       LOCATE RETURN CODE TO AREA+10
         LA    R1,ERRORTBL             POINT TO 1ST ERROR TABLE ENTRY
MLRELOOK CLC   CIRLOCRC,4(R1)          FOUND THE ENTRY?
         BE    MOVETEXT                YES
         CLI   4(R1),X'FF'             LAST ENTRY?
         BNE   MLADVNC                 NO
         SR    R2,R2                   CLEAR FOR INSERT
         IC    R2,CIRLOCRC             PICK UP LOCATE RETURN CODE
         CVD   R2,DBLWD                CONVERT TO DECIMAL
         UNPK  ERRORFFC,DBLWD+5(3)     UNPACK TO MESSAGE
         OI    ERRORFFC+2,C'0'         AND CLEAR SIGN
         B     MOVETEXT                YES
MLADVNC  L     R1,0(R1)                NO, POINT TO NEXT ENTRY
         B     MLRELOOK                LOOP TO FIND ENTRY
MOVETEXT IC    R2,5(R1)                GET TEXT LENGTH
         MVC   11(120,R6),BLANKS       CLEAR TEXT AREA
         EX    R2,EXMOVE               MOVE TEXT TO CALLER'S AREA
         B     RETURN                  RETURN TO CALLER
EXMOVE   MVC   11(1,R6),6(R1)          EXECUTED MOVE
MLVOLS   LA    R12,CIRVOLNT            POINT TO FIRST VOLUME ENTRY
MLCKEND  CLI   0(R12),X'FF'            END OF LIST?
         BE    MLLASTNT                YES
         MVC   0(6,R6),CIRVOLSR-CIRVOLNT(R12) NO, MOVE VOLSER
         LA    R6,6(R6)                ADVANCE TO NEXT VOLSER FIELD
         LA    R12,12(R12)             ADVANCE TO NEXT IKJEHCIR ENTRY
         B     MLCKEND                 LOOP THRU THE VOLUME LIST
MLLASTNT MVC   0(6,R6),HIVAL           SET FINAL ENTRY
         B     RETURN                  GO RETURN TO CALLER
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
RETURN   L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                      DOUBLEWORD WORK AREA
*
RETCODE  DS    F                      RETURN CODE
SAVER15  DS    F                      SAVE AREA FOR R15 FROM IKJEHCIR
BLANKS   DC    CL120' '               BLANKS
HIVAL    DC    6X'FF'                 HEX FF'S
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DS    CL132
*
*
ERRORTBL DS    0F                   LOCATE ERROR DESCRIPTIONS
ERROR04  DC    A(ERROR08),AL1(04),AL1(L'ERROR04D-1)
ERROR04D DC    C'THE REQUIRED CATALOG DOES NOT EXIST OR IT CANNOT BE OPX
               ENED.'
ERROR08  DC    A(ERROR0C),AL1(08),AL1(L'ERROR08D-1)
ERROR08D DC    C'EITHER THE ENTRY WAS NOT FOUND IN THE CATALOG, THE USEX
               R IS NOT AUTHORIZED, OR A GDG ALIAS WAS FOUND.'
ERROR0C  DC    A(ERROR10),AL1(12),AL1(L'ERROR0CD-1)
ERROR0CD DC    C'EITHER AN INDEX OR GDG BASE WAS FOUND, AN ALIAS WAS FOX
               UND, OR AN INCORRECT LOW-LEVEL GDG NAME WAS FOUND.'
ERROR10  DC    A(ERROR14),AL1(16),AL1(L'ERROR10D-1)
ERROR10D DC    C'A DATASET EXISTS AT OTHER THAN THE LOWEST INDEX LEVEL X
               SPECIFIED.'
ERROR14  DC    A(ERROR18),AL1(20),AL1(L'ERROR14D-1)
ERROR14D DC    C'A SYNTAX ERROR EXISTS IN THE NAME.'
ERROR18  DC    A(ERROR1C),AL1(24),AL1(L'ERROR18D-1)
ERROR18D DC    C'EITHER A PERMANENT I/O ERROR OCCURRED, THERE WAS A NONX
               -ZERO ESTAE RETURN CODE WAS FOUND OR THERE WAS A PARAMETX
               ER LIST ERROR.'
ERROR1C  DC    A(ERRORFF),AL1(28),AL1(L'ERROR1CD-1)
ERROR1CD DC    C'THE RELATIVE ADDRESS SUPPLIED TO THE LOCATE ROUTINE ISX
                OUTSIDE OF THE SYSCTLG DATASET EXTENTS.'
ERRORFF  DC    A(ERRORFF),AL1(255),AL1(L'ERRORFFD+2)
ERRORFFD DC    C'SEE DESCRIPTION OF MESSAGE IDC3009I IN THE SYSTEM MESSX
               AGES MANUAL FOR THE LOCATE RETURN CODE '
ERRORFFC DC    CL3'  '
         LTORG
*********************************************************************
*      PARAMETERS FOR THE IKJEHCIR ROUTINE                          *
*********************************************************************
CIRPARM  DS    0F                     PARAMETER LIST FOR IKJEHCIR
CIROPT   DC    XL1'04'                   REQUEST FOR VOLUME INFO
         DC    XL2'00'                   RESERVED
CIRLOCRC DC    XL1'00'                   LOCATE RETURN CODE
CIRSRCH  DC    A(0)                      POINTER TO DSNAME
CIRVOL   DC    A(0)                      POINTER TO USERCAT DSNAME
CIRWA    DC    A(CIRWORK)                POINTER TO VOLUME WORK AREA
CIRSAVE  DC    A(CIRREGS)                POINTER TO REGISTER SAVE AREA
CIRPSWD  DC    A(0)                      POINTER TO CATALOG PASSWORD
CIRREGS  DS    16F                    REGISTER SAVE AREA FOR IKJEHCIR
CIRWORK  DS    0F                     VOLUME INFORMATION FROM IKJEHCIR
         DC    AL2(CIRWEND-CIRWORK)      LENGTH OF AREA
DATALIN  DC    AL2(0)                    LENGTH OF DATA RETURNED
CIRNUMVL DS    XL1                       NUMBER OF RESIDENT VOLUMES
CIRVOLNT DS    0CL12                     VOLUME ENTRY
CIRDEVTY DS    XL4                         DEVICE TYPE
CIRVOLSR DS    CL6                         VOLUME SERIAL
CIRFILSQ DS    XL2                         FILE SEQUENCE NUMBER
         DS    254CL12                   REMAINING 254 ENTRIES
CIRWEND  DC    X'FFFFFFFFFFFFFFFF'    INSURE END OF LIST FOUND
DSNVOLS  CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*********************************************************************
*  CVT DESCRIPTION (REQUIRED BY THE CALLTSSR MACRO)                 *
*********************************************************************
         CVT   DSECT=YES
         END

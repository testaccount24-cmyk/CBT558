         TITLE '***** CATALOGD: CHECK FOR CATALOGED DATASETS ***'
***********************************************************************
* READS A FILE OF RECORDS CONTAINING DSNAME IN COL 1-44 AND VOLSER IN *
* COL 46-51. ISSUES A LOCATE TO READ THE CATALOG FOR THE DSNAME. IF   *
* NOT FOUND, OR IF FOUND, BUT CATALOG HAS A DIFFERING VOLSER, THE     *
* RECORD IS PRINTED. IF A CATALOG RECORD IS FOUND FOR THE SAME VOLSER *
* THE RECORD IS WRITTEN TO OUTPUT1.                                   *
*                                                                     *
* AUTHOR R THORNTON MAR 99                                            *
***********************************************************************
         PRINT NOGEN
CATALOGD SVLNK R3,R4
OPENS    OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
*   MAINLINE ROUTINE.                                                 *
***********************************************************************
MAINLINE GET   INPUT1,INREC            READ NEXT DSNAME/VOLSER RECORD
         LOCATE BYDSN                  READ CATALOG FOR SPEC DSNAME
         LTR   R15,R15                 SUCCESSFUL CATALOG READ?
         BZ    GOTCAT                  YES
PRTREC   MVC   PRT,INREC               NO, MOVE TO PRINT
         BAL   R9,PRINT                PRINT THE RECORD
         B     MAINLINE                AND GO READ NEXT
GOTCAT   CLC   IVOLSER,VOLVOL          CATALOGED ON THIS VOLUME?
         BNE   PRTREC                  NO
         MVC   OUTREC,INREC            MOVE TO OUTPUT RECORD
         PUT   OUTPUT1,OUTREC          WRITE THE RECORD
         B     MAINLINE                TO NEXT INPUT RECORD
***********************************************************************
*           END OF JOB ROUTINE                                        *
***********************************************************************
EOJ      CLOSE (PRINT1,,INPUT1,,OUTPUT1) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        PRINT A LINE                                                 *
***********************************************************************
PRINT    PUT   PRINT1,PRT              WRITE PRINT LINE
         MVC   PRT,BLANX               CLEAR PRINT LINE
         BR    R9                      EXIT
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
BYDSN    CAMLST NAME,IDSNAME,,VOLAREA READ CATALOG LIST
VOLAREA  DS    0D                      VOLUME LIST RETURNED BY LOCATE
VOLCNT   DS    H                       NUMBER OF VOLUMES RETURNED
VOLNTRY  DS    0CL12                   VOLUME ENTRY (1 OF 20 MAX)
VOLDEV   DS    XL4                       DEVICE TYPE
VOLVOL   DS    CL6                       VOLUME SERIAL NUMBER
         DS    H
         DS    19CL(L'VOLNTRY)         REMAINING 19 ENTRY FIELDS
         DS    CL80
PRT      DC    CL80' '                 PRINT AREA
BLANX    DC    CL80' '                 CONSTANT BLANKS
*
INREC    DS    0CL80                   INPUT RECORD
IDSNAME  DC    CL44' '                    DSNAME
         DC    CL1' '
IVOLSER  DC    CL6' '                     VOLUME SERIAL
         DC    CL1' '
IDSORG   DC    CL3' '                     DSORG
         DC    CL1' '
IRECFM   DC    CL3' '                     RECFM
         DC    CL1' '
ILRECL   DC    CL5' '                     LRECL
         DC    CL1' '
IBLKSIZE DC    CL5' '                     BLKSIZE
         DC    CL9' '
*
OUTREC   DS    0CL80                    OUTPUT RECORD
ODSNAME  DC    CL44' '                    DSNAME
         DC    CL1' '
OVOLSER  DC    CL6' '                     VOLUME SERIAL
         DC    CL1' '
ODSORG   DC    CL3' '                     DSORG
         DC    CL1' '
ORECFM   DC    CL3' '                     RECFM
         DC    CL1' '
OLRECL   DC    CL5' '                     LRECL
         DC    CL1' '
OBLKSIZE DC    CL5' '                     BLKSIZE
         DC    CL9' '
***********************************************************************
*        PRINT AND CONTROL FILES                                      *
***********************************************************************
PRINT1 DCB     DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         END

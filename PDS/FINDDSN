         PRINT NOGEN
FINDDSN  SVLNK R3
*
***********************************************************************
*  FINDS SELECTED DATASET NAMES IN PROC LIBRARIES. USER ENTERS UP TO
*  100 DATASET NAMES, ONE PER RECORD, IN THE SYSIN DATASET. THE PROC
*  LIBRARY IS SCANNED FOR DD CARDS HAVING DSN= PARAMETERS. WHEN
*  FOUND, THE JCL DATASET NAME IS USED TO SEARCH THE TABLE OF NAMES
*  ENTERED BY THE USER. IF A MATCH IS FOUND, AN OUTPUT RECORD IS
*  WRITTEN SHOWING THE MEMBER NAME AND RECORD IN THE MEMBER CONTAINING
*  THE REFERENCE. REQUIRED DD CARDS FOR THE RUN ARE:
*                 INPUT1:   THE PROC LIBRARY TO BE SEARCHED
*                           RECFM=FB,LRECL=80 IS HARD-CODED.
*                 SYSIN:    THE TABLE OF DATASET NAMES
*                           RECFM=FB,LRECL=80 IS HARD-CODED.
*                 SYSPRINT: OUTPUT DATA
*                           RECFM=FB,LRECL=133 IS HARD-CODED.
*
*  AUTHOR R THORNTON JAN 1982
*
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN FILES
         LA    R12,DSNTBL              POINT TO DATASET NAME TABLE
GETUSER  GET   SYSIN,RECORD            READ IN USER DATA
         MVC   0(44,R12),RECORD        DATASET NAME TO TABLE
         LA    R12,44(R12)             TO NEXT ENTRY
         MVI   0(R12),X'FF'            TABLE END
         B     GETUSER                 GO READ AGAIN
ENDIN    CALL  READDIR,OPEN            OPEN DIRECTORY FOR INPUT
         CALL  READMEM,OPEN            OPEN MEMBERS FOR INPUT
*
*
*
NEXDIR   CALL  READDIR,(READ,DIRCTY)   READ DIRECTORY RECORD
         CLI   DIRCTY,X'FF'            END OF DIRECTORY
         BE    EOJ                     YES, ALL DONE
         MVC   FINDTTR,MEMTTR          TTR TO FIND PARAMETER
         CALL  READMEM,FIND            GO FIND MEMBER
NEXREC   CALL  READMEM,(READ,RECORD)   READ NEXT RECORD
         CLI   RECORD,X'FF'            END OF MEMBER?
         BE    NEXDIR                  YES, GO READ DIRECTORY
         CLC   RECORD(2),=C'//'        COL 1-2 = //
         BNE   NEXREC                  NO, NOT JCL
         CLI   RECORD+2,C'*'           IS IT A COMMENT?
         BE    NEXREC                  YES
         LA    R1,RECORD+2             POINT TO COL 3
         LA    R2,9                    NUMBER BYTES TO SEARCH
FINDBLNK CLI   0(R1),C' '              BLANK FOUND?
         BE    FNDNONBK                YES
         LA    R1,1(R1)                NO, STEP OVER 1 BYTE
         BCT   R2,FINDBLNK             NO, CONTINUE SEARCH
         B     NEXREC                  NO BLANK IN 3-12, SKIP RECORD
FNDNONBK LA    R2,RECORD+71            POINT TO END OF RECORD
         SR    R2,R1                   NBR BYTES LEFT IN RECORD
CKSOLID  CLI   0(R1),C' '              FOUND SOLID CHARACTER?
         BNE   CHKOPER                 YES
         LA    R1,1(R1)                NO, STEP TO NEXT BYTE
         BCT   R2,CKSOLID              AND CONTINUE SEARCH
         B     NEXREC                  NOT FOUND, SKIP RECORD
CHKOPER  CLC   0(3,R1),=C'DD '         DD CARD?
         BNE   NEXREC                  NO, SKIP THE RECORD
DDCARD   LA    R1,3(R1)                STEP PAST DD
DDBLNK   CLI   0(R1),C' '              FOUND NON-BLANK?
         BNE   CKDSNEQ                 YES, CHECK FOR DSN=
         LA    R1,1(R1)                NO, STEP TO NEXT BYTE
         BCT   R2,DDBLNK               CONTINUE
         B     NEXREC                  NOT FOUND, SKIP RECORD
CKDSNEQ  CLC   0(4,R1),=C'DSN='        DSN=?
         BNE   CKDSNAME                NO
         LA    R1,4(R1)                STEP PAST THE DSN=
         B     DSNSTRT                 CONTINUE
CKDSNAME CLC   0(7,R1),=C'DSNAME='     DSNAME=?
         BNE   NEXREC                  NO, SO SKIP THE RECORD
         LA    R1,7(R1)                STEP PAST THE DSNAME=
DSNSTRT  MVC   DSNNAME,BLANX           CLEAR DATASET FIELD
         LA    R12,DSNNAME             POINT TO DATASET NAME FIELD
         LA    R2,45                   MAX DSN SIZE+1
CKDSNEND CLI   0(R1),C' '              END OF NAME?
         BE    GOTDSN                  YES
         CLI   0(R1),C','              END OF NAME?
         BE    GOTDSN                  YES
         MVC   0(1,R12),0(R1)          MOVE NAME BYTE
         LA    R1,1(R1)                TO NEXT RECORD BYTE
         LA    R12,1(R12)              TO NEXT NAME BYTE
         BCT   R2,CKDSNEND             LOOP TO MOVE DSN NAME
         B     NEXREC                  TOO LONG, SKIP TO NEXT REC
GOTDSN   LA    R12,DSNTBL              POINT TO DATASET NAME TABLE
CHKDSN   CLC   DSNNAME,0(R12)          FOUND THE DATASET NAME?
         BE    WRTUSER                 YES, TELL USER
         LA    R12,44(R12)             NO, STEP TO NEXT ENTRY
         CLI   0(R12),X'FF'            END OF TABLE?
         BE    NEXREC                  YES, GO TO NEXT RECORD
         B     CHKDSN                  ELSE CONTINUE SEARCH
WRTUSER  MVC   OUTREC,BLANX            CLEAR OUTPUT
         MVC   OUTREC(8),MEMNAME       SHOW MEMBER NAME
         MVI   OUTREC+8,C':'           DELIMIT
         MVC   OUTREC+10(80),RECORD    RECORD TO OUTPUT
         PUT   SYSPRINT,OUTREC         WRITE TO USER
         B     NEXREC                  GO TO NEXT RECORD
*
*
*
EOJ      CALL  READDIR,CLOSE           CLOSE DIRECTORY FILE
         CALL  READMEM,CLOSE           CLOSE MEMBER FILE
         CLOSE (SYSIN,,SYSPRINT)       CLOSE PRINT AND INPUT FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDR
         LM    R14,R12,12(R13)         RESTORE HIS REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER
READ     DC    C'R'                    READ PARAMETER
CLOSE    DC    C'C'                    CLOSE PARAMETER
*
*
*
DIRCTY   DS    0CL11                   READ DIRECTORY PARAMETER
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF MEMBER
*
*
*
FIND     DS    0CL5                    FIND PARAMETER
         DC    C'FT'                   FIND BY TTR
FINDTTR  DS    XL3                     TTR TO FIND
*
*
*
DSNNAME  DC    CL44' '                 DSN= FROM DD CARD
BLANX    DC    CL133' '                BLANKS
RECORD   DS    CL80                    INPUT RECORD AREA
OUTREC   DS    CL133                   OUTPUT RECORD AREA
*
*
*
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80,EODAD=ENDIN
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=133
         LTORG
DSNTBL   DS    100CL44                 DATASET NAMES FOR SEARCH
         END

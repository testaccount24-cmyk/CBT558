         PRINT NOGEN
PDSDIRX  SVLNK R3
********************************************************************
* READS THE DIRECTORY OF THE PDS GIVEN BY THE INPUT1 DD CARD AND   *
* WRITES AN OUTPUT FILE CONTAINING 80-BYTE RECORDS. EACH RECORD    *
* CONTAINS 8 MEMBER NAMES (EXCEPT THE LAST, OF COURSE).            *
*                                                                  *
* AUTHOR R THORNTON 04/19/2002                                     *
********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT)        OPEN OUTPUT FILE
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
         LA    R12,OUTREC              POINT TO FIRST BYTE OF RECORD
RDIRCTY  CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF DIRECTORY
         BH    EOJ                     YES, END OF JOB
         C     R12,FULL                CURRENT RECORD IS FULL?
         BL    MOVENAME                NO
         PUT   OUTPUT1,OUTREC          YES, WRITE OUTPUT RECORD
         MVC   OUTREC,BLANKS           CLEAR IT TO BLANKS
         LA    R12,OUTREC              POINT TO FIRST BYTE OF RECORD
MOVENAME MVC   0(8,R12),MEMNAME        MOVE MEMBER NAME TO RECORD
         LA    R12,9(R12)              STEP TO NEXT LOCATION
         B     RDIRCTY                 CONTINUE
EOJ      PUT   OUTPUT1,OUTREC          WRITE LAST OUTPUT RECORD
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*                      *** STORAGE AND FILES ***                      *
***********************************************************************
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
*
FULL     DC    A(OUTREC+66)            PAST THIS, RECORD IS FULL
BLANKS   DC    CL80' '                 BLANKS
*
OUTREC   DS    0CL80                   OUTPUT RECORD
OUTDATA  DC    CL72' '                   MEMBER NAMES COL 1-72
         DC    CL8' '                    BLANKS COL 73-80
EOF      EQU   C'Z'
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         END

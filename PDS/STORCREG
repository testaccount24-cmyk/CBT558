         TITLE '***** STORCREG: STORE CONTROL REGISTERS *****'
         PRINT NOGEN
STORCREG SVLNK R3,R4
*
**********************************************************************
*  DISPLAY THE CPU CONTROL REGISTERS                                 *
*                                                                    *
*  AUTHOR R. THORNTON OCT 1985                                       *
**********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT)       OPEN PRINTER FILE
         SMODE KEY=ZERO,STATE=S GET SUPVR STATE
         STCTL R0,R15,CTLREGS          STORE THE CONTROL REGS
         SMODE KEY=NZERO,STATE=P       RETURN TO PROG STATE
         LA    R12,CTLREGS             POINT TO CONTROL REGISTERS
CRLUP    MVC   PRT(5),=CL5'CREG '      LITERAL TO PRINT
         UNPK  PRT+5(2),REGNO          REGISTER NBR TO PRINT
         OI    PRT+6,C'0'              CLEAR SIGN
         AP    REGNO,=P'1'             ADD 1 TO REGISTER NBR
         MVC   PRT+7(3),=C' = '        LITERAL TO PRINT
         UNPK  PRT+10(9),0(5,R12)      UNPACK CONTROL REG VALUE
         MVI   PRT+18,C' '             CLEAR TRASH BYTE AT END
         TR    PRT+10(8),TRTBLA-C'0'   MAKE PRINTABLE
         LA    R12,4(R12)              STEP TO NEXT CONTROL REGISTER
         PUT   SYSPRINT,PRT            DISPLAY VALUE
         CP    REGNO,=P'15'            WAS IT REG 15?
         BL    CRLUP                   NO, CONTINUE
         CLOSE SYSPRINT                YES, CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        COMMON DATA AREAS                                            *
*                                                                     *
***********************************************************************
*
REGNO    DC    PL2'0'                  REGISTER NUMBER
CTLREGS  DC    16F'0'                  CONTROL REGISTER SAVE AREA
TRTBLA   DC    C'0123456789ABCDEF'     TRANSLATE TABLE
PRT      DC    CL80' '
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         END

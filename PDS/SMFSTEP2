      TITLE '**** SUM CPU TIME BY PROGRAM FOR SMFSTEP2 OUTPUT ****'
         PRINT NOGEN
SMFSTEP2 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFSTEP2,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFSTEP2'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
*  READ OUTPUT FROM SMFSTEPT AFTER IT IS SORTED IN PROGRAM NAME      *
*  SEQUENCE. CPU TIME IS SUMMED FOR EACH PROGRAM AND ONE RECORD IS   *
*  WRITTEN FOR EACH PROGRAM NAME.                                    *
*                                                                    *
*  THE OUTPUT RECORD CONTAINS JOBNAME, TOTAL CPU TIME, NUMBER RUNS,  *
*  AND AVERAGE CPU TIME PER RUN.                                     *
*  AUTHOR R. THORNTON APR 1994                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
MAINLINE GET   INPUT1,INPREC           GET SMFSTEPT RECORD
         CLC   INPPGM,OUTPGM           SAME PROGRAM?
         BE    SAMEPGM                 YES
         CLC   OUTPGM,=CL8'        '   ANY PROGRAM IN PROGRESS?
         BE    NEWPGM                  NO
         ZAP   TOTRUNS,TOTRUNS         ANY RUNS?
         BZ    NEWPGM                  NO
         ZAP   TOTCPU,TOTCPU           ANY CPU TIME?
         BNP   NEWPGM                  NO
         MVC   OUTTCPU,EDITCPU         EDIT MASK
         ED    OUTTCPU,TOTCPU          TOTAL CPU TO OUTPUT
         MVC   OUTRUNS,EDITRUN         EDIT MASK
         ED    OUTRUNS,TOTRUNS         TOTAL RUNS TO OUTPUT
         DP    TOTCPU,TOTRUNS          COMPUTE AVERAGE CPU PER RUN
         MVC   OUTAVG,EDITAVG          EDIT MASK
         ED    OUTAVG,TOTCPU           AVERAGE CPU PER RUN TO OUTPUT
         TR    OUTREC,TRCHARS          LEAVE ONLY PRINTABLE CHARACTERS
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
NEWPGM   MVC   OUTPGM,INPPGM           PROGRAM NAME TO OUTPUT
         ZAP   TOTRUNS,=P'0'           ZERO TOTAL RUNS
         ZAP   TOTCPU,=P'0'            ZERO TOTAL CPU
SAMEPGM  AP    TOTRUNS,=P'1'           ADD 1 TO NUMBER RUNS
         ICM   R1,15,INPJBCPU          GET TOTAL CPU TIME
         CVD   R1,DBLWD                CONVERT TO PACKED DECIMAL
         AP    TOTCPU,DBLWD            ACCUMULATE TOTAL CPU
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         GET CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE = 0
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
DBLWD    DS    D                       WORK AREA
TOTRUNS  DC    PL3'0'                  TOTAL RUNS
TOTCPU   DC    PL12'0'                 TOTAL CPU TIME IN SEC/100
EDITCPU  DC    XL25'2020202020202020202020202020202020206B2020214B2020'
EDITRUN  DC    XL5'2020202021'
EDITAVG  DC    XL19'2020202020202020202020206B2020214B2020'
*                                                                    *
INPREC   DS    0CL80
INPSID   DS    CL4             SMF30SID: SYSTEM ID
INPJOBN  DS    CL8             SMF30JBN: JOB NAME
INPSTEP  DS    CL8             SMF30STM: STEP NAME
INPPGM   DS    CL8             SMF30PGM: PROGRAM NAME
INPSTDAT DS    PL4             SMF30RSD: START DATE = 0CYYDDDF
INPSTTIM DS    XL4             SMF30RST: START TIME = SEC/100
INPJBCPU DS    XL4             SMF30CPT: TASK CPU TIME = SEC/100
INPSRCPU DS    XL4             SMF30CPS: SRB CPU TIME = SEC/100
INPIOS   DS    XL4             SMF30TEP: TOTAL EXCPS
         DS    CL32
*
OUTREC   DS    0CL80
OUTPGM   DC    CL8' '          PROGRAM NAME
         DC    CL1' '
OUTTCPU  DC    CL25' '         TOTAL CPU TIME
         DC    CL1' '
OUTRUNS  DC    CL5' '          TOTAL RUNS
         DC    CL1' '
OUTAVG   DC    CL19' '         AVERAGE CPU PER RUN
         DC    CL20' '
*
TRCHARS  DC    XL16'40404040404040404040404040404040' 00-0F
         DC    XL16'40404040404040404040404040404040' 10-1F
         DC    XL16'40404040404040404040404040404040' 20-2F
         DC    XL16'40404040404040404040404040404040' 30-3F
         DC    XL16'404040404040404040404A4B4C4D4E4F' 40-4F
         DC    XL16'504040404040404040405A5B5C5D5E5F' 50-5F
         DC    XL16'606140404040404040406A6B6C6D6E6F' 60-6F
         DC    XL16'404040404040404040407A7B7C7D7E7F' 70-7F
         DC    XL16'40404040404040404040404040404040' 80-8F
         DC    XL16'40404040404040404040404040404040' 90-9F
         DC    XL16'40404040404040404040404040404040' A0-AF
         DC    XL16'40404040404040404040404040404040' B0-BF
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040' C0-CF
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040' D0-DF
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040' E0-EF
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040' F0-FF
         LTORG
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

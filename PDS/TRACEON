***********************************************************************
* PULLED FROM IBMLINK SRD AS TRACEON ON 5/6/93                        *
* THIS PROGRAM WILL TURN ON THE GETMAIN/FREEMAIN TRACE FOR            *
* MVS/ESA RELEASES 3.1.0 / 3.1.1 / 3.1.3 / 4.2.0 AND 4.2.2 ONLY.      *
* FOR MVS/ESA RELEASE 4.3.0 PLEASE USE THE 'DIAGXX' PARMLIB MEMBER.   *
*                                                                     *
*                                                                     *
*                                                                     *
* THIS PROGRAM NEEDS TO BE ALTERED TO FIT YOUR NEEDS, THEN            *
* ASSEMBLED AND LINKED INTO AN AUTHORIZED LIBRARY AS                  *
* AC=1. DO NOT USE RENT,REUS AS LINKEDIT ATTRIBUTES. THE PROGRAM      *
* NEEDS TO BE EXECUTED FROM THIS AUTHORIZED LIBRARY TO TURN ON        *
* THE TRACE.                                                          *
* AS DISTRIBUTED IT WILL TRACE GETMAINS AND FREEMAINS ISSUED IN       *
* ASID 1 FOR SUBPOOLS 227, 228, 231, AND 241.                         *
*                                                                     *
* THE TRACE AS SET BY THIS PROGRAM WILL NOT PERSIST ACCROSS IPLS.     *
* IF THE TRACE IS NEEDED ACROSS MULTIPLE IPLS, THIS PROGRAM CAN BE    *
* RUN AFTER EACH IPL OR SUPERZAP CAN BE USED TO ALTER THE NUCLEUS AS  *
* DECRIBED IN DOC APAR OY19890 OR MVS/ESA COMPONENT DIAGNOSIS AND     *
* LOGIC: VSM LY28-1809.                                               *
*                                                                     *
* THE GETMAIN/FREEMAIN TRACE CAN BE FILTERED BY ASID AND/OR SUBPOOL.  *
* THE DATA COLLECTED BY THE TRACE CAN ALSO BE MODIFIED.               *
* SEE INSTRUCTIONS FURTHER BELOW IN THE PROGRAM ON HOW TO MODIFY THE  *
* TRACE OPTIONS UNDER COMMENTS:                                       *
*             FILTER BY ASID                                          *
*             FILTER BY SUBPOOL                                       *
*             FILTER BY SUBPOOL RANGE                                 *
*             MODIFY TRACE DATA                                       *
*                                                                     *
*  NOTE, WHEN FILTERING BY ASID WITHOUT FILTERING BY SUBPOOL,         *
*  SOME GLOBAL ENTRIES WILL STILL BE TRACED.  THESE TRACED            *
*  REQUESTS ARE NOT ASSOCIATED WITH ANY PARTICULAR ASID, AND          *
*  THEREFORE ARE NOT FILTERED OUT.  ADDITIONAL FILTERING BY           *
*  SUBPOOL CAN FILTER THESE ENTRIES OUT.                              *
*                                                                     *
***********************************************************************
* ***NOTE*** GTF MUST BE STARTED TRACING USRP F65 RECORDS.            *
***********************************************************************
*                                                                     *
*  FORMAT OF THE TRACE RECORD PRODUCED BY GETMAIN/FREEMAIN TRACE      *
*                                                                     *
* WHEN THIS RECORD IS PROCESSED OUTSIDE THE IPCS ENVIRONMENT, IT      *
* IS PRECEDED BY A LENGTH X'10' FIELD CONTAINING INTERNAL GTF DATA.   *
*                                                                     *
*  HEX      HEX                                                       *
* OFFSET   LENGTH   CONTENTS OF THE FIELD                             *
*                                                                     *
*    0       4      ASCB ADDRESS (INSERTED BY GTF)                    *
*    4       8      JOBNAME (INSERTED BY GTF)                         *
*    C       4      'REGS'                                            *
*   10       2      X'0040' (LENGTH OF FIELD TO FOLLOW)               *
*   12      40      REGISTERS (13, 14, 15, 0-12)                      *
*   52       4      'RETA'                                            *
*   56       2      X'0004' (LENGTH OF FIELD TO FOLLOW)               *
*   58       4      CALLER'S RETURN ADDRESS                           *
*   5C       4      'RETC'                                            *
*   60       2      X'0001' (LENGTH OF FIELD TO FOLLOW)               *
*   62       1      VSM INTERNAL RETURN CODE                          *
*   63       4      ENTRY TYPE - IS EITHER:                           *
*                    'SVC' FOR SVC ENTRY                              *
*                    'GLBR' FOR GLOBAL BRANCH                         *
*                    'PC' FOR STORAGE REQUEST                         *
*                    'LCBR' FOR LOCAL BRANCH                          *
*   67       2      X'0000' (LENGTH DELIMETER)                        *
*   69       4      'GET ' OR 'FREE'                                  *
*   6D       2      X'0000' (LENGTH DELIMITER)                        *
*   6F       4      REQUEST TYPE - IS EITHER:                         *
*                    'LIST' FOR LIST REQUEST                          *
*                    'VAR' FOR VARIABLE LENGTH REQUEST                *
*                    'ELEM' FOR ELEMENT REQUEST                       *
*   73       2      X'0000' (LENGTH DELIMETER)                        *
*   75       4      'SVCN'                                            *
*   79       2      X'0001' (LENGTH OF FIELD TO FOLLOW)               *
*   7B       1      SVC NUMBER (X'78' FOR PC ENTRY)                   *
*   7C       4      'STGA'                                            *
*   80       2      X'0004' (LENGTH OF FIELD TO FOLLOW)               *
*   82       4      STORAGE ADDRESS (ADDRESS RETURNED FROM VSM ON     *
*                    GETMAIN, OR ADDRESS TO BE FREEMAINED)            *
*   86       4      'STGL'                                            *
*   8A       2      X'000C' (LENGTH OF FIELD TO FOLLOW)               *
*   8C       4      ACTUAL LENGTH OF THE GETMAIN OR FREEMAIN          *
*   90       4      MINIMUM LENGTH OR 0                               *
*   94       4      MAXIMUM LENGTH OR 0                               *
*   98       4      'SPKY'                                            *
*   9C       2      X'0003' (LENGTH OF FIELD TO FOLLOW)               *
*   9E       3      X'00SSK0', WHERE 'SS' IS SUBPOOL, 'K' IS KEY      *
*   A1       4      'PFLG'                                            *
*   A5       2      X'0001' (LENGTH OF FIELD TO FOLLOW)               *
*   A7       1      FLAGS FROM VSWKPFLG                               *
*   A8       4      'ASID'                                            *
*   AC       2      X'0002' (LENGTH OF FIELD TO FOLLOW)               *
*   AE       2      ASID NUMBER                                       *
*   B0       4      'TCB '                                            *
*   B4       2      X'0004' (LENGTH OF FIELD TO FOLLOW)               *
*   B6       4      TCB ADDRESS                                       *
*   BA       4      'TIME'                                            *
*   BE       2      X'0008' (LENGTH OF FIELD TO FOLLOW)               *
*   C0       8      TIMESTAMP (IN TOD CLOCK FORMAT)                   *
*                                                                     *
* NOTE:  SEE ALSO APAR OY19890 FOR LAYOUT AND FULL EXPLANATION OF     *
*        TRACE.                                                       *
***********************************************************************
*
TRACEON  CSECT
TRACEON  AMODE 31
         BALR 12,0
         USING *,12
         MODESET KEY=ZERO,MODE=SUP
         L   R10,X'10'                       GET CVT POINTER
         L   R10,X'230'(,R10)                GET GDA POINTER
***********************************************************************
* PROCESS ASIDS                                                       *
***********************************************************************
         L   R9,X'18C'(,R10)                 GET TRAC POINTER
         L   R8,TRACTRAS-TRAC(,R9)           ADDRESSABILITY TO TRAS
         L   R1,NUMASID                      GET NUMBER OF ASIDS
         ST  R1,TRASNUM-TRAS(,R8)            UPDATE TRAS
         LTR R1,R1                           SPECIFIC ASID(S)?
         BNZ PROCASID                        YES, PROCESS LIST
         XC  ASIDLIST(32),ASIDLIST           NO, TRACE ALL ASIDS
PROCASID MVC TRASLIST-TRAS(32,R8),ASIDLIST   MOVE ASID LIST TO TRAS
***********************************************************************
* PROCESS SUBPOOLS                                                    *
***********************************************************************
         L   R7,TRACTRSP-TRAC(,R9)           ADDRESSABILITY TO TRSP
         L   R1,NUMSP                        GET NUMBER OF SUBPOOLS
         ST  R1,TRSPNUM-TRSP(,R7)            UPDATE TRAS
         LTR R1,R1                           SPECIFIC SUBPOOL(S)?
         BNZ PROCSP                          YES, PROCESS LIST
         XC  SPLIST(16),SPLIST               NO, TRACE ALL SUBPOOLS
PROCSP   MVC TRSPLIST-TRSP(16,R7),SPLIST     MOVE SUBPOOL LIST TO TRSP
***********************************************************************
* PROCESS SUBPOOL RANGES                                              *
***********************************************************************
         L   R6,TRACTRSR-TRAC(,R9)           ADDRESSABILITY TO TRSR
         L   R1,NUMPAIR                      NUMBER OF SP PAIRS
         ST  R1,TRSRNUM-TRSR(,R6)            UPDATE TRSR
         LTR R1,R1                           RANGE OF SPOOLS SPECIFIED?
         BNZ PROCRANG                        YES, PROCESS RANGE
         XC  RANGLIST(32),RANGLIST           NO, CLEAR RANGELIST
PROCRANG MVC TRSRLIST-TRSR(32,R6),RANGLIST   MOVE LIST TO TRSR
***********************************************************************
* ACTIVATE TRACE                                                      *
***********************************************************************
         MVC TRACFLGS-TRAC(8,R9),TRFLGS
         OI  X'D8'(R10),GDAGFSTR             TURN ON TRACE
         OI  TRACCTL-TRAC(R9),X'80'          INDICATE TRACE ON
         BR  14
*
***********************************************************************
* FILTER BY ASID                                                      *
*                                                                     *
* TO FILTER BY ASID, SPECIFY THE NUMBER OF ASIDS TO BE TRACED IN      *
* NUMASID.  THEN MODIFY THE ASIDLIST TO INCLUDE THE ASID TO BE TRACED.*
* A MAXIMUM OF 16 SPECIFIC ASIDS CAN BE TRACED.  TO TRACE ALL ASIDS   *
* SPECIFY 0 IN NUMASID 0000 IN ASID1-16.                              *
*                                                                     *
* THE DEFAULT ASID TO BE TRACED IS MASTER (ASID1).                    *
***********************************************************************
NUMASID  DC F'1'                      NUMBER OF ASIDS TO BE TRACED
ASIDLIST DS 0F                        UP TO 16 ASIDS TO BE TRACED
ASID1    DC H'569'
ASID2    DC H'0000'
ASID3    DC H'0000'
ASID4    DC H'0000'
ASID5    DC H'0000'
ASID6    DC H'0000'
ASID7    DC H'0000'
ASID8    DC H'0000'
ASID9    DC H'0000'
ASID10   DC H'0000'
ASID11   DC H'0000'
ASID12   DC H'0000'
ASID13   DC H'0000'
ASID14   DC H'0000'
ASID15   DC H'0000'
ASID16   DC H'0000'
*
***********************************************************************
* FILTER BY SUBPOOL                                                   *
*                                                                     *
* TO FILTER BY SUBPOOL, SPECIFY THE NUMBER OF SUBPOOLS TO BE TRACED IN*
* NUMSP.  THEN MODIFY THE SPLIST TO INCLUDE THE SUBPOOLS TO BE TRACED.*
* A MAXIMUM OF 16 SPECIFIC SUBPOOL CAN BE TRACED.  TO TRACE ALL       *
* SUBPOOLS, SPECIFY 0 FOR NUMSP.                                      *
*                                                                     *
* THE DEFAULT SUBPOOLS TO BE TRACED ARE CSA SP 227 228 231 AND 241.   *
***********************************************************************
NUMSP    DC F'4'                      NUMBER OF SUBPOOLS TO BE TRACED
SPLIST   DS 0F                        UP TO 16 SUBPOOLS TO BE TRACED
SP1      DC AL1(227)                  DECIMAL NUMBER 0-255
SP2      DC AL1(228)
SP3      DC AL1(231)
SP4      DC AL1(241)
SP5      DC AL1(00)
SP6      DC AL1(00)
SP7      DC AL1(00)
SP8      DC AL1(00)
SP9      DC AL1(00)
SP10     DC AL1(00)
SP11     DC AL1(00)
SP12     DC AL1(00)
SP13     DC AL1(00)
SP14     DC AL1(00)
SP15     DC AL1(00)
SP16     DC AL1(00)
*
***********************************************************************
* FILTER BY SUBPOOL RANGE                                             *
*                                                                     *
* TO FILTER A RANGE OF SUBPOOLS, SPECIFY THE NUMBER OF SUBPOOL RANGES *
* TO BE TRACED IN NUMPAIR.  THIS WILL ALLOW MORE THAN 16 SPECIFIC     *
* SUBPOOLS TO BE TRACED. THEN MODIFY THE RANGE LIST TO INCLUDE THE    *
* THE BEGINNING AND ENDING SUBPOOL IDS IN EACH RANGE.  UP TO 16       *
* RANGES CAN BE TRACED.  FILTERING BY SUBPOOL AND SUBPOOL RANGE ARE   *
* MUTUALLY EXCLUSIVE.                                                 *
*                                                                     *
* THE DEFAULT IS NO RANGE.                                            *
***********************************************************************
NUMPAIR  DC F'0'                      NUMBER OF SUBPOOLS RANGES
RANGLIST DS 0F                        UP TO 16 SUBPOOLS RANGES
SPB1     DC AL1(00)                   DECIMAL NUMBER 0-255 OF THE
SPE1     DC AL1(00)                   BEGINNING AND ENDING SPID
SPB2     DC AL1(00)
SPE2     DC AL1(00)
SPB3     DC AL1(00)
SPE3     DC AL1(00)
SPB4     DC AL1(00)
SPE4     DC AL1(00)
SPB5     DC AL1(00)
SPE5     DC AL1(00)
SPB6     DC AL1(00)
SPE6     DC AL1(00)
SPB7     DC AL1(00)
SPE7     DC AL1(00)
SPB8     DC AL1(00)
SPE8     DC AL1(00)
SPB9     DC AL1(00)
SPE9     DC AL1(00)
SPB10    DC AL1(00)
SPE10    DC AL1(00)
SPB11    DC AL1(00)
SPE11    DC AL1(00)
SPB12    DC AL1(00)
SPE12    DC AL1(00)
SPB13    DC AL1(00)
SPE13    DC AL1(00)
SPB14    DC AL1(00)
SPE14    DC AL1(00)
SPB15    DC AL1(00)
SPE15    DC AL1(00)
SPB16    DC AL1(00)
SPE16    DC AL1(00)
*
***********************************************************************
* MODIFY TRACE DATA                                                   *
*                                                                     *
* THE FOLLOWING BIT MAP INDICATES THE DATA TO BE TRACE FOR EACH       *
* GETMAIN/FREEMAIN ENTRY:                                             *
* 1... .... ....   REGISTERS ON ENTRY TO VSM                          *
* .1.. .... ....   CALLER'S RETURN ADDRESS                            *
* ..1. .... ....   INTERNAL VMS CODE                                  *
* ...1 .... ....   TYPE OF REQUEST                                    *
* .... 1... ....   SVC NUMBER ASSOCIATED WITH REQUEST                 *
* .... .1.. ....   ADDRESS OF STORAGE BEING OBTAINED OR FREED         *
* .... ..1. ....   LENGTH OF AREA                                     *
* .... ...1 ....   SUBPOOL AND KEY                                    *
* .... .... 1...   REQUEST FLAGS                                      *
* .... .... .1..   ASID FROM WHICH REQUEST WAS MADE                   *
* .... .... ..1.   ASSOCIATED TCB ADDRESS                             *
* .... .... ...1   TIME OF REQUEST                                    *
*                                                                     *
* THE DEFAULT TRACES ALL.  TRDATA = B'11111111111110000'              *
***********************************************************************
TRFLGS   DS 0F
TRDATA   DC B'1111111111110000'  BIT MAP INDICATING DATA TO BE TRACED
TRRSV    DC X'000000000000'      RESERVED
R10      EQU 10                  GDA
R9       EQU 9                   TRAC
R8       EQU 8                   TRAS
R7       EQU 7                   TRSP
R6       EQU 6                   TRSR
R1       EQU 1                   TRSR
GDAGFSTR EQU X'20'
TRAC     DSECT
TRACID   DS CL4
TRACFLGS DS CL8
TRACTRAS DS A
TRACTRSP DS A
TRACTRSR DS A
TRACCTL  DS AL1
TRAS     DSECT
TRASID   DS CL4
TRASNUM  DS F
TRASLIST DS 16H
TRSP     DSECT
TRSPID   DS CL4
TRSPNUM  DS F
TRSPLIST DS 16X
TRSR     DSECT
TRSRID   DS CL4
TRSRNUM  DS F
TRSRLIST DS 16X
         END

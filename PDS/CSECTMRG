         TITLE '***** MERGE CSECTLST FILES FOR ENDEVOR LIBRARIES *****'
CSECTMRG CSECT
CSECTMRG AMODE 31
CSECTMRG RMODE 24
         PRINT NOGEN
***********************************************************************
* THE CSECTLST PROGRAM IS RUN AGAINST ALL THE ENDEVOR LOAD LIBRARIES, *
* AND IS THEN SORTED IN CSECT NAME ORDER. THIS PROGRAM READS THE      *
* SORTED FILE AND WRITES A SINGLE RECORD TO THE OUTPUT1 FILE FOR EACH *
* CSECT THAT IS THE ENTRY POINT TO A MEMBER. IF THE CSECT NAME OCCURS *
* AS ENTRY POINT TO MULTIPLE MEMBERS ON THE SAME LIBRARY OR DIFFERENT *
* ENDEVOR LOAD LIBRARIES, EXTRA SEGMENTS ARE ADDED FOR EACH LIBRARY/  *
* MEMBER COMBINATION. OUTPUT RECORDS CONTAIN THE CSECT NAME, LANGUAGE *
* CODE, AND LIBRARY/MEMBER ELEMENT FOR EACH COMBINATION FOUND.        *
*                                                                     *
*                                                                     *
* AUTHOR R THORNTON - OCT 2000                                        *
***********************************************************************
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CSECTMRG,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPEN                    BRANCH AROUND CONSTANTS
         DC    CL8'CSECTMRG'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
OPEN     OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
* MAINLINE PROCESSING                                                 *
***********************************************************************
MAINLINE GET   INPUT1,INREC            READ NEXT INPUT1 RECORD
         CLI   INENTRY,C'*'            THIS AN ENTRY POINT CSECT?
         BNE   MAINLINE                NO, IGNORE IT
         CLC   INCSECT,OUTCSECT        SAME CSECT AS LAST?
         BNE   NEWREC                  NO
         CLC   INLANG,OUTLANG          SAME LANGUAGE?
         BE    SAMECS                  YES
NEWREC   CLC   OUTRDW,H0               ANY DATA IN OUTPUT?
         BE    NEWCS                   NO, IGNORE IT
         PUT   OUTPUT1,OUTREC          WRITE THE OUTPUT RECORD
NEWCS    MVC   OUTRDW,PFXLEN           FIXED LENGTH
         MVC   OUTCSECT,INCSECT        MOVE CSECT NAME
         MVC   OUTLANG,INLANG          MOVE LANGUAGE
         LA    R1,OUTDM                POINT TO 1ST DSNAME/MEMBER
         ST    R1,CURRDM               SAVE THE ADDRESS
SAMECS   L     R1,CURRDM               POINT TO CURRENT DSNAME/MEMBER
         MVC   0(3,R1),INNODE2         MOVE 2ND DSNAME NODE 1-3
         MVC   3(8,R1),INMEM           MOVE MEMBER NAME
         LA    R1,L'OUTDM(R1)          POINT TO NEXT DM FIELD
         ST    R1,CURRDM               SAVE POINTER
         LH    R1,OUTRDW               PICK UP RDW
         LA    R1,L'OUTDM(R1)          ADD LENGTH OF DM FIELD
         STH   R1,OUTRDW               UPDATE THE RDW
         B     MAINLINE
****************************************************************
* END OF JOB.                                                  *
****************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
****************************************************************
*                     DATA AND WORK AREAS
****************************************************************
CURRDM   DC    A(OUTDM)                @ FIRST OUTDM FIELD
ENDOUT   DC    A(OUTEND)               @ END OF OUTPUT RECORD
BLANKS   DC    CL11' '                 CONSTANT BLANKS
H0       DC    H'0'                    CONSTANT 0
PFXLEN   DC    AL2(L'OUTPFX)           OUTPUT RECORD PREFIX LENGTH
DBLWD    DS    D                       DOUBLEWORD WORK AREA
*
INREC    DS    0CL80                   INPUT RECORD
INMEM    DS    CL8                       MEMBER NAME
         DS    CL1
INENTRY  DS    CL1                       * IF ENTRY POINT FOR MODULE
INCSECT  DS    CL8                       MEMBER NAME
         DS    CL1
INLANG   DS    CL3                       ASM OR COB
         DS    CL1
INADDR   DS    CL6                       CSECT OFFSET, PRINTABLE HEX
         DS    CL1
INSIZE   DS    CL6                       CSECT SIZE IN PRINTABLE HEX
         DS    CL1
INDSN    DS    0CL43                     SYSLIB'S DSNAME
         DS    CL6                         FIRST NODE: EMVSP.
INNODE2  DS    CL3                         SECOND NODE 1-3
         DS    CL34                        REST OF DSNAME
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=VB,LRECL=27962
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
*
         LTORG
*
         DS    0F
OUTREC   DS    0C                     OUTPUT RECORD
OUTPFX   DS    0CL16                     FIXED LENGTH PREFIX
OUTRDW   DC    H'00'                     RECORD LENGTH
         DC    XL2'00'
OUTCSECT DC    CL8' '                    CSECT NAME
         DC    CL1' '                    RESERVED FOR MATCH CODE
OUTLANG  DC    CL3' '                    ASM OR COB OR ???
OUTDM    DC    0CL11' '                  DSNAME INDICATOR AND MEMBER
OUTDSNI  DC    CL3' '                       DSNAME INDICATOR
OUTMEM   DC    CL8' '                       MEMBER NAME
         DS    CL27962                   ROOM FOR REMAINING OUTDM'S
OUTEND   EQU   *
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

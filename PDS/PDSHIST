         PRINT NOGEN
         TITLE 'PRINT LOAD LIBRARY DIRECTORY WITH LINKEDIT DATES'
PDSHIST  SVLNK R3
*
*******************************************************************
* THIS PROGRAM READS THE DIRECTORY OF A LOAD LIBRARY TO OBTAIN    *
* THE MEMBER NAMES. FOR EACH MEMBER FOUND, A FIND MACRO IS ISSUED *
* TO POSITION TO THE TEXT FOR THE MEMBER, AND THE MEMBER IS READ  *
* UNTIL A TYPE 2 IDR RECORD IS FOUND. THE LINK-EDIT DATE IS TAKEN *
* FROM THE IDR RECORD AND PRINTED WITH THE MEMBER NAME. IF NO TYPE*
* 2 IDR RECORD IS FOUND, THE MEMBER NAME IS NOT PRINTED.          *
* SEE LMODHIST FOR A CONTROL-CARD DRIVEN PROGRAM.                 *
*                                                                 *
* FIXED TO SHOW LKED DATES IN YYYY/DDD FORMAT, USING 65 AND LATER *
* TO MEAN 1900+YR, AND 64 AND EARLIER TO MEAN 2000+YR. SG JULY 07 *
*******************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,PDSMEM,,PDSDIR) OPEN FILES
         GETMAIN R,LV=32768           PDS BLOCK AREA
         ST    R1,PDSAREA             SAVE THE ADDRESS
*
*******************************************************************
* MAINLINE ROUTINE: READ THE PDS DIRECTORY. FOR EACH DIRECTORY    *
* ENTRY, READ THE MEMBER TO LOCATE THE TYPE 2 ESD ENTRY WHICH     *
* CONTAINS THE LINK-EDIT DATE. WHEN FOUND, PRINT THE MEMBER NAME  *
* AND LINK-EDIT DATE.                                             *
*******************************************************************
*
NEWMEM   BAL   R9,READDIR              READ NEXT DIRECTORY ENTRY
         CLI   MEMBER,0                END OF FILE
         BE    EOJ                     YES, STOP RUN
         BAL   R9,FINDMEM              LOCATE MEMBER
         CLI   MEMBER,0                MEMBER FOUND O.K.?
         BE    NEWMEM                  NO
NEXTREC  BAL   R9,READMEM              READ NEXT RECORD IN MEMBER
         CLI   MEMBER,0                IS IT EOF?
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         CLI   0(R12),X'80'            IS IT AN IDR RECORD?
         BNE   NEXTREC                 NO
         CLI   2(R12),X'02'            TYPE 2 IDR RECORD?
         BNE   NEXTREC                 NO
         MVC   PRTNAME,MEMBER          NAME TO PRINT
         MVC   PRTDATE,=X'402120202061202020' EDIT WORD
         ED    PRTDATE+1,14(R12)         EDIT LINKEDIT DATE
         MVC   PRTDATE(3),=C' 19'
         MVI   PRTDATE+9,C' '
         CLI   15(R12),X'64'
         BH    OLDCENT
         MVC   PRTDATE(3),=C' 20'
OLDCENT  DS    0H
         BAL   R9,PRINT                PRINT THE LINE
         B     NEWMEM                  GO GET ANOTHER DIRECTORY ENT
*
*******************************************************************
* PRINT SUBROUTINE                                                *
*******************************************************************
*
PRINT    PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
         MVI   PRTCC,C' '              SET SINGLE SPACE CONTROL
         MVC   PRT,PRTCC               CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
*
*******************************************************************
* ROUTINE TO READ THE DIRECTORY IN AN INPUT PDS AND RETURN        *
* ENTRIES TO THE CALLER. EACH DIRECTORY ENTRY RETURNED IS         *
* COMPOSED OF AN 8 BYTE MEMBER NAME FOLLOWED BY THE 3 BYTE TTR    *
* ADDRESS OF THE BEGINNING OF THE MEMBER                          *
*******************************************************************
*
READDIR  LH    R12,DIRBYTS             GET NBR BYTES REMAINING IN BLOCK
         CH    R12,ELEVEN              ANY MORE ENTRIES IN BLOCK
         BNL   GETADR                  YES
NEXREC   GET   PDSDIR                  READ DIRECTORY BLOCK
         LR    R12,R1                  GET BLOCK ADDRESS
         MVC   DIRBYTS,0(R12)          MOVE NBR BYTES IN BLOCK
         CLC   DIRBYTS,ZERO            ANY BYTES IN BLOCK
         BE    DIREOF                  NO, EOF
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         MVC   MEMBER,0(R12)           MOVE MEMBER NAME FOR CALLER
         CLI   MEMBER,X'FF'            VALID MEMBER NAME?
         BE    DIREOF                  NO, END OF DIRECTORY
         NI    11(R12),CLRBITS         CLEAR ALIAS, TTR BITS
         SR    R11,R11                 CLEAR WORK REG
         IC    R11,11(R12)             GET NBR USER HALFWORDS
         SLL   R11,1                   MULTIPLY BY 2
         LA    R11,12(R11)             CALCULATE RECORD LENGTH
         LH    R12,DIRBYTS             GET NBR BYTES REMAINING
         SR    R12,R11                 SUBTRACT THIS ENTRY LENGTH
         BP    STREM                   ANY LEFT
         SR    R12,R12                 NO, SET TO ZERO
STREM    STH   R12,DIRBYTS             STORE REMAINING BYTES
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         BR    R9                      RETURN TO CALLER
DIREOF   MVI   MEMBER,0                INDICATE EOF TO CALLER
         BR    R9                      RETURN TO CALLER
*
*******************************************************************
* ROUTINE TO LOCATE A PDS MEMBER BY NAME                          *
*******************************************************************
*
FINDMEM  FIND  PDSMEM,MEMBER,D         FIND MEMBER BY NAME
         LTR   R15,R15                 ANY ERROR
         BCR   8,R9                    NO, RETURN TO CALLER
         MVI   MEMBER,0                INDICATE NOT FOUND
         BR    R9                      GO BACK TO CALLER
*
*******************************************************************
* ROUTINE TO READ THE NEXT RECORD FROM THE MEMBER OF A PDS        *
*******************************************************************
*
READMEM  L     R12,PDSAREA             POINT TO INPUT BUFFER
         READ  INDECB,SF,PDSMEM,(12),'S'  READ NEXT BLOCK
         CHECK INDECB                  WAIT FOR COMPLETION
         BR    R9                      EXIT
MEMEOF   MVI   MEMBER,0                INDICATE END OF FILE
         BR    R9                      GO BACK TO CALLER
*
*******************************************************************
* END OF JOB - ALL DIRECTORY ENTRIES PROCESSED                    *
*******************************************************************
*
EOJ     CLOSE (SYSPRINT,,PDSDIR,,PDSMEM) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*******************************************************************
* STORAGE AND FILE AREAS                                          *
*******************************************************************
*
PRTLINE  DS    0CL133                  PRINT LINE
PRTCC    DC    C'1'                    CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT TEXT
PRTNAME  DS    CL8                     MEMBER NAME
         DC    CL4' '
PRTDATE  DC    CL9'  YY/DDD'           LINK-EDIT DATE
         DC    CL111' '
DIRBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ELEVEN   DC    H'11'
ZERO     DC    H'0'
RECADDR  DS    F
CLRBITS  EQU   X'1F'
MEMBER   DS    CL8                     MEMBER NAME
PDSAREA  DS    A                       PDS BLOCK STORAGE AREA
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,LRECL=81,BLKSIZE=81
PDSDIR   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSLIB,RECFM=F,                X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
PDSMEM   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,EODAD=MEMEOF
         END

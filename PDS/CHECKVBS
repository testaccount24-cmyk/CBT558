         TITLE '*** CHECKVBS: CHECK RECORDS IN VBS FILES *****'
CHECKVBS CSECT
*********************************************************************
* CHECKS RECORDS IN A RECFM=VBS FILE. RESPECIALIZE FOR EACH USE.    *
*                                                                   *
* AUTHOR R THORNTON JAN 2002                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CHECKVBS,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'CHECKVBS'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN INPUT FILE
         MVC   OUTPUT1+62(2),INPUT1+62 COPY INPUT BLOCKSIZE
         MVC   OUTPUT1+82(2),INPUT1+82 COPY INPUT LRECL
         MVC   OUTPUT1+36(1),INPUT1+36 COPY INPUT RECFM
         OPEN  (OUTPUT1,OUTPUT)        OPEN OUTPUT FILE
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,INDATA           READ INPUT RECORD
         AP    INRECS,P1               MAINTAIN RECORD COUNTER
         CLC   INDATA+10(8),=CL8'SYSAJES2'
         BNE   MAINLINE                NO, LOOP BACK
         CLC   INDATA(10),=XL10'DE1E0054A0710102018F'
         BNE   MAINLINE                NO, LOOP BACK
NEXPRT   MVC   PRT(12),=XL12'402020206B2020206B202020' EDIT WORD
         ED    PRT(12),INRECS          EDIT COUNT
         MVC   PRT+13(100),INDATA      PRINTABLE DATA
         TR    PRT+13(100),TRCHAR      TURN NON-PRINTABLES TO DOTS
         MVC   PRTCC,C'2'              SET DOUBLE SPACE
         BAL   R9,PRINT                PRINT THE LINE
         MVZ   PRT+13(100),INDATA      MOVE ZONES
         TR    PRT+13(100),TRZONES     MAKE ZONES PRINTABLE
         BAL   R9,PRINT                PRINT THE LINE
         XC    PRT,PRT                 SET PRT TO HEX ZEROES
         MVN   PRT+13(100),INDATA      MOVE NUMERICS
         TR    PRT+13(100),TRNUMBR     MAKE NUMERICS PRINTABLE
         BAL   R9,PRINT                PRINT THE LINE
         AP    PRINTED,P1              ADD 1 TO PRINT LINES
         CP    PRINTED,=P'50'          PRINTED 50 LINES?
         BH    EOJ                     YES, ALL THROUGH
         GET   INPUT1,INDATA           READ INPUT RECORD
         B     NEXPRT                  CONTINUE
*********************************************************************
*        WRITE INPUT RECORD TO OUTPUT FILE                          *
*********************************************************************
WRITEOUT ST    R9,WO9                  SAVE RETURN ADDRESS
         MVC   OUTRDW,INPUT1+82        MOVE LRECL TO RDW
         PUT   OUTPUT1,OUTREC          WRITE IT TO OUTPUT
WOXIT    L     R9,WO9                  GET RETURN ADDRESS
         BR    R9                      RETURN
WO9      DC    A(0)                    RETURN ADDRESS FOR WRITEOUT
*********************************************************************
*        PRINT ROUTINE                                              *
*********************************************************************
PRINT    ST    R9,PR9                  SAVE RETURN ADDRESS
         PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRT,BLANKS              CLEAR PRINT LINE
PRXIT    L     R9,PR9                  GET RETURN ADDRESS
         BR    R9                      RETURN
PR9      DC    A(0)                    RETURN ADDRESS FOR PRINT
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1,,PRINT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
RETCODE  DC    F'0'                    PROGRAM RETURN CODE
PRINTED  DC    PL5'0'                  PRINT LINE COUNTER
INRECS   DC    PL5'0'                  INPUT RECORD COUNTER
P1       DC    P'1'                    CONSTANT 1
PRTLINE  DS    0CL133                  PRINT LINE
PRTCC    DC    C' '                      CARRIAGE CONTROL
PRT      DC    CL132' '                  PRINT DATA
BLANKS   DC    CL133' '                BLANKS
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,RECFM=FBA,LRECL=133
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
         LTORG
CHECKVBS CSECT
OUTREC   DS    0F              OUTPUT RECORD AREA
OUTRDW   DS    H
         DC    H'00'
TRCHAR   DC    256C'.'
         ORG   TRCHAR+C'A'
         DC    C'ABCDEFGHI'
         ORG   TRCHAR+C'J'
         DC    C'JKLMNOPQR'
         ORG   TRCHAR+C'S'
         DC    C'STUVWXYZ'
         ORG   TRCHAR+C'0'
         DC    C'0123456789'
         ORG
TRZONES  DC    256C'.'
         ORG   TRZONES+0
         DC    C'0'
         ORG   TRZONES+X'10'
         DC    C'1'
         ORG   TRZONES+X'20'
         DC    C'2'
         ORG   TRZONES+X'30'
         DC    C'3'
         ORG   TRZONES+X'40'
         DC    C'4'
         ORG   TRZONES+X'50'
         DC    C'5'
         ORG   TRZONES+X'60'
         DC    C'6'
         ORG   TRZONES+X'70'
         DC    C'7'
         ORG   TRZONES+X'80'
         DC    C'8'
         ORG   TRZONES+X'90'
         DC    C'9'
         ORG   TRZONES+X'A0'
         DC    C'A'
         ORG   TRZONES+X'B0'
         DC    C'B'
         ORG   TRZONES+X'C0'
         DC    C'C'
         ORG   TRZONES+X'D0'
         DC    C'D'
         ORG   TRZONES+X'E0'
         DC    C'E'
         ORG   TRZONES+X'F0'
         DC    C'F'
         ORG
TRNUMBR  DC    C'0123456789ABCDEF'
         DC    240C'.'
INDATA   DS    CL32768
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

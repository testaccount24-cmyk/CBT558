         TITLE '*** PDSDSN: CREATE PDS RELINK CONTROL CARD ***'
PDSDSN CSECT
*********************************************************************
* WRITES A 2 RECORD OUTPUT DATASET TO BE USED AS INPUT TO THE PDS   *
* UTILITY SO THAT IT WILL CREATE JCL AND CONTROL STATEMENTS TO      *
* RELINK ALL MEMBERS OF THE LIBRARY WHOSE NAME IS SUPPLIED IN THE   *
* PARM FIELD OF THE EXEC STATEMENT.                                 *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING PDSDSN,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'PDSDSN'             PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        PROCESS PARM FIELD                                         *
*********************************************************************
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),H0              ANY PARM DATA?
         BE    BADPARM                 NO, CONTINUE
         CLC   0(2,R2),MAXPARM         VALID PARM LENGTH?
         BNH   PARMOK                  YES
BADPARM  WTO   'PDSDSN: INVALID PARM ON EXEC STATEMENT',ROUTCDE=11
         ABEND 111,DUMP
PARMOK   LH    R1,0(R2)                GET PARM LENGTH
         BCTR  R1,0                    SUBTRACT 1 FOR LENGTH CODE
         EX    R1,MOVEDSN              MOVE DSNAME TO OUTPUT
         LA    R1,PARMDSNM(R1)         POINT PAST DSNAME
         MVI   1(R1),C''''             SET ENDING APOSTROPHE FOR DSNAME
         OPEN  (PDSPARM,OUTPUT)        OPEN FILE
         PUT   PDSPARM,DSNREC          WRITE DSNAME RECORD
         PUT   PDSPARM,MAPREC          WRITE MAP CONTROL RECORD
         CLOSE PDSPARM                 CLOSE FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN
MOVEDSN  MVC   PARMDSNM(1),2(R2)       SAVE PARM DSNAME
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
MAXPARM  DC    H'44'                   MAX PARM DATA LENGTH
H0       DC    H'0'                    CONSTANT ZERO
DSNREC   DS    0CL80                   DSNAME CONTROL RECORD FOR PDS
         DC    CL5'PDS '''
PARMDSNM DC    CL75' '
MAPREC   DC    CL80'MAP : JCL'         MAP CONTROL RECORD FOR PDS
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PDSPARM  DCB   DSORG=PS,MACRF=PM,DDNAME=PDSPARM,                       X
               RECFM=FB,LRECL=80
         LTORG
PDSDSN CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

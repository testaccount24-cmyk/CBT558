         TITLE 'INVOKE SORT FROM ASSEMBLER PROGRAM'
         PRINT NOGEN
INVKSORT SVLNK R3
         OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT,INPUT1) OPEN FILES
         CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE SYSOUT
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    ALOCWRK1                NO
         MVC   PRT,=CL80'DUT0060:  UNABLE TO ALLOCATE SYSOUT'
ALOCERR  BAL   R9,PRINT                PRINT MESSAGE
         ABEND 333,DUMP                TERMINATE WITH ERROR
ALOCWRK1 MVC   DDN,=CL8'SORTWK01'      DDNAME FOR WORK FILE
         MVC   SPACETYP(6),=X'C300020001D9' CYL,(2,1),RLSE
         MVC   DSNAME,=CL44'&&SORTWK01' FILL IN DSNAME
         CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE SORTWK01
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    ALOCWRK2                NO
         MVC   PRT,=CL80'DUT0060:  UNABLE TO ALLOCATE SORTWK01'
         B     ALOCERR
ALOCWRK2 MVC   DDN,=CL8'SORTWK02'      DDNAME FOR WORK FILE
         MVC   DSNAME,=CL44'&&SORTWK02' FILL IN DSNAME
         CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE SORTWK02
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    CALLSORT                NO
         MVC   PRT,=CL80'DUT0060:  UNABLE TO ALLOCATE SORTWK02'
         B     ALOCERR
CALLSORT LA    R1,POINTER              POINT TO SORT PARAMETER LIST
         LINK  EP=SORT                 INVOKE SORT
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   SORTERR                 BRANCH IF ERROR OCCURRED
         CLOSE (INPUT1,,OUTPUT1,,PRINT1) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
SORTERR  MVC   PRT,=CL80'DUT0060: ERROR OCCURRED DURING SORT'
         BAL   R9,PRINT                PRINT ERROR MESSAGE
         ABEND 888,DUMP
*
********************************************************************
*        E15 SORT EXIT ROUTINE: SUPPLIES ALL RECORDS TO BE SORTED  *
*        TO THE SORT PROGRAM.                                      *
********************************************************************
*
         USING E15EXIT,R15
E15EXIT  STM   R14,R12,12(R13)         E15 EXIT FOR SORT
         L     R3,BASEREG              INITIALIZE BASE REG
         DROP  R15
         LA    R15,SORTSAVE            POINT TO SAVE AREA
         ST    R15,8(R13)              SET LSA IN SORT SAVE AREA
         ST    R13,4(R15)              SET HSA IN E15 SAVE AREA
         LR    R13,R15                 INITIALIZE SAVE POINTER
         GET   INPUT1                  READ NEXT RECORD
         L     R14,4(R13)              POINT TO SORT'S SAVE AREA
         ST    R1,24(R14)              REC ADDR TO R1 IN SAVE
         LA    R15,12                  TELL SORT TO INSERT RECORD
         B     SORTRTRN                RETURN TO SORT
*
********************************************************************
*        E15 SORT EXIT ROUTINE: END OF FILE OCCURRED ON INPUT FILE.*
*        TELL SORT NOT TO CALL E15 EXIT AGAIN.                     *
********************************************************************
*
EOFIN    LA    R15,8                   END OF INPUT FILE, TELL SORT
         B     SORTRTRN                GO RETURN TO SORT
*
********************************************************************
*        E35 SORT EXIT ROUTINE: RECEIVES SORTED RECORDS FROM THE   *
*        SORT PROGRAM.                                             *
********************************************************************
*
         USING E35EXIT,R15
E35EXIT  STM   R14,R12,12(R13)         E35 EXIT FOR SORT
         L     R3,BASEREG              INITIALIZE BASE REGISTER
         DROP  R15
         LA    R15,SORTSAVE            POINT TO SAVE AREA
         ST    R15,8(R13)              SET LSA IN SORT SAVE AREA
         ST    R13,4(R15)              SET HSA IN E15 SAVE AREA
         LR    R13,R15                 INITIALIZE SAVE POINTER
         L     R12,0(R1)               GET RECORD ADDRESS
         LTR   R12,R12                 END OF FILE?
         BZ    TELLDEL                 YES
         PUT   OUTPUT1,0(R12)          WRITE THE RECORD
TELLDEL  LA    R15,4                   TELL SORT TO DELETE
*
********************************************************************
*        E35 AND E15 SORT EXIT ROUTINE COMMON RETURN ROUTINE.      *
********************************************************************
*
SORTRTRN L     R13,4(R13)              POINT TO SORT SAVE AREA
         L     R14,12(R13)             RESTORE SORT REG 14
         LM    R0,R12,20(R13)          RESTORE SORT REGS 0-12
         BR    R14                     RETURN TO SORT
SORTSAVE DS    9D                      E35 REGISTER SAVE AREA
*
********************************************************************
*        PRINT ROUTINE                                             *
********************************************************************
*
PRINT    PUT   PRINT1,PRT              WRITE PRINT LINE
         MVI   PRT,C' '                BEGIN LINE CLEAR
         MVC   PRT+1(79),PRT           FINISH CLEARING
         BR    R9                      EXIT
*
********************************************************************
*        DATA AND FILE AREAS                                       *
********************************************************************
*
BASEREG  DC    A(INVKSORT)             BASE REG
PRT      DS    CL80                    PRINT AREA
POINTER  DS    0F                      POINTER TO SORT PARAMETER LIST
         DC    X'80'                   END OF LIST INDICATOR
         DC    AL3(SORTPARM)           @ SORT PARAMETER LIST
         CNOP  2,4                     ALIGN PARAMETER LIST
SORTPARM DS    0H                      SORT PARAMETER LIST
         DC    AL2(PARMEND-PARMSTRT)   LENGTH OF PARAMETER LIST
PARMSTRT DC    A(SORTSTMT)             SORT STATEMENT POINTER
         DC    A(SRTSTEND)             END OF SORT STATEMENT
         DC    A(RECDSTMT)             RECORD STATEMENT POINTER
         DC    A(RECSTEND)             END OF RECORD STATEMENT
         DC    A(E15EXIT)              E15 EXIT ROUTINE
         DC    A(E35EXIT)              E35 EXIT ROUTINE
         DC    X'00',C'MAX'            CORE=MAX
         DC    X'01',AL3(12288)        RESERVE 12K
PARMEND  EQU   *
SORTSTMT DC    C' SORT FIELDS=(1,8,CH,A)'
SRTSTEND DC    C' '
RECDSTMT DC    C' RECORD TYPE=F,LENGTH=80'
RECSTEND DC    C' '
*
*
*
***********************************************************************
*       PARAMETERS FOR DYNALLOC                                       *
***********************************************************************
*
DAPARM1  DS    0F                      * 1ST PARAMETER FOR DYNALLOC *
DDN      DC    CL8'SYSOUT'             DDNAME
DSNAME   DC    CL44'&&SYSOUT'          DATASET NAME
MEMBER   DC    XL8'00'                 MEMBER NAME
DISP     DC    XL3'00'                 DISPOSITION:
*                                    BYTE1: 0=OLD,M=MOD,N=NEW,S=SHR
*                                    BYTE2: U=UNCATLG,C=CATLG,D=DELETE
*                                    K=KEEP; BYTE3: SAME AS BYTE2
UNITNAME DC    CL8'DISK'               UNIT NAME
UNITCNT  DC    XL2'00'                 UNIT COUNT
VOLSER   DC    XL6'00'                 VOLUME SERIAL NUMBER
LBLTYPE  DC    XL1'00'                 LABEL TYPE:
*                                      N=NL, S=SL, B=BLP
DSSEQ    DC    XL2'00'                 DATASET SEQUENCE NBR (TAPE)
FRECLOS  DC    XL1'00'                 F: FREE=CLOSE
RETPRD   DC    XL2'00'                 RETENTION PERIOD IN DAYS
RECFM    DC    XL3'00'                 RECORDING FORMAT:
*                                      BYTE1: F=FIXED, V=VARIABLE,
*                                      U=UNDEFINED; BYTE2: B=BLOCKED,
*                                      BYTE3: A=ASA CONTROL CHARACTERS
LRECL    DC    XL2'00'                 LOGICAL RECORD LENGTH
BLKSIZE  DC    XL2'00'                 BLOCK SIZE
DEN      DC    XL1'00'                 TAPE DENSITY: 1=1600,6=6250
BUFNO    DC    XL2'00'                 BUFNO
KEYLEN   DC    XL2'00'                 KEY LENGTH
DSORG    DC    XL1'00'                 DATASET ORGANIZATION:
*                                      V=VSAM, P=PDS, D=DIRECT, S=SEQ
SPACETYP DC    CL1'T'                  SPACE TYPE:
*                                      C=CYL, T=TRK
PSPAC    DC    XL2'01'                 PRIMARY SPACE AMOUNT
SSPAC    DC    XL2'00'                 SECONDARY SPACE AMOUNT
RLSE     DC    CL1'00'                 R=RLSE
DRCTYBLK DC    XL2'00'                 NBR DIRECTORY BLOCKS
*
*
*
DAPARM2  DS    0F                      2ND PARAMETER FOR DYNALLOC
REQCODE  DC    CL1'A'                  REQUEST CODE:
*                                      A=ALLOCATE A DATASET
*                                      V=VOLSER FEEDBACK
RETRNCD  DC    CL2' '                  DYNALLOC RETURN CODE
ERRRET   DC    CL4' '                  ERROR RETURN CODE
INFORET  DC    CL4' '                  INFO RETURN CODE
*
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,                        X
               RECFM=FB,LRECL=80,EODAD=EOFIN
*
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         END

         TITLE '***** MATCH CSECTMRG AND ENDVDATA FILES *****'
CSECTMCH CSECT
CSECTMCH AMODE 31
CSECTMCH RMODE 24
         PRINT NOGEN
***********************************************************************
* THE CSECTMCH PROGRAM ATTEMPTS TO MATCH THE ENDEVOR ELEMENTS FOUND   *
* BY THE ENDVDATA PROGRAM WITH ENTRY-POINT CSECTS IN ENDEVOR LIBRARIES*
* FOUND BY USE OF THE CSECTLST PROGRAM. PURPOSE IS TO MATCH ENDEVOR   *
* ELEMENTS WITH LOAD MODULE CSECTS SO THAT UNMATCHED ELEMENTS AND     *
* CSECTS CAN BE RESEARCHED.                                           *
*                                                                     *
* THE CSECTS FILE IS SORTED IN CSECT NAME ORDER, AND THE ELEMENT FILE *
* IN ELEMENTS NAME ORDER. CSECT NAMES AND ELEMENT NAMES ARE COMPARED. *
* ALL INPUT RECORDS ARE REWRITTEN. WHEN THERE IS NO MATCH, THE MATCH  *
* CODE IS SET TO BLANK.                                               *
* WHEN A MATCH OCCURS, THE ELEMENT NAME IS COMPARED TO THE CSECT NAME *
* AND THE LANGUAGES ARE ALSO COMPARED. MATCH CODE IS SET AS FOLLOWS:  *
*  9: ELEMENT MATCHES CSECT AND MEMBER. LANGUAGES MATCH               *
*  8: ELEMENT MATCHES CSECT BUT NOT MEMBER. LANGUAGES MATCH.          *
*  7: ELEMENT MATCHES CSECT AND MEMBER. LANGUAGES DO NOT MATCH.       *
*  6: ELEMENT MATCHES CSECT BUT NOT MEMBER. LANGUAGES DO NOT MATCH.   *
*  BLANK: ELEMENT DOES NOT MATCH CSECT.                               *
*                                                                     *
* RELATED PROGRAMS: CSECTLST, CSECTMRG, ENDVDATA, PROCSCAN, CSECTMCH2 *
* AUTHOR R THORNTON - OCT 2000                                        *
***********************************************************************
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CSECTMCH,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPEN                    BRANCH AROUND CONSTANTS
         DC    CL8'CSECTMCH'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
*        OPEN FILES. READ FIRST RECORD IN EACH INPUT FILE             *
***********************************************************************
OPEN     OPEN  (UNMCHCS,OUTPUT,UNMCHEL,OUTPUT,CSECTS,,ELEMENTS)
         LA    R6,BLANKS               POINT R6 TO SAFE LOCATION
         LA    R7,BLANKS               POINT R7 TO SAFE LOCATION
         BAS   R15,READCS              READ FIRST CSECTS FILE RECORD
         USING CSREC,R6                MAINTAINED THROUGHOUT
         BAS   R15,READEL              READ FIRST ELEMENTS FILE RECORD
         USING ELREC,R7                MAINTAINED THROUGHOUT
***********************************************************************
* MAINLINE PROCESSING: MATCH RECORDS. WRITE UNMATCHED RECORDS TO THE  *
* APPROPRIATE UNMATCHED OUTPUT FILE WITH MATCH CODE = BLANK.          *
***********************************************************************
MAINLINE MVI   CSMATCH,C' '            CLEAR MATCH CODE
         MVI   ELMATCH,C' '            CLEAR MATCH CODE
         CLC   CSCSECT,ELELEM          FOUND A MATCH?
         BE    MLMATCH                 YES
         BL    MLCSLOW                 NO, CSECT IS LOW
MLELLOW  PUT   UNMCHEL,0(R7)           WRITE UNMATCHED ELEMENT RECORD
         BAS   R15,READEL              READ FIRST ELEMENTS FILE RECORD
         B     MAINLINE                CONTINUE MATCHING
MLCSLOW  CLC   0(2,R6),=H'26'          VALID RECORD LENGTH?
         BL    MLJUSRD                 NO
         PUT   UNMCHCS,0(R6)           WRITE UNMATCHED CSECTS RECORD
MLJUSRD  BAS   R15,READCS              READ FIRST CSECTS FILE RECORD
         B     MAINLINE                CONTINUE MATCHING
MLMATCH  AP    MATCH,P1                ADD 1 TO MATCH COUNT
         MVI   LANGMCH,0               CLEAR MATCH SWITCH
         MVI   MBRMCH,0                CLEAR MATCH SWITCH
         CLC   CSLANG,ELTYPE           SAME LANGUAGE?
         BE    MLSAML                  YES
         CLC   ELTYPE,=C'ASSEM'        ELEMENT TYPE IS ASSEM?
         BNE   MLCKMBR                 NO
         CLC   CSLANG,=C'ASM'          CSECT LANGUAGE IS ASM?
         BNE   MLCKMBR                 NO
MLSAML   MVI   LANGMCH,1               SET LANGUAGE MATCH SWITCH
MLCKMBR  CLC   CSMEM,ELELEM            ELEMENT NAME MATCHES CSECT NAME?
         BNE   MLMSMCH                 NO
         MVI   MBRMCH,1                INDICATE ELEMENT = MEMBER NAME
         CLI   LANGMCH,1               MEMBER MATCHES, DOES LANGUAGE?
         BNE   MLMNOTL                 NO
         MVI   ELMATCH,C'9'            YES, SET MATCH CODE = 9
         B     MLWRTEM                 GO WRITE THEM
MLMNOTL  MVI   ELMATCH,C'7'            SET MATCH CODE = 7
         B     MLWRTEM                 GO WRITE THE RECORDS
MLMSMCH  CLI   LANGMCH,1               MEMBER NOT A MATCH, IS LANGUAGE?
         BNE   MLNONE                  NO LANGUAGE MATCH EITHER
         MVI   ELMATCH,C'8'            LANGUAGE, NOT MEMBER, CODE = 7
         B     MLWRTEM                 GO WRITE THE RECORDS
MLNONE   MVI   ELMATCH,C'6'            SET MATCH CODE = 6
MLWRTEM  MVC   CSMATCH,ELMATCH         SET SAME CODE, BOTH RECORDS
         PUT   UNMCHCS,0(R6)           WRITE UNMATCHED CSECTS RECORD
         PUT   UNMCHEL,0(R7)           WRITE UNMATCHED ELEMENT RECORD
         BAS   R15,READCS              READ NEXT CSECTS FILE RECORD
         BAS   R15,READEL              READ NEXT ELEMENTS FILE RECORD
         B     MAINLINE                CONTINUE MATCHING
****************************************************************
* READ NEXT CSECTS FILE RECORD                                 *
****************************************************************
READCS   ST    R15,RCS15               SAVE RETURN ADDRESS
         CLI   CSCSECT,EOF             AT EOF?
         BE    CSEOF                   YES
         GET   CSECTS                  READ NEXT CSECTS FILE RECORD
         LR    R6,R1                   POINT TO CSECTS RECORD
         AP    CSIN,P1                 INCREMENT READ COUNTER
         L     R15,RCS15               GET RETURN ADDRESS
         BR    R15                     EXIT
CSEOF    LA    R6,ENDFILE-(CSCSECT-CSREC) SET EOF FLAG
         CLI   ELELEM,EOF              ELEMENTS FILE AT EOF?
         BE    EOJ                     YES, ALL THROUGH
         L     R15,REL15               NO, GET RETURN ADDRESS
         BR    R15                     AND EXIT
RCS15    DC    A(0)                    RETURN ADDRESS
****************************************************************
* READ NEXT ELEMENTS FILE RECORD                               *
****************************************************************
READEL   ST    R15,REL15               SAVE RETURN ADDRESS
         CLI   ELELEM,EOF              AT EOF?
         BE    ELEOF                   YES
         GET   ELEMENTS                READ NEXT ELEMENTS RECORD
         LR    R7,R1                   POINT TO ELEMENTS RECORD
         AP    ELIN,P1                 INCREMENT READ COUNTER
         L     R15,REL15               GET RETURN ADDRESS
         BR    R15                     EXIT
ELEOF    LA    R7,ENDFILE-(ELELEM-ELREC) SET EOF FLAG
         CLI   CSCSECT,EOF             CSECTS FILE AT EOF?
         BE    EOJ                     YES, ALL THROUGH
         L     R15,REL15               NO, GET RETURN ADDRESS
         BR    R15                     AND EXIT
REL15    DC    A(0)                    RETURN ADDRESS
****************************************************************
* END OF JOB.                                                  *
****************************************************************
EOJ      ED    MSGCNT,MATCH            EDIT MATCH COUNT INTO MESSAGE
         LA    R12,MCHMSG              POINT TO MATCH MESSAGE AREA
         WTO   TEXT=(R12),ROUTCDE=11   DISPLAY MATCH RECORD COUNT
         CLOSE (UNMCHCS,,UNMCHEL,,CSECTS,,ELEMENTS) CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
****************************************************************
*                     DATA AND WORK AREAS                      *
****************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
EOF      EQU   X'FF'                   EOF FLAG
ENDFILE  DC    X'FF'                   END OF FILE
CSIN     DC    PL4'0'                  NBR CSECTS INPUT RECORDS READ
ELIN     DC    PL4'0'                  NBR ELEMENTS INPUT RECORDS READ
MATCH    DC    PL4'0'                  MATCH COUNT
LANGMCH  DC    X'00'                   1 = LANGUAGE MATCH
MBRMCH   DC    X'00'                   1 = MEMBER NAME MATCH
P1       DC    P'1'                    CONSTANT 1
BLANKS   DC    CL80' '                 CONSTANT BLANKS
MCHMSG   DC    AL2(MSGEND-MSG)         WTO MESSAGE LENGTH
MSG      DS    0C
MSGCNT   DC    X'40206B2020206B202020'
         DC    C' MATCHING RECORDS FOUND'
MSGEND   EQU   *
*
CSECTS   DCB   DSORG=PS,MACRF=GL,DDNAME=CSECTS,EODAD=CSEOF,            X
               RECFM=VB,LRECL=27962
ELEMENTS DCB   DSORG=PS,MACRF=GL,DDNAME=ELEMENTS,EODAD=ELEOF,          X
               RECFM=FB,LRECL=80
UNMCHCS  DCB   DSORG=PS,MACRF=PM,DDNAME=UNMCHCS,                       X
               RECFM=VB,LRECL=27962
UNMCHEL  DCB   DSORG=PS,MACRF=PM,DDNAME=UNMCHEL,                       X
               RECFM=FB,LRECL=80
*
         LTORG
*
         DS    0H
*********************************************************************
*             CSECT RECORD DSECT                                    *
*********************************************************************
CSREC    DSECT ,                      CSECTS RECORD
CSRDW    DS    H                         RECORD LENGTH
         DS    XL2
CSCSECT  DS    CL8                       CSECT NAME
CSMATCH  DS    CL1                       MATCH CODE
CSLANG   DS    CL3                       ASM OR COB OR ???
CSDM     DS    0CL11                     DSNAME INDICATOR AND MEMBER
CSDSNI   DS    CL3                          DSNAME INDICATOR
CSMEM    DS    CL8                          MEMBER NAME
         DS    CL27962                   ROOM FOR REMAINING OUTDM'S
*********************************************************************
*             ELEMENTS RECORD DSECT                                 *
*********************************************************************
***********************************************************************
ELREC    DSECT ,                      ELEMENTS RECORD
ELELEM   DS    CL8               ELEMENT NAME
ELMATCH  DS    CL1               MATCH CODE
ELCOMP   DS    CL11              COMPILER DESCRIPTOR
         DS    CL1
ELENV    DS    CL8               ENVIRONMENT NAME
         DS    CL1
ELSYS    DS    CL8               SYSTEM NAME
         DS    CL1
ELSUB    DS    CL8               SUBSYSTEM NAME
         DS    CL1
ELTYPE   DS    CL5               TYPE NAME
         DS    CL1
ELPRGRP  DS    CL8               PROCESSOR GROUP NAME
         DS    CL1
ELPROC   DS    CL8               PROCESSOR NAME
         DS    CL9
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

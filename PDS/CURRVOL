         TITLE '*** CURRVOL: GET CURRENT VOLUME SERIAL ***'
         PRINT NOGEN
***********************************************************************
* CALLER PASSES THE ADDRESS OF AN OPEN DCB AND A 6-BYTE AREA IN WHICH *
* THE VOLUME SERIAL CURRENTLY IN USE BY THE FILE IS RETURNED. IF ANY  *
* ERROR OCCURS, HEX FF'S ARE RETURNED IN THE VOLUME SERIAL FIELD.     *
* AUTHOR R THORNTON NOVEMBER 1993                                     *
***********************************************************************
*
CURRVOL  CSECT
CURRVOL  AMODE 31
CURRVOL  RMODE ANY
         USING *,R15
         STM   R14,R12,12(R13)         STORE CALLER'S REGS
         LR    R3,R15                  SET UP NEW BASE REG
         DROP  R15
         USING CURRVOL,R3
         LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R13,SAVE+4              SAVE AREA BACK POINTER
         ST    R15,8(R13)              SAVE AREA FORWARD POINTER
         LR    R13,R15                 POINT REG 13 TO MY SAVE AREA
         L     R4,4(R1)                POINT TO CALLER'S VOLSER AREA
         USING VOLAREA,R4
         MVC   VOLSER,HIVALS           PRESET TO HEX FF'S IN CASE
         L     R5,0(R1)                POINT TO CALLER'S DCB
         N     R5,CLRHI                32-BIT ADDRESS, CLEAR HI BYTE
         USING IHADCB,R5
         TM    DCBOFLGS,X'10'          IS FILE OPEN?
         BZ    RETURN                  NO, ERROR
         L     R6,DCBDEBAD             POINT TO DEB FOR THE FILE
         N     R6,CLRHI                32-BIT ADDRESS, CLEAR HI BYTE
         USING DEBBASIC,R6
         L     R7,DEBSUCBA             POINT TO THE UCB FOR THE FILE
         N     R7,CLRHI                32-BIT ADDRESS, CLEAR HI BYTE
         USING UCBCMSEG,R7
         MVC   VOLSER,UCBVOLI          CURRENT VOLSER TO CALLER AREA
RETURN   L     R13,SAVE+4              POINT TO CALLER'S SAVE AREA
         LM    R14,12,12(R13)          RESTORE CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE = 0
         BR    R14                     RETURN
*********************************************************************
* PROGRAM DATA AREAS                                                *
*********************************************************************
SAVE     DS    18F                     REGISTER SAVE AREA
CLRHI    DC    X'00FFFFFF'             AND MASK TO CLEAR HIGH BYTE
HIVALS   DC    6X'FF'                  HEX FF'S (HIGH-VALUES)
         LTORG
*********************************************************************
* PROGRAM DSECTS                                                    *
*********************************************************************
VOLAREA  DSECT ,                       CALLER'S VOLSER RETURN AREA
VOLSER   DS    CL6                     CALLER'S VOLSER RETURN AREA
         DCBD  DSORG=PS                DCB DSECT
         IEZDEB
         IEFUCBOB
CURRVOL CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '***** APPLY COBOL VERSION TO CSECTLST FILE *****'
COBLSETY SVLNK R3,R4
         PRINT NOGEN
*
***********************************************************************
* READS THE SORTED OUTPUT FILES FROM CSECTLST AND COBLTYPE AND MATCHES*
* THEM ON CSECT NAME. WHEN A MATCH IS FOUND, INFORMATION IN THE       *
* COBLTYPE FILE IS ADDED TO THE CSECTLST FILE RECORD. OUTPUT FILE     *
* CONTAINS ONE RECORD FOR EACH RECORD IN THE CSECTLST FILE.           *
*                                                                     *
* INPUT1 SPECIFIES THE FILE CREATED BY THE CSECTLST PROGRAM           *
* INPUT2 SPECIFIES THE FILE CREATED BY THE COBLTYPE PROGRAM           *
*                                                                     *
* AUTHOR R THORNTON - NOV 1994                                        *
***********************************************************************
* OPEN FILES, GET STORAGE FOR TABLES.                          *
****************************************************************
*
         OPEN  (INPUT1,,INPUT2,,OUTPUT1,OUTPUT) OPEN FILES
         BAS   R9,GETIN1                 READ INPUT1 RECORD
         BAS   R9,GETIN2                 READ INPUT2 RECORD
*
****************************************************************
* MAINLINE PROCESSING FOR THE LOAD MODULE.                     *
****************************************************************
*
MAINLINE CLC   CMEMBR,TMEMBR             SAME MEMBER NAME?
         BL    CSECTLOW                  NO, CSECTLST RECORD LOW
         BH    TYPELOW                   NO, TYPE RECORD LOW
         CLC   CCSECT,TCSECT             SAME CSECT NAME?
         BL    CSECTLOW                  NO, CSECTLST RECORD LOW
         BH    TYPELOW                   NO, TYPE RECORD LOW
MATCH    MVC   OMEMBR,CMEMBR             MEMBER NAME
         MVC   OENTPT,CENTPT             ENTRY POINT INDICATOR
         MVC   OCSECT,CCSECT             CSECT NAME
         MVC   OLANG,TTRAN               LANGUAGE TRANSLATOR NAME
         MVC   OADDR,CADDR               ENTRY POINT OFFSET
         MVC   OSIZE,CSIZE               MODULE SIZE
         MVC   ODSNAME,CDSNAME           LOAD LIBRARY DSNAME
         BAS   R9,PUTOUT1                WRITE OUTPUT RECORD
         BAS   R9,GETIN1                 READ INPUT1 RECORD
         BAS   R9,GETIN2                 READ INPUT2 RECORD
         B     MAINLINE                  CONTINUE
TYPELOW  BAS   R9,GETIN2                 READ COBLTYPE RECORD
         B     MAINLINE                  CONTINUE
CSECTLOW MVC   OMEMBR,CMEMBR             MEMBER NAME
         MVC   OENTPT,CENTPT             ENTRY POINT INDICATOR
         MVC   OCSECT,CCSECT             CSECT NAME
         MVC   OLANG(3),CLANG            LANGUAGE TRANSLATOR NAME
         MVC   OLANG+3(7),=CL7' ????  '  SHOW NO MATCH
         MVC   OADDR,CADDR               ENTRY POINT OFFSET
         MVC   OSIZE,CSIZE               MODULE SIZE
         MVC   ODSNAME,CDSNAME           LOAD LIBRARY DSNAME
         BAS   R9,PUTOUT1                WRITE OUTPUT RECORD
         BAS   R9,GETIN1                 READ INPUT1 RECORD
         B     MAINLINE                  CONTINUE
****************************************************************
* WRITE RECORD TO OUTPUT1 FILE                                 *
****************************************************************
PUTOUT1  PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         BR    R9                      RETURN TO CALLER
****************************************************************
* READ INPUT1 (CSECTLST) RECORD                                *
****************************************************************
GETIN1   GET   INPUT1,CSEREC           READ CSECTLST RECORD
         BR    R9                      RETURN TO CALLER
EOF1     CLI   TMEMBR,X'FF'            ALREADY AT EOF2?
         BE    EOJ                     YES, END OF JOB
         MVC   CMEMBR,=XL8'FFFFFFFFFFFFFFFF' SET EOF INPUT1
         BR    R9                      RETURN TO CALLER
****************************************************************
* READ INPUT2 (COBLTYPE) RECORD                                *
****************************************************************
GETIN2   GET   INPUT2,TYPREC           READ COBLTYPE RECORD
         BR    R9                      RETURN TO CALLER
EOF2     CLI   CMEMBR,X'FF'            ALREADY AT EOF1?
         BE    EOJ                     YES, END OF JOB
         MVC   TMEMBR,=XL8'FFFFFFFFFFFFFFFF' SET EOF INPUT2
         BR    R9                      RETURN TO CALLER
****************************************************************
* END OF JOB.                                                  *
****************************************************************
EOJ      CLOSE (INPUT1,,INPUT2,,OUTPUT1) CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
****************************************************************
*                     DATA AND WORK AREAS
****************************************************************
*
*
*
TYPREC   DS    0CL80                   COBLTYPE RECORD
TMEMBR   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
TCSECT   DC    CL8' '                    CSECT NAME
         DC    CL1' '
TTRAN    DC    CL10' '                   TRANSLATOR NAME
         DC    CL1' '
TVERML   DC    CL4' '                    TRANSLATOR VER/MOD LEVEL
         DC    CL1' '
TPCPLDT  DC    CL6' '                    COMPILE DATE
         DC    CL1' '
         DC    CL65' '
*
*
*
CSEREC   DS    0CL80                   CSECTLST RECORD
CMEMBR   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
CENTPT   DC    CL1' '                    * IF ENTRY POINT FOR MODULE
CCSECT   DC    CL8' '                    CSECT NAME
         DC    CL1' '
CLANG    DC    CL3' '                    LANGUAGE TRANSLATOR NAME
         DC    CL1' '
CADDR    DC    CL6' '                    CSECT OFFSET, PRINTABLE HEX
         DC    CL1' '
CSIZE    DC    CL6' '                    CSECT SIZE IN PRINTABLE HEX
         DC    CL1' '
CDSNAME  DC    CL43' '                   SYSLIB'S DSNAME
*
*
*
OUTREC   DS    0CL80                   COBLSETY OUTPUT RECORD
OMEMBR   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
OENTPT   DC    CL1' '                    * IF ENTRY POINT FOR MODULE
OCSECT   DC    CL8' '                    CSECT NAME
         DC    CL1' '
OLANG    DC    CL10' '                   LANGUAGE TRANSLATOR NAME
         DC    CL1' '
OADDR    DC    CL6' '                    CSECT OFFSET, PRINTABLE HEX
         DC    CL1' '
OSIZE    DC    CL6' '                    CSECT SIZE IN PRINTABLE HEX
         DC    CL1' '
ODSNAME  DC    CL36' '                   SYSLIB'S DSNAME
*
*
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOF1,             X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         END
         END

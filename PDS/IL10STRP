DDLS     TITLE 'DLTCBL INTERFACE FOR CL10'
         PRINT NOGEN
* THIS PROGRAM IS THE DBLTCBL INTERFACE MODULE FOR PROGRAMS USING IP10*
* DATABASE.  IT ESTABLISHES ONE ADDRESS FOR IP10 USUAGE AND SETS UP   *
* ADDRESSES FOR USE BY THE IP10 CBLTDLI INTERFACE MODULE.  UP TO 10   *
* VIEWS OF IP10 CAN BE USED WITH THIS MODULE.  THE MODULE BASES SPLITS*
* ON FIVE BEGINNING WITH DATABASE NAME OF IP11.                       *
*                                                                     *
DLITCBL  CSECT
         PRINT GEN
*****REGISTER EQUATES*******
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING DLITCBL,R12
         STM   R14,R12,12(R13)
         LR    R12,R15        INITIALIZE BASE REGISTER
         LR    R15,R13        SAVE CALLERS SAVE AREA
         LA    13,SAVEAREA    SET 13 TO SAVE AREA
         ST    15,4(0,13)     BACK CHAIN
         ST    13,8(0,15)     FOREWARD CHAIN
         L     15,16(0,15)    RESTORE 15
         SPACE 1
P1       EQU   *
         ST    R1,PCBADDR     SAVE LIST OF PCB ADDRESSES
         LR    R2,R1
         LA    R7,PCBLISTO    SET UP OUTPUT PCB ADDRESSES
         LA    R5,SPLTSAVE    SET UP WORKAREAS FOR SPLIT FILE
         LR    R4,R5          SAVE BEGINNING OF TABLE
         USING PCBMASK,R9     SET UP AREA FOR DUMMY PCBMASK
*                        LOOP TO CHECK ALL PCB ADDRESSES
         SPACE 2
PCBLOOP  EQU   *
         L     R9,0(0,R1)     LOAD PCB ADDRESS
         LTR   R9,R9
         BC    8,PCBFDA
         ST    R9,HOLD9
CONTON   EQU   *
         CLI   12(R9),X'C0'   IS THIS A DB PCB
         BNL   PCBFDB         YES CHECK SPLIT
         BAL   R14,SETPCBL    NO  SET PCB ADDRESS
PCBLP1   EQU   *
         LA    R1,4(0,R1)     STEP TO NEXT PCB ADDRESS
         B     PCBLOOP        GO PROCESS NEXT ENTRY
PCBFDA   EQU   *
         MVI   ENDSW,C'Y'     SET THAT THIS IS END
PCBFDB   EQU   *
         CLC   0(4,R9),=C'IP11'  IP10 FIRST SPLIT
         BL    PCBFDC                 THRU
         CLC   0(4,R9),=C'IP15'  IP10 LAST SPLIT
         BH    PCBFDC
         B     SETSPLT            YES GO ESTABLISH ADDRESS
PCBFDC   EQU   *
         CLI   ENDSW,C'Y'         END OF PCB LIST
         BE    LASTPCBA           YES SET LAST ADDRESS
         BAL   R14,SETPCBL        NO SET PCB ADDRESS
         CLI   HOLD9,X'80'
         BE    LASTPCBA
         B     PCBLP1             NO LOOP TO READ NEXT
SETSPLT  EQU   *
         CLC   0(4,R9),=C'IP11'   IP10 DATABASE 1
         BNE   SETSPLTC           NO SET FOR NEXT SPILT
         CLI   ENDSW,C'1'         FIRST IP10 VIEW
         BE    FIRSTTME           YES BYPASS ADDRESS ADJUST
         LA    R4,84(0,R4)        SET FOR NEXT GROUP OF SPLIT
         LR    R5,R4
FIRSTTME EQU   *
         MVI   ENDSW,C'N'         TURN OFF FIRST VIEW
         ST    R9,64(0,R5)        STORE ACTUAL PCB ADDRESS
         ST    R5,0(0,R7)         STORE DUMMY ADDRESS IN OUT
         LA    R5,4(0,R5)         UP ADDRESS FOR NEXT SPLIT
         LR    R8,R7              SAVE LAST OUT ADDRESS
         LA    R7,4(0,R7)         UP OUT ADDRESS TO NEXT
         B     PCBLP1             LOOP TO READ NEXT
SETSPLTC EQU   *
         ST    R9,64(0,R5)        STORE ACTUAL PCB ADDRESS
         CLI   ENDSW,C'Y'         END OF PCB ADDRESSES
         BE    LASTPCBA           YES BRANCH TO SET LAST ADDRESS
         CLI   HOLD9,X'80'
         BE    LASTPCBA
         LA    R5,4(0,R5)         NO  UP ADDRESS FOR NEXT SPLIT
         B     PCBLP1                LOOP TO READ NEXT
SETPCBL  EQU   *
         ST    R9,0(0,R7)         STORE PCB ADDRESS IN OUT LIST
         LR    R8,R7              SAVE LAST PCB ADDRESS
         LA    R7,4(0,R7)         SET OUT LIST TO NEXT
         BR    R14                RETURN
LASTPCBA EQU   *
         LR    R7,R8             YES RESTORE PCB OUT LIST TO LAST
         OI    0(R7),X'80'       SET END OF LIST FOR NEW LIST
         LA    R1,PCBLISTO       RESET PARAMETER REGISTER NEW LIST
         L     R15,VDLITCBL
         BALR  R14,R15           GO
         B     ALLDONE
ALLDONE  EQU   *
         L     R13,4(0,R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
HOLD9    DC    F'0'
ENDSW    DC    CL1'N'
SAVEAREA DC    18F'0'
VDLITCBL DC    V(SETCBLNM)
*                           SPLIT SAVE AREA
         SPACE 1
PCBADDR  DC    F'0'
*                  PCB OUTLIST FOR APPLICATIONS
PCBLISTO DC    20F'0'
*               10 WORKAREA PCBS-64 BYTES PLUS 5 FOUR BYTE ADDRESSES
SPLTSAVE DC    10XL84'00'
         SPACE 2
         LTORG
*                        DSECT FOR DBPCB (FEEDBACK)
         SPACE 2
PCBMASK  DSECT
FDBDNAME DS    CL8
FSEGLEV  DS    CL2
FRETCODE DS    CL2
FPROCOPT DS    CL4
FDL1USE  DS    F
FSEGNAME DS    CL8
FLENFDBK DS    F
FUNMSENS DS    F
FKEYFBAK DS    CL48
         END

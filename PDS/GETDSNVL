         TITLE '*** GET DSNAME AND VOLSER FOR A DATASET ***'
         PRINT NOGEN
GETDSNVL SVLNK R3,,,SUB
**************************************************************
* THIS SUBROUTINE USES A DDNAME PASSED BY THE CALLER TO OPEN *
* A DATASET FOR THE PURPOSE OF DETERMINING THE VOLUME SERIAL *
* NUMBER VIA THE JFCB. THE DSNAME, VOLUME SERIAL, DEVICE TYPE*
* INDICATOR, AND A DATA PRESENT INDICATOR ARE RETURNED TO THE*
* THE CALLING PROGRAM.
* THE CALLING PROGRAM DOES NOT NEED TO OPEN, OR EVEN TO      *
* DEFINE THE FILE TO OBTAIN THIS DATA. COBOL CALLING EXAMPLE:*
*            .                                               *
*            .                                               *
*            .                                               *
*            WORKING-STORAGE SECTION.                        *
*            .                                               *
*            01  GETDSNVL-PARAMETERS.                        *
*                05  GETDSNVL-VER       PIC X VALUE '0'.     *
*                05  FILE-DDNAME        PIC X(8).            *
*                05  RETURN-STATUS      PIC X.               *
*                    88  TAPE-FILE        VALUE 'T'.         *
*                    88  DISK-FILE        VALUE 'D'.         *
*                    88  ERROR-OCCURRED   VALUE 'E'.         *
*                05  DATA-STATUS        PIC X.               *
*                    88  DATA-PRESENT     VALUE 'P'.         *
*                    88  EMPTY-FILE       VALUE 'E'.         *
*                05  DATASET-NAME       PIC X(44).           *
*                05  VOLUME-SERIAL      PIC X(6).            *
*                                                            *
*        .                                                   *
*        .                                                   *
*        .                                                   *
*            PROCEDURE DIVISION.                             *
*        .                                                   *
*        .                                                   *
*        .                                                   *
*                MOVE 'INPUT3' TO FILE-DDNAME.               *
*                CALL 'GETDSNVL' USING GETDSNVL-PARAMETERS.  *
*                IF ERROR-OCCURRED
*                    PERFORM GETDSNVL-FAILED THRU EXIT-GF.   *
*                                                            *
*                                                            *
*                                                            *
* AUTHOR: R. THORNTON 12/21/92  FOR JOE GOLDSBY              *
**************************************************************
*                                                            *
***********************************************************************
*        POINT TO PARAMETER AREA                                      *
***********************************************************************
         L     R6,0(R1)                POINT TO PARAMETER AREA
         USING PARAMS,R6
         LA    R7,DCB                  POINT TO THE DCB
         USING IHADCB,R7
***********************************************************************
*        MOVE CALLER'S DDNAME TO DCB AND TRY TO OPEN IT               *
***********************************************************************
         MVC   DCBDDNAM,PDDNAME        MOVE DDNAME TO DCB
         OPEN  DCB                     TRY TO OPEN THE FILE
         TM    DCBOFLGS,DCBOFOPN       DID IT OPEN SUCCESSFULLY?
         BO    GETJFCB                 YES
SENDERR  MVI   PSTATUS,C'E'            NO, SEND BACK ERROR
         XC    PDSNAME,PDSNAME         CLEAR DSNAME
         XC    PVOLSER,PVOLSER             AND VOLUME SERIAL
         B     RETURN                  RETURN TO CALLER
***********************************************************************
*        LOCATE THE JFCB AND SEND INFORMATION TO CALLER               *
***********************************************************************
GETJFCB  RDJFCB DCB                    READ THE JFCB
         LTR   R15,R15                 SUCCESSFUL JFCB READ?
         BNZ   SENDERR                 NO, ERROR
         MVC   PDSNAME,JFCBDSNM        SEND BACK DSNAME
         MVC   PVOLSER,JFCBVOLS            AND 1ST VOLUME SERIAL
         L     R8,DCBDEBAD             POINT TO DEB
         USING DEBBASIC,R8
         L     R8,DEBSUCBA             POINT TO THE UCB
         CLI   18(R8),X'80'         (UCBDVCLS,UCB3TAPE): IS IT A TAPE?
         BE    SHOWTAPE                YES
         CLI   18(R8),X'20'  (UCBDVCLS,UCB3DACC): IS IT DIRECT ACCESS?
         BNE   SENDERR                 NO, ERROR
         MVI   PSTATUS,C'D'            INDICATE DISK
         B     CHKDATA                 GO CHECK FOR DATA
SHOWTAPE MVI   PSTATUS,C'T'            INDICATE TAPE
CHKDATA  MVI   PDATAIND,C'E'           INDICATE EMPTY FILE
         GET   DCB                     TRY TO READ
         MVI   PDATAIND,C'P'           SUCCESSFUL, SHOW DATA PRESENT
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
RETURN   CLOSE DCB                     CLOSE THE DCB
         FREEPOOL DCB                  FREE THE BUFFER POOL
         L     R13,4(R13)              POINT TO CALLER'S SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        PROGRAM CONSTANTS                                            *
***********************************************************************
         PRINT GEN
PARAMS   DSECT ,                       CALLER'S PARAMETER AREA
PVERSION DS    CL1                       VERSION C'0'
PDDNAME  DS    CL8                       DDNAME
PSTATUS  DS    CL1                       RETURN STATUS: T=TAPE, D=DISK,
*                                                       E=ERROR
PDATAIND DS    CL1                       DATA STATUS: E = EMPTY FILE,
*                                                     P = DATA PRESENT
PDSNAME  DS    CL44                      DATASET NAME
PVOLSER  DS    CL6                       VOLUME SERIAL
GETDSNVL CSECT
EXLST    DS    0F
         DC    X'87'                     JFCB EXIT, END OF LIST
         DC    AL3(INFMJFCB)             POINTER TO JFCB AREA
         IEFJFCBN LIST=YES
DCB      DCB   DSORG=PS,MACRF=GL,DDNAME=DCB,EXLST=EXLST,EODAD=RETURN
         DCBD   DSORG=PS
         IEZDEB
         END

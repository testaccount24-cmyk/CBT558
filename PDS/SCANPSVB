         PRINT NOGEN
         TITLE '*****SCANPSVB: SCAN DSORG=PS, RECFM=V FOR STRINGS'
SCANPSVB SVLNK R3
*
***********************************************************************
* SCAN FILE OF RECFM=V RECORDS FOR STRINGS  AND WRITE AN OUTPUT FILE  *
* RECORD FOR EACH RECORD CONTAINING A SPECIFIED STRING.  INPUT1 FILE  *
* AND OUTPUT1 FILE MUST HAVE RECFM=V OR RECFM=VB  LOGICAL RECORDS.    *
*                                                                     *
* THE READER1 DATASET IS USED TO ENTER THE STRINGS TO BE LOCATED IN   *
* THE SOURCE STATEMENT PDS. UP TO 500 STRINGS MAY BE ENTERED, ONE PER *
* RECORD. THE STRING IS ASSUMED TO BEGIN IN COLUMN 1 OF THE READER1   *
* RECORDS, AND ENDS EITHER WITH THE LAST NON-BLANK COLUMN IN THE      *
* RECORD, OR ONE COLUMN LEFT OF THIS, IF THE LAST NON-BLANK COLUMN    *
* CONTAINS THE < TERMINATING SIGN. IF THE LAST CHARACTER OF THE STRING*
* TO BE FOUND IS <, THEN 2 CONSECUTIVE <'S MUST END THE STRING. USE OF*
* THE TERMINATING < PERMITS TRAILING BLANKS TO BE INCLUDED IN STRINGS.*
*                                                                     *
* THE READER1 INPUT FILE MUST HAVE RECFM=FB,LRECL=80, WHICH IS HARD-  *
* CODED.                                                              *
*                                                                     *
* THE OUTPUT1 FILE MUST HAVE RECFM=VB WHICH IS HARD-CODED.            *
*                                                                     *
* AUTHOR R THORNTON FEB 1988                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN INPUT AND OUTPUT FILES. GET STORAGE FOR 500    *
* STRING RECORDS. READ THE READER1 FILE, STORING THE STRINGS IN THE   *
* TABLE. EACH STRING IS STORED LEFT JUSTIFIED IN THE LAST 71 BYTES OF *
* THE TABLE ENTRY. THE FIRST BYTE OF EACH ENTRY IS USED TO CONTAIN    *
* THE LENGTH CODE (LENGTH-1) OF THE STRING.                           *
***********************************************************************
*
         OPEN  (OUTPUT1,OUTPUT,INPUT1,,READER1) OPEN FILES
READCARD GET   READER1,STRCARD         READ STRING RECORD
         L     R1,CURRTBL              POINT TO CURRENT ENTRY
         C     R1,STREND               PAST END OF TABLE?
         BH    TOOMANY                 YES, ERROR
         LA    R15,71                  GET MAX POSSIBLE LENGTH CODE
         LA    R14,STRCARD+71          POINT TO LAST POSSIBLE TERM
CRDLNBK  CLI   0(R14),C' '             FOUND LAST NON-BLANK COLUMN?
         BNE   LASNONBK                YES
         BCTR  R15,0                   NO, DECREMENT LENGTH CODE
         BCT   R14,CRDLNBK             DECREMENT POINTER AND LOOP
LASNONBK CLI   0(R14),C'<'             GOT THE TERMINATOR SIGN?
         BNE   SAVELENG                NO
         BCTR  R15,0                   YES, DON'T INCLUDE IN LENGTH
SAVELENG STC   R15,0(R1)               YES, SAVE STRING LENGTH
         MVC   1(71,R1),BLANKS         PRE-CLEAR ENTRY
         EX    R15,TBLMOVE             MOVE STRING TO TABLE
         LA    R1,72(R1)               ADVANCE TO NEXT ENTRY
         ST    R1,CURRTBL              SAVE CURRENT ENTRY POINTER
         B     READCARD                CONTINUE
TBLMOVE  MVC   1(1,R1),STRCARD         EXECUTED MOVE
TOOMANY  WTO   'SCANPSVB: TOO MANY CARDS, 1ST 500 USED',ROUTCDE=11
*
***********************************************************************
* INITIALIZATION: SORT THE STRINGS READ IN FROM READER1 IN ASCENDING  *
* ORDER.                                                              *
***********************************************************************
*
ENDCARDS L     R1,CURRTBL              GET NEXT TABLE ADDRESS
         MVI   0(R1),X'00'             SET END ENTRY LENGTH = 1
         MVI   1(R1),X'FF'             SET END ENTRY DATA = FF
         BCTR  R1,0                    BACK UP TO RHE
         BCTR  R1,0                    BACK UP TO RHE-1
         ST    R1,STREND               SET ACTUAL TABLE END
         L     R1,STRTBL               POINT TO TABLE START
NEXPASS  LR    R2,R1                   POINT TO 1ST PASS ENTRY
NEXNTRY  LA    R2,72(R2)               POINT TO NEXT ENTRY
         C     R2,STREND               PAST END OF TABLE?
         BH    ENDPASS                 YES
         CLC   1(72,R2),1(R1)          2ND ENTRY IS LOW?
         BL    SRTSWCH                 YES, GO SWITCH
         B     NEXNTRY                 NO, CONTINUE PASS
SRTSWCH  MVC   STRCARD,0(R1)           SAVE 1ST ENTRY
         MVC   0(72,R1),0(R2)          MOVE 2ND TO 1ST
         MVC   0(72,R2),STRCARD        MOVE 1ST TO SECOND
         B     NEXNTRY                 CONTINUE PASS
ENDPASS  LA    R1,72(R1)               POINT TO NEXT TABLE ENTRY
         C     R1,STREND               PAST END OF TABLE?
         BL    NEXPASS                 NO
*
***********************************************************************
* INITIALIZATION: FOR EACH STRING FOUND IN THE TABLE, MOVE A HEX FF   *
* TO THAT OFFSET FROM THE BEGINNING OF THE TRTSCH TABLE. NEXT,        *
* MULTIPLY THE FIRST BYTE BY 4 AND ADD IT TO THE STARTING ADDRESS OF  *
* THE 256-WORD PTR TABLE. IF THE WORD AT THIS LOCATION IS NOT ZERO,   *
* PUT THE ADDRESS OF THE STRING ENTRY IN IT.                          *
***********************************************************************
*
         L     R1,STRTBL               POINT TO TABLE START
NEXTRNG  SR    R2,R2                   CLEAR FOR INSERT
         IC    R2,1(R1)                GET 1ST BYTE OF STRING
         LA    R15,TRTSCH(R2)          POINT TO TRT TABLE BYTE
         MVI   0(R15),X'FF'            PICK UP 1ST BYTE OF STRING
         SLL   R2,2                    MULTIPLY LEADING BYTE BY 4
         LA    R15,PTR(R2)             POINT TO POINTER TABLE
         CLC   0(4,R15),F0             FIRST TIME HERE?
         BNE   STEPSTRG                NO
         ST    R1,0(R15)               YES, STEP TO NEXT STRING
STEPSTRG LA    R1,72(R1)               STEP TO NEXT ENTRY
         C     R1,STREND               PAST END OF TABLE?
         BH    CHKINPDS                YES
         B     NEXTRNG                 ELSE CONTINUE SEARCH
*
***********************************************************************
*     INITIALIZATION:  CHECK THE INPUT FILE RECFM TO INSURE THAT IT IS*
* RECFM=V OR RECFM=VB, AND IS PHYSICALLY SEQUENTIAL.                  *
***********************************************************************
*
CHKINPDS RDJFCB INPUT1                 READ JFCB FOR INPUT FILE
         TM    JFCRECFM,X'C0'          POSSIBLE VARIABLE RECORDS?
         BNM   BADINPUT                NO, ERROR
         TM    JFCRECFM,X'40'          VARIABLE LENGTH RECORDS?
         BZ    BADINPUT                NO, ERROR
         CLI   JFCDSORG,X'40'          DSORG=PS?
         BE    MAINLINE                YES, SIMPLE SEQUENTIAL
BADINPUT WTO   'SCANPSVB: INVALID INPUT FILE. FILE MUST BE',ROUTCDE=11
         WTO   '         DSORG=PS OR DSORG=PO, AND',ROUTCDE=11
         WTO   '         RECFM=V OR RECFM=VB',ROUTCDE=11
         ABEND 777,DUMP                TERMINATE IN ERROR
*
***********************************************************************
* READ RECORDS TO END OF FILE. FOR EACH RECORD READ, SCAN FOR STRINGS.*
* IF A STRING IS FOUND IN AN INPUT RECORD, WRITE IT TO THE OUTPUT FILE*
***********************************************************************
*
MAINLINE GET   INPUT1                  READ NEXT RECORD
         LR    R11,R1                  COPY RECORD START ADDRESS
         ST    R11,MEMRECAD            SAVE RECORD START ADDRESS
         AP    INCOUNT,P1              INCREMENT RECORD COUNTER
         LR    R12,R11                 COPY RECORD START ADDRESS
         AH    R12,0(R12)              COMPUTE END OF RECORD ADDRESS
         LA    R11,3(R11)              GET RECORD DATA ADDRESS - 1
TRTSCAN  SR    R2,R2                   CLEAR FOR TRT INSERT
         LR    R1,R12                  END ADDRESS
         SR    R1,R11                  COMPUTE REMAINING LENGTH
         CH    R1,H255                 255 BYTES LEFT?
         BNH   EXTRT                   NO
         LA    R1,255                  LENGTH FOR 256 BYTES
EXTRT    BCTR  R1,0                    MINUS 1 FOR LENGTH CODE
         BCTR  R1,0                    MINUS 1 FOR TRT OFFSET
         LTR   R1,R1                   ANY LEFT
         BM    MAINLINE                NO
         EX    R1,TRTSTRG              TEST FOR STRING
         BZ    MAINLINE                NONE FOUND, GO READ AGAIN
         CR    R1,R12                  FOUND WITHIN RECORD?
         BH    MAINLINE                NO, EXIT
         LA    R1,0(R1)                CLEAR HI ORDER BYTE
         LR    R11,R1                  COPY ADDRESS FOUND
         LR    R15,R12                 COPY END ADDRESS
         SR    R15,R11                 LENGTH REMAINING IN RECORD
         SR    R2,R2                   CLEAR FOR INSERT
         IC    R2,0(R11)               GET FIRST BYTE OF STRING
         SLL   R2,2                    MULTIPLY BY PTR TBL ENTRY SIZE
         LA    R2,PTR(R2)              POINT TO PTR TABLE ENTRY
         L     R2,0(R2)                POINT TO 1ST POSSIBLE STRING
         LTR   R2,R2                   NULL POINTER?
         BZ    TRTSCAN                 YES
STRSRCH  C     R2,STREND               PAST END OF TABLE?
         BH    TRTSCAN                 YES, NO STRING FOUND
         SR    R1,R1                   CLEAR FOR INSERT
         IC    R1,0(R2)                GET STRING LENGTH FROM ENTRY
         CR    R1,R15                  RECORD CAN CONTAIN STRING?
         BNH   DOCMPR                  YES, GO COMPARE FOR STRING
SYSRSTEP LA    R2,72(R2)               STEP TO NEXT STRING ENTRY
         CLC   1(1,R2),0(R11)          SAME STARTING BYTE?
         BH    TRTSCAN                 NOT FOUND IF TABLE IS HIGH
         B     STRSRCH                 ELSE GO COMPARE
COMPARE  CLC   0(1,R11),1(R2)          EXECUTED COMPARE
DOCMPR   EX    R1,COMPARE              COMPARE RECORD:STRING ENTRY
         BNE   SYSRSTEP                BRANCH IF NOT EQUAL
PRTMATCH L     R11,MEMRECAD            POINT TO RECORD
         PUT   OUTPUT1,0(R11)          WRITE OUTPUT RECORD
         B     MAINLINE                GO GET NEXT RECORD
TRTSTRG  TRT   1(1,R11),TRTSCH         SCAN FOR POSSIBLE STRING
*
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1,,READER1) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
MEMRECAD DC    A(0)                    ADDRESS OF RECORD
STRTBL   DC    A(STRINGS)              FIRST STRING TABLE ADDRESS
CURRTBL  DC    A(STRINGS)              CURRENT STRING TABLE ADDRESS
STREND   DC    A(ENDSTRS)              END OF STRING TABLE
P0       DC    PL1'0'                  CONSTANT 0
P1       DC    PL1'1'                  CONSTANT 1
INCOUNT  DC    PL3'0'                  RECORD COUNT WITHIN MEMBER
TESTNAME DC    XL8'FFFFFFFFFFFFFFFF'   TEST COMPARISON VALUE (TESTPT1)
TESTCNT  DC    PL3'99999'              TEST COMPARISON VALUE (TESTPT2)
STRCARD  DS    CL80                    STRING RECORD FROM INPUT1
BLANKS   DC    CL90' '                 CONSTANT BLANKS
HIVAL    DC    XL8'FFFFFFFFFFFFFFFF'   CONSTANT HIGH-VALUES
H80      DC    H'80'                   CONSTANT 80
H255     DC    H'255'                  CONSTANT 255
LDADDRS  DS    0F                      ADDRESS TABLE
F0       DC    F'0'                    CONSTANT ZERO
PTR      DC    256F'0'                 STRING POINTER TABLE
*
JFCAD    DS    0F
         DC    X'87'                   END OF LIST, JFCB ADDR
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
JFCB     DS    CL176                   JFCB FOR INPUT1
JFCLRECL EQU   JFCB+104,2              LRECL
JFCRECFM EQU   JFCB+100,1              RECFM
JFCDSORG EQU   JFCB+98,1               DSORG
JFCELNM  EQU   JFCB+44,8               MEMBER NAME
*
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,EXLST=JFCAD,            X
               EODAD=EOJ
READER1  DCB   DSORG=PS,MACRF=GM,DDNAME=READER1,                       X
               RECFM=FB,LRECL=80,EODAD=ENDCARDS
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=VB
         LTORG
*
*        TRT SCANNING TABLE TO FIND POSSIBLE STRING
TRTSCH   DC    256X'00'
*
*        STRING TABLE AREA WITH ROOM FOR 500 STRINGS
STRINGS  DS    CL36000
ENDSTRS  DC    XL73'00'
         END

WTOREGS  SVLNK R3
*********************************************************************
* PROGRAM TO DISPLAY THE CONTENT OF ALL 16 GENERAL REGISTERS USING  *
* WTOS TO DISPLAY THE REGISTERS AND THEIR VALUES, ONE LINE PER REG: *
*                                                                   *
*      R0  = 0810B9F6                                               *
*      R1  = 861132BC                                               *
*          .                                                        *
*          .                                                        *
*          .                                                        *
*      R15 = 00F31968                                               *
*                                                                   *
* AUTHOR R THORNTON APR 1996                                        *
*********************************************************************
         MVC   MSGTEXT,BLANKS          CLEAR DISPLAY LINE
         STM   R0,R15,REGSAREA         SAVE REGISTER CONTENTS
         LA    R15,REGSAREA            POINT TO REG 0 VALUE
         ST    R15,SAVR15              STORE INITIAL R15 VALUE
         ZAP   REGNBR,=P'0'            SET REG NBR CTR TO 0
NEXREG   L     R15,SAVR15              GET POINTER VALUE
         CP    REGNBR,=P'16'           ALL THROUGH?
         BE    EOJ                     YES
         MVI   MSGREGNO,C'R'           MOVE DELIMITER
         UNPK  MSGREGNO+1(2),REGNBR    MOVE REGISTER NUMBER
         OI    MSGREGNO+2,C'0'         CLEAR SIGN ON NUMBER
         AP    REGNBR,=P'1'            ADD 1 TO REGISTER NUMBER CTR
         LA    R15,4(R15)              STEP TO NEXT REGISTER VALUE
         ST    R15,SAVR15              SAVE REG 15 ACROSS CALL
         BAS   R9,HEX2CHAR             CONVERT NEXT REG TO CHARS
         MVC   MSGREGVL,CHARS          MOVE CHAR EQUIVALENT
WTOEXP   LA    R1,MSGWTO               POINT TO MESSAGE
         SVC   35                      ISSUE WTO
         B     NEXREG                  CONTINUE THROUGH ALL REGS
**********************************************************************
*                  END OF JOB - ALL REGS WTO'ED                      *
**********************************************************************
EOJ      L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
**********************************************************************
* CONVERT REAL HEX TO PRINTABLE CHARACTERS. ON ENTRY, R15 POINTS TO  *
* A FIELD CONTAINING 4 BYTES OF HEX DATA. ON RETURN, THE CHARACTER   *
* EQUIVALENT IS IN THE FIELD CHARS (8 BYTES).                        *
**********************************************************************
HEX2CHAR UNPK  CHARS(9),0(5,R15)       UNPACK HEX
         TR    CHARS,MAKECHAR-C'0'        AND MAKE PRINTABLE
         BR    R9
**********************************************************************
*             DATA AREAS                                             *
**********************************************************************
BLANKS   DC    CL125' '                BLANKS
MAKECHAR DC    C'0123456789ABCDEF'     HEX2CHAR TR TABLE
HEX      DC    F'0'                    REAL HEX FIELD
         DC    X'00'                   DO NOT MOVE: MUST FOLLOW HEX
CHARS    DS    CL8                     CHARACTER HEX FIELD
         DC    X'00'                   DO NOT MOVE: MUST FOLLOW CHARS
REGNBR   DC    PL2'0'                  REGISTER NUMBER
SAVR15   DS    F                       SAVE AREA FOR REG 15
REGSAREA DS    16F                     REGISTER AREA
*
*
*
MSGWTO   DS    0F                      WTO LENGTH ERROR MESSAGE
         DC    AL2(MSGEND-MSGWTO)      AREA LENGTH
         DC    X'8000'                 MCS FLAGS
MSGTEXT  DS    0CL125                  TEXT OF MESSAGE
MSGREGNO DS    CL3                     REGISTER NUMBER
         DS    CL2
MSGREGVL DS    CL8                     REGISTER VALUE
         DS    CL112
MSGEND   EQU   * END OF TEXT
         DC    X'0200'                 DESC=7
         DC    X'0020'                 ROUTCDE=11
         END

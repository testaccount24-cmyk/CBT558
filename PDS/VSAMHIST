         TITLE 'VSAMHIST - VSAM DATA SET ANALYSIS'
*
*    VSAMHIST - VSAM DATASET ANALYSIS
*
*    WRITTEN:
*       RICHARD G. NIKULA
*       WASHINGTON STATE DEPARTMENT OF TRANSPORTAION.
*
*
*    THE VSAMHIST PROGRAM ANALYZES VSAM DATASETS AND PRODUCES A
*  HISTOGRAM OF THE SPACE UTILIZATION WITHIN EACH CONTROL INTERVAL.
*  THIS PROGRAM CAN BE USED AGAINST FIXED OR VARIABLE KEYED
*  SEQUENCED DATASETS.  WITH THIS INFORMATION FREE SPACE ESTIMATES
*  CAN BE ADJUSTED TO IMPROVE UTILIZATION.  THE FOLLOWING DD
*  STATEMENTS ARE REQUIRED:
*
*  VSAMDS - DATASET TO BE ANALYZED
*  PRINT  - HISTOGRAM ANALYSIS SYSOUT
*           DCB=(BLKSIZE=N*133) MUST BE SPECIFIED
*  KEYOUT - THIS SYSOUT CONTAINS THE STARTING KEY AND ENDING KEY FOR
*           EACH CONTROL INTERVAL. MAY BE DUMMY'D TO ELIMINATE.
*           DCB=(BLKSIZE=N*133) MUST BE SPECIFIED
*  SORTIN - TEMPORARY DATASET TO HOLD INTERNAL SORT DATA.
*           DCB=(BLKSIZE=N*133) MUST BE SPECIFIED
*  SORTOUT- TEMPORARY DATASET TO HOLD SORTED DATA FOR PROGRAM ANALYSIS
*           DCB=(BLKSIZE=N*133) MUST BE SPECIFIED
*  SORT?? - OTHER SORT SUPPORT DATASETS AS REQUIRED.
*
*    THE PRINT DD STATEMENT WILL PRODUCE A UTILIZATION HISTOGRAM.  THE
*  FORMAT (IN CONDENSED FORM) IS:
*
*  0++10++20++30++40++50++60+70++80++90++100           RECORDS     CI
*  XXXXX                                                   5        0
*  XXXXXXXXXXXXX                                           8        1
*  XXXXXXXXXXXXXXXXX                                       9        2
*   << 2 CONTROL INTERVALS WITH NO RECORDS >>
*  XXXXX                                                   5        5
*
*    EACH X REPRESENTS 1 PERCENT OF USE.  THE RECORDS FIELD SHOWS THE
*  NUMBER OF RECORDS STORED WITHIN THE CI.  ALSO PROVIDED IS THE
*  CURRENT CI #.  THE SPECIAL << --- >> RECORD IS INSERTED WHEN A
*  COMPLETE CI IS EMPTY.  THIS REPORT IS PRODUCED IN LOGICAL ORDER.
*
*    THE KEYOUT DD  STATEMENT IS USED TO DISPLAY THE START AND END KEY
*  OF EACH CONTROL INTERVAL (UP TO A MAXIMUM OF 61 CHARACTERS).
*  THIS REPORT IS PRODUCED IN PHYSICAL SEQUENCE.  TRACKING KEY RANGE
*  CHANGES OVER A PERIOD OF SEVERAL DAYS MAY INDICATE WHY CERTAIN CI
*  OR CA SPLITS ARE OCCURRING.
*
*    IF THE DATASET BEING ANALYSED IS PASSWORD PROTECTED, A PARM MUST
*  BE PASSED TO THE PROGRAM (E.G. EXEC PGM=VSAMHIST,PARM=XYZ).
*
*    IF ANY ERROR CONDITIONS ARE ENCOUNTERED DURING PROCESSING, THE
*  REASON FOR THE ERROR AND THE RETURN CODE (OR FEEDBACK) WILL BE
*  DISPLAYED ON THE PRINT SYSOUT.
*
*    SAMPLE JCL:
*
*    // EXEC PGM=VSAMHIST,PARM='PASSWORD'
*    //VSAMDS   DD  DSN=VSAM.DATASET,DISP=SHR
*    //PRINT    DD  SYSOUT=*,DCB=(BLKSIZE=1330)
*    //KEYOUT   DD  SYSOUT=*,DCB=(BLKSIZE=1330)
*    //SORTIN   DD  SPACE=(TRK,(10,10)),UNIT=VIO,DCB=BLKSIZE=6118
*    //SORTOUT  DD  SPACE=(TRK,(10,10)),UNIT=VIO,DCB=BLKSIZE=6118
*    //SORTWK01 DD  SPACE=(TRK,(10,10)),UNIT=VIO
*    //SORTWK02 DD  SPACE=(TRK,(10,10)),UNIT=VIO
*    //SORTWK03 DD  SPACE=(TRK,(10,10)),UNIT=VIO
*    //SYSOUT   DD  SYSOUT=*
         EJECT
*********************************************************************
*                                                                   *
*              INITIALISATION                                       *
*                                                                   *
*********************************************************************
         SPACE 2
VSAMHIST SVLNK R12
         L     R2,0(R1)
         CLC   0(2,R2),=H'0'
         BE    NOPSWD
         LH    R1,0(R2)
         BCTR  R1,0
         EX    R1,MVEPSWD
         SPACE 2
NOPSWD   EQU   *
         OPEN  (SORTIN,(OUTPUT),KEYOUT,(OUTPUT))
         OPEN  (VSAMACB)
         SPACE 2
         TESTCB ACB=VSAMACB,OFLAGS=OPEN
         BNZ   OPENERR
         SPACE 2
         LA    R3,VSAMACB               POINT TO ACB
         USING IFGACB,R3                GET ADDRESSABILITY
         L     R4,ACBAMBL               POINT TO AMBL
         L     R5,52(R4)                POINT TO AMB
         MVC   DSNAME,136(R5)           GET DSNAME
         DROP  R3
         SPACE 2
         SHOWCB ACB=VSAMACB,AREA=CINV,LENGTH=16,                       X
               FIELDS=(CINV,LRECL,KEYLEN,RKP)
         L     R3,CINV                  CONTROL INT. SIZE
         SH    R3,=H'4'                 ADJUST FOR CIDF
         ST    R3,MAXPOS                CI LENGTH
         L     R2,KEYLEN
         BCTR  R2,0
         CH    R2,=H'61'                MAX TO WRITE
         BNH   R2OK
         LH    R2,=H'61'
         EJECT
*********************************************************************
*                                                                   *
*              START PROCESSING FILE                                *
*                                                                   *
*********************************************************************
         SPACE 2
R2OK     EQU   *
         ST    R2,KEYLEN
         POINT RPL=RPL1                 POINT TO INIT RECORD
         LTR   R15,R15                  DID IT WORK
         BNZ   POINTERR                 NO.....
         XR    R7,R7
         XR    R5,R5
         L     R6,MAXPOS                PREPARE TO GET REC LEN
         SPACE 2
GETLOOP  EQU   *
         GET   RPL=RPL1                 GET FIRST RECORD
         LTR   R15,R15
         BNZ   GETERR
         AP    ALLREC,=PL1'1'
         SHOWCB RPL=RPL1,AREA=RBA,LENGTH=8,FIELDS=(RBA,RECLEN)
         SPACE 2
         L     R3,RBA                   RBA OF RECORD READ
         XR    R2,R2
         D     R2,CINV                  DETERMINE WHICH CI
         C     R3,LASTCI                SAME??
         BNE   CLEAR                    NO
         L     R1,RECAREA               ADDRESS FOR RECORD
         A     R1,RKP
         L     R2,KEYLEN                ADDRESS FOR KEY
         EX    R2,MOVEKEY               MOVE HIGHKEY
         CLI   INIT,X'00'               FIRST RECORD
         BNE   CONT                     NO
         MVC   LOWKEY,HIGHKEY           COPY THE RECORD
         MVI   INIT,X'FF'               SET FLAG
         SPACE 2
CONT     LA    R7,1(R7)                 ONE MORE RECORD IN THIS CI
         A     R5,RECLEN                ADD LENGTH
         CLC   RECLEN,PREVLEN           SAME AS LAST RECORD?
         BNE   NEWSIZE
         CLI   HADONE,X'FF'             IS THIS FIRST RECORD?
         BNE   FIRST                    YES
         B     ENDLEN
         SPACE 2
FIRST    EQU   *
         MVI   HADONE,X'FF'             SHOW GREATER THAN 2
         SH    R6,=H'3'                 STILL HAS ONE RDF
         B     ENDLEN
         SPACE 2
NEWSIZE  EQU   *
         MVI   HADONE,X'00'             RESET COUNT FLAG
         MVC   PREVLEN,RECLEN           NEW RECORD LENGHT
         SH    R6,=H'3'                 HAS ONE RDF
         B     ENDLEN
         SPACE 2
ENDLEN   EQU   *
         B     GETLOOP
         SPACE 2
CLEAR    BAL   R9,RESET
         B     GETLOOP
         EJECT
*********************************************************************
*                                                                   *
*              END OF INPUT                                         *
*                                                                   *
*********************************************************************
         SPACE 2
EODAD    BAL   R9,RESET
         SPACE 2
         SHOWCB ACB=VSAMACB,LENGTH=20,AREA=STATS,                      X
               FIELDS=(NSSS,NCIS,CINV,LRECL,NEXT)
         SPACE 2
         CLOSE (VSAMACB)
         CLOSE (SORTIN,,KEYOUT)
         SPACE 2
         LA    R1,SORTCON
         LINK  EP=SORT
         LTR   R15,R15
         BNZ   SORTERR
         SPACE 2
         OPEN  (SORTOUT,(INPUT),PRINT,(OUTPUT))
         AP    PAGECNT,=P'1'            INCREMENT PAGE COUNTER
         MVC   PAGENO,EDWRD1            MOVE EDIT PATTERN
         ED    PAGENO,PAGECNT           INSERT PAGE NUMBER
         PUT   PRINT,HDR1
         PUT   PRINT,HDR2
         PUT   PRINT,HDR3
         SPACE 2
GETSORT  GET   SORTOUT,LINE
         PACK  PACKFD,NUMCI+1(6)
         MVC   NUMCI,EDWRD2
         ED    NUMCI(7),PACKFD+1
         MVC   SAVEFD,PACKFD
         SP    PACKFD,=PL1'1'
         CP    PACKFD,PREVFD
         BE    NOSKIP
         SP    PACKFD,PREVFD
         MVC   SKIPPED,EDWRD2
         ED    SKIPPED,PACKFD+1
         PUT   PRINT,SKIPLINE
         AP    COUNTER,=PL1'1'
         SPACE 2
NOSKIP   MVC   PREVFD,SAVEFD
         PUT   PRINT,LINE
         AP    COUNTER,=PL1'1'
         CP    COUNTER,=PL2'56'
         BL    SAMEPG
         AP    PAGECNT,=P'1'               INCREMENT PAGE COUNTER
         MVC   PAGENO,EDWRD1               MOVE EDIT PATTERN
         ED    PAGENO,PAGECNT              INSERT PAGE NUMBER
         PUT   PRINT,HDR1
         PUT   PRINT,HDR2
         PUT   PRINT,HDR3
         MVI   LINE,C'0'
         ZAP   COUNTER,=PL1'0'
         SPACE 2
SAMEPG   B     GETSORT
         EJECT
*********************************************************************
*                                                                   *
*              PRODUCE TOTAL LINE                                   *
*                                                                   *
*********************************************************************
         SPACE 2
EODAD2   EQU   *
         MVC   TOTRECS,=X'40206B2020206B202120'
         ED    TOTRECS(10),ALLREC
         SPACE 2
         L     R1,CASPLTS               LOAD NUM OF CA SPLITS
         CVD   R1,CVDAREA                CONVERT TO DECIMAL
         MVC   CASPLITS,EDWRD2           MOVE IN EDIT WORD
         ED    CASPLITS,CVDAREA+5        EDIT
         L     R1,CISPLTS               LOAD NUM OF CI SPLITS
         CVD   R1,CVDAREA                CONVERT TO DECIMAL
         MVC   CISPLITS,EDWRD2           MOVE IN EDIT WORD
         ED    CISPLITS,CVDAREA+5        EDIT
         L     R1,CISZE                 LOAD CI SIZE
         CVD   R1,CVDAREA                CONVERT TO DECIMAL
         MVC   CISIZE,EDWRD2             MOVE IN EDIT WORD
         ED    CISIZE,CVDAREA+5          EDIT
         L     R1,MAXRECL               LOAD MAX LRECL
         CVD   R1,CVDAREA                CONVERT TO DECIMAL
         MVC   MAXLRECL,EDWRD2           MOVE IN EDIT WORD
         ED    MAXLRECL,CVDAREA+5        EDIT
         L     R1,XTNTS                 LOAD NUM OF EXTENTS
         CVD   R1,CVDAREA                CONVERT TO DECIMAL
         MVC   NUMXTNTS,EDWRD1           MOVE IN EDIT WORD
         ED    NUMXTNTS,CVDAREA+6        EDIT
         SPACE 2
         AP    PAGECNT,=P'1'            INCREMENT PAGE COUNTER
         MVC   PAGENO,EDWRD1            MOVE EDIT PATTERN
         ED    PAGENO,PAGECNT           INSERT PAGE NUMBER
         PUT   PRINT,HDR1
         SPACE 2
         PUT   PRINT,FINAL
         CLOSE (PRINT,,SORTOUT)
         SPACE 2
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
*********************************************************************
*                                                                   *
*              BUILD PRINT RECORD FOR LAST CI PROCESSED             *
*                                                                   *
*********************************************************************
         SPACE 2
RESET    EQU   *
         ST    R7,TOTREC
         L     R1,LASTCI                PREVIOUS CI
         ST    R3,LASTCI                AND UPDATE
         CVD   R1,CVDAREA
         MVC   NUMCI+1(6),=X'402020202120'
         ED    NUMCI+1(6),CVDAREA+5
         MVC   KEYREC(6),NUMCI+1
         PUT   KEYOUT,KEYREC
         L     R1,RECAREA               ADDRESS OF RECORD
         LTR   R1,R1                    IS THIS EOF
         BZ    SKIPNEXT
         A     R1,RKP
         L     R2,KEYLEN                ADDRESS FOR KEY
         EX    R2,MOVEKEY               MOVE HIGHKEY
         MVC   LOWKEY,HIGHKEY
         SPACE 2
SKIPNEXT XR    R4,R4                    (R5 HAS TOTAL USED BYTES)
         M     R4,=F'100'
         DR    R4,R6                    % USED
         LTR   R5,R5
         BZ    ENDLOOP
         LA    R1,PERCENT
         SPACE 2
LOOPER   MVI   0(R1),C'*'
         LA    R1,1(R1)
         BCT   R5,LOOPER
         SPACE 2
ENDLOOP  L     R1,TOTREC                TOTAL USED
         CVD   R1,CVDAREA
         MVC   TOTAL,EDWRD2
         ED    TOTAL(7),CVDAREA+5
         PUT   SORTIN,LINE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         LA    R7,1                     RESET RECORDS IN THIS CI
         MVC   PREVLEN,RECLEN
         L     R5,RECLEN
         MVI   HADONE,X'00'            RESET HAD ONE FLAG
         L     R6,MAXPOS
         SH    R6,=H'3'                 GOT FIRST RECORD
         BR    R9
         EJECT
*********************************************************************
*                                                                   *
*              ERROR ROUTINES                                       *
*                                                                   *
*********************************************************************
         SPACE 2
OPENERR  EQU   *
         SHOWCB ACB=VSAMACB,LENGTH=4,AREA=ERROR,FIELDS=(ERROR)
         SPACE 2
WRITERR  OPEN  (PRINT,OUTPUT)
         L     R1,ERROR
         CVD   R1,CVDAREA
         MVC   ERRMSG(4),EDWRD1
         ED    ERRMSG(4),CVDAREA+6
         PUT   PRINT,PROCMSG
         CLOSE (PRINT)
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         LA    R15,8
         BR    R14
POINTERR EQU   *
         SHOWCB RPL=RPL1,LENGTH=4,AREA=ERROR,FIELDS=(FDBK)
         MVC   TYPERR,=CL26' POINT FAILED, FEEDBACK='
         B     WRITERR
         SPACE 2
GETERR   EQU   *
         SHOWCB RPL=RPL1,LENGTH=4,AREA=ERROR,FIELDS=(FDBK)
         MVC   TYPERR,=CL26' GET FAILED,   FEEDBACK='
         B     WRITERR
         SPACE 2
SORTERR  EQU   *
         ST    R15,ERROR
         MVC   TYPERR,=CL26' SORT FAILED, REGISTER 15='
         B     WRITERR
         EJECT
*********************************************************************
*                                                                   *
*              EXECUTED INSTRUCTIONS, STORAGE AREAS, ETC.           *
*                                                                   *
*********************************************************************
         SPACE 2
MVEPSWD  MVC   PASSWORD+1(0),2(R2)     **** EXECUTED ONLY ***
MOVEKEY  MVC   HIGHKEY(0),0(R1)        **** EXECUTED ONLY ***
CVDAREA  DS    D
ERROR    DS    F
RECAREA  DS    F
TOTREC   DS    F
MAXPOS   DS    F
LASTCI   DC    F'0'
CINV     DS    F
LRECL    DS    F
KEYLEN   DS    F
RKP      DS    F
*
RBA      DS    F
RECLEN   DS    F
PREVLEN  DC    F'-1'               ASSURE NO MATCH INITIALLY
*
STATS    DS    0D                  STATS FROM SHOWCB
CASPLTS  DS    F                     CA SPLITS
CISPLTS  DS    F                     CI SPLITS
CISZE    DS    F                     CI SIZE
MAXRECL  DS    F                     MAXIMUM LOG. RECORD LENGTH
XTNTS    DS    F                     NUMBER OF EXTENTS
*
HADONE   DS    X
BYTE0    DC    X'00'
INIT     DS    X
ALLREC   DC    PL4'0'
PREVFD   DC    PL4'-1'
PAGECNT  DC    PL2'0'                   PAGE COUNTER
SAVEFD   DS    CL4
PACKFD   DS    PL4
COUNTER  DC    PL2'0'
EDWRD1   DC    X'40202120'              FIRST EDIT WORD
EDWRD2   DC    X'4020206B202120'        SECOND EDIT WORD
PASSWORD DC    X'08',CL8' '             FILE PASSWORD (IF ANY)
         EJECT
*********************************************************************
*                                                                   *
*              PRINT LINES AND HEADINGS                             *
*                                                                   *
*********************************************************************
         SPACE 2
HDR1     DS    0CL133
         DC    CL2'1'
         DC    CL51'SPACE UTILIZATION HISTOGRAM FOR DATA COMPONENT -'
DSNAME   DC    CL44' '
         DC    CL28' '
         DC    CL4'PAGE'
PAGENO   DC    CL4' '
         SPACE 2
HDR2     DS    0CL133
         DC    CL2'0'
         DC    CL7'CONTROL'
         DC    CL5' '
         DC    CL100'++++0++++1++++1++++2++++2++++3++++3++++4++++4++++5X
               ++++5++++6++++6++++7++++7++++8++++8++++9++++9++++*'
         DC    CL5' '
         DC    CL7'RECORDS'
         DC    CL7' '
         SPACE 2
HDR3     DS    0CL133
         DC    CL1' '
         DC    CL8'INTERVAL'
         DC    CL5' '
         DC    CL100'++++5++++0++++5++++0++++5++++0++++5++++0++++5++++0X
               ++++5++++0++++5++++0++++5++++0++++5++++0++++5++++*'
         DC    CL5' '
         DC    CL7' '
         DC    CL7' '
         SPACE 2
LINE     DS    0CL133
         DC    CL2' '
NUMCI    DC    CL7' '
         DC    CL5' '
PERCENT  DC    CL100' '
         DC    CL5' '
TOTAL    DS    CL7
         DC    CL7' '
         SPACE 2
FINAL    DS    0CL133
         DC    CL22'-TOTAL RECORDS'
TOTRECS  DS    CL10
         DC    CL11'  CA SPLITS'
CASPLITS DS    CL7
         DC    CL11'  CI SPLITS'
CISPLITS DS    CL7
         DC    CL9'  CI SIZE'
CISIZE   DS    CL7
         DC    CL11'  MAX LRECL'
MAXLRECL DS    CL7
         DC    CL9'  EXTENTS'
NUMXTNTS DS    CL4
         DC    CL18' '
         SPACE 2
SKIPLINE DS    0CL133
         DC    CL19' <<<<<<<<<<<<<<<<<<'
SKIPPED  DS    CL7
         DC    CL107' CONTROL INTERVAL(S) WITH NO RECORDS >>>>>>>>>>>>'
         SPACE 2
PROCMSG  DS    0CL133
TYPERR   DC    CL26' OPEN ERROR RETURN CODE ='
ERRMSG   DS    CL4
         DC    CL103' '
         SPACE 2
KEYREC   DS    0CL133
         DC    CL7' '
LOWKEY   DC    CL62' '
         DC    CL1' '
HIGHKEY  DC    CL62' '
         DC    CL1' '
         EJECT
*********************************************************************
*                                                                   *
*              SORT FIELDS, ETC.                                    *
*                                                                   *
*********************************************************************
         SPACE 2
         DS    0F
SORTCON  DC    X'80',AL3(SORTPARM)
         DS    CL2
SORTPARM DC    AL2(SORTLEN)
         DC    A(SORTSTAT)
         DC    A(SORTSTND)
         DC    A(RECSTAT)
         DC    A(RECSTAND)
         DC    A(0)
         DC    A(0)
SORTLEN  EQU   *-SORTPARM-2
SORTSTAT DC    C' SORT FIELDS=(1,9,CH,A)'
SORTSTND DC    C' '
RECSTAT  DC    C' RECORD TYPE=F,LENGTH=133'
RECSTAND DC    C' '
         EJECT
*********************************************************************
*                                                                   *
*              LITERALS                                             *
*                                                                   *
*********************************************************************
         SPACE 2
         LTORG
         EJECT
*********************************************************************
*                                                                   *
*              ACB'S, DCB'S AND RPL'S                               *
*                                                                   *
*********************************************************************
         SPACE 2
         DS    0F
EXLST    EXLST  EODAD=EODAD
         SPACE 2
VSAMACB  ACB   AM=VSAM,DDNAME=VSAMDS,EXLST=EXLST,PASSWD=PASSWORD
         SPACE 2
RPL1     RPL   ACB=VSAMACB,AREA=RECAREA,AREALEN=4,ARG=BYTE0,KEYLEN=1,  X
               OPTCD=(LOC,GEN,KGE)
         SPACE 2
SORTOUT  DCB   DDNAME=SORTOUT,LRECL=133,MACRF=GM,DSORG=PS,RECFM=FB,    X
               EODAD=EODAD2
         SPACE 2
SORTIN   DCB   DDNAME=SORTIN,LRECL=133,MACRF=PM,DSORG=PS,RECFM=FB
         SPACE 2
PRINT    DCB   DDNAME=PRINT,LRECL=133,MACRF=PM,DSORG=PS,RECFM=FBA
         SPACE 2
KEYOUT   DCB   DDNAME=KEYOUT,LRECL=133,MACRF=PM,DSORG=PS,RECFM=FBA
         EJECT
*********************************************************************
*                                                                   *
*              DSECTS                                               *
*                                                                   *
*********************************************************************
         SPACE 2
         IFGACB
         SPACE 2
         END

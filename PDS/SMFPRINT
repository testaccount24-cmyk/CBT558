         TITLE '***** PRINT CERTAIN FIELDS FROM AN SMF STRIP FILE ***'
*        PRINT NOGEN
SMFPRINT CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFPRINT,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFPRINT'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
*                                                                    *
*                                                                    *
*  AUTHOR R. THORNTON SEP 1985                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,INPUT1) OPEN FILES
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
         USING SMFRECD,R6
MAINLINE GET   INPUT1,SMFRCD+4         GET INPUT AREA ADDRESS
         LA    R6,SMFRCD               POINT TO INPUT AREA
         MVC   SMFRDW(2),INPUT1+82     MOVE LENGTH TO OUTPUT
         XC    SMFRDW+2(2),SMFRDW+2    CLEAR RDW BYTES 2-3
         LA    R6,SMFRCD               POINT TO SMF RECORD
         SR    R1,R1                   CLEAR FOR INSERT
         IC    R1,SMFTYPE              GET RECORD TYPE
         CVD   R1,DBLWD                CONVERT TO DECIMAL
         UNPK  PRTTYPE,DBLWD           UNPACK TO PRINT
         OI    PRTTYPE+2,C'0'          CLEAR SIGN
         ICM   R1,15,SMFTIME           GET RECORD CREATION TIME
         BAL   R9,TIMECVRT             GO CONVERT CREATION TIME
         MVC   PRTTIME,WORK            MOVE TIME TO PRINT
         MVC   PRTDATE,EDDATE          EDIT WORD FOR DATE
         ED    PRTDATE,SMFDATE         EDIT CREATION DATE
         MVC   PRTSYSID,SMFSYSID       SYSTEM ID TO PRINT
         MVC   PRTJBN,SMFJBN           JOB NAME TO PRINT
         ICM   R1,15,SMFRST            GET READER START TIME
         BAL   R9,TIMECVRT             GO CONVERT START TIME
         MVC   PRTRST,WORK             MOVE TIME TO PRINT
         MVC   PRTRSD,EDDATE           EDIT WORD FOR DATE
         ED    PRTRSD,SMFDATE          EDIT READER START DATE
         BAL   R9,PRINT                WRITE PRINT LINE
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        TIME CONVERSION ROUTINE                                      *
***********************************************************************
*
TIMECVRT CVD   R1,DBLWD                CONVERT IT TO DECIMAL
         DP    DBLWD,=P'100'           COMPUTE WHOLE SECONDS
         UNPK  WORK+9(2),DBLWD+6(2)    UNPACK DECIMAL PLACES
         OI    WORK+10,C'0'            CLEAR SIGN
         MVI   WORK+8,C'.'             MOVE DECIMAL POINT
         ZAP   DBLWD,DBLWD(6)          RIGHT JUSTIFY SECONDS
         DP    DBLWD,=P'60'            COMPUTE WHOLE MINUTES
         UNPK  WORK+6(2),DBLWD+6(2)    UNPACK SECONDS
         OI    WORK+7,C'0'             CLEAR SIGN
         MVI   WORK+5,C':'             DELIMIT MIN:SEC
         ZAP   DBLWD,DBLWD(6)          RIGHT JUSTIFY MINUTES
         DP    DBLWD,=P'60'            COMPUTE WHOLE HOURS
         UNPK  WORK+3(2),DBLWD+6(2)    UNPACK MINUTES
         OI    WORK+4,C'0'             CLEAR SIGN
         MVI   WORK+2,C':'             DELIMIT HRS:MIN
         UNPK  WORK(2),DBLWD(6)        UNPACK HOURS
         OI    WORK+1,C'0'             CLEAR SIGN
         BR    R9                      RETURN
*
***********************************************************************
*        PRINT SUBROUTINE                                             *
***********************************************************************
*
PRINT    PUT   SYSPRINT,PRTLINE        WRITE THE PRINT LINE
         MVI   PCC,C' '                SET SINGLE SPACING
         MVC   PRT,PCC                 CLEAR PRINT LINE
         BR    R9                      RETURN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (SYSPRINT,,INPUT1)      CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
RETCODE  DC    F'0'                    RETURN CODE
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DC    CL1' '                  CARRIAGE CONTROL BYTE
PRT      DS    0CL132                  PRINT DATA
         DC    CL5'TYPE='
PRTTYPE  DS    CL3                     SMF RECORD TYPE
         DC    CL7', TIME='
PRTTIME  DS    CL11                    RECORD CREATION TIME
         DC    CL7', DATE='
PRTDATE  DS    CL7                     RECORD CREATION DATE
         DC    CL9', SYSTEM='
PRTSYSID DS    CL4                     SYSTEM ID
         DC    CL6', JOB='
PRTJBN   DS    CL8                     JOB NAME
         DC    CL10', RDRTIME='
PRTRST   DS    CL11                    READER START TIME
         DC    CL10', RDRDATE='
PRTRSD   DS    CL7                     READER START DATE
         DC    CL27' '
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
BLANX    DC    CL133' '                CONSTANT BLANKS
EDDATE   DC    XL7'40202061202020'     EDIT MASK FOR DATE
WORK     DS    CL11                    WORK AREA
TRCHAR   DC    CL16'0123456789ABCDEF'  TRANSLATE TABLE
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
*
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
*
*
*        SMF RECORD DSECT
*
SMFRECD  DSECT ,                       *** S M F D A T A R E C O R D S
SMFRDW   DS    H                       RECORD DESCRIPTOR WORD
         DS    H                       LAST 2 BYTES OF RDW
SMFDATA  DS    0CL14                   *** SMF DATA RECORD HEADER ***
SMFSYS   DS    XL1                     0=MVT, 2=MVS
SMFTYPE  DS    XL1                     RECORD TYPE
SMFTIME  DS    XL4                     TIME 1/100 SEC
         DS    XL1
SMFDATE  DS    PL3                     DATE 00YYDDD
SMFSYSID DS    CL4                     SYSTEM ID
SMFJBN   DS    CL8                     JOB NAME
SMFRST   DS    XL4                     READER START TIME
         DS    XL1
SMFRSD   DS    PL3                     READER START DATE
SMFUIF   DS    CL8                     USER IDENTIFICATION
SMFPRINT CSECT
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
SMFRCD   DS    CL32768                 SMF RECORD INPUT AREA
         END

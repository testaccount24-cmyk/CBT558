         TITLE '*** SVCDREF2: REFORMAT SVCDUMP INFORMATION ***'
         PRINT NOGEN
SVCDREF2 SVLNK R3
***********************************************************************
* REFORMATS THE DEL.MRGDUMP.SORT INTO 80-BYTE RECORDS FOR READABILITY *
* AND EASE OF USE WITH BROWSE.                                        *
*                                                                     *
* AUTHOR R THORNTON SEP 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN FILES AND WRITE LEGEND LINES                   *
***********************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
         LA    R12,LEGEND               POINT TO LEGEND TABLE
WRTLGND  PUT   OUTPUT1,0(R12)           WRITE LEGENT LINE
         LA    R12,L'LEGEND(R12)        STEP TO NEXT LINE
         CLI   0(R12),X'FF'             ALL THROUGH?
         BNE   WRTLGND                  NO, CONTINUE
***********************************************************************
* MAINLINE PROCESSING: REFORMAT THE 230-BYTE INPUT RECORDS INTO       *
* SEVERAL 80-BYTE RECORDS.                                            *
***********************************************************************
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         PUT   OUTPUT1,HEADER          WRITE HEADER RECORD
         MVC   OTRECD,BLANKS           CLEAR FOR FORMAT
         MVC   OTDMPNBR,INDMPNBR       DUMP NBR
         MVC   OTDSNAME,INDSNAME       DSNAME ABBREVIATION, LAST DUMP
         MVC   OTLSTDAT,INLSTDAT       YYMMDD DATE OF LAST DUMP
         MVC   OTLSTTIM,INLSTTIM       HHMM TIME OF LAST DUMP
         MVC   OTFSTDAT,INFSTDAT       MMDD DATE OF 1ST DUMP
         MVC   OTTOTAL,INTOTAL         TOTAL DUMPS
         MVC   OTADUMPS,INADUMPS       TOTAL SYSA DUMPS
         MVC   OTBDUMPS,INBDUMPS       TOTAL SYSB DUMPS
         MVC   OTREPDAT,INREPDAT       PROBLEM REPORT DATE MMDD
         MVC   OTIBMNBR,INIBMNBR       IBM PROBLEM NUMBER
         MVC   OTPTFNBR,INPTFNBR       FIXING PTF NUMBER
         MVC   OTAPLYA,INAPLYA         DATE PTF ON SYSA MMDD
         MVC   OTAPLYB,INAPLYB         DATE PTF ON SYSB MMDD
         PUT   OUTPUT1,OTRECD          WRITE FIRST DUMP LINE
         MVC   OTRECD,BLANKS           CLEAR FOR FORMAT
         MVC   OTRECD+5(67),INTITLE    DUMP TITLE
         PUT   OUTPUT1,OTRECD          WRITE FIRST TITLE LINE
         MVC   OTRECD,BLANKS           CLEAR FOR FORMAT
         CLC   INTITLE+67(40),BLANKS   ANY MORE TITLE?
         BE    CHEKNOTE                NO
         MVC   OTRECD+5(49),INTITLE+67 DUMP TITLE, PART 2
         PUT   OUTPUT1,OTRECD          WRITE SECOND TITLE LINE
         MVC   OTRECD,BLANKS           CLEAR FOR FORMAT
CHEKNOTE CLC   INNOTE,BLANKS           ANY NOTE?
         BE    LASTLINE                NO
         MVC   OTRECD+5(60),INNOTE     NOTE
         PUT   OUTPUT1,OTRECD          WRITE HEADER RECORD
LASTLINE PUT   OUTPUT1,COMMENT         WRITE COMMENT SEPARATOR LINE
         B     MAINLINE                GO READ NEXT RECORD
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
BLANKS   DC    CL80' '                 BLANKS
COMMENT  DC    CL80'*'                 COMMENT RECORD
HEADER   DS    0CL80                   HEADER RECORD
         DC    CL5'*DMP '               DUMP NBR
         DC    CL7'DSNAME '             DSNAME ABBREVIATION, LAST DUMP
         DC    CL7'  DATE '             MMDD DATE OF LAST DUMP
         DC    CL5'TIME '               MMDD DATE OF 1ST DUMP
         DC    CL5'FRST '               MMDD DATE OF 1ST DUMP
         DC    CL5'TOTL '               TOTAL DUMPS
         DC    CL5'SYSA '               TOTAL SYSA DUMPS
         DC    CL5'SYSB '               TOTAL SYSB DUMPS
         DC    CL5'REPT '               PROBLEM REPORT DATE MMDD
         DC    CL6' IBM# '              IBM PROBLEM NUMBER
         DC    CL8'PTF-NBR '            FIXING PTF NUMBER
         DC    CL5'APLA '               DATE PTF ON SYSA MMDD
         DC    CL5'APLB '               DATE PTF ON SYSB MMDD
         DC    CL7' '
*
OTRECD   DS    0CL80                   OUTPUT RECORD
OTDMPNBR DS    CL4                      DUMP NBR IF 1ST CARD OF DUMP
         DS    CL1
OTDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
         DS    CL1
OTLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
         DS    CL1
OTLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
         DS    CL1
OTFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
         DS    CL1
OTTOTAL  DS    CL4                      TOTAL DUMPS
         DS    CL1
OTADUMPS DS    CL4                      TOTAL SYSA DUMPS
         DS    CL1
OTBDUMPS DS    CL4                      TOTAL SYSB DUMPS
         DS    CL1
OTREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
         DS    CL1
OTIBMNBR DS    CL5                      IBM PROBLEM NUMBER
         DS    CL1
OTPTFNBR DS    CL7                      FIXING PTF NUMBER
         DS    CL1
OTAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
         DS    CL1
OTAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
         DS    CL8
*
INRECD   DS    0CL236                  INPUT RECORD
INDMPNBR DS    CL4                      DUMP NBR
INDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
INLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
INLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
INFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
INTOTAL  DS    CL4                      TOTAL DUMPS
INADUMPS DS    CL4                      TOTAL SYSA DUMPS
INBDUMPS DS    CL4                      TOTAL SYSB DUMPS
INREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
INIBMNBR DS    CL5                      IBM PROBLEM NUMBER
INPTFNBR DS    CL7                      FIXING PTF NUMBER
INAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
INAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
INTITLE  DS    CL116                    DUMP TITLE
INNOTE   DS    CL60                     NOTE
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=236
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
         LA    R12,LEGEND               POINT TO LEGEND LINE TABLE
LEGEND   DS    0CL80                    LEGEND OUTPUT LINES
         DC    CL50'**************************************************'
         DC    CL30'*********************         '
         DC    CL50'* DMP     SEQUENTIALLY ASSIGNED DUMP NUMBER FOR RE'
         DC    CL30'FERENCE.            *         '
         DC    CL50'* DSNAME  ABBREVIATED DSNAME OF THE TAPE DATASET C'
         DC    CL30'REATED BY R99SYS_U: *         '
         DC    CL50'*         SYSTEM.SYS_.DUMP_.G____V00 FILLED IN FRO'
         DC    CL30'M THE ABBREVIATION. *         '
         DC    CL50'* DATE    DATE OF THE MOST RECENT DUMP AS YYMMDD. '
         DC    CL30'                    *         '
         DC    CL50'* TIME    TIME OF THE MOST RECENT DUMP AS HHMM.   '
         DC    CL30'                    *         '
         DC    CL50'* FRST    DATE OF THE FIRST DUMP FOUND WITH THIS  '
         DC    CL30'TITLE AS MMDD.      *         '
         DC    CL50'* TOTL    TOTAL DUMPS FOUND WITH THIS TITLE.      '
         DC    CL30'                    *         '
         DC    CL50'* SYSA    TOTAL DUMPS ON SYSA WITH THIS TITLE.    '
         DC    CL30'                    *         '
         DC    CL50'* SYSB    TOTAL DUMPS ON SYSB WITH THIS TITLE.    '
         DC    CL30'                    *         '
         DC    CL50'* REPT    DATE REPORTED TO IBM AS A PROBLEM AS MMD'
         DC    CL30'D.                  *         '
         DC    CL50'* IBM#    IBM PROBLEM NUMBER ASSIGNED.            '
         DC    CL30'                    *         '
         DC    CL50'* PTF-NBR PTF NUMBER FOR THE FIX TO THE PROBLEM.  '
         DC    CL30'                    *         '
         DC    CL50'* APLA    DATE PTF WENT INTO PRODUCTION ON SYSA.  '
         DC    CL30'                    *         '
         DC    CL50'* APLB    DATE PTF WENT INTO PRODUCTION ON SYSB.  '
         DC    CL30'                    *         '
         DC    CL50'* TITLE   UP TO 116 CHARACTERS OF THE DUMP TITLE  '
         DC    CL30'USING 1 OR 2 LINES. *         '
         DC    CL50'* NOTE    UP TO 60 CHARACTERS OF TEXT CONCERNING T'
         DC    CL30'HE PROBLEM.         *         '
         DC    CL50'**************************************************'
         DC    CL30'*********************         '
         DC    CL80'*'
         DC    CL80'*'
         DC    X'FF'
         END

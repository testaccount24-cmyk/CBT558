         TITLE 'WTO    - OPERATIONS COMMUNICATION'
*        REGISTER USAGE FOLLOWS...                                    *
R0       EQU   0                   WORK REG                           *
R1       EQU   1                   PTR TO INPUT PARM                  *
R2       EQU   2                   WORK REG                           *
R11      EQU   11                  PTR TO FIRST MESSAGE IF NOZERO PARM*
R12      EQU   12                  BASE REG                           *
R13      EQU   13                  PTR TO IPOSAVE                     *
R14      EQU   14                  LINKAGE REG                        *
R15      EQU   15                  LINKAGE REG                        *
ML       EQU   70                  MSG LENGTH                         *
***********************************************************************
WTOMSG   CSECT                     BEGINING OF PROGRAM
         USING *,R12               SET UP BASE REG
         SAVE  (14,12),,IPO.WTO..&SYSDATE    SAVE REGESTERS
         LR    R12,R15             SET UP ADDRESSABILITY
         LR    R2,R13              SAVE ADDR OF PREVIOUS SAVEAREA
         LA    R13,IPOSAVE         POINT TO CURRENT SAVEAREA
         ST    R13,8(R2)           LINK PREVIOUS SAVEAREA TO IPOSAVE
         ST    R2,4(R13)           LINK IPOSAVE TO PREVIOUS SAVEAREA
         LR    R11,R1              PTR PARM ADDR - FIRST COMMAND
         L     R11,0(R11)          SAVE ADDR OF PARM IN R11
IPOPARM  LH    R2,0(R11)           LENGTH TO R2
         C     R2,=F'0'            PARM LENGTH = 0?
         BE    IPOGETIN            YES - GET RECORD FROM SYSIN
         C     R2,=A(ML)           COMPARE LENGTH OF TO MAX LENGTH
         BNH   IPOMOVE             LENGTH OF PARM OKAY
         LA    R2,ML               RESET PARM LENGTH TO MAX
IPOMOVE  MVC   IPOMSG,=CL(ML)' '   CLEAR OUT MSG AREA
         BCTR  R2,0                LENGTH-1 FOR MVC
         EX    R2,IPOMVC           MOVE PARM INTO MSGAREA
         B     IPOGOTIN            PROCESS FIRST MSG
IPOGETIN TM    INPUT+48,X'10'      INPUT FILE OPEN?
         BO    IPOGET              BR IF SO
         OPEN  (INPUT)             OPEN IT
         TM    INPUT+48,X'10'      DID IT OPEN
         BZ    IPOEND              NO - NO VERICATION (NO WTOR)
IPOGET   GET   INPUT               READ RECORD FROM SYSIN
         MVC   IPOMSG,0(R1)        MOVE ML CHAR OF INPUT TO MSG AREA
IPOGOTIN CLC   IPOMSG,=CL(ML)' '   TEST FOR BLANK MESSAGE
         BE    IPOGETIN            SKIP BLANK RECORD
         MVC   IPOWTOL+4(ML),IPOMSG PLUG WTO MSG
         SMODE KEY=0,STATE=SUP
         WTO   MF=(E,IPOWTOL)      DISPLAY MSG
         SMODE KEY=8,STATE=PROB
         TM    OUTPUT+48,X'10'     OUTPUT FILE OPEN?
         BO    IPOOUT              BR IF SO
         OPEN  (OUTPUT,(OUTPUT))   OPEN IT
         TM    OUTPUT+48,X'10'     DID OUTPUT OPEN
         BZ    IPOOUTNO            NO - SKIP OUTPUT
IPOOUT   PUT   OUTPUT,IPOMSG       SAME MSG TO OUTPUT FILE
IPOOUTNO B     IPOGETIN            READ NEXT RECORD
IPOEOD   CLOSE (INPUT)             YES - REPEAT MSG - CLOSE INPUT
IPOEND   L     R13,IPOSAVE+4       RESTORE R13
         RETURN (14,12),T,RC=0     RETURN TO MVS
OUTPUT   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
INPUT    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),EODAD=IPOEOD
IPOMVC   MVC   IPOMSG(0),2(R11)    EXEC INSTRUCTION TO MOVE PARM
IPOSAVE  DS    18F                 IPOSAVE AREA
IPORPECB DC    F'0'                ECB FOR WTOR
IPOAREA  DC    0F'0',AL2(ML+4,0)   LENGTH FOR OUTPUT IF RECFM=VB
IPOMSG   DC    CL(ML)' '           STORAGE FOR ONE MSG
IPOPAD   DC    CL(80-ML)' '        PAD TO 80 CHARACTERS
IPOREPLY DS    CL1                 REPLY FOR WTO
IPOWTOL  WTO   '1234567891123456789212345678931234567894123456789512345X
               678961234567897',ROUTCDE=(1),DESC=(1),MF=L
         LTORG                   , LTORG NEEDED FOR ADDRESSABILITY
         END                     , END OF PROGRAM

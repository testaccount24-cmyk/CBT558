         TITLE '*** LKEDANAL: PRINT LIST OF MODULES LINKED ***'
LKEDANAL CSECT
*********************************************************************
* SCANS THE SYSPRINT OUTPUT FROM A LINKEDIT JOB WITH MANY MODULES   *
* LINKED LOOKING FOR ERROR MESSAGES. WHEN THE LAST MESSAGE FOR A    *
* FOR A MODULE HAS BEEN READ, A RECORD IS WRITTEN TO THE OUTPUT     *
* FILE SHOWING THE MEMBER NAME AND ERROR MESSAGE LEVEL FOUND.       *
*                                                                   *
* AUTHOR R THORNTON                                                 *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING LKEDANAL,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'LKEDANAL'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES, POSITION INPUT TO FIRST IEW MESSAGE, GET       *
*        STORAGE FOR MESSAGES                                       *
*********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT,PRINT2,OUTPUT,PRINT3,OUTPUT) OPEN
         OPEN  (PRINT4,OUTPUT,INPUT1)  OPEN FILES
         PUT   PRINT1,PRT1LINE         WRITE HEADING LINE TO PRINT1
         MVC   PRT1LINE,BLANKS         CLEAR PRINT LINE
         PUT   PRINT2,PRT2LINE         WRITE HEADING LINE TO PRINT2
         MVC   PRT2LINE,BLANKS         CLEAR PRINT LINE
         PUT   PRINT3,PRT3LINE         WRITE HEADING LINE TO PRINT3
         MVC   PRT3LINE,BLANKS         CLEAR PRINT LINE
         PUT   PRINT4,PRT4LINE         WRITE HEADING LINE TO PRINT4
         MVC   PRT4LINE,BLANKS         CLEAR PRINT LINE
INITLOOP BAS   R9,GETIN                READ INPUT RECORD
         CLC   INREC+1(8),=CL8'IEW2278I' INVOCATION MESSAGE?
         BNE   INITLOOP                NO, LOOP UNTIL FOUND
         STORAGE OBTAIN,LENGTH=121000  GET STORAGE FOR RECORD TABLE
         ST    R1,FRSTRECA             SAVE RECORD TABLE ADDRESS
         A     R1,=F'121000'           COMPUTE END ADDRESS
         ST    R1,LASTRECA             SAVE END ADDRESS
         MVC   P1CURRA,P1FRSTA         RESET BEGIN ADDRESS
         MVC   P2CURRA,P2FRSTA         RESET BEGIN ADDRESS
         MVC   P3CURRA,P3FRSTA         RESET BEGIN ADDRESS
*********************************************************************
MAINSTRT MVC   CURRECA,FRSTRECA        RESET RECORD TABLE ADDRESS
         MVC   PRT4LINE,BLANKS         CLEAR PRINT LINE
MAINREAD BAS   R9,GETIN                READ INPUT RECORD
         CLC   INREC+1(3),=CL3'IEW'    THIS AN IEW MESSAGE?
         BNE   MAINREAD                NO
         CLC   INREC+4(5),=CL5'2231I'  IS IT LAST MESSAGE FOR MEMBER?
         BE    MLENDMBR                YES
         CLC   INREC+4(5),=CL5'2850I'  IS IT THE SAVED MESSAGE?
         BE    MLSAVED                 YES
         CLI   INREC+8,C'I'            THIS AN INFO ONLY MESSAGE?
         BE    MAINREAD                YES
         CLI   INREC+8,C'W'            THIS A "W" LEVEL ERROR?
         BNE   MLTRYE                  NO
         CLC   INREC+4(5),=CL5'2646W'  RMODE CONFLICT MESSAGE?
         BE    MLARCON                 YES
         CLC   INREC+4(5),=CL5'2651W'  AMODE CONFLICT MESSAGE?
         BNE   MLWTSTU                 NO
MLARCON  OI    PRT4ERRW,1              SHOW AMODE/RMODE CONFLICT
         B     MAINREAD                GO READ NEXT MSG
MLWTSTU  CLC   INREC+4(5),=CL5'2454W'  SYMBOL UNRESOLVED MESSAGE?
         BNE   MLOTW                   NO
         OI    PRT4ERRW,2              INDICATE UNRESOLVED SYMBOLS
         B     MLSAVREC                GO SAVE RECORD
MLOTW    OI    PRT4ERRW,4              INDICATE OTHER W LEVEL ERRORS
         B     MLSAVREC                CONTINUE
MLTRYE   CLI   INREC+8,C'E'            THIS AN "E" LEVEL ERROR?
         BNE   MLTRYS                  NO
         MVI   PRT4ERRE,C'E'           YES, SET PRINT INDICATOR
         B     MLSAVREC                CONTINUE
MLTRYS   CLI   INREC+8,C'S'            THIS AN "S" LEVEL ERROR?
         BNE   MLOTHER                 NO
         MVI   PRT4ERRS,C'S'           YES, SET PRINT INDICATOR
         B     MLSAVREC                CONTINUE
MLOTHER  MVC   PRT4ERRO,INREC+8        SET ERROR INDICATOR IN PRINT
MLSAVREC L     R1,CURRECA              POINT TO CURRENT RECORD AREA
         MVC   0(121,R1),INREC         SAVE THE RECORD
         LA    R1,121(R1)              POINT TO NEXT RECORD
         ST    R1,CURRECA              UPDATE RECORD POINTER
         B     MAINREAD                CONTINUE
MLSAVED  LA    R1,INREC+15             POINT TO MEMBER NAME BYTE 1
         LA    R2,8                    MAX MEMBER NAME SIZE
         LA    R15,PRT4MEM             POINT TO PRINT MEMBER NAME
MLMOVE1  MVC   0(1,R15),0(R1)          MOVE 1 BYTE OF NAME
         LA    R1,1(R1)                MOVE OVER A BYTE IN INPUT RECORD
         CLI   0(R1),C' '              PAST END OF NAME?
         BE    MAINREAD                YES, CONTINUE
         LA    R15,1(R15)              MOVE OVER 1 BYTE IN PRINT
         BCT   R2,MLMOVE1              LOOP TO MOVE THE NAME
         B     MAINREAD                ALL MOVED, CONTINUE
MLENDMBR CLI   PRT4ERRE,C' '           ANY "E" LEVEL MSGS?
         BNE   MLEND4                  YES
         CLI   PRT4ERRS,C' '           ANY "S" LEVEL MSGS?
         BNE   MLEND4                  YES
         CLI   PRT4ERRO,C' '           ANY OTHER NON-W LEVEL MSGS?
         BNE   MLEND4                  YES
         CLI   PRT4ERRW,C' '           ANY LEVEL W MSGS?
         BE    MLENDNO                 NO NON-INFO MESSAGES
         TM    PRT4ERRW,1              ANY RMODE/AMODE MESSAGES?
         BZ    MLCKURSV                NO
         CLC   P2CURRA,P2LASTA         PRINT2 LINE IS FULL?
         BL    MLMOVP2                 NO
         PUT   PRINT2,PRT2LINE         YES, WRITE IT
         MVC   PRT2LINE,BLANKS         CLEAR IT
         MVC   P2CURRA,P2FRSTA         RESET POINTER
MLMOVP2  L     R1,P2CURRA              POINT TO CURRENT PRT2LINE MEM
         MVC   0(8,R1),PRT4MEM         MEMBER NAME TO PRT2LINE
         LA    R1,9(R1)                ADVANCE TO NEXT MEMBER NAME AREA
         ST    R1,P2CURRA              UPDATE CURRENT ADDRESS
MLCKURSV TM    PRT4ERRW,2              ANY UNRESOLVED SYMBOLS?
         BZ    MLFULLW                 NO
         CLC   P3CURRA,P3LASTA         PRINT2 LINE IS FULL?
         BL    MLMOVP3                 NO
         PUT   PRINT3,PRT3LINE         YES, WRITE IT
         MVC   PRT3LINE,BLANKS         CLEAR IT
         MVC   P3CURRA,P3FRSTA         RESET POINTER
MLMOVP3  L     R1,P3CURRA              POINT TO CURRENT PRT2LINE MEM
         MVC   0(8,R1),PRT4MEM         MEMBER NAME TO PRT2LINE
         LA    R1,9(R1)                ADVANCE TO NEXT MEMBER NAME AREA
         ST    R1,P3CURRA              UPDATE CURRENT ADDRESS
MLFULLW  TM    PRT4ERRW,4              ANY OTHER W-LEVEL MESSAGES?
         BZ    MAINSTRT                NO
         MVI   PRT4ERRW,C'W'           INDICATE W-LEVEL MESSAGES
         B     MLEND4                  CONTINUE
MLENDNO  CLC   P1CURRA,P1LASTA         PRINT1 LINE IS FULL?
         BL    MLMOVP1                 NO
         PUT   PRINT1,PRT1LINE         YES, PRINT IT
         MVC   PRT1LINE,BLANKS         CLEAR IT
         MVC   P1CURRA,P1FRSTA         RESET PRINT1 ADDRESS
MLMOVP1  L     R1,P1CURRA              POINT TO CURRENT PRT1LINE MEM
         MVC   0(8,R1),PRT4MEM         MOVE IN MEMBER NAME
         LA    R1,9(R1)                STEP TO NEXT
         ST    R1,P1CURRA              UPDATE PRT1LINE MEM PTR
         B     MAINSTRT                GO TO NEXT MEMBER
MLEND4   PUT   PRINT4,PRT4LINE         PRINT THE MEMBER LINE
         MVC   PRT4LINE,BLANKS         CLEAR PRINT LINE
         CLC   CURRECA,FRSTRECA        ANY RECORDS TO PRINT?
         BE    MAINSTRT                NO
         L     R12,FRSTRECA            YES, POINT TO FIRST RECORD
MLPRECD  MVC   PRT4LINE+10(121),0(R12)  MOVE RECORD TO PRINT LINE
         PUT   PRINT4,PRT4LINE         PRINT MESSAGE LINE
         MVC   PRT4LINE,BLANKS         CLEAR PRINT LINE
         LA    R12,121(R12)            STEP TO NEXT STORED RECORD
         C     R12,CURRECA             ANY MORE RECORDS TO PRINT?
         BL    MLPRECD                 YES
         B     MAINSTRT                NO, GO TO NEXT MEMBER
*********************************************************************
*             READ INPUT FILE SUBROUTINE                            *
*********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLC   P1CURRA,P1FRSTA         PRINT1 LINE IS EMPTY?
         BE    EOJCK2                  YES
         PUT   PRINT1,PRT1LINE         NO, PRINT IT
EOJCK2   CLC   P2CURRA,P2FRSTA         PRINT1 LINE IS EMPTY?
         BE    EOJCK3                  YES
         PUT   PRINT2,PRT2LINE         NO, PRINT IT
EOJCK3   CLC   P3CURRA,P3FRSTA         PRINT1 LINE IS EMPTY?
         BE    EOJCLOSE                YES
         PUT   PRINT3,PRT3LINE         NO, PRINT IT
EOJCLOSE CLOSE (INPUT1,,PRINT1,,PRINT2,,PRINT3,,PRINT4) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DS    F                       RETURN CODE
FRSTRECA DC    A(0)                    POINTER TO RECORD TABLE
LASTRECA DC    A(0)                    POINTER TO END OF RECORD TABLE
CURRECA  DC    A(0)                    POINTER TO CURRENT RECORD IN TBL
P1FRSTA  DC    A(PRT1LINE+1)           FIRST MEM AREA IN PRT1LINE
P1LASTA  DC    A(PRT1LINE+120)         LAST PRT1LINE MEM ADDR
P1CURRA  DC    A(0)                    ADDRESS OF CURRENT PRT1LINE MEM
P2FRSTA  DC    A(PRT2LINE+1)           FIRST MEM AREA IN PRT2LINE
P2LASTA  DC    A(PRT2LINE+120)         LAST PRT2LINE MEM ADDR
P2CURRA  DC    A(0)                    ADDRESS OF CURRENT PRT2LINE MEM
P3FRSTA  DC    A(PRT3LINE+1)           FIRST MEM AREA IN PRT3LINE
P3LASTA  DC    A(PRT3LINE+120)         LAST PRT3LINE MEM ADDR
P3CURRA  DC    A(0)                    ADDRESS OF CURRENT PRT3LINE MEM
BLANKS   DC    CL133' '                BLANKS
*
PRT1LINE DC    0CL133                  NO ERROR/WARNING LISTING
         DC    CL50' '
         DC    CL34'MEMBERS WITH NO WARNINGS OR ERRORS'
         DC    CL49' '
*
PRT2LINE DC    0CL133                  NO ERROR/WARNING LISTING
         DC    CL50' '
         DC    CL34'MEMBERS WITH AMODE/RMODE CONFLICTS'
         DC    CL49' '
*
PRT3LINE DC    0CL133                  NO ERROR/WARNING LISTING
         DC    CL51' '
         DC    CL31'MEMBERS WITH UNRESOLVED SYMBOLS'
         DC    CL51' '
*
PRT4LINE DS    0CL133                  PRINT LINE
PRT4MEM  DS    CL8                       MEMBER NAME
         DS    CL1
PRT4ERRW DS    CL1                       "W" LEVEL ERROR INDICATOR
         DS    CL1
PRT4ERRE DS    CL1                       "E" LEVEL ERROR INDICATOR
         DS    CL1
PRT4ERRS DS    CL1                       "S" LEVEL ERROR INDICATOR
         DS    CL1
PRT4ERRO DS    CL1                       OTHER ERROR INDICATOR
         DS    CL117
         ORG PRT4LINE
         DC    CL48' '
         DC    CL38'ALL OTHER LINKEDIT WARNINGS AND ERRORS'
         DC    CL47' '
         ORG
*
INREC    DS    CL160                    INPUT RECORD
*
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=133
PRINT2   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT2,                        X
               RECFM=FB,LRECL=133
PRINT3   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT3,                        X
               RECFM=FB,LRECL=133
PRINT4   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT4,                        X
               RECFM=FB,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ
         LTORG
LKEDANAL CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

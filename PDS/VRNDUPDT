         PRINT NOGEN
         TITLE 'RANDOM UPDATE OF THE NETMAN COMPONENT CONFIG FILE'
******************************************************************
* READS A SEQUENTIAL FILE OF RECORDS CONTAINING THE KEYS OF      *
* RECORDS ON THE COMPONENT CONFIGURATION FILE THAT ARE TO BE     *
* MARKED AS "OPERATIONAL", RATHER THAN "DOWN".                   *
*                                                                *
*          AUTHOR R THORNTON FEB 1990                            *
******************************************************************
VRNDUPDT SVLNK R3,R4
******************************************************************
*          OPEN THE FILES                                        *
******************************************************************
         OPEN  (SYSPRINT,OUTPUT,OUTPUT1,,INPUT1) OPEN FILES
******************************************************************
*          READ NEXT INPUT RECORD                                *
******************************************************************
MAINLINE GET   INPUT1,INRECD           READ IMS DATA RECORD
         AP    INRECS,=P'1'            INCREMENT COUNTER
******************************************************************
*          READ COMPONENT CONFIGURATION FILE FOR UPDATE          *
******************************************************************
         MVC   CCENTRY,INKEY           MOVE INPUT KEY TO ARGUMENT
         GET   RPL=NETMNRPL            READ THE RECORD TO BE ERASED
         LTR   R15,R15                 SUCCESSFUL?
         BZ    CHEKOPNL                YES,
         BAL   R9,MANIPERR             NO
******************************************************************
*        SEE IF UPDATE IS NEEDED. MODIFY IF SO                   *
******************************************************************
CHEKOPNL CLI   CCSTNFC,X'00'           IS IT OPERATIONAL?
         BE    MAINLINE                YES, DON'T UPDATE
         MVI   CCSTNFC,X'00'           NO, MAKE IT OPERATIONAL
******************************************************************
*        UPDATE (REWRITE) THE MODIFIED RECORD                    *
******************************************************************
         PUT   RPL=NETMNRPL            REWRITE THE RECORD
         LTR   R15,R15                 SUCCESSFUL?
         BZ    ATRNTOTL                YES
         BAL   R9,MANIPERR             NO
******************************************************************
*        PRINT THE KEY AND COUNT UPDATES                         *
******************************************************************
ATRNTOTL AP    UPDTRNS,=P'1'           INCREMENT COUNTER
         MVC   PRTLINE,BLANX           CLEAR PRINT LINE
         MVC   PRT(8),CCLOGNM          LOGICAL NAME
         MVI   PRT+9,C':'
         MVC   PRT+11(3),CCELCNR       LOCATION
         MVI   PRT+14,C'-'
         MVC   PRT+15(4),CCESYSM       SYSTEM
         MVI   PRT+19,C'-'
         MVC   PRT+20(5),CCETYPE       TYPE
         MVI   PRT+25,C'-'
         MVC   PRT+26(5),CCEUNIT       UNIT
         PUT   SYSPRINT,PRTLINE
         B     MAINLINE                GO GET ANOTHER RECORD
******************************************************************
*          THIS ROUTINE IS ENTERED WHEN A DATA MANIPULATION      *
*          MACRO RESULTS IN A VSAM ERROR                         *
******************************************************************
MANIPERR ST    R15,MANIP15             SAVE RETURN CODE
         ST    R9,MANIPER9             SAVE POINTER BACK
         CLI   MANIP15+3,4             RPL IN USE?
         BNE   MANIPSHO                NO
         BAL   R9,RPLUSED              YES
MANIPSHO SHOWCB RPL=NETMNRPL,AREA=MANFDBK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 DID SHOW WORK O.K.?
         BZ    MANIPRNF                YES
         BAL   R9,MODFAIL              NO
MANIPRNF CLI   MANFDBK+3,X'10'         IS IT NO RECORD FOUND?
         BNE   MANIPMSG                NO, OTHER ERROR
         BAL   R9,NOTFND               GO HANDLE
MANIPMSG WTO   '*** VSAM LOGICAL ERROR, R15=8, FEEDBACK=',             X
               ROUTCDE=11
         SR    R1,R1                   CLEAR FOR INSERT
         IC    R1,MANFDBK              PICK UP FEEDBACK CODE
         MH    R1,=H'50'               TIMES MESSAGE SIZE
         LA    R1,VSAM8(R1)            POINT TO MESSAGE
         MVC   MSGTEXT,0(R1)           MOVE MESSAGE TO WTO BUFFER
         LA    R1,MSGWTO               POINT TO MESSAGE
         SVC   35                      ISSUE WTO
         LA    R10,601                 SET ABEND CODE
         BAL   R9,ABEND                ABEND
******************************************************************
*          THIS ROUTINE IS ENTERED WHEN A MODCB OR SHOWCB MACRO  *
*          RESULTS IN A VSAM ERROR                               *
******************************************************************
MODFAIL  ST    R15,MOD15               SAVE RETURN CODE
         ST    R9,MODFAIL9             SAVE RETURN ADDRESS
         WTO   '***  VSAM ERROR IN EXECUTION OF MODCB OR SHOWCB ***'   X
               ROUTCDE=11
         LA    R10,604                 PUT IN ABEND CODE
         BAL   R9,ABEND                ABEND
******************************************************************
*          THIS ROUTINE IS ENTERED IF THE FILE IS IN USE         *
******************************************************************
RPLUSED  ST    R9,RPLUSED9           SAVE RETURN ADDRESS
         WTO   '*** RPL IN USE ***',ROUTCDE=11
         LA    R10,605
         BAL   R9,ABEND
******************************************************************
*          THIS ROUTINE IS ENTERED WHEN THE RECORD IS NOT FOUND  *
******************************************************************
NOTFND   ST    R9,NOTFND9            SAVE RETURN ADDRESS
         WTO   '*** RECORD NOT FOUND ***'
         LA    R10,606
         BAL   R9,ABEND
******************************************************************
*          THIS ROUTINE IS ENTERED WHEN A VSAM ERROR REQUIRES    *
*          ABNORMALLY TERMINATING THE PROGRAM.                   *
******************************************************************
ABEND    ST    R9,ABEND9             SAVE RETURN ADDRESS
         ABEND (10),DUMP
******************************************************************
*          END OF JOB: ALL INPUT RECORDS PROCESSED               *
******************************************************************
EOJ      MVC   PRTLINE,BLANX           CLEAR PRINT LINE
         MVC   PRT(7),=X'4020206B202120' EDIT WORD
         ED    PRT(7),INRECS           INPUT RECORD COUNT
         MVC   PRT+8(12),=C'RECORDS READ' IDENTIFY COUNT
         PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
         MVC   PRT,BLANX               CLEAR PRINT LINE
         MVC   PRT(7),=X'4020206B202120' EDIT WORD
         ED    PRT(7),UPDTRNS          UPDATED TRANSACTION COUNT
         MVC   PRT+8(20),=C'TRANSACTIONS UPDATED' IDENTIFY COUNT
         PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
         CLOSE (INPUT1,,INPUT1,,SYSPRINT) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
******************************************************************
*          DATA AREAS AND VSAM CONTROL                           *
******************************************************************
MANIP15  DS    A(0)
MANFDBK  DC    A(0)
MANIPER9 DC    A(0)                  RETURN ADDRESS
MODFAIL9 DC    A(0)                  RETURN ADDRESS
MOD15    DS    A(0)
MODFDBK  DC    A(0)
RPLUSED9 DC    A(0)                  RETURN ADDRESS
NOTFND9  DC    A(0)                  RETURN ADDRESS
ABEND9   DC    A(0)                  RETURN ADDRESS
INRECS   DC    PL3'0'                INPUT RECORD COUNT
UPDTRNS  DC    PL3'0'                UPDATED TRANSACTION COUNT
BLANX    DC    CL133' '              BLANKS
*
*
*
MSGWTO   DS    0F                      WTO LENGTH ERROR MESSAGE
         DC    AL2(MSGEND-MSGWTO)      AREA LENGTH
         DC    X'8000'                 MCS FLAGS
MSGTEXT  DC    CL125' '                TEXT OF MESSAGE
MSGEND   EQU   * END OF TEXT
         DC    X'0200'                 DESC=7
         DC    X'0020'                 ROUTCDE=11
*
*
*
PRTLINE  DS    0CL133          PRINT LINE
PRTCC    DC    CL1' '          CARRIAGE CONTROL
PRT      DC    CL132' '        PRINT LINE
*
*
*
VSAM8    DC    CL50'00: UNKNOWN ERROR'
         DC    CL50'04: END OF FILE'
         DC    CL50'08: DUPLICATE RECORD'
         DC    CL50'0C: KEY SEQUENCE CHECK ERROR'
         DC    CL50'10: RECORD NOT FOUND'
         DC    CL50'14: EXCLUSIVE BUFFER CONTROL BY ANOTHER'
         DC    CL50'18: VOLUME CANNOT BE MOUNTED'
         DC    CL50'1C: DATASET CANNOT BE EXTENDED'
         DC    CL50'20: NO RECORD WITH SPECIFIED RBA'
         DC    CL50'24: NO KEY RANGE CONTAINS RECORD'
         DC    CL50'28: NOT ENOUGH VIRTUAL STORAGE'
         DC    CL50'2C: WORK AREA TOO SMALL FOR RECORD'
         DC    CL50'30: INVALID TERMRPL REQUEST'
         DC    CL50'34: PREVIOUS REQUEST WAS TERMRPL'
         DC    CL50'38: UNKNOWN ERROR'
         DC    CL50'3C: UNKNOWN ERROR'
         DC    CL50'40: NOT ENOUGH VIRTUAL STORAGE'
         DC    CL50'44: NOT OPENED FOR PROCESS REQUESTED'
         DC    CL50'48: KSDS REQUEST ISSUED FOR ESDS'
         DC    CL50'4C: INVALID PUT TYPE FOR THIS FILE'
         DC    CL50'50: INVALID ERASE SITUATION'
         DC    CL50'54: INVALID OPTCD=LOC SITUATION'
         DC    CL50'58: INVALID POSITIONING'
         DC    CL50'5C: UPDATE MISSING PRECEDING GET FOR UPDATE'
         DC    CL50'60: ATTEMPTED TO CHANGE PRIME KEY'
         DC    CL50'64: ATTEMPTED TO CHANGE LENGTH'
         DC    CL50'68: INVALID/CONFLICTING RPL OPTIONS'
         DC    CL50'6C: RECLEN EXCEEDS MAX OR IS ZERO'
         DC    CL50'70: KEYLEN EXCEEDS MAX OR IS ZERO'
         DC    CL50'74: ONLY PUT IS VALID ON INITIAL LOAD'
         DC    CL50'78: REQUEST OPERATING UNDER INCORRECT TCB'
         DC    CL50'7C: REQUEST CANCELLED FOR JRNAD'
         DC    CL50'80: UNKNOWN ERROR'
         DC    CL50'84: SPANNED RECORD LOCATE RETRIEVAL ATTEMPTED'
         DC    CL50'88: ADDRESSED GET FOR SPANNED RECORD IN KSDS'
         DC    CL50'8C: INCONSISTENT SPANNED RECORD'
         DC    CL50'90: INVALID ALT INDEX POINTER'
         DC    CL50'94: ALT INDEX POINTER MAX EXCEEDED'
         DC    CL50'98: NOT ENOUGH BUFFERS AVAILABLE'
         DC    CL50'9C: INVALID CONTROL INTERVAL FOUND'
         DC    CL50'A0: 1 OR MORE CANDIDATES WITH BUFFER TO WRITE'
         DC    CL50'A4: INVALID OPTION FOR CNVTAD/MNTACQ/ACQRANGE'
         DC    CL50'A8: INVALID PARM LIST FOR CNVTAD/MNTACQ/ACQRANGE'
         DC    CL50'AC: ACQUIRE ERROR FOR MNTACQ/ACQRANGE REQUEST'
         DC    CL50'B0: STAGING FAILURE FOR MNTACQ/ACQRANGE REQUEST'
         DC    CL50'B4: RBA/VOLUME ERROR FOR MNTACQ/ACQRANGE REQUEST'
         DC    CL50'B8: CATALOG ERROR FOR CNVTAD REQUEST'
         DC    CL50'BC: ACQUIRE ECB STORAGE NOT AVAILABLE'
         DC    CL50'C0: INVALID RELATIVE RECORD NUMBER'
         DC    CL50'C4: ADDRESSED REQUEST FOR RRDS'
         DC    CL50'C8: REQUESTED ADDRESSED OR CI ACCESS THRU PATH'
         DC    CL50'CC: BACKWARD PUT NOT ALLOWED'
         DC    CL50'D0: ENDREQ AGAINST RPL WITH OUTSTANDING WAIT'
         DC    CL50'D4: UNKNOWN ERROR'
         DC    CL50'D8: UNKNOWN ERROR'
         DC    CL50'DC: UNKNOWN ERROR'
         DC    CL50'E0: INVALID BUFFER CONTENT FOR MRKBFR OUT'
         DC    CL50'E4: CROSS MEMORY CALLER NOT IN SUPVR STATE'
         DC    CL50'E8: UPAD ERROR'
         DC    CL50'EC: VALIDITY CHECK FOR SHARE OPTIONS 3 OR 4'
         DC    CL50'F0: INVALID REQUEST FOR SHARED RESOURCES'
         DC    CL50'F4: REG 14 STACK SIZE TOO SMALL'
         DC    CL50'F8: REG 14 RETURN OFFSET WENT NEGATIVE'
         DC    CL50'FC: UNKNOWN ERROR'
*
*
*
         DS    0F
NTMRECD  DS    0CL739
CCPRKEY  DS    0CL54
CCNODNM  DS    0CL27
CCNLCNR  DC    CL8' '         PARENT LOCATION
CCNSYSM  DC    CL4' '         PARENT SYSTEM
CCNTYPE  DC    CL10' '        PARENT TYPE
CCNUNIT  DC    CL5' '         PARENT UNIT
CCENTRY  DS    0CL27                       *** RECORD KEY ***
CCELCNR  DC    CL8'IMSP    '   LOCATION    ***
CCESYSM  DC    CL4'SYSA'       SYSTEM      ***
CCETYPE  DC    CL10'PGM   '    TYPE (E.G.) ***
CCEUNIT  DC    CL5'00001'      UNIT (ASCENDING SEQUENCE) ***
CCLNMKY  DS    0CL16
CCLOGNM  DC    CL16' '        LOGICAL NAME
CCCMSER  DC    CL10' '        SERIAL NUM
CCSHACM  DC    PL2'0'         SHARED COUNT
CCSHARE  DS    0CL27
CCSHLOC  DC    CL8' '         SHARED LOCATION
CCSHSYS  DC    CL4' '         SHARED SYSTEM
CCSHTYP  DC    CL10' '        SHARED TYPE
CCSHUNT  DC    CL5' '         SHARED UNIT
CCGRACM  DC    PL2'0'         GROUPED COUNT
CCGROUP  DS    0CL27
CCGRLOC  DC    CL8' '         GROUPED LOCATION
CCGRSYS  DC    CL4' '         GROUPED SYSTEM
CCGRTYP  DC    CL10' '        GROUPED TYPE
CCGRUNT  DC    CL5' '         GROUPED UNIT
CCUSRLNK DS    0CL27
CCUSLOC  DC    CL8' '         USER LOCATION
CCUSSYS  DC    CL4' '         USER SYSTEM
CCUSTYP  DC    CL10' '        USER TYPE
CCUSUNT  DC    CL5' '         USER UNIT
CCFLAGS  DS    0CL19
CCFFIN   DC    FL1'0'         FINANCIAL DATA
CCSTORD  DC    FL1'0'         STATUS = CO
CCSTINS  DC    XL1'80'        STATUS = CI
CCSTALC  DC    XL1'80'        ALLOCATED = X
CCSTFUN  DC    XL1'80'        STATUS = OP
CCSTNFC  DC    FL1'0'         STATUS = DW
CCSTDFR  DC    FL1'0'         STATUS = DR
CCSTSUB  DC    FL1'0'         STATUS = SB
LASTCHN  DC    XL1'80'        NO CHILDREN ?
CCFEATUR DC    FL1'0'         FEATURE = X
CCFEATS  DC    FL1'0'         FEATURE CHILDREN
CCSHRANY DC    FL1'0'         SHARED (ANY)
CCALSTD  DC    FL1'0'         COST STD
CCALTAX  DC    FL1'0'         COST TAX
CCALDEP  DC    FL1'0'         COST DEPR
CCFILL1  DC    FL4'0'
CCPROB   DS    0CL37
CCFILL4A DC    PL3'0'
CCLSRDTE DC    PL4'0'         RESL DATE OF
CCLSRTME DC    PL3'0'         RESL TIME OF
CCPSCNT  DC    PL4'0'         NUMBER OF
CCSECDWN DC    PL2'0'
CCCFDTE  DC    PL4'0'         ACTIVE FAIL DATE
CCCFTIM  DC    PL3'0'         ACTIVE FAIL ID
CCLSDATE DC    PL4'0'         MOST RECENT
CCLSTIME DC    PL3'0'         MOST RECENT
CCFILL4G DC    CL2' '
CCCFLCID DC    CL2' '         ACTIVE FAIL LOC
CCFILL2  DC    CL3' '
CCORDER  DS    0CL28
CCPONBR  DC    CL8' '         PUR ORDER NUM
CCDTORD  DC    PL4'0'         ORDER DATE
CCDTSHP  DC    PL4'0'         SHIP DATE
CCDTREC  DC    PL4'0'         RECEIVED DATE
CCDTINS  DC    PL4'900101'    INSTALL DATE
CCDTRMV  DC    PL4'0'         REMOVAL DATE
CCMISC   DS    0CL256
CCCMNT1  DC    CL40' '        COMMENT (LINE 1)
CCCMNT2  DC    CL40' '        COMMENT (LINE 2)
CCCUSER  DC    CL8' '         USER DATA (COMP)
CCPHLOC  DC    CL8' '         PHYSICAL LOC
CCFCOUNT DC    PL2'0'         FEATURE COUNT
CCMODEL  DC    CL4' '         MODEL
CCCMADR  DC    CL4' '         ADDR
CCEVEND  DC    CL4' '         MANUFACTURER
CCECLS2  DC    CL4' '         CLASS CODE
CCCOPLG  DC    CL2' '         LOG (COMP)
CCCINIT  DC    CL3' '         REQID (COMP UPD)
CCCDATE  DC    PL4'0'         DATE (COMP UPD)
CCCTIME  DC    PL3'0'         TIME (COMP UPD)
CCFINIT  DC    CL3' '         REQID (FINL UPD)
CCFDATE  DC    PL4'0'         DATE (FINL UPD)
CCFTIME  DC    PL3'0'         TIME (FINL UPD)
CCPORT   DC    CL3' '         PORT
CCLOGNM2 DC    CL16' '        LOGICAL NAME 2
CCPLONBR DC    CL8' '         PLANT ORDER NUM
CCCMNT3  DC    CL40' '        COMMENT (LINE 3)
CCFILL3  DC    CL21' '
CCALACCT DC    CL15' '        COST ACCT
CCALITEM DC    CL5' '         COST ITEM
CCDTCONF DC    PL4'0'         CONFIRM DATE
CCDTDWN  DC    PL4'0'
CCTMDWN  DC    PL3'0'
CCILIFE  DC    CL1' '         ITC LIFE
CCFIN    DS    0CL234
CCCONTRT DS    0CL33
CCCTYPE  DC    CL2' '         CTYPE
CCCVEND  DC    CL4' '         VENDOR (FINL)
CCCNUMB  DC    CL10' '        CONTRACT
CCCSCHED DC    CL4' '         SCHED
CCCSTART DC    PL4'0'         START DATE(CONT)
CCCTERM  DC    PL2'0'         TERM
CCCFREQ  DC    CL1' '         FRQ
CCCAMOUN DC    PL6'0'         AMOUNT
         DC    CL66' '        REMAINING 2 OCCURRENCES
CCITC    DS    0CL26
CCISTART DC    PL4'0'         ITC START DATE
CCILETTR DC    PL4'0'         ITC LETTER DATE
CCISTMT  DC    CL12' '        ITC STMT NUM
CCIBASIS DC    PL6'0'         ITC BASIS
CCACRUAL DS    0CL34
CCCADATE DC    PL4'0'         ACCRUAL DATE
CCCABASE DC    PL6'0'         ACCRUAL BASIS
CCAACCPC DC    PL3'0'         ACCRUAL %
CCAACCUM DC    PL6'0'         ACCRUAL $
CCAMAXPC DC    PL3'0'         ACCRUAL MAX %
CCAMAXAM DC    PL6'0'         ACCRUAL MAX $
CCAPURCH DC    PL6'0'         ACCRUAL PURCHASE
CCDEP    DS    0CL35
CCDTYPE  DC    CL2' '         DEPR METHOD
CCDLIFE  DC    PL2'0'         DEPR LIFE
CCDSTART DC    PL4'0'         DEPR START DATE
CCDSBOOK DC    PL6'0'         DEPR START BOOK
CCDSALV  DC    PL6'0'         DEPR SALVAGE_VAL
CCDACCT  DC    CL15' '        DEPR ACCOUNT
CCFMISC  DS    0CL40
CCDEPT   DC    CL8' '         COMPANY/DEPT
CCGLCODE DC    CL15' '        GL CD
CCFUSER  DC    CL15' '        USER_(FINL)
CCFOPLG  DC    CL2' '         LOG (FINL)
*
*
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133
*
VSAMMSG  DC    CL256' '       VSAM OPEN/CLOSE/PHYSICAL ERROR MESSAGE
*
         PRINT GEN
OUTPUT1  ACB   AM=VSAM,                                                X
               DDNAME=OUTPUT1,                                         X
               MAREA=VSAMMSG,                                          X
               MLEN=256,                                               X
               MACRF=(KEY,DIR,OUT)
*
NETMNRPL RPL   AM=VSAM,                                                X
               ACB=OUTPUT1,                                            X
               AREA=NTMRECD,                                           X
               AREALEN=739,                                            X
               ARG=CCENTRY,                                            X
               MSGAREA=VSAMMSG,                                        X
               MSGLEN=256                                              X
               OPTCD=(KEY,DIR,SYN,UPD,MVE,FKS,KEQ)
          LTORG
*
*
*
INRECD   DS    0CL80           INPUT DATA RECORD
         DS    CL24                RDW AND UNUSED FIELDS
INKEY    DS    CL27                KEY OF RECORD TO DELETE
         DS    CL800
          END

DLITCBL  CSECT
**********************************************************************
* THIS IS A RECONSTRUCTED SOURCE PROGRAM FOR THE DLITCBL CSECT AS    *
* EXTRACTED FROM EMVSP.OUTPUT.LOADLIB(IL10STRP). THE IL10STRP MEMBER *
* CONTAINS ONLY THE DLITCBL CSECT. NEITHER IL10STRP NOR DLITCBL ARE  *
* ENDEVOR ELEMENTS. THIS PROGRAM APPEARS TO BE A GENERIC INITIALIZA- *
* TION PROGRAM THAT ENDS WITH A CALL TO "SETCBLNM". BEFORE USE, IT   *
* IS MODIFIED BY A COMPOSITE LINK SO THAT ITS CSECT NAME (DLITCBL)   *
* IS CHANGED, AS WELL AS THE PROGRAM IT CALLS (SETCBLNM).            *
* AN EXAMPLE OF THIS CAN BE FOUND IN EMVSP.OUTPUT.LINKEDIT(INQ900A): *
*     CHANGE DLITCBL(INQ900),SETCBLNM(INQ900T)                       *
*     INCLUDE SYSLIB(IL10STRP)                                       *
*     CHANGE INQ900(INQ900T)                                         *
*     INCLUDE COBLOAD(INQ900)                                        *
*     INCLUDE SYSLIB(INQ900FE)                                       *
*     ALIAS INQ9A1                                                   *
*       .                                                            *
*       .                                                            *
*       .                                                            *
**********************************************************************
         STM   R14,R12,12(R13)          SAVE CALLER'S REGISTERS
         LR    R12,R15                  COPY PROGRAM START ADDRESS
         USING DLITCBL,R12
         LR    R15,R13                  COPY CALLER'S SAVE AREA ADDRESS
         LA    R13,SAVE                 POINT TO DLITCBL'S SAVE AREA
         ST    R15,4(,R13)              SAVE LSA POINTER
         ST    R13,8(,R15)              SAVE HSA POINTER
         L     R15,16(,R15)             RESTORE R15
         ST    R1,PARMPTR               SAVE PARM ADDRESS
         LR    R2,R1                    R2 NOW POINTS TO PARMS
         LA    R7,PARMTBL               R7 POINTS TO PARM ADDRESS TBL
         LA    R5,FAKEPCB               R5 POINTS TO FAKE PCB TABLE
         LR    R4,R5                    R4 POINTS THERE, TOO
PARMSCAN L     R9,0(,R1)                R9 POINTS TO NEXT INCOMING PARM
         USING ODBPCB,R9
         LTR   R9,R9                    IS THERE ANY PCB ADDRESS?
         BZ    NULLPCBA                 NO
         ST    R9,CURRPCB               YES, SAVE CURRENT PCB ADDRESS
         CLI   DBPCBPRO,X'C0'           PCB PROCOPT : HEX 'C0'
         BNL   PROCOPTC                 BRANCH IF HIGH OR EQUAL
         BAL   R14,BLDPRMTB             GO SAVE PCB ADDRESS IN TBL
PARMADV  LA    R1,4(,R1)                STEP TO NEXT INCOMING PCB ADR
         B     PARMSCAN                 CONTINUE PARM SCAN
NULLPCBA MVI   PARMSWCH,C'Y'            INDICATE THIS IS LAST PCB ADR
PROCOPTC CLC   DBPCBDBD(4),IP11         THIS PCB DBNAME IS "IP11"?
         BL    NOTIP1X                  NO, LOWER
         CLC   DBPCBDBD(4),IP15         THIS PCB DBNAME IS "IP15"?
         BH    NOTIP1X                  NO, HIGHER
         B     POSSIP1X                 BETWEEN IP11 AND IP15
NOTIP1X  CLI   PARMSWCH,C'Y'            IS THIS THE LAST PCB ADDRESS?
         BE    LASTPARM                 YES
         BAL   R14,BLDPRMTB             GO SAVE R9 IN PCB ADDR TBL
         CLI   CURRPCB,X'80'            IS THIS THE LAST PARAMETER?
         BE    LASTPARM                 YES
         B     PARMADV                  GO ADVAANCE TO NEXT PARM
POSSIP1X CLC   DBPCBDBD(4),IP11         THIS PCB DBDNAME IS "IP11"?
         BNE   NOTIP11                  NO
         CLI   PARMSWCH,C'1'            ??? NEVER GETS SET TO C'1' ???
         BE    UNDOEND                  ??? CAN NEVER BRANCH HERE ???
         LA    R4,84(,R4)               STEP TO NEXT FAKE PCB IN TABLE
         LR    R5,R4                    R5 POINTS TO SAME FAKE PCB
UNDOEND  MVI   PARMSWCH,C'N'            INDICATE NOT LAST PCB ADDRESS
         ST    R9,64(,R5)               SAVE CURRENT PCB ADDR IN FAKE
         ST    R5,0(,R7)                SAVE FAKE PCB ADDR IN PCB TBL
         LA    R5,4(,R5)                ?
         LR    R8,R7                    R8 POINTS TO CURR PCB TBL ENTRY
         LA    R7,4(,R7)                ADVANCE R7 TO NEXT PCB ENTRY
         B     PARMADV                  GO ADVANCE TO NEXT PARM
NOTIP11  ST    R9,64(,R5)               SAVE CURRENT PCB ADDR IN FAKE
         CLI   PARMSWCH,C'Y'            IS THIS THE LAST PCB ADDRESS?
         BE    LASTPARM                 YES
         CLI   CURRPCB,X'80'            IS THIS THE LAST PCB ADDRESS?
         BE    LASTPARM                 YES
         LA    R5,4(,R5)                ?
         B     PARMADV                  GO ADVANCE TO NEXT PARM
BLDPRMTB ST    R9,0(,R7)                SAVE PARM ADDRESS IN R9
         LR    R8,R7                    SAVE CURRENT PARM PTR
         LA    R7,4(,R7)                ADVANCE TO NEXT PARM ADDR
         BR    R14                      RETURN TO CALLER
LASTPARM LR    R7,R8                    R7 POINTS TO LAST PARM IN TBL
         OI    0(R7),X'80'              SET LAST PARM FLAG
         LA    R1,PARMTBL               R1 POINTS TO PARM ADDRESS TBL
         L     R15,SETCBLNM             POINT TO NEXT PROGRAM
         BALR  R14,R15                  CALL NEXT PROGRAM
         B     RETURN                   GO TO EXIT
RETURN   L     R13,4(,R13)              R13 POINTS TO CALLER'S SAVE
         LM    R14,R12,12(R13)          RESTORE CALLER'S REGS
         SR    R15,R15                  SET RETURN CODE = 0
         BR    R14                      RETURN TO CALLER
         DROP  R12
CURRPCB DC     A(0)                     POINTER TO CURRENT PARM
PARMSWCH DC    C'N'                     PARAMETER SWITCH
SAVE     DC    18F'0'                   REGISTER SAVE AREA
SETCBLNM DC    V(SETCBLNM)              NAME OF NEXT PROGRAM TO CALL
PARMPTR  DC    A(0)                     PARM ADDR (R1 ON ENTRY)
PARMTBL  DC    20A(0)                   PARM ADDRESS TABLE
FAKEPCB  DC    10XL84'00'               FAKE PCB TABLE (10 ENTRIES)
IP11     DC    C'IP11'                  LITERAL "IP11"
IP15     DC    C'IP15'                  LITERAL "IP15"
***********************************************************************
* THE DSECTS BELOW WERE CAPTURED FROM AN ASSEMBLY THAT SPECIFIED THE  *
* MACRO IMSVS.MACLIB(DFSPCBS) ON 4/8/2001.                            *
***********************************************************************
***********************************************************************
**                                                                   **
**                D A T A   B A S E   P C B                          **
**                                                                   **
***********************************************************************
***********************************************************************
DBPCB    DSECT                                                          01-DFSPC
DBPCBBEG DS    0F             START OF PREFIX AREA                      01-DFSPC
DBPCBLEN DS    H              LENGTH OF THIS DBPCB MINUS PREFIX         01-DFSPC
DBPCBPLN DS    H              LENGTH OF THIS PREFIX                     01-DFSPC
DBPCBNUM DS    H              RELATIVE PCB NUMBER                       01-DFSPC
DBPCBFLG DS    X              PCB TYPE FLAG, USED ONLY FOR GBP          01-DFSPC
*                             NOT AFTER BLKS ARE BUILT
DBPCBDB  EQU   X'00'          DB OR ONLINE PCB INDICATOR                01-DFSPC
DBPCBCH  EQU   X'01'          BATCH PCB INDICATOR                       01-DFSPC
DBPCBGS  EQU   X'02'          GSAM PCB INDICATOR                        01-DFSPC
DBPCBMSD EQU   X'04'          MSDB PCB INDICATOR                        01-DFSPC
DBPCBDED EQU   X'08'          DEDB PCB INDICATOR                        01-DFSPC
DBPCBUPD EQU   X'10'          F/P DB UPDATE INTENT INDICATOR            01-DFSPC
DBPCBAFP EQU   X'20'          IPDS ADVANCED FUNCTION PRINT              01-DFSPC
DBPCB62  EQU   X'40'          PCB IS FOR LU6.2 DEVICE                   01-DFSPC
DBPCBMSV EQU   X'80'          F/P DEDB VIEW=MSDB SPECIFIED              01-DFSPC
*
DBPCBFL2 DS    X              PCB TYPE FLAG 2                           01-DFSPC
DBPCBFP  EQU   X'80'          F/P PCB INDICATOR                         01-DFSPC
DBPCBTP  EQU   X'40'          DC PCB INDICATOR (ALT TPCB)               01-DFSPC
DBPCBDLI EQU   X'20'          DL/I DB PCB INDICATOR                     01-DFSPC
DBPCBIO  EQU   X'10'          I/O PCB INDICATOR                         01-DFSPC
DBPCBPNM EQU   X'08'          PCB HAS NAME                              01-DFSPC
DBPCBLNO EQU   X'04'          PCB NOT IN APPL PCB LIST                  01-DFSPC
*
DBPCBEST DS    CL2            EXTENDED STATUS CODE                      01-DFSPC
DBPCBESC DS    CL2            SUB CODE                                  01-DFSPC
*
DBPCBFL3 DS    X              PCB FLAG BYTE 3                           01-DFSPC
*
         DS    X              RESERVED FLAG BYTE                        01-DFSPC
         DS    CL2            RESERVED                                  01-DFSPC
DBPRTEA  DS    F              ROUTE TABLE ADDRESS                       01-DFSPC
DBPCBEPC DS    F              EPCB ADDRESS                              01-DFSPC
DBPCBCTL DS    F              USER PCB ADDRESS (KEY 8 PCB)              01-DFSPC
DBPCBNAM DS    CL8            DBPCB NAME                                01-DFSPC
DBPCBXTN DS    F              DBPCB EXTENSION ADDRESS                   01-DFSPC
DBPCBSGL DS    F              SEGMENT LENGTH                            01-DFSPC
         DS    F                                                        01-DFSPC
DBPCBGB  DS    0A             GSAM CTL BLK (GB) ADDR                    01-DFSPC
DBPCBJCB DS    F              JCB ADDRESS                               01-DFSPC
DBPCBPRE DS    F              PREFIX ADDRESS COMPAT WITH TPCB           01-DFSPC
ODBPCB   DS    0F                                                       01-DFSPC
DBPCBDBD DS    CL8    +0      DBD NAME                                  01-DFSPC
DBPCBLEV DS    CL2    +8      LEVEL FEEDBACK                            01-DFSPC
DBPCBSTC DS    CL2    +10     STATUS CODES (RETURNED TO USER)           01-DFSPC
DBPCBPRO DS    CL4    +12     PROCESSING OPTIONS                        01-DFSPC
DBPCBPFX DS    F      +16     PREFIX ADDRESS                            01-DFSPC
DBPCBSFD DS    CL8    +20     SEGMENT NAME FEEDBACK                     01-DFSPC
DBPCBLKY DS    0F     +28     MAX LENGTH OF KEY FEEDBACK AREA PSB'S PCB 01-DFSPC
DBPCBOPT DS    0XL1   +28     DB PCB OPTIONS (ONLY IN PSBGEN OUTPUT     01-DFSPC
*                             NOT PRESENT AFTER BLOCKS ARE BUILT)
DBPCBMLP EQU   X'01'          MULTIPLE POSITIONING REQUESTED            01-DFSPC
DBPCBMKL DS    F      +28     CURRENT LENGTH OF KEY FEEDBACK AREA       01-DFSPC
*                             OR GSAM FEEDBACK AREA
DBPCBNSS DS    F      +32     NO OF SENSITIVE SEGMTS IN PCB             01-DFSPC
DBPCBSZ2 EQU   *-ODBPCB       SIZE OF PCB WITHOUT KEY FEEDBACK AREA     01-DFSPC
DBPCBKFD DS    0CL256         KEY FEEDBACK AREA                         01-DFSPC
DBPCBGFD DS    0CL12          GSAM FEEDBACK AREA                        01-DFSPC
DBPCBRRA DS    XL8            RELATIVE RECORD ADDR                      01-DFSPC
DBPCBURL DS    F              UNDEFINED RCD LENGTH FLD, THIS FL         01-DFSPC
*                             APPLIES ONLY TO RECFM=U
GSPCBLEN EQU   *-ODBPCB       GSAM PCB LENGTH                           01-DFSPC
GSPCBLN  EQU   *-DBPCB        GSAM PCB LENGTH WITH PREFIX               01-DFSPC
         ORG   DBPCBKFD       REALIGN                                   01-DFSPC
***********************************************************************
***********************************************************************
**                                                                   **
**        D A T A   B A S E   P C B   E X T E N S I O N              **
**                                                                   **
***********************************************************************
***********************************************************************
DBPCBX   DSECT                                                          01-DFSPC
DBPXID   DS    CL4                 'DBPX' EYECATCHER                    01-DFSPC
DBPXFLG1 DS    X                   FLAGS                                01-DFSPC
DBPX1XIT EQU   X'80'               SEGMENT EXITS DEFINED                01-DFSPC
*        EQU   X'40'               RESERVED
*        EQU   X'20'               RESERVED
*        EQU   X'10'               RESERVED
*        EQU   X'08'               RESERVED
*        EQU   X'04'               RESERVED
*        EQU   X'02'               RESERVED
*        EQU   X'01'               RESERVED
DBPXFLG2 DS    X                   RESERVED                             01-DFSPC
DBPXFLG3 DS    X                   RESERVED                             01-DFSPC
DBPXFLG4 DS    X                   RESERVED                             01-DFSPC
**
DBPXNAML DS    H                   LENGTH OF DB PCB NAME                01-DFSPC
DBPXDBDL DS    H                   LENGTH OF DBD VERSION ID             01-DFSPC
DBPXNAME DS    A                   ADDRESS OF NAME OF DB PCB IF         01-DFSPC
*                                      DEFINED DURING PSBGEN
DBPXDBDV DS    A                   ADDRESS OF DBD VERSION ID FOR        01-DFSPC
*                                      DBD USED FOR THIS PCB
DBPXXITB DS    A                   ADDRESS OF EXIT BLOCKS, IF NEEDED    01-DFSPC
*                                      TO BUILD XPCB FOR EXITS
DBPXCAPD DS    A                   ADDRESS OF CAPTURED DATA CHAIN       01-DFSPC
DBPXALET DS    F                   ALET FOR CAPTURED DATA               01-DFSPC
DBPXINQY DS    A                   ADDRESS OF INQY OUTPUT               01-DFSPC
DBPXLEN  EQU   *-DBPCBX                                                 01-DFSPC
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** COBSUBEM: PRINT COBOL SUBROUTINE INFO ***'
         PRINT NOGEN
***********************************************************************
* READS FILE OF LOAD LIBRARY CSECT DATA CREATED BY PROGRAM COBLSUBR   *
* AFTER IT IS SORTED IN CSECT ORDER. CSECTS NAMED $PRIVATE OR BLANKS  *
* ARE DROPPED. OTHER CSECTS ARE ALSO IGNORED IF THEY AREN'T INCLUDED  *
* IN AT LEAST ONE MEMBER WHERE THEY ARE NOT THE MAIN ENTRY POINT. A   *
* CSECT IS CONSIDERED TO BE A SUBROUTINE WHEN IT IS INCLUDED IN AT    *
* LEAST ONE MEMBER IN WHICH IT IS NOT THE INITIAL ENTRY POINT. FOR    *
* EACH "SUBROUTINE" FOUND, IF THERE IS NO MEMBER IN WHICH THE CSECT   *
* IS THE INITIAL ENTRY POINT A MESSAGE IS PRINTED QUESTIONING THE     *
* ORIGIN OF THE SUBROUTINE. IF ANY MEMBER IS FOUND IN WHICH THE CSECT *
* IS THE INITIAL ENTRY POINT AND IN WHICH ILB MODULES ARE FOUND, A    *
* WARNING MESSAGE IS PRINTED INDICATING THE SUBROUTINE WAS NOT LINK   *
* EDITED WITH NCAL.                                                   *
*                                                                     *
* A LIBRARY CONTAINING ENDEVOR SYSTEM ID'S IS READ AND STORED. FOR    *
* EACH LINE PRINTED, THE ENDEVOR SYSTEM ID IS PRINTED, IF FOUND.      *
*                                                                     *
* IF A PARM IS INCLUDED ON THE JCL EXEC STATEMENT, A SUMMARY REPORT   *
* IS GIVEN, SHOWING ONLY ONE LINE PER SUBROUTINE. WITH NO PARM, THE   *
* REPORT SHOWS ONE LINE FOR EACH SUBROUTINE PLUS ONE LINE FOR EACH    *
* PROGRAM THAT USES THE SUBROUTINE.                                   *
*                                                                     *
* THE PRINT DATA IS ALSO WRITTEN TO A FILE WHICH CAN BE EDITED TO MAKE*
* CORRECTIONS. NO HEADING RECORDS ARE WRITTEN TO THIS FILE,           *
*                                                                     *
* AUTHOR R THORNTON JUN 1992                                          *
***********************************************************************
*
COBSUBEM SVLNK R3
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
         L     R2,0(R1)                POINT TO PARM DATA
         CLC   0(2,R2),H0              ANY PARM DATA?
         BE    OPENS                   NO
         MVI   SUMMARY,1               YES, SET SUMMARY SWITCH
OPENS    OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
         BAS   R9,HEADING              PRINT HEADERS
***********************************************************************
*        LOAD THE ENDEVOR MEMBER-TO-SYSTEM ID TABLE                   *
***********************************************************************
         L     R12,ENDEVTBA            POINT TO THE TABLE
ENDEVLD  GET   INPUT2,ENDEVREC         READ A TABLE RECORD
         MVC   0(8,R12),TBLMEMBR       MOVE MEMBER NAME
         MVC   8(8,R12),TBLSYS         MOVE SYSTEM ID
         LA    R12,16(R12)             STEP TO NEXT ENTRY
         C     R12,ENDEVNDA            PAST END OF TABLE?
         BL    ENDEVLD                 NO
         WTO   'ENDEVOR MEMBER-TO-SYSTEM ID TABLE FULL',ROUTCDE=11
         ABEND 111,DUMP
EOT      ST    R12,ENDEVNDA            UPDATE TABLE END ADDRESS
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE GET   INPUT1,INREC            READ INPUT RECORD
         CLC   INCSECT,BLANKS          BLANK CSECT?
         BE    MAINLINE                YES
         CLC   INCSECT,=CL8'$PRIVATE'  CSECT NAME IS $PRIVATE?
         BE    MAINLINE                YES
         CLC   INCSECT,CURCSECT        SAME CSECT NAME IN PROGRESS?
         BE    SAMCSECT                YES
         BAS   R9,NEWCSECT             TERMINATE OLD CSECT
SAMCSECT CLI   INMAIN,C' '             CSECT IS INITIAL ENTRY POINT?
         BE    REFONLY                 NO, ONLY A REFERENCE
         CP    SUBREFS,=P'0'           THIS CSECT A "SUBROUTINE"?
         BE    MAINLINE                NO
         CLI   ORIGFND,1               ALREADY FOUND AN ORIGIN?
         BNE   SETORGFD                NO
         MVC   PNOTE+5(4),=C'DUPL'     YES, SHOW DUPLICATE
         MVI   PFLAG,C'*'              SET ERROR FLAG
SETORGFD MVI   ORIGFND,1               SHOW ORIGIN FOUND
         CLI   INILB,C' '              ILB MODULES IN THE MEMBER?
         BE    SETOLD                  NO
         MVC   PNOTE(4),=C'ILBS'       SHOW ORIGIN CONTAINS ILB'S
         MVI   PFLAG,C'*'              SET ERROR FLAG
SETOLD   MVC   PDSNAME,INDSNAME        DSNAME TO PRINT
         MVC   PMEMBR,INMEMBR          MEMBER NAME TO PRINT
         MVC   PCSECT,CURCSECT         CSECT NAME TO PRINT
         BAS   R9,GETSYSTM             GO GET ENDEVOR SYSTEM ID
         BAS   R9,PRINT                PRINT A LINE
         B     MAINLINE                CONTINUE
REFONLY  AP    SUBREFS,=P'1'           ADD 1 TO NUMBER REFERENCES
         L     R12,TBLCURRA            POINT TO CURRENT ENTRY
         C     R12,TBLENDA             PAST END?
         BNL   MAINLINE                YES
         MVC   0(80,R12),INREC         SAVE THE RECORD
         LA    R12,80(R12)             POINT TO NEXT TABLE ENTRY
         ST    R12,TBLCURRA            SAVE CURRENT POINTER
         B     MAINLINE                CONTINUE
***********************************************************************
*        TERMINATE OLD CSECT, INITIALIZE FOR NEW                      *
***********************************************************************
NEWCSECT ST    R9,NC9                  SAVE RETURN ADDRESS
         CP    SUBREFS,=P'0'           CURRENT CSECT EVER CALLED?
         BE    INITNEW                 NO
         CLI   ORIGFND,1               WAS ORIGIN FOUND?
         BE    CHKTABL                 YES
         MVC   PNOTE(4),=C'ORG?'       SHOW UNKNOWN ORIGIN
         MVI   PFLAG,C'*'              SET ERROR FLAG
         MVC   PDSNAME,QUESTION        DSNAME IS UNKNOWN
         MVC   PMEMBR,QUESTION            AND MEMBER NAME
         MVC   PCSECT,CURCSECT         CSECT NAME TO PRINT
         BAS   R9,GETSYSTM             GO GET ENDEVOR SYSTEM ID
         BAS   R9,PRINT                PRINT A LINE
CHKTABL  CLC   TBLSTRTA,TBLCURRA       ANYTHING IN TABLE?
         BE    INITNEW                 NO
         CLI   SUMMARY,1               SUMMARY REPORT DESIRED?
         BE    INITNEW                 YES
PRTTBL   L     R12,TBLSTRTA            POINT TO FIRST TABLE ENTRY
CKEND    C     R12,TBLCURRA            FINISHED PRINTING REFERENCES?
         BNL   INITNEW                 YES, GO INITIALIZE FOR NEXT
         MVC   PDSNAME,INDSNAME-INREC(R12) DSNAME TO PRINT
         MVC   PMEMBR,INMEMBR-INREC(R12)   MEMBER NAME TO PRINT
         BAS   R9,GETSYSTM             GO GET ENDEVOR SYSTEM ID
         BAS   R9,PRINT                PRINT A LINE
         MVC   PCSECT,BLANKS           BLANK CSECT NAME AFTER 1ST
         LA    R12,80(R12)             ADVANCE TO NEXT ENTRY
         B     CKEND                   LOOP THRU REFERENCES
INITNEW  MVC   CURCSECT,INCSECT        SAVE NEW CSECT NAME
         ZAP   SUBREFS,=P'0'           CLEAR NUMBER CALLS
         MVI   ORIGFND,0               CLEAR ORIGIN FOUND SWITCH
         MVC   TBLCURRA,TBLSTRTA       RESET TABLE POINTER
         L     R9,NC9                  GET RETURN ADDRESS
         BR    R9                      RETURN
***********************************************************************
*        PRINT A LINE ROUTINE                                         *
***********************************************************************
PRINT    ST    R9,PRT9                 SAVE RETURN REG
         PUT   PRINT1,PRT              WRITE THE PRINT LINE
         PUT   OUTPUT1,PRT             WRITE THE OUTPUT FILE RECORD
         MVC   PRT,BLANKS              CLEAR PRINT LINE
         AP    LINECT,P1               ADD 1 TO LINE COUNTER
         CP    LINECT,P58              END OF PAGE?
         BNH   PEXIT                   NO
         BAS   R9,HEADING              YES, DO HEADING
PEXIT    L     R9,PRT9                 GET RETURN ADDRESS
         BR    R9                      RETURN
***********************************************************************
*        PRINT HEADING ROUTINE                                        *
***********************************************************************
HEADING  AP    PAGENO,P1               INCREMENT PAGE NUMBER
         MVC   HDRPAGE,PAGEDIT         EDIT MASK
         ED    HDRPAGE,PAGENO          PAGE NBR TO HEADER
         PUT   PRINT1,HEADER1          WRITE HEADER LINE 1
         PUT   PRINT1,HEADER2          WRITE HEADER LINE 2
         PUT   PRINT1,HEADER3          WRITE HEADER LINE 3
         ZAP   LINECT,P5               RESET LINE COUNTER
         MVI   PCC,C'0'                DOUBLESPACE NEXT LINE
         BR    R9                      RETURN
***********************************************************************
*        LOCATE ENDEVOR SYSTEM ID USING TABLE SEARCH                  *
***********************************************************************
GETSYSTM ST    R12,GS12                SAVE R12 ACROSS ROUTINE
         MVC   PSYSTM,=CL8'????????'   SET DEFAULT SYSTEM NAME
         L     R12,ENDEVTBA            POINT TO ENDEVOR TABLE
TBSRCH   C     R12,ENDEVNDA            PAST END OF TABLE?
         BH    GSXIT                   YES, NOT FOUND
         CLC   PMEMBR,0(R12)           FOUND MEMBER ENTRY?
         BE    ENDEVFND                YES
         LA    R12,16(R12)             NO, ADVANCE TO NEXT ENTRY
         B     TBSRCH                  AND CONTINUE SEARCH
ENDEVFND MVC   PSYSTM,8(R12)           SYSTEM ID TO PRINT
GSXIT    L     R12,GS12                RESTORE R12
         BR    R9                      EXIT
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      BAS   R9,NEWCSECT             FINISH CURRENT CSECT
         CLOSE (PRINT1,,OUTPUT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
PRT9     DS    A                       PRINT RETURN ADDRESS
NC9      DS    A                       RETURN ADDRESS FOR NEWCSECT
GS12     DS    A                       R12 SAVE FOR GETSYSTM
TBLSTRTA DC    A(SAVEREFS)             TABLE START ADDRESS
TBLENDA  DC    A(ENDREFS)              TABLE END ADDRESS
TBLCURRA DC    A(SAVEREFS)             CURRENT TABLE ADDRESS
ENDEVTBA DC    A(ENDEVOR)              ENDEVOR SYSTEM ID TBL ADDR
ENDEVNDA DC    A(ENDTBL)               ENDEVOR TABLE END ADDRESS
*
RETCODE  DS    F                       RETURN CODE
H0       DC    H'0'                    ZERO
SUMMARY  DC    X'00'                   1 = SUMMARY REPORT
CURCSECT DC    CL8' '                  CURRENT CSECT NAME
SUBREFS  DC    PL3'0'                  SUBROUTINE IF > 0
QUESTION DC    CL20'????????????????????' CONSTANT QUESTION MARKS
ORIGFND  DC    X'00'                   ORIGIN MEMBER FOUND IF 1
BLANKS   DC    CL80' '                 BLANKS
P0       DC    P'0'                    ZERO
P1       DC    P'1'                    ONE
P5       DC    P'5'                    FIVE
P58      DC    P'58'                   58
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
PAGENO   DC    PL2'0'                  PAGE COUNTER
PAGEDIT  DC    XL4'40202020'           PAGE NUMBER EDIT MASK
*
INREC    DS    0CL80                   OUTPUT RECORD
INCSECT  DS    CL8                        CSECT NAME
INMAIN   DS    CL1                        * = CSECT IS MAIN E.P.
INMEMBR  DS    CL8                        MEMBER NAME
INILB    DS    CL1                        * = MEMBER CONTAINS ILB'S
         DC    CL1' '
INDSNAME DC    CL43' '                    DSNAME CONTAINING MEMBER
         DC    CL18' '
*
PRT      DS    0CL80                   DETAIL PRINT LINE
PCC      DC    CL1' '                    CARRIAGE CONTROL
PFLAG    DC    CL1' '                    * = EXCEPTION
PCSECT   DC    CL8' '                    CSECT NAME
         DC    CL3' '
PMEMBR   DC    CL8' '                    INCLUDED IN MEMBER NAME
         DC    CL2' '
PDSNAME  DC    CL20' '
         DC    CL1' '
PSYSTM   DC    CL8' '                  ENDEVOR SYSTEM ID
         DC    CL3' '
PNOTE    DC    CL4' '
         DC    CL1' '
         DC    CL20' '
*
ENDEVREC DS    0CL133                  ENDEVOR MEMBER-TO-SYSTEM RECD
         DS    CL1
TBLMEMBR DS    CL8                     MEMBER NAME
         DS    CL14
TBLSYS   DS    CL8
         DS    CL102
*
HEADER1  DS    0CL80                   HEADER LINE 1
         DC    CL31'1'
         DC    CL18'SUBROUTINE LISTING'
         DC    CL22' '
         DC    CL4'PAGE'
HDRPAGE  DC    CL4' '
         DC    CL1' '
*
HEADER2  DS    0CL80                   HEADER LINE 2
         DC    CL2'0'
         DC    CL11'SUBROUTINE '
         DC    CL10'INCLUDED  '
         DC    CL21'OF LIBRARY'
         DC    CL11'ENDEVOR'
         DC    CL5'NOTES'
         DC    CL20' '
*
HEADER3  DS    0CL80                   HEADER LINE 3
         DC    CL2' '
         DC    CL11'NAME       '
         DC    CL10'IN MEMBER '
         DC    CL21'          '
         DC    CL11'SYSTEM   '
         DC    CL5'     '
         DC    CL20' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FBA,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOT,              X
               RECFM=FBA,LRECL=133
         LTORG
COBSUBEM CSECT
SAVEREFS DS    100CL80
ENDREFS  EQU   *
ENDEVOR  DS    7000CL16        ENDEVOR MEMBER-TO-SYSTEM ID TABLE
ENDTBL   EQU   *
         END

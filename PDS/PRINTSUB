PRINTSUB SVLNK R3
* THIS PROGRAM PRINTS THE SUBROUTINE-TO-CALLING-PROGRAM
* LISTING USING THE FILE CREATED BY FINDSUBR AFTER IT
* IS SORTED IN SUBROUTINE NAME ORDER.
         OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
         BAL   R9,HEADING              PRINT FIRST HEADING
         MVI   CC,C'+'                 SET NO SPACE PRINT FOR 1ST LINE
*
*
*
READ     GET   INPUT1                  READ SUBROUTINE XREF RECORD
         LR    R5,R1                   GET RECORD ADDRESS
         USING INREC,R5
         CLC   INSUBR,INPGM            REFERENCE TO ITSELF
         BE    READ                    YES, BYPASS IT
         CLC   INSUBR,XZROS            SUBROUTINE NAME IS HEX ZEROS
         BNE   CKSAME                  NO, CONTINUE
         MVC   INSUBR,NOID             MOVE NO IDENT LITERAL
CKSAME   CLC   HLDSUBR,INSUBR          SAME SUB-ROUTINE
         BE    ADDLINE                 YES, ADD TO CURRENT LINE
         BAL   R9,PRINT                PRINT LAST LINE OF PREV SUBRTN
         MVC   HLDSUBR,INSUBR          SAVE SUBROUTINE NAME
         MVC   PRTSUBR,INSUBR          SUBROUTINE NAME TO PRINT
         MVI   CC,C'0'                 SET DOUBLE SPACING
         LA    R12,PRTPGMS             GET ADDR TO BEGIN PROGRAM PRINT
         ST    R12,NEXPGM              SAVE BEGIN PRINT ADDRESS
ADDLINE  L     R12,NEXPGM              GET ADDRESS TO PRINT PROGRAM
         MVC   0(8,R12),INPGM          PROGRAM NAME TO PRINT
         LA    R12,10(R12)             STEP TO NEXT PRINT LOCATION
         ST    R12,NEXPGM              STORE UPDATED PRINT ADDRESS
         C     R12,ENDPRT              END OF PRINT LINE
         BL    READ                    NO
         LA    R12,PRTPGMS             RESET PRINT ADDRESS
         ST    R12,NEXPGM              SAVE PRINT ADDRESS
         BAL   R9,PRINT                GO PRINT LAST LINE
         B     READ
*
*
*
PRINT    EQU   *                       *** PRINT ROUTINE ***
         PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         AP    LINECT,P1               ADD 1 TO LINE COUNT
         CLI   CC,C'0'                 WAS IT DOUBLE SPACED
         BNE   CLRLINE                 NO
         AP    LINECT,P1               ADD 1 MORE TO LINE COUNTER
CLRLINE  MVI   CC,C' '                 SET SINGLE SPACING
         MVC   PRTSUBR(132),CC         CLEAR PRINT LINE
         CP    LINECT,ENDPAGE          AT END OF PAGE
         BH    HEADING                 YES
         BR    R9                      NO, RETURN
*
*
*
HEADING  EQU   *                       *** HEADING ROUTINE ***
         ZAP   LINECT,P3               RESET LINE COUNTER
         AP    PAGENO,P1               ADD 1 TO PAGE COUNTER
         MVC   HDPAGE,ED1              EDIT WORD TO HEADER PAGE
         ED    HDPAGE,PAGENO           EDIT PAGE NUMBER TO HEADER
         MVI   CC,C' '                 BEGIN CLEARING
         MVC   PRTSUBR(132),CC         PRINT LINE
         MVI   CC,C'0'                 SET DOUBLE SPACING
         PUT   PRINT1,HEAD1            WRITE HEADER LINE 1
         PUT   PRINT1,HEAD2            WRITE HEADER LINE 2
         BR    R9                      RETURN
*
*
*
EOF      CLOSE (INPUT1,,PRINT1)        CLOSE FILES
         L     R13,4(R13)              GET CALLERS SAVE ADDR
         LM    R14,R12,12(R13)         RESTORE CALLERS REGS
         BR    R14                     RETURN TO CALLER
*
*
*
NOID     DC    CL8'NO IDENT'
XZROS    DC    XL8'00'
LINECT   DC    PL2'0'                  LINE COUNTER
P1       DC    P'1'                    CONSTANT 1
NEXPGM   DS    F                       CURRENT PRINT ADDRESS
ENDPAGE  DC    P'56'                   LINE COUNTER VALUE AT END OF PAG
PAGENO   DC    PL2'0'                  CURRENT PAGE NUMBER
P3       DC    P'3'                    CONSTANT 3
ED1      DC    X'40202020'             PAGE NUMBER EDIT WORD
HLDSUBR  DC    CL8' '                  CURRENT SUBROUTINE NAME
ENDPRT   DC    A(PRTPGMS+111)          END PRINT LINE ADDR
*
*
*
PRTLINE  DS    0CL133                  *** PRINT LINE ***
CC       DS    CL1                     CARRIAGE CONTROL BYTE
PRTSUBR  DS    CL8                     SUBROUTINE NAME
         DS    CL2
PRTPGMS  DS    12CL10                  ROOM TO PRINT 12 PROGRAM NAMES
         DS    CL2
*
*
*
INREC    DSECT ,                       *** INPUT RECORD DEFINITION ***
INPGM    DS    CL8                     CALLING PROGRAM NAME
INSUBR   DS    CL8                     CALLED SUBROUTINE NAME
PRINTSUB CSECT
*
*
*
HEAD1    DC    CL44'1'
         DC    CL81'*** SUBROUTINE TO PROGRAM CROSS-REFERENCE ***'
         DC    CL4'PAGE'
HDPAGE   DS    CL4
*
*
*
HEAD2    DC    CL133'0SUB-RTN        CALLING PROGRAMS'
*
*
*
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               LRECL=133,RECFM=FA,BLKSIZE=133
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,EODAD=EOF,              X
               RECFM=FB,LRECL=16
         END

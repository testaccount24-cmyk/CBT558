         TITLE '***** MODSCB: CHANGE FILE EXPIRATION DATE *****'
         PRINT NOGEN
MODSCB   SVLNK R3,R4
*
**********************************************************************
*  THE PARM FIELD IS USED TO DETERMINE WHETHER THE FILE EXPIRATION   *
*  DATE IS TO BE SET TO 00/000 (PARM=LO) OR TO 99/365 (PARM=HI).     *
*  NEXT, THE TIOT IS SCANNED FOR DDNAMES BEGINNING WITH THE LETTERS  *
*  MODSCB WITH ZERO OR MORE FOLLOWING CHARACTERS. WHEN FOUND, THE    *
*  FORMAT 1 DSCB IS READ FROM THE VTOC, THE EXPIRATION DATE IS SET   *
*  PER PARM FIELD SPECIFICATION, AND THE FORMAT 1 LABEL IS THEN      *
*  REWRITTEN. A PRINT LINE IS WRITTEN TO SHOW THE ACTION TAKEM. IF   *
*  ANY ERROR OCCURS AN ERROR MESSAGE IS WRITTEN AND THE PROGRAM      *
*  CONTINUES WITH THE NEXT MODSCBXX DDNAME IN THE TIOT.              *
*                                                                    *
*  AUTHOR R. THORNTON REWRITTEN AUG 1987                             *
**********************************************************************
*
**********************************************************************
*        ANALYZE PARM FIELD FOR PARM=LO OR PARM=HI                   *
**********************************************************************
ANALPARM L     R2,0(R1)                 GET PARM FIELD ADDR
         CLC   0(2,R2),TWO              2 CHARACTER PARM DATA?
         BNE   BADPARM                  NO, ERROR
         CLC   2(2,R2),HI               PARM VALUE IS HI
         BE    ALLOCDYN                 YES
         CLC   2(2,R2),LO               PARM VALUE IS LO
         BE    SETUPLOW                 ERROR IN PARM FIELD
BADPARM  WTO   'INVALID PARM: MUST BE LO OR HI',ROUTCDE=11
         ABEND 888,DUMP                 TERMINATE WITH ERROR
SETUPLOW MVC   USEXPDT(3),LODATE        SET UP LOW DATE
         MVC   SETMESG,SETLOW           SET UP LOW MESSAGE
***********************************************************************
* ALLOCATE A DD STATEMENT FOR USE BY EXCP AND OPEN THE DCB USED FOR   *
* EXCP. IF EITHER THE ALLOCATE OR OPEN FAILS, A U0333 ABEND WILL BE   *
* PRODUCED WITH AN EXPLANATORY MESSAGE.                               *
***********************************************************************
ALLOCDYN OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE A TEMPORARY DATASET
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    SETDDNM                 NO
ALLOCERR WTO   'ERROR: DYNAMIC ALLOC FAILED FOR WORK FILE',ROUTCDE=11
         ABEND 888,DUMP                TERMINATE WITH ERROR
SETDDNM  MVC   VTOC+40(8),DDN          MOVE GENERATED DDNAME
         OPEN  (VTOC,OUTPUT)           OPEN FILE FOR EXCP
         TM    VTOC+48,X'10'           WAS OPEN SUCCESSFUL?
         BO    SAVEUE                  YES
OPENERR  WTO   'ERROR: UNABLE TO OPEN WORK FILE',ROUTCDE=11
         ABEND 888,DUMP                TERMINATE WITH ERROR
SAVEUE   L     R12,VTOC+44             @ DEB
         MVC   SAVUCB,33(R12)          SAVE DEB UCB ADDRESS
         MVC   SAVEXT,38(R12)          SAVE EXTENTS
**********************************************************************
*        LOCATE THE TIOT                                             *
**********************************************************************
FINDTIOT L     R1,16                   GET CVT ADDR
         L     R1,0(R1)                GET TCB POINTER ADDR
         L     R1,0(R1)                GET TCB ADDR
         L     R1,12(R1)               GET TIOT ADDR
         LA    R8,24(R1)               GET 1ST DD ENTRY ADDR
         ST    R8,THISDD               SAVE DD ENTRY ADDR
         B     CKTEND                  GO BEGIN TIOT SEARCH
**********************************************************************
*        SEARCH TIOT FOR NEXT DDNAME BEGINNING WITH MODSCB           *
**********************************************************************
SCHTIOT  L     R8,THISDD               GET TIOT ADDR ENTRY
STEPTIOT SR    R2,R2                   CLEAR WORK REG
         IC    R2,0(R8)                GET DD ENTRY LENGTH
         AR    R8,R2                   GET NEXT DD ENTRY ADDR
         ST    R8,THISDD               SAVE IT'S ADDRESS
CKTEND   CLC   0(4,R8),ZEROS           END OF TIOT
         BE    EOJ                     YES, STOP RUN
         CLC   4(6,R8),MODNAM          DDNAME BEGINS MODSCB+
         BNE   STEPTIOT                NO, CONTINUE SEARCH
***********************************************************************
*        TEST FOR VALID ONLINE 3380 UCB ADDRESS                       *
***********************************************************************
TESTUCB  L     R10,16(R8)              POINT TO UCB
         CLI   2(R10),X'FF'            THIS A GOOD UCB ADDRESS?
         BNE   INVUCB                  NO, ABORT
         TM    3(R10),X'80'            DEVICE IS ONLINE?
         BZ    INVUCB                  NO, ABORT
         CLI   18(R10),X'20'           IS IT DIRECT ACCESS DEVICE?
         BNE   INVUCB                  NO, ABORT
         CLI   19(R10),X'0E'           IS IT A 3380?
         BNE   INVUCB                  NO, ABORT
***********************************************************************
*        COMPUTE VTOC START ADDRESS FROM UCB TTR                      *
***********************************************************************
VTOCSTRT SR    R14,R14                 CLEAR FOR DIVIDE
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R15,3,24(R10)           PICK UP VTOC START TRACK
         D     R14,=F'15'              COMPUTE CYL-HD
         STH   R15,VTOCCYL             SAVE VTOC CYLINDER NBR
         STH   R14,VTOCHD              SAVE VTOC HEAD NUMBER
***********************************************************************
*        SET DEB UCB FOR THIS DEVICE, EXTENTS FOR VTOC                *
***********************************************************************
DEBUCBEX L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         STCM  R10,7,33(R12)           CHANGE DEB UCB ADDRESS
         MVC   38(10,R12),ALLCYL       SET DEB START/END EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
***********************************************************************
*        SET UP EXCP TO READ THE FORMAT 1 DSCB AND READ IT            *
***********************************************************************
READFMT1 MVC   VTSRCH(4),VTOCCYL       START AT VTOC BEGIN CCHH ADDR
         MVI   VTREC,1                 START WITH RECORD 1
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R15,7,12(R8)            POINT TO TIOEJFCB
         MVC   DSNAME,16(R15)          MOVE DSNAME FOR SEARCH
         MVI   RWCCW,6                 SET CCW TO READ DATA
         EXCP  VTOCIOB                 LOCATE AND READ FORMAT 1 DSCB
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             NORMAL COMPLETION
         BNE   READERR                 NO, ABORT
***********************************************************************
*        SET FORMAT 1 EXPIRATION DATE AND REWRITE FORMAT 1 DSCB       *
***********************************************************************
REWFMT1  MVC   DS1EXPDT,USEXPDT        SET EXPIRATION DATE
         MVI   RWCCW,5                 CHANGE READ COMMAND TO WRITE
         EXCP  VTOCIOB                 LOCATE AND REWRITE DATA PORTION
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             I/O COMPLETED NORMALLY
         BE    SUCCESS                 YES
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
REWRTER  MVC   MSGTEXT,BADREW          REWRITE I/O ERROR MSG
         B     WTORSLT                 GO WRITE MESSAGE
INVUCB   MVC   MSGTEXT,BADUCB          INVALID UCB ERROR MSG
         B     WTORSLT                 GO WRITE MESSAGE
READERR  MVC   MSGTEXT,BADREAD         FORMAT 1 READ ERROR MSG
         B     WTORSLT                 GO WRITE MESSAGE
SUCCESS  MVC   MSGTEXT,EXPSET          GOOD EXPDT CHANGE
         MVI   FOUNDSW,1               SET FOUND SWITCH
WTORSLT  PUT   SYSPRINT,PRTLINE        PRINT MESSAGE
         B     SCHTIOT                 CONTINUE TIOT SEARCH
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      L     R12,VTOC+44             @ DEB
         SMODE KEY=0,STATE=P           GET KEY ZERO
         MVC   33(3,R12),SAVUCB        RESTORE DEB UCB ADDRESS
         MVC   38(10,R12),SAVEXT       RESTORE DEB EXTENTS
         SMODE KEY=NZERO,STATE=P       BACK TO NORMAL STATES
         CLI   FOUNDSW,1               ANY MODSCB DDNAMES FOUND?
         BE    EOJ2                    YES
         WTO   'ERROR: NO MODSCB DDNAMES FOUND',ROUTCDE=11
         ABEND 999,DUMP                ERROR END
EOJ2     CLOSE (SYSPRINT,,VTOC)        CLOSE FILES USED
         L     R13,4(R13)              GET O/S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE O/S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO O/S
*
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
*
SAVUCB   DS    XL3                     SAVED DEB UCB ADDRESS
SAVEXT   DS    XL10                    SAVED DEB EXTENT
TWO      DC    H'2'                    CONSTANT 2
USEXPDT  DC    AL1(99),AL2(365)        HIGH EXPIRATION DATE 99/365
LODATE   DC    XL3'00'                 LOW EXPIRATION DATE, ZEROS
THISDD   DS    F                       CURRENT TIOT DD ENTRY ADDR
MODNAM   DC    CL6'MODSCB'             DDNAME PREFIX
ZEROS    DC    XL4'00'                 ZEROES
LO       DC    C'LO'                   CONSTANT LO
HI       DC    C'HI'                   CONSTANT HI
BLANX    DC    CL80' '                 CONSTANT BLANKS
FOUNDSW  DC    X'00'                   MODSCB DDNAMES FOUND
ALLCYL   DS    0H                      DEB EXTENTS
VTOCCYL  DS    H                       VTOC CYLINDER
VTOCHD   DS    H                       VTOC HEAD
         DC    H'32767',H'32767'       END DEB EXTENT
*
*
PRTLINE  DS    0CL133                  PRINT LINE
         DC    CL1' '                  CARRIAGE CONTROL BYTE
DSNAME   DS    CL44 DATASET NAME
MSGTEXT  DS    CL36                    MESSAGE TEXT
         DC    CL52' '
*
*
BADREAD  DC    CL36'ABORTED: ERROR READING FORMAT1 LABEL'
BADREW   DC    CL36'ABORTED: ERROR REWRITING FORMAT1 LBL'
BADUCB   DC    CL36'ABORTED: INVALID UCB OR DEVICE TYPE '
EXPSET   DC    CL24' EXPIRATION DATE SET TO '
SETMESG  DC    CL6'99/365'
         DC    CL6' '
SETLOW   DC    CL6'00/000'
*
*
*
DAPARM1  DS    0F                      * 1ST PARAMETER FOR DYNALLOC *
DDN      DC    CL8'INPUT9'             DDNAME
         DC    CL44'&&MODSCB'          DATASET NAME
         DC    XL8'00'                 MEMBER NAME
DISPOS   DC    XL3'00'                 DISPOSITION:
*                                    BYTE1: 0=OLD,M=MOD,N=NEW,S=SHR
*                                    BYTE2: U=UNCATLG,C=CATLG,D=DELETE
*                                    K=KEEP; BYTE3: SAME AS BYTE2
UNNAME   DC    CL8'DISK'               UNIT NAME
         DC    XL2'00'                 UNIT COUNT
VOLUSRL  DC    XL6'00'                 VOLUME SERIAL NUMBER
         DC    XL1'00'                 LABEL TYPE:
*                                      N=NL, S=SL, B=BLP
         DC    XL2'00'                 DATASET SEQUENCE NBR (TAPE)
         DC    XL1'00'                 F: FREE=CLOSE
         DC    XL2'00'                 RETENTION PERIOD IN DAYS
         DC    XL3'00'                 RECORDING FORMAT:
*                                      BYTE1: F=FIXED, V=VARIABLE,
*                                      U=UNDEFINED; BYTE2: B=BLOCKED,
*                                      BYTE3: A=ASA CONTROL CHARACTERS
         DC    XL2'00'                 LOGICAL RECORD LENGTH
         DC    XL2'00'                 BLOCK SIZE
         DC    XL1'00'                 TAPE DENSITY: 1=1600,6=6250
         DC    XL2'00'                 BUFNO
         DC    XL2'00'                 KEY LENGTH
         DC    XL1'00'                 DATASET ORGANIZATION:
*                                      V=VSAM, P=PDS, D=DIRECT, S=SEQ
SPCTYPE  DC    CL1'T'                  SPACE TYPE:
*                                      C=CYL, T=TRK
SPCSIZE  DC    XL2'01'                 PRIMARY SPACE AMOUNT
         DC    XL2'00'                 SECONDARY SPACE AMOUNT
         DC    CL1'00'                 R=RLSE
         DC    XL2'00'                 NBR DIRECTORY BLOCKS
         DC    XL5'00'                 EXPIRATION DATE YYDDD
*
*
*
DAPARM2  DS    0F                      2ND PARAMETER FOR DYNALLOC
         DC    CL1'A'                  REQUEST CODE:
*                                      A=ALLOCATE A DATASET
*                                      V=VOLSER FEEDBACK
RETRNCD  DC    CL2' '                  DYNALLOC RETURN CODE
ERRRET   DC    CL4' '                  ERROR RETURN CODE
INFORET  DC    CL4' '                  INFO RETURN CODE
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
CMDCHN   DS    0D                      COMMAND CHAIN TO READ/WRITE
         CCW   X'1A',VTSRCH,X'70',5 READ HA TO POSITION TO INDEX PT
         CCW   X'A9',DSNAME,X'40',44 SEARCH DSCB'S FOR THAT NAMED
         CCW   8,*-8,0,0               TIC
RWCCW    CCW   6,DS1FMTID,0,96         READ FORMAT 1 DSCB
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,                      X
               RECFM=FBA,LRECL=133,BLKSIZE=133
VTOC     DCB   DDNAME=MODSCB,MACRF=E
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
         DSCBDEF  DSECT=NO,FMT2=NO,FMT3=NO,FMT4=NO,FMT5=NO,FMT6=NO
         END

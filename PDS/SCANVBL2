         PRINT NOGEN
         TITLE '*****SCANVBL2: SCAN RECFM=V PDS FOR HARD-CODED DATA **'
SCANVBL2 SVLNK R3
*
***********************************************************************
* SCAN FILE OF RECFM=V RECORDS FOR STANDARD HEADER. IF FOUND, FORMAT  *
* AN OUTPUT RECORD GIVING THE MEMBER NAME, CREATOR, TYPICAL USER(S)   *
* AND LAST CHANGER. IF A MEMBER HAVING STANDARD HEADER ALSO CONTAINS  *
* A CALL TO CLISTMON, WRITE A MESSAGE. IF A MEMBER WITHOUT STANDARD   *
* HEADER DOES NOT CONTAIN A CALL TO CLISTMON, WRITE A MESSAGE.        *
*                                                                     *
* THE SYSOUT FILE MUST HAVE RECFM=FB,LRECL=80, WHICH IS HARD-CODED.   *
*                                                                     *
* AUTHOR R THORNTON AUG 1998                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN INPUT AND OUTPUT FILES.                        *
***********************************************************************
         OPEN  (SYSOUT,OUTPUT,SYSLIB) OPEN FILES
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
         CALL  READVMEM,OPEN           OPEN MEMBER FILE
***********************************************************************
* MAINLINE PROCESSING: LOCATE THE NEXT MEMBER AND POSITION TO IT. FOR *
* FOR EACH MEMBER, PERFORM THE MEMBER ROUTINE. WHEN THERE ARE NO MORE *
* MEMBERS TO BE PROCESSED, TERMINATE.                                 *
***********************************************************************
MAINLINE CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
         CALL  READVMEM,FIND           LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BE    MAINLINE                NO, TRY NEXT MEMBER
         BAS   R9,MEMBER               GO PROCESS THE MEMBER
         B     MAINLINE                THEN GET NEXT ONE
***********************************************************************
* READ ALL RECORDS IN THE MEMBER. THE LINE ROUTINE IS CALLED FOR EACH *
* RECORD. AT END OF MEMBER, OR AFTER A CALL IS FOUND FOR CLISTMON     *
* OUTPUT LINES ARE FORMATTED IF A STANDARD HEADER WAS FOUND, IF A     *
* MEMBER WITH STANDARD HEADER HAD A CALL TO CLISTMON, OR IF A MEMBER  *
* WITHOUT STANDARD HEADER DID NOT HAVE A CLISTMON CALL.               *
***********************************************************************
MEMBER   ST    R9,MB9                  SAVE RETURN ADDRESS
         MVI   CALLCLST,0              RESET INDICATOR
         MVI   HEADER,0                RESET INDICATOR
         MVI   GOTAUTH,0               RESET INDICATOR
         MVI   GOTTYP,0                RESET INDICATOR
         MVC   HDRLINE,BLANKS          CLEAR HEADER LINE
MBREAD   CALL  READVMEM,(READ,MEMRECAD) READ NEXT RECORD IN MEMBER
         CLI   MEMRECAD,EOF            IS IT EOF
         BE    MBREND                  YES, GO CHECK RESULTS
         BAS   R9,LINE                 GO PROCESS THE LINE
         CLI   COMMENT,1               WAS IT A COMMENT LINE?
         BE    MBREAD                  YES, GO READ NEXT
         CLI   CALLCLST,1              WAS IT A CALL TO CLISTMON?
         BNE   MBREAD                  NO, GO READ NEXT RECORD
MBREND   CLI   HEADER,1                WAS A HEADER FOUND?
         BNE   MBCHKCAL                NO
         PUT   SYSOUT,HDRLINE          YES, PRINT HEADER DATA
         CLI   CALLCLST,1              WAS A CALL TO CLISTMON FOUND?
         BNE   MBXIT                   NO, EXIT
         MVC   HCNAME,MEMNAME          MEMBER NAME TO MESSAGE
         PUT   SYSOUT,HADCALL          YES, WRITE MESSAGE
         B     MBXIT                   AND EXIT
MBCHKCAL CLI  CALLCLST,1               WAS A CALL TO CLISTMON FOUND?
         BE    MBXIT                   YES, EXIT
         MVC   NCNAME,MEMNAME          MEMBER NAME TO MESSAGE
         PUT   SYSOUT,NOCALL           NO, WRITE MESSAGE
MBXIT    L     R9,MB9                  GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
* THE LINE ROUTINE PROCESSES EACH LINE TO DETERMINE WHETHER IT IS A   *
* COMMENT OR NOT. COMMENT LINES ARE PROCESSED BY THE COMNTLIN ROUTINE *
* NON-COMMENTS ARE PROCESSED BY THE NONCMNT ROUTINE                   *
***********************************************************************
LINE     ST    R9,LN9                  SAVE RETURN ADDRESS
         L     R11,MEMRECAD            GET RECORD ADDRESS
         LR    R12,R11                 POINT TO
         AH    R12,0(R11)                 END OF RECORD
         LA    R11,4(R11)              POINT TO 1ST BYTE OF RECORD
LNCKND   CR    R11,R12                 PAST END OF RECORD?
         BNL   LNXIT                   YES, EXIT
         CLI   0(R11),C' '             AT A BLANK?
         BE    LNADV1                  YES
         CLI   0(R11),C'0'             POSSIBLE NUMERIC DIGIT?
         BL    LNAT1ST                 NO
         CLI   0(R11),C'9'             IS IT A NUMERIC DIGIT?
         BH    LNAT1ST                 NO
LNADV1   LA    R11,1(R11)              STEP PAST LEADING DIGITS, BLANKS
         B     LNCKND                  LOOP TO FIND START OF DATA
LNAT1ST  CLC   0(2,R11),SLSHSTAR       THIS A COMMENT LINE?
         BNE   LNNOTCMT                NO
         MVI   COMMENT,1               INDICATE THIS IS A COMMENT LINE
         CLI   HEADER,1                HEADER ALREADY FOUND?
         BE    LNXIT                   YES
         BAS   R9,COMNTLIN             GO PROCESS COMMENT LINE
         B     LNXIT                   AND EXIT
LNNOTCMT MVI   COMMENT,0               INDICATE THIS IS NOT A COMMENT
         BAS   R9,NONCMNT              GO PROCESS NON-COMMENT LINE
LNXIT    L     R9,LN9                  GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
* PROCESS COMMENT LINES. ON ENTRY, R11 POINTS TO THE LEADING /* AND   *
* R12 POINTS AT THE RIGHT END OF THE LINE +1. EACH COMMENT LINE IS    *
* CHECKED TO SEE IF IT IS PART OF THE STANDARD HEADER. IF SO, NEEDED  *
* INFORMATION IS SAVED, AND THE HEADER SWITCH IS SET.                 *
***********************************************************************
COMNTLIN ST    R9,CL9                  SAVE RETURN ADDRESS
         LA    R11,2(R11)              STEP PAST THE /*
CLCKEND  CR    R11,R12                 STILL WITHIN LINE?
         BNL   CLXIT                   NO
         CLI   0(R11),C' '             AT A BLANK?
         BNE   CLCKAUTH                NO
         LA    R11,1(R11)              YES, STEP PAST IT
         B     CLCKEND
CLCKAUTH CLC   0(L'AUTHOR,R11),AUTHOR  THIS AN AUTHOR LINE?
         BE    CLGOTA                  YES
         CLC   0(L'TYPICAL,R11),TYPICAL THIS A TYPICAL USER LINE?
         BE    CLGOTT                  YES
         CLI   GOTAUTH,1               AUTHOR FOUND?
         BNE   CLXIT                   NO
         CLI   GOTTYP,1                TYPICAL USER(S) FOUND?
         BNE   CLXIT                   NO
         CLI   2(R11),C'/'             POSSIBLE LAST CHANGE LINE
         BNE   CLXIT                   NO, EXIT
         CLI   5(R11),C'/'             POSSIBLE LAST CHANGE LINE
         BNE   CLXIT                   NO, EXIT
         CLI   11(R11),C' '            USERID PRESENT?
         BE    CLXIT                   NO, EXIT
         MVI   HEADER,1                INDICATE THIS CONTAINS A HEADER
         CLC   11(7,R11),BC0THOR       IS IT BC0THOR?
         BE    CLXIT                   YES
         MVC   HDRLAST,11(R11)         MOVE LAST CHANGER ID
         B     CLXIT                   EXIT
CLGOTA   LA    R11,L'AUTHOR(R11)       STEP PAST LITERAL
CLGACKND CR    R11,R12                 PAST END OF LINE?
         BNL   CLXIT                   YES, EXIT
         CLI   0(R11),C' '             AT AUTHOR NAME/USERID?
         BNE   CLGAMVAU                YES
         LA    R11,1(R11)              NO, ADVANCE 1 BYTE
         B     CLGACKND                CONTINUE SEARCH
CLGAMVAU MVC   HDRAUTH,0(R11)          MOVE AUTHOR FIELD
         MVI   GOTAUTH,1               INDICATE AUTHOR FOUND
         MVC   HDRMEM,MEMNAME          MOVE MEMBER NAME
         B     CLXIT                   GO SET UP HEADER
CLGOTT   LA    R11,L'TYPICAL(R11)      STEP PAST LITERAL
CLGTCKND CR    R11,R12                 PAST END OF LINE?
         BNL   CLXIT                   YES, EXIT
         CLI   0(R11),C' '             AT USER FIELD?
         BNE   CLGTMVTY                NO, EXIT
         LA    R11,1(R11)              ADVANCE 1 BYTE
         B     CLGTCKND                CONTINUE SEARCH
CLGTMVTY MVC   HDRTYP,0(R11)           MOVE TYPICAL USER(S) FIELD
         MVI   GOTTYP,1                INDICATE TYPICAL USER(S) FOUND
CLXIT    L     R9,CL9                  GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
* PROCESS NON-COMMENT LINES. ON ENTRY R11 POINTS TO THE FIRST DATA    *
* CHARACTER ON THE LINE AND R12 POINTS AT THE RIGHT END OF THE LINE   *
* +1. THE LINE IS CHECKED TO SEE IF IT CONTAINS A CALL TO THE CLISTMON*
* ROUTINE. IF IT DOES, THE CALLCLST SWITCH IS SET.                    *
***********************************************************************
NONCMNT  ST    R9,NC9                  SAVE RETURN ADDRESS
         CLI   0(R11),C'"'             BEGINS WITH '"'?
         BNE   NCCHKCL                 NO
         LA    R11,1(R11)              YES, STEP PAST IT
NCCHKCL  CLC   0(5,R11),CALLWORD       IS IT A CALL?
         BNE   NCXIT                   NO
         LA    R11,5(R11)              STEP PAST THE WORD "CALL "
NCCHKND  CR    R11,R12                 PAST END OF RECORD?
         BNL   NCXIT                   YES, EXIT
         CLI   0(R11),C' '             AT A BLANK?
         BNE   NCATNXT                 NO
         LA    R11,1(R11)              YES, ADVANCE 1 BYTE
         B     NCCHKND                 CONTINUE SEARCH
NCATNXT  CLC   0(L'TARGET,R11),TARGET  THIS A CALL TO CLISTMON?
         BNE   NCXIT                   NO, EXIT
         MVI   CALLCLST,1              INDICATE THIS IS A CLISTMON CALL
NCXIT    L     R9,NC9                  GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
* END OF INPUT PDS DIRECTORY AND THEREFORE END OF JOB                 *
***********************************************************************
EOJ      CLOSE (SYSOUT,,SYSLIB)        CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
MEMRECAD DS    A                       ADDRESS OF MEMBER RECORD
NC9      DS    A                       RETURN ADDRESS FOR NONCMNT
CL9      DS    A                       RETURN ADDRESS FOR COMNTLIN
MB9      DS    A                       RETURN ADDRESS FOR MEMBER
LN9      DS    A                       RETURN ADDRESS FOR LINE
*
EOF      EQU   X'FF'                   EOF INDICATOR
SLSHSTAR DC    CL2'/*'                 BEGINNING OF COMMENT
CALLCLST DS    XL1                     1=CALL FOR CLISTMON FOUND
HEADER   DS    XL1                     1=STANDARD HEADER FOUND
COMMENT  DS    XL1                     1=RECORD IS A COMMENT
GOTAUTH  DS    XL1                     1=AUTHOR FOUND
GOTTYP   DS    XL1                     1=GOT TYPICAL USER(S)
BC0THOR  DC    CL8'BC0THOR'            CONSTANT BC0THOR
CALLWORD DC    CL5'CALL '              CONSTANT "CALL "
TARGET   DC    C'''TEST.LOAD(CLISTMON)'''
AUTHOR   DC    CL8'AUTHOR: '           STANDARD HEADER AUTHOR LINE
TYPICAL  DC    CL17'TYPICAL USER(S): ' STANDARD HEADER TYPICAL USER
BLANKS   DC    CL80' '                 BLANKS
*
HDRLINE  DS    0CL80                   STANDARD HEADER DATA LINE
HDRMEM   DS    CL8                        MEMBER NAME
         DS    CL1
HDRAUTH  DS    CL22                       AUTHOR
         DS    CL1
HDRLAST  DS    CL8                        LAST CHANGER
         DS    CL1
HDRTYP   DS    CL39                       TYPICAL USER(S)
*
OPEN     DC    CL9'OSYSLIB'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
*
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
*
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
*
HADCALL  DS    0CL80                   STD HDR W/CALL FOR CLISTMON
         DC    CL10'**ERROR** '
HCNAME   DS    CL8                     MEMBER NAME
         DC    CL62' HAS HEADER AND ALSO A CALL TO CLISTMON'
NOCALL   DS    0CL80                   NO HDR, NO CALL TO CLISTMON
         DC    CL10'**ERROR** '
NCNAME   DS    CL8                     MEMBER NAME
         DC    CL62' HAS NO HEADER, NO CALL TO CLISTMON'
*
SYSLIB   DCB   DSORG=PS,MACRF=GM,DDNAME=SYSLIB,EODAD=EOJ
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,                        X
               RECFM=FB,LRECL=80
         LTORG
         END

         TITLE '***** LIST ALL ONLINE VOLUMES *****'
         PRINT GEN
VOLLIST2 SVLNK R3,R4
         LA    R5,4095(R4)          SET UP
         LA    R5,1(R5)               THIRD BASE REG
         USING VOLLIST2+8192,R5
*
**********************************************************************
*  LOOKS THROUGH ALL ONLINE DASD VOLUMES AND REPORTS ON WHETHER EACH *
*  VOLUME HAS AN INDEXED VTOC, AND IF IT IS ENABLED. USES LSPACE     *
*  MACRO TO ACCESS THE VTOC. WHEN NO VTOC INDEX IS INDICATED, AN     *
*  OBTAIN.CAMLST MACRO IS ISSUED TO TRY TO READ THE FORMAT 1 DSCB FOR*
*  SYS1.VTOCIX.VVVVVV, WHERE VVVVVV IS THE VOLUME SERIAL. IF FOUND,  *
*  THE VTOC INDEX IS ASSUMED PRESENT, BUT DISABLED, RATHER THAN NOT  *
*  PRESENT AT ALL.                                                   *
*  AUTHOR R. THORNTON JUN 1990                                       *
**********************************************************************
*
***********************************************************************
* OPEN SYSPRINT FOR PRINTING                                          *
***********************************************************************
*
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),H0              ANY PARM DATA?
         BE    OPENS                   NO
*        CLC   2(2,R2),____            PARM VALUE IS ____
         MVI   PSW,1                   SET PRINT PSW
         MVC   HDREND,HDRLITS          MOVE IN HEADER FIELDS
OPENS    OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         MVC   PRT,HEADING             HEADING TO PRINT
         BAL   R9,PRINT                WRITE HEADING
         MVC   PRT,BLANKS              CLEAR PRINT LINE
***********************************************************************
*       LOOK THROUGH THE UCB TABLE IN O/S FOR ONLINE DASD DEVICE UCBS.*
*       WHEN FOUND, PRINT A LINE USING UCB DATA.                      *
***********************************************************************
VOLLOOP  L     R1,=A(PRMLST)           POINT TO PARMLIST
         L     R15,CVTPTR              POINT TO CVT
         USING CVTMAP,R15
         MVC   VOLSYSID,CVTSNAME       SYSTEM ID TO OUTPUT
         L     R15,CVTUCBSC            GET UCB SEARCH ROUTINE ADDR
         DROP  R15
         BALR  R14,R15                 GO SEARCH FOR UCB
         LTR   R15,R15                 FOUND UCB?
         BNZ   EOJ                     NO, ALL THROUGH HERE
         L     R2,ADDRUCB              YES, GET THE UCB ADDRESS
         USING UCBOB,R2
         TM    UCBSTAT,UCBONLI         IS IT ONLINE?
         BZ    VOLLOOP                 NO, IGNORE IT
         MVI   VOLCODE,C' '            CLEAR DEVICE CODE
         CLI   UCBUNTYP,X'0B'          IS IT A 3350?
         BE    GOT3350                 YES
         CLI   UCBUNTYP,X'0E'          IS IT A 3380?
         BE    GOT3380                 YES
         CLI   UCBUNTYP,X'0F'          IS IT A 3390?
         BE    GOT3390                 YES
         MVC   VOLTYPE,=C'????'        NO, UNKNOWN DEVICE
         B     SETVTOC                 CONTINUE
GOT3390  MVC   VOLTYPE,=C'3390'        DEVICE=3390
         B     SETVTOC                 CONTINUE
GOT3380  MVC   VOLTYPE,=C'3380'        DEVICE=3380
         B     SETVTOC                 CONTINUE
GOT3350  MVC   VOLTYPE,=C'3350'        DEVICE=3350
SETVTOC  ICM   R15,15,UCBVTOC          GET VTOC TTR0 TO R15
         CLI   PSW,0                   PRINT VTOC TTR?
         BE    MOVVLSR                 NO
         BAL   R9,HEXPRNT              CONVERT TO PRINTABLE
         MVC   VOLVTOC,WORK            VTOC TTR TO PRINT
MOVVLSR  MVC   VOLVOL,UCBVOLI          VOLUME SERIAL TO PRINT
         MVC   VOLCUU,UCBNAME          CHANNEL-UNIT ADDRESS TO PRINT
TSTMNT   MVC   VOLMNT,=CL4'PRIV'       ASSUME PRIVATE
         TM    UCBSTAB,UCBBPRV         IS IT PRIVATE?
         BO    LSPACE1                 YES
         MVC   VOLMNT,=CL4'PUBL'       ASSUME PUBLIC
         TM    UCBSTAB,UCBBPUB         IS IT PUBLIC?
         BO    LSPACE1                 YES
         MVC   VOLMNT,=CL4'STOR'       ASSUME STORAGE
         TM    UCBSTAB,UCBBSTR         IS IT STORAGE?
         BO    LSPACE1                 YES
         MVC   VOLMNT,=CL4'????'       UNKNOWN VOLUME STATUS
LSPACE1  LSPACE UCB=(2),DATA=LSPACEDT,F4DSCB=IECSDSL4 GET VTOC DATA
         LTR   R15,R15                 ANY ERROR?
         BZ    GOODLSP                 NO
         MVC   VOLINDX(12),=CL12'LSPACE ERROR'
         B     PRTALINE
GOODLSP  MVC   LSPCSTAT,LSPDSTAT       SAVE
         NI    LSPCSTAT,X'C0'          LEAVE ONLY '80' AND '40'
         MVC   FMT4STAT,DS4VTOCI       SAVE
         NI    FMT4STAT,X'41'          LEAVE ONLY '40' AND '01'
         TM    LSPDSTAT,X'80'          IS VTOC INDEXED?
         BO    TSTDISB                 YES
         MVC   CAMVOL,VOLVOL           VOLSER FOR CAMLST
         MVC   DSNVOL,VOLVOL           VOLSER SUFFIX FOR DSN
         MVI   DSNVOL+6,C' '           CLEAR FOLLOWING BYTE
         TM    DSNVOL,C'0'             NUMERIC VOLUME SERIAL?
         BNO   OBTAIN                  NO
TRYV     MVI   DSNVOL,C'V'             YES, PREFIX WITH A V
         MVC   DSNVOL+1(7),VOLVOL      AND FOLLOW WITH VOLUME SERIAL
OBTAIN   OBTAIN CAMLST                 TRY TO READ VTOC INDEX LABEL
         LTR   R15,R15                 WAS THE VTOC INDEX DSCB FOUND?
         BZ    ISDISB                  YES, MUST BE DISABLED
         CLI   DSNVOL+6,C' '           ALREADY TRIED A LEADING V?
         BE    TRYV                    NO
         MVC   VOLINDX,=CL4'NONE'      SHOW NOT INDEXED
         B     PRTVTOC                 GO PRINT
TSTDISB  TM    LSPDSTAT,X'40'          IS VTOC INDEX DISABLED?
         BZ    ISDISB                  YES
         MVC   VOLINDX,=CL4'ACTV'      INDICATE ACTIVE INDEX
         B     PRTVTOC                 GO PRINT
ISDISB   MVC   VOLINDX,=CL4'DISA'      INDICATE VTOC INDEX DISABLED
PRTVTOC  MVC   VOL0FRE,EDMSK           EDIT MASK TO PRINT
         L     R1,LSPDF0S              GET DSCB0 COUNT
         CVD   R1,DBLWD                CONVERT
         ED    VOL0FRE,DBLWD+5         EDIT TO PRINT
         MVC   VOLVIRS,EDMSK           EDIT MASK TO PRINT
         L     R1,LSPDVIRS             GET FREE VIR COUNT
         CVD   R1,DBLWD                CONVERT
         ED    VOLVIRS,DBLWD+5         EDIT TO PRINT
         CLI   PSW,0                   PRINT FREE EXTENTS?
         BE    PFRECYL                 NO
         MVC   VOLXTNT,EDMSK           EDIT MASK TO PRINT
         L     R1,LSPDNEXT             GET NBR FREE EXTENTS COUNT
         CVD   R1,DBLWD                CONVERT
         ED    VOLXTNT,DBLWD+5         EDIT TO PRINT
PFRECYL  MVC   VOLTCYL,EDMSK           EDIT MASK TO PRINT
         L     R1,LSPDTCYL             GET NBR FREE CYLINDERS COUNT
         CVD   R1,DBLWD                CONVERT
         ED    VOLTCYL,DBLWD+5         EDIT TO PRINT
         MVC   VOLLCYL,EDMSK           EDIT MASK TO PRINT
         L     R1,LSPDLCYL             FREE CYL IN LARGEST XTNT
         CVD   R1,DBLWD                CONVERT
         ED    VOLLCYL,DBLWD+5         EDIT TO PRINT
         CLC   VOLTYPE,=C'3380'        DEVICE=3380?
         BNE   CHKIF90                 NO
         MVI   VOLCODE,C'K'            ASSUME K UNIT
         CLC   DS4DEVSZ(2),=X'0A60'    IS IT A K? (2,656 TRACKS)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'E'            NO, ASSUME E UNIT
         CLC   DS4DEVSZ(2),=X'06EB'    IS IT AN E? (1771 TRACKS)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'D'            NO, ASSUME D UNIT
         CLC   DS4DEVSZ(2),=X'0376'    IS IT A D? (886 TRACKS)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'?'            UNKNOWN UNIT CODE
         B     PRTALINE                CONTINUE
CHKIF90  CLC   VOLTYPE,=C'3390'        DEVICE=3390?
         BNE   PRTALINE                NO
         MVI   VOLCODE,C'2'            ASSUME MODEL 2 UNIT
         CLC   DS4DEVSZ(2),=X'08B3'    IS IT A 2? (2,227 CYL)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'3'            NO, ASSUME MODEL 3 UNIT
         CLC   DS4DEVSZ(2),=X'0D0C'    IS IT A 3? (3,340 CYL)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'9'            NO, ASSUME MODEL 9 UNIT
         CLC   DS4DEVSZ(2),=X'2724'    IS IT A 9? (10,020 CYL)
         BE    PRTALINE                YES
         MVI   VOLCODE,C'?'            UNKNOWN UNIT CODE
PRTALINE BAL   R9,PRINT                WRITE VOLUME DATA MESSAGE
         B     VOLLOOP                 CONTINUE
***********************************************************************
*           END OF JOB ROUTINE                                        *
***********************************************************************
EOJ      CLOSE SYSPRINT                CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        PRINT A LINE                                                 *
***********************************************************************
PRINT    PUT   SYSPRINT,PRT            WRITE PRINT LINE
CLRPRT   MVC   PRT,BLANKS              CLEAR PRINT LINE
         BR    R9                      EXIT
***********************************************************************
*        ROUTINE TO CONVERT HEX TO PRINTABLE CHARACTERS (2 CHARACTERS *
*        PER HEX BYTE. ON ENTRY, HEX TO BE CONVERTED IS IN R15. THE   *
*        CHARACTER EQUIVALENT IS PLACED IN THE FIRST 8 BYTES OF WORK. *
***********************************************************************
HEXPRNT  STCM  R15,15,WORK              SAVE HEX VALUE
         UNPK  WORK(9),WORK(5)          CONVERT TO 1 NIBBLE PER BYTE
         MVZ   WORK(8),XZROS            CLEAR ZONES FOR TRANSLATE
         TR    WORK(8),TRTBLA           TRANSLATE TO PRINTABLE
         BR    R9                       EXIT
***********************************************************************
*        ROUTINE TO CONVERT CHARACTERS TO HEX, TWO CHARACTERS PER BYTE*
*        ON ENTRY, R14 POINTS TO THE HEX FIELD AND R15 CONTAINS THE   *
*        NUMBER OF CHARACTER PAIRS TO CONVERT (80 MAX). THE HEX       *
*        RESULT IS STORED IN THE HEX FIELD. CHERR=1 IF ANY OF THE     *
*        CHARACTERS ARE INVALID (NOT 0-9 OR A-F).                     *
***********************************************************************
CHARHEX  LA    R12,HEX                  POINT TO HEX FIELD
         MVI   CHERR,0                  CLEAR ERROR INDICATOR
NEXPAIR  MVC   0(1,R12),1(R14)          MOVE SECOND CHAR (NUMERIC)
         TR    0(1,R12),TRCHAR          TRANSLATE
         CLI   0(R12),X'FF'             INVALID CHARACTER?
         BNE   DO1ST                    NO
CHERRS   MVI   CHERR,1                  INDICATE INVALID CHARACTER
         BR    R9                       EXIT
DO1ST    MVC   WORK(1),0(R14)           MOVE 1ST CHAR (ZONE)
         TR    WORK(1),TRCHAR           TRANSLATE
         CLI   WORK,X'FF'               INVALID CHARACTER?
         BE    CHERRS                   YES, ERROR
         PACK  WORK(1),WORK(1)          INVERT ZONE BYTE
         MVZ   0(1,R12),WORK            SET ZONE IN PLACE
         LA    R12,1(R12)               STEP OVER 1 BYTE IN CHARS
         LA    R14,2(R14)               STEP OVER 2 CHARS IN INPUT
         BCT   R15,NEXPAIR              CONTINUE
         BR    R9                       EXIT
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
DBLWD    DS    D                       WORK
WORK100  DC    XL100'00'               WORK AREA FOR CVTUCBSC
ADDRUCB  DC    A(0)                    UCB ADDR FROM IOSVSUCB
H0       DS    H'0'                    CONSTANT 0
H3       DC    H'3'                    CONSTANT 3
H4       DC    H'4'                    CONSTANT 4
H7       DC    H'7'                    CONSTANT 7
SYSTMID  DS    CL8                     SYSTEM ID
NUMTST   DC    CL8'00000000'           NUMERIC TEST AREA
ZEROS    DC    CL8'00000000'           CONSTANT ZEROS
XZROS    DC    XL8'00'                 HEX ZEROS
HEX      DS    XL80                    HEX RESULT FROM CHARHEX
CHERR    DC    X'00'                   ERROR INDICATOR FROM CHARHEX
DEVCLASS DC    AL1(UCB3DACC)           SEARCH DEVICE TYPE
WORK     DS    CL11                    WORK AREA
TRTBLA   DC    C'0123456789ABCDEF'     TRANSLATE TABLE
PSW      DC    X'00'                   PRINT PSW
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
P50      DC    P'50'                   CONSTANT 50
EDMSK    DC    XL7'4020206B202020'     EDIT MASK
BLANKS   DC    CL80' '                 CONSTANT BLANKS
LSPCSTAT DS    CL1' '                  LSPACE STATUS: 80=INDEX
*                                      DISABLED, C0=INDEX ACTIVE, 00=
*                                      VTOC NOT INDEXED
FMT4STAT DS    CL1' '                  FORMAT 4 DSCB STATUS: 40=INDEX
*                                      DISABLED, 01=INDEX ACTIVE, 00=
*                                      VTOC NOT INDEXED
*
CAMLST   CAMLST SEARCH,CAMDSN,CAMVOL,DSCBAREA
CAMDSN   DS    0CL44                   DSNAME FOR VTOC INDEX
         DC    CL12'SYS1.VTOCIX.'        DSNAME PREFIX CONSTANT
DSNVOL   DS    CL6                       DSNAME SUFFIX IS VOLSER
         DC    CL26' '                   TRAILING SPACES
CAMVOL   DS    CL6                     VOLUME SERIAL
DSCBAREA DS    CL140                   AREA TO READ DSCB
*                                      DISABLED, 01=INDEX ACTIVE, 00=
*                                      VTOC NOT INDEXED
*
PRMLST   DS    0F                      PARMLIST FOR CVTUCBSC
         DC    A(WORK100)                 WORK AREA ADDRESS
         DC    A(DEVCLASS)                DEVICE CLASS TYPE BYTE
         DC    XL1'80',AL3(ADDRUCB)       UCB ADDRESS RETURN FIELD
*
LSPACEDT DS    0XL36                   DATA RETURNED BY LSPACE
LSPDRETN DS    XL1                     STATUS: 80=RETURNED SPACE INFO,
*                                      40=RETURNED FORMAT0 DSCB COUNT,
*                                      20=RETURNED FREE VIR COUNT
LSPDSTAT DS    XL1                     STATUS: 80=INDEX EXISTS FOR
*                                      VTOC, 40=INDEX VTOC ACTIVE
LSPDRSV1 DS    XL2                     RESERVED
LSPDNEXT DS    XL4                     NBR FREE EXTENTS
LSPDTCYL DS    XL4                     TOTAL FREE CYLINDERS
LSPDTTRK DS    XL4                     TOTAL ADDITIONAL FREE TRACKS
LSPDLCYL DS    XL4                     NBR CYL IN LARGEST FREE XTNT
LSPDLTRK DS    XL4                     NBR ADDNL TRACKS IN LGST FREE
LSPDF0S  DS    XL4                     FORMAT0 DSCB COUNT
LSPDVIRS DS    XL4                     FREE VIR COUNT
LSPDFRAG DS    XL4                     FRAGMENTATION INDEX
*
         IECSDSL1 4                    GENERATE FORMAT 4 DSCB
*
TRCHAR   DC    256X'FF'                TRANSLATE TABLE FOR CHARHEX
         ORG   TRCHAR+X'81'            ORG TO LOWER CASE A
         DC    X'0A0B0C0D0E0F'         A-F = 0A-0F
         ORG   TRCHAR+X'C1'            ORG TO UPPER CASE A
         DC    X'0A0B0C0D0E0F'         A-F = 0A-0F
         ORG   TRCHAR+C'0'             ORG TO ZERO
         DC    X'00010203040506070809' 0-9 = 00-09
         ORG
*
*
*
PRT      DS    0CL80                   PRINT AREA
VOLSYSID DC    CL4' '                  SYSTEM ID
         DC    CL1' '
VOLVOL   DC    CL6' '                    VOLSER
         DC    CL1' '
VOLTYPE  DC    CL4' '                    TYPE
VOLCODE  DC    CL1' '
         DC    CL1' '
VOLCUU   DC    CL3' '                    CUU
         DC    CL1' '
VOLINDX  DC    CL4' '                    VTOC IDX
         DC    CL1' '
VOLMNT   DC    CL4' '                    VOLUME MOUNT STATUS
VOL0FRE  DC    CL7' '                    DSCB0S
         DC    CL2' '
VOLTCYL  DC    CL7' '                    FREE CYL
         DC    CL3' '
VOLLCYL  DC    CL7' '                    LGST XTNT
VOLVIRS  DC    CL7' '                    VIRS
         DC    CL1' '
VOLVTOC  DC    CL6' '                    VTOC TTR
VOLXTNT  DC    CL7' '                    XTNTS
         DC    CL2' '
*
*
*
HEADING  DS    0CL80                   HEADER LINE
         DC    CL4'SYST'                 SYSTEM ID
         DC    CL7' VOLSER'              VOLSER
         DC    CL6' TYPE '               TYPE
         DC    CL4' CUU'                 CUU
         DC    CL5' INDX'                VTOC INDEX STATUS
         DC    CL5'  MNT'                VOLUME MOUNT STATUS
         DC    CL7' DSCB0S'              FREE DSCB0S
         DC    CL9' FREE-CYL'            FREE CYL
         DC    CL10' LGST-XTNT'          LARGEST XTNT
         DC    CL7'   VIRS'              FREE VIRS
HDREND   DC    CL17' '
*
HDRLITS  DC    CL7' VTOCTR'              VTOC TTR
         DC    CL7'  XTNTS'              FREE XTNTS
         DC    CL2' '
*
*
*
***********************************************************************
*        PRINT FILE                                                   *
***********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         LTORG
         CVT   DSECT=YES               GENERATE CVT DSECT
         IEFUCBOB ,                    GENERATE UCB DSECT
VOLLIST2 CSECT
         END

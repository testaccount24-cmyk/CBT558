IGC0024C CSECT
* THIS TYPE 3, UNRESTRICTED SVC PLACES THE USER IN A REQUESTED
* PROTECT KEY, AND/OR IN SUPERVISOR STATE. REG 0 CONTAINS THE
* DESIRED PROTECT KEY IN IT'S LOW ORDER BYTE. IF THE VALUE IN
* THE REGISTER IS GREATER THAN FF, THE PROTECT KEY IS SET FROM
* THE VALUE IN THE TCB (TCBPKF). THE STATE CODE IS PLACED IN
* REGISTER 1: IF REGISTER 1 IS ZERO, THE CALLER IS PLACED IN
* SUPERVISOR STATE, IF REGISTER 1 IS EQUAL TO 1, USER IS
* PLACED IN PROGRAM STATE. WHEN REGISTER 1 IS NEITHER 0 NOR
* 1, NO ACTION IS TAKEN BY THIS SVC.
*
* AUTHOR R THORNTON JAN 1978
*
* ON ENTRY, REGISTERS CONTAIN THE FOLLOWING:
*   REG 0  = VALUE PASSED BY CALLER
*   REG 1  = VALUE PASSED BY CALLER
*   REG 2  = UNPREDICTABLE
*   REG 3  = ADDRESS OF CVT
*   REG 4  = ADDRESS OF TCB
*   REG 5  = ADDRESS OF SVRB
*   REG 6  = ADDRESS OF E.P. TO IGC0024C
*   REG 7  = ADDRESS OF ASCB
*   REG 8  = UNPREDICTABLE
*   REG 9  = UNPREDICTABLE
*   REG A  = UNPREDICTABLE
*   REG B  = UNPREDICTABLE
*   REG C  = UNPREDICTABLE
*   REG D  = VALUE PASSED BY CALLER
*   REG E  = RETURN ADDRESS FOR IGC0024C ON COMPLETION
*   REG F  = VALUE PASSED BY CALLER
*
* REGISTERS ARE SAVED BY THE CONTROL PROGRAM BEFORE ENTRY TO IGC0024C.
* ON RETURN, REGISTERS 2-E ARE RESTORED TO THEIR ORIGINAL VALUES, WHILE
* REGISTERS 0, 1, AND F, MAINTAIN THE VALUES CONTAINED AT TIME OF
*          EXIT FROM IGC0024C.
*
* CHARACTERISTICS: ENABLED, REFRESHABLE, NO LOCKS HELD OR USED,
*                  LOCATED IN SYS1.LPALIB, SUPERVISOR STATE,
*                  PROTECT KEY ZERO.
*
* METHOD OF OPERATION:
*  1. TEST REG 1 FOR A VALUE OF 0 OR 1. IF NEITHER, EXIT
*     WITHOUT PROCESSING.
*  2. LOCATE THE RB FOR THE CALLER BY USE OF THE RBLINK FIELD
*     OF THE SVRB BUILT FOR IGC0024C (ADDRESS IN REG 5).
*  3. IF REG 0 IS GREATER THAN FF, GET THE PROTECT KEY FROM
*     THE CALLER'S TCB (ADDRESS IN REG 4).
*  4. PLACE THE REQUESTED PROTECT KEY AND STATE IN THE RESUME
*     PSW IN THE CALLER'S RB.
*  5. RETURN TO THE CONTROL PROGRAM VIA BRANCH TO THE ADDRESS
*     IN REGISTER E.
*
*
*
         USING IGC0024C,BASE
PKEY     EQU   0
STATE    EQU   1
RB       EQU   2
CVT      EQU   3
TCB      EQU   4
SVRB     EQU   5
BASE     EQU   6
ASCB     EQU   7
EVEN     EQU   8
ODD      EQU   9
RETURN   EQU   14
RETCODE  EQU   15
*
*
*
         LA    RETCODE,4               SET ERROR RETURN CODE
         LTR   STATE,STATE             TEST REG 1 = ZERO
         BZ    R1OK                    YES, O.K.
         C     STATE,F1                TEST REG 1 = 1
         BE    R1OK                    YES, O.K.
         BR    RETURN                  REG 1 INVALID, RETURN
R1OK     L     RB,28(SVRB)             GET CALLER'S RB ADDRESS
         C     PKEY,FF                 REG 0 > FF
         BNH   GOTKEY                  NO
         SR    PKEY,PKEY
         IC    PKEY,28(TCB)            GET TCBPKF
         SRL   PKEY,4                  SHIFT OUT LOW 4 BITS
GOTKEY   LR    EVEN,PKEY               COPY PROTECT KEY
         SR    ODD,ODD                 CLEAR ODD REG
         ICM   ODD,8,17(RB)            GET PK, CMWP FROM CALLER'S PSW
         SLL   ODD,4                   SHIFT OUT ORIGINAL PROTECT KEY B
         SLDL  EVEN,3                  SHIFT CMW BITS TO EVEN REGISTER
         SLL   EVEN,1                  MAKE P BIT ZERO, SUPVR STATE
         LTR   STATE,STATE             CALLER WANTS SUPVR STATE
         BE    SETPSW                  YES, ALL SET
         LA    EVEN,1(EVEN)            NO, SET PROBLEM STATE
SETPSW   STC   EVEN,17(RB)             SET PROT KEY AND STATE
         SR    RETCODE,RETCODE         SET RETCODE FOR NORMAL COMPL
         BR    RETURN                  RETURN TO CALLER
*
*
*
F1       DC    F'1'
FF       DC    F'255'
         END

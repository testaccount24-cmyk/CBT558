         MACRO
&NAME    SMODE &KEY=,&STATE=
         GBLC  &SK
         GBLC  &SS
         GBLC  &SENTRY
         AIF   ('&SENTRY' EQ '1').GOTDEF
.*  IF MACRO HAS ALREADY BEEN ENTRERED USE LAST KEY AND STATE
.*  AS DEFAULTS, ELSE USE KEY=NZERO AND STATE=P AS DEFAULTS
&SK      SETC  '257'
&SS      SETC  '1'
&SENTRY  SETC  '1'
.GOTDEF  ANOP
         AIF   (T'&KEY EQ 'O').USEKDEF
         AIF   ('&KEY' EQ 'NZERO').KEYN0
         AIF   ('&KEY' EQ 'N').KEYN0
&SK      SETC  '&KEY'
         AGO   .USEKDEF
.KEYN0   ANOP
&SK      SETC  '257'
.USEKDEF ANOP
         AIF   (T'&STATE EQ 'O').USESDEF
&SS      SETC  '1'                     SET TO PROGRAM STATE
         AIF   ('&STATE' NE 'S').USESDEF
&SS      SETC  '0'                     SET TO SUPERVISOR STATE
.USESDEF ANOP
&NAME    LA    0,&SK                   SET PROTECT KEY
         LA    1,&SS                   SET STATE
         SVC   243                     ISSUE SETMODE SVC
         MEND

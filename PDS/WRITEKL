WRITEKL  CSECT
WRITEKL  AMODE 31
WRITEKL  RMODE 24
***********************************************************************
* THIS SUBROUTINE IS CALLED BY THE COBOL PROGRAM REFORMAT TO WRITE A  *
* FILE OF RECORDS WHOSE LRECL IS SPECIFIED BY JCL AND NOT BY HARD CODE*
* IN THE PROGRAM. THIS ROUTINE IS NEEDED, AS COBOL IS UNABLE TO DEFINE*
* A FILE WITHOUT SPECIFYING THE LRECL.                                *
*                                                                     *
* STANDARD LINKAGE IS USED WITH A PARAMETER LIST OF TWO ELEMENTS:     *
* 1. A ONE-BYTE REQUEST CODE. "W" IS A REQUEST TO WRITE A RECORD, AND *
*    "C" IS A REQUEST TO CLOSE THE FILE.                              *
* 2. ADDRESS OF THE RECORD TO BE WRITTEN WHEN REQUEST CODE IS "W".    *
*                                                                     *
* NO REQUEST CODE IS NEEDED FOR OPENING THE FILE, AS THIS IS DONE     *
* AUTOMATICALLY WHEN THE FIRST WRITE REQUEST IS RECEIVED.             *
*                                                                     *
* WHEN THE CLOSE REQUEST IS RECEIVED, IF THE FILE IS NOT OPEN, IT WILL*
* BE OPENED FIRST, THEN CLOSED. THIS WILL INSURE AN END OF FILE IS    *
* WRITTEN EVEN IF THIS SUBROUTINE IS ONLY CALLED ONCE, FOR CLOSE.     *
*                                                                     *
* REWRITTEN FROM A DISASSEMBLY BY R THORNTON OCTOBER 2000             *
***********************************************************************
***********************************************************************
* INITIALIZE REGISTERS, CHAIN SAVE AREAS.                             *
***********************************************************************
         STM   R14,R12,12(R13)          SAVE CALLER'S REGISTERS
         LR    R3,R15                   COPY ENTRY POINT ADDR TO BASE
         USING WRITEKL,R3
         CNOP  0,4                      POSITION TO FULLWORD
         BAL   R15,AROUND               R15 POINTS TO SAVE AREA
SAVEAREA DC    18F'0'                   REGISTER SAVE AREA
AROUND   ST    R15,8(R13)               SET LSA POINTER
         ST    R13,SAVEAREA+4           SET HSA POINTER
         LR    R13,R15                  COPY SAVE AREA ADDRESS TO R13
         B     START                    BRANCH AROUND EYECATCHER
         DC    C'WRITEKL '              PROGRAM NAME EYECATCHER
***********************************************************************
* PICK UP PARAMETERS, AND CHECK REQUEST CODE FOR REQUIRED ACTION.     *
***********************************************************************
START    LM    R4,R5,0(R1)              LOAD PARAMETER REGS:
*                                         R4 POINTS TO REQUEST CODE
*                                         R5 POINTS TO RECORD AREA
         CLI   0(R4),C'W'               REQUEST IS FOR WRITE?
         BE    WRITE                    YES, GO TO WRITE ROUTINE
         CLI   0(R4),C'C'               REQUEST IS FOR CLOSE?
         BE    CLOSE                    YES, GO TO CLOSE ROUTINE
***********************************************************************
* PROCESSING COMPLETE, RETURN TO CALLER.                              *
***********************************************************************
RETURN   L     R13,4(R13)               POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)          LOAD CALLER'S REGS
         SR    R15,R15                  CLEAR RETURN CODE TO ZERO
         BR    R14                      RETURN TO CALLER
***********************************************************************
* WRITE THE RECORD SUPPLIED BY THE CALLER TO THE OUTPUT1 FILE. IF THE *
* FILE IS NOT YET OPEN, FIRST OPEN IT.                                *
***********************************************************************
WRITE    TM    OUTPUT1+48,X'10'         IS THE OUTPUT1 FILE OPEN?
         BO    PUT                      YES
         BAL   R9,OPEN                  NO, GO OPEN OUTPUT1
PUT      PUT   OUTPUT1,(R5)             WRITE CALLER'SOUTPUT1 RECORD
         B     RETURN                   GO BACK TO CALLER
***********************************************************************
* CLOSE THE OUTPUT1 FILE. IF IT IS NOT OPEN, FIRST OPEN IT.           *
***********************************************************************
CLOSE    TM    OUTPUT1+48,X'10'         IS THE OUTPUT1 FILE OPEN?
         BO    DOCLOSE                  YES
         BAL   R9,OPEN                  NO, GO OPEN OUTPUT1
DOCLOSE  CLOSE OUTPUT1                  CLOSE THE OUTPUT1 FILE
         B     RETURN                   GO BACK TO CALLER
***********************************************************************
* OPEN THE OUTPUT1 FILE FOR OUTPUT.                                   *
***********************************************************************
OPEN     OPEN  (OUTPUT1,OUTPUT)         OPEN THE OUTPUT1 FILE
         BR    R9                       EXIT
***********************************************************************
* DEFINE THE OUTPUT1 FILE                                             *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,RECFM=FB,DDNAME=OUTPUT1
***********************************************************************
*             REGISTER EQUATES                                        *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

***********************************************************************
* THIS SUBROUTINE WILL ISSUE A WTO WITH ROUTCDE=(11) USING THE 72     *
* BYTE TEXT AREA PASSED BY THE CALLER USING STANDARD MVS LINKAGE      *
* CONVENTIONS. THE MESSAGE WILL BE OF LOW INTENSITY, WHICH CAN BE     *
* DETECTED AND USED BY THE AUTO-OPS FACILITY, IF DESIRED.             *
*                                                                     *
* WTOPGMR IS ABLE TO RUN REENTRANT, REFRESHABLE, REUSABLE, AMODE(31)  *
* AND RMODE(ANY).                                                     *
*                                                                     *
* WTOPGMR IS BASED ON THE IDEA BEHIND JEFF QUADE'S WTOHI PROGRAM, BUT *
* USES NONE OF THAT CODE, AND IS LIMITED TO THE WTO PORTION OF WTOHI. *
*                                                                     *
* AUTHOR R. THORNTON                                                  *
***********************************************************************
WTOPGMR  CSECT
WTOPGMR  AMODE 31
WTOPGMR  RMODE ANY
*PROCESS RENT
         USING *,R15               SET UP BASE REG
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         DROP  R15
         USING WTOPGMR,R3
         L     R12,0(R1)               GET PARM FIELD ADDR
         STORAGE OBTAIN,LENGTH=ENDWS-WORKSTOR GET WORKING STORAGE
         LR    R15,R1                  POINT TO MY REGISTER SAVE AREA
         USING WORKSTOR,R15
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,REGSAVE+4           SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         DROP  R15
         USING WORKSTOR,R13
         B     SETUPWTO                BRANCH AROUND CONSTANTS
         DC    CL8'WTOPGMR'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
SETUPWTO MVC   MSGLENG,=AL2(MSGEND-MSGWTO) SET LENGTH IN WTO AREA
         MVC   MSGMCS,=XL2'8000'       MCS CODES TO WTO AREA
         MVC   MSGTEXT,0(R12)          CALLERS WTO TEXT TO WTO AREA
         MVC   MSGDESC,=XL2'0000'      DESCRIPTOR CODES TO WTO AREA
         MVC   MSGROUTE,=XL2'0020'     ROUTCDE=11 CODE TO WTO AREA
         LA    R1,MSGWTO               POINT TO WTO AREA
         SVC   35                      ISSUE THE WTO
RETURN   LR    R1,R13                  POINT TO GOTTEN STORAGE
         L     R13,4(R13)              POINT TO CALLER'S SAVE
         STORAGE RELEASE,LENGTH=ENDWS-WORKSTOR,ADDR=(1) FREE STORAGE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RC=0
         BR    R14                     RETURN TO CALLER
WORKSTOR DSECT ,                       WORKING STORAGE
REGSAVE  DS    18F                     REGISTER SAVE AREA
*
MSGWTO   DS    0F                      WTO LENGTH ERROR MESSAGE
MSGLENG  DC    AL2(MSGEND-MSGWTO)      AREA LENGTH
MSGMCS   DC    X'8000'                 MCS FLAGS
MSGTEXT  DC    CL72' '                 TEXT OF MESSAGE
MSGEND   EQU   * END OF TEXT
MSGDESC  DC    X'0000'                 DESCRIPTOR CODES
MSGROUTE DC    X'0020'                 ROUTCDE=11
ENDWS    EQU   *
WTOPGMR  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END                     , END OF PROGRAM

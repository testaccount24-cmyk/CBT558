         TITLE '*** RMODE31: RUN RMODE=31, DO STANDARD I/O *****'
RMODE31 CSECT
RMODE31 AMODE 31
RMODE31 RMODE ANY
*********************************************************************
* PROGRAM DESIGNED TO RUN AMODE=31,RMODE=31 AND DO STANDARD I/O     *
* USING DCB'S THAT ARE MOVED TO 24-BIT STORAGE. THE MODE=31 OPERAND *
* IS USED ON I/O MACROS TO CAUSE USE OF THE "LONG PARAMETER LIST".  *
* AUTHOR R THORNTON WITH EXAMPLES FROM W ROGERS 8/2/2000            *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING RMODE31,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'RMODE31'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
* MOVE THE FILEIN DCB BELOW THE LINE AND OPEN IT FOR INPUT            *
***********************************************************************
START    L     R12,FILEINL             LENGTH OF DCB
         STORAGE OBTAIN,LENGTH=(12),LOC=BELOW GET 24-BIT STORAGE
         ST    R1,FILEINA              STORE THE 24-BIT ADDRESS
         BCTR  R12,0                   LENGTH FOR EXECUTED MOVE
         EX    R12,MOVDCB1             MOVE THE DCB TO 24-BIT STORAGE
         LR    R12,R1                  COPY DCB ADDRESS FOR OPEN
         OPEN  ((12),INPUT),MODE=31    OPEN FILEIN (LONG PARMLIST)
         TM    48(R12),X'10'           DID IT OPEN OK?
         BO    OPEN2                   YES
         ABEND 111,DUMP                TERMINATE IN ERROR
MOVDCB1  MVC   0(1,R1),FILEIN          MOVE DCB TO 24-BIT STORAGE
***********************************************************************
* MOVE THE FILOUT DCB BELOW THE LINE AND OPEN IT FOR OUTPUT           *
***********************************************************************
OPEN2    L     R12,FILOUTL             LENGTH OF DCB
         STORAGE OBTAIN,LENGTH=(12),LOC=BELOW GET 24-BIT STORAGE
         ST    R1,FILOUTA              STORE THE 24-BIT ADDRESS
         BCTR  R12,0                   LENGTH FOR EXECUTED MOVE
         EX    R12,MOVDCB2             MOVE THE DCB TO 24-BIT STORAGE
         LR    R12,R1                  COPY DCB ADDRESS FOR OPEN
         OPEN  ((12),OUTPUT),MODE=31   OPEN FILOUT (LONG PARMLIST)
         TM    48(R12),X'10'           DID IT OPEN OK?
         BO    READ1                   YES
         ABEND 222,DUMP                TERMINATE IN ERROR
MOVDCB2  MVC   0(1,R1),FILOUT          MOVE DCB TO 24-BIT STORAGE
***********************************************************************
* READ THE NEXT RECORD IN THE FILEIN FILE                             *
***********************************************************************
READ1    L     R12,FILEINA             GET THE 24-BIT ADDRESS
         GET   ((12),INREC)            READ NEXT RECORD
         MVC   OUTREC,INREC            FORMAT THE OUTPUT RECORD
***********************************************************************
* WRITE A RECORD TO THE FILOUT FILE                                   *
***********************************************************************
         L     R12,FILOUTA             GET THE 24-BIT ADDRESS
         PUT   ((12),OUTREC)           OPEN FILEIN (LONG PARMLIST)
***********************************************************************
* CLOSE THE FILEIN DCB AND FREE ITS 24-BIT STORAGE                    *
***********************************************************************
         L     R12,FILEINA             GET 24-BIT FILEIN DCB ADDRESS
         CLOSE ((12)),MODE=31          CLOSE THE FILE (LONG PARMLIST)
         L     R11,FILEINL             LENGTH OF DCB
         STORAGE RELEASE,LENGTH=(11),ADDR=(12) FREE THE 24-BIT STORAGE
         XC    FILEINA,FILEINA         CLEAR THE 24-BIT ADDRESS
***********************************************************************
* CLOSE THE FILOUT DCB AND FREE ITS 24-BIT STORAGE                    *
***********************************************************************
         L     R12,FILOUTA             GET 24-BIT FILOUT DCB ADDRESS
         CLOSE ((12)),MODE=31          CLOSE THE FILE (LONG PARMLIST)
         L     R11,FILOUTL             LENGTH OF DCB
         STORAGE RELEASE,LENGTH=(11),ADDR=(12) FREE THE 24-BIT STORAGE
         XC    FILOUTA,FILOUTA         CLEAR THE 24-BIT ADDRESS
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 RETURN
         BR    R14                     RETURN
***********************************************************************
* WORKING STORAGE AREAS                                               *
***********************************************************************
INREC    DS    CL80                    RECORD FROM FILEIN
OUTREC   DS    CL80                    RECORD TO FILOUT
***********************************************************************
* DCB'S AND RELATED FIELDS                                            *
***********************************************************************
FILEIN   DCB   DSORG=PS,MACRF=GM,DDNAME=FILEIN,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
FILEINL  DC    A(*-FILEIN)             LENGTH OF DCB
FILEINA  DC    A(0)                    24-BIT DCB ADDRESS
*
FILOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=FILOUT,                        X
               RECFM=FB,LRECL=80
FILOUTL  DC    A(*-FILOUT)             LENGTH OF DCB
FILOUTA  DC    A(0)                    24-BIT DCB ADDRESS
***********************************************************************
* REGISTER EQUATES                                                    *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

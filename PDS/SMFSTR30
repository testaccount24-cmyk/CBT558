         TITLE '***** STRIP TYPE 30 RECORDS FROM AN SMF FILE *****'
         PRINT NOGEN
SMFSTR30 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFSTR30,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFSTR30'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
*  STRIP ALL TYPE 30 SMF RECORDS FOR THE JOBLOG NUMBERS GIVEN ON     *
*  THE READER1 FILE. THE READER1 FILE HAS HARD-CODED RECFM=FB, AND IS*
*  EXPECTED TO BE FORMATTED AS THOUGH OUTPUT FROM SMFSTR14. ONLY THE *
*  JOBNAME, READER START AND CREATION DATE/TIME FIELDS ARE USED.     *
*  UP TO 1000 JOBLOG NUMBERS MAY BE ENTERED, PER RUN. SINCE THE INPUT*
*  TAPES ARE IN VBS FORMAT, INDIVIDUAL VOLUMES MAY NOT BE USED, OR   *
*  S002 ABENDS MAY OCCUR.                                            *
*                                                                    *
*  THE EXEC STATEMENT PARM CAN BE "PARM=JOBLOG" TO PRODUCE AN OUTPUT *
*  FILE CONTAINING JOBLOG NUMBER, PROGRAM NAME, STEP NAME, USER ID,  *
*  JES2 JOB NUMBER, SMF TYPE 30 SUBTYPE, AND PROGRAMMER NAME,        *
*  OTHERWISE THE OUTPUT FILE WILL CONTAIN THE ENTIRE INPUT SMF RECORD*
*  FOR PARM=JOBLOG, THE OUTPUT1 DCB INFORMATION SHOULD BE RECFM=FB,  *
*  LRECL=139, BLKSIZE=MULTIPLE OF 139, OTHERWISE THE OUTPUT1 DCB     *
*  SHOULD BE RECFM=VB,LRECL=32716,BLKSIZE=32720.                     *
*                                                                    *
*  AUTHOR R. THORNTON APR 1986, MODIFIED JULY 1990                   *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
         L     R2,0(R1)                 POINT TO PARM DATA
         CLC   2(6,R2),=C'JOBLOG'       JOBLOG STRIP OUTPUT?
         BNE   OPENS                    NO
         MVI   JLOGSW,X'F0'             SET JOBLOG SWITCH
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1,,READER1)
         PUT   OUTPUT1,HEADER1          PRINT HEADER
         PUT   OUTPUT1,HEADER2          PRINT HEADER
         PUT   OUTPUT1,HEADER3          PRINT HEADER
         PUT   OUTPUT1,HEADER4          PRINT HEADER
         L     R12,JBLGTBLA             POINT TO DSNAME TABLE
         USING JBLGTBL,R12
         LA    R11,1000                 MAX TABLE ENTRIES
TBLOAD   GET   READER1                  READ NEXT DSNAME CARD
         LR    R10,R1                   COPY RECORD ADDRESS
         USING JLOGRECD,R10
         CLI   JLOGJBN,C'A'             THIS A TABLE RECORD?
         BL    TBLOAD                   NO, READ AGAIN
         MVC   JBLGJBN,JLOGJBN          MOVE JOB NAME TO TABLE
         PACK  JBLGRSD,JLOGRSD          READER START DATE TO TABLE
         PACK  DBLWD,JLOGRST            PACK READER START TIME
         CVB   R1,DBLWD                 CONVERT TO BINARY
         ST    R1,JBLGRST               READER START TIME TO TABLE
         PACK  JBLGDTE,JLOGDTE          RECORD CREATE DATE TO TABLE
         PACK  DBLWD,JLOGTME            PACK RECORD CREATE TIME
         CVB   R1,DBLWD                 CONVERT TO BINARY
         ST    R1,JBLGTME               RECORD CREATE TIME TO TABLE
         MVC   JBLGDDN,JLOGDDN          MOVE JOB NAME TO TABLE
         MVC   JBLGDSN,JLOGDSN          MOVE JOB NAME TO TABLE
         ST    R12,JBLGTBND             SAVE LAST ENTRY ADDRESS
         LA    R12,L'JBLGNTRY(R12)      POINT TO NEXT ENTRY
         BCT   R11,TBLOAD               CONTINUE LOAD
ENDCTL   MVI   0(R12),X'FF'             SET TABLE STOPPER
         DROP  R10
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
         USING SMFRECD,R6
MAINLINE GET   INPUT1,SMFRCD+4         GET INPUT AREA ADDRESS
         LA    R6,SMFRCD               POINT TO INPUT AREA
         CLI   SMF30RTY,30             TYPE 30 RECORD?
         BNE   MAINLINE                NO, SKIP IT
BEGIN01  ICM   R10,15,SMF30SOF         SUBSYSTEM SECTION OFFSET
         BZ    MAINLINE                NO SUBSYSTEM SECTION
         AR    R10,R6                  SUBSYSTEM SECTION ADDRESS
         USING SMF30S01,R10
         MVC   SUBTYPE,SMF30TYP+1      SAVE TYPE 30 SUB-TYPE
         DROP  R10
BEGIN02  ICM   R10,15,SMF30IOF         IDENTIFICATION SECT OFFSET
         BZ    MAINLINE                NO IDENTIFICATION SECTION
         AR    R10,R6                  IDENTIFICATION SECT ADDRESS
         USING SMF30S02,R10
         L     R12,JBLGTBLA            POINT TO 1ST TABLE ENTRY
B2CKJOBN CLC   JBLGJBN,SMF30JBN        LOOKING FOR THIS JOBNAME?
         BNE   B2STEP                  NO
         CLC   JBLGRST(8),SMF30RST     CHECK READER START TIME/DATE
         BE    JLOGBRCH                FOUND ONE, GO TO OUTPUT
B2STEP   LA    R12,L'JBLGNTRY(R12)     STEP TO NEXT ENTRY
         CLI   0(R12),X'FF'            END OF TABLE?
         BNE   B2CKJOBN                NO
         B     MAINLINE                YES, GO READ SMF FILE
*          DROP  R10
* BEGIN03  ICM   R10,15,SMF30UOF         UNIT RECORD SECTION OFFSET
*          BZ    BEGIN04                 NO UNIT RECORD SECTION
*          AR    R10,R6                  UNIT RECORD SECTION ADDRESS
*          USING SMF30S03,R10
*          DROP  R10
* BEGIN04  ICM   R10,15,SMF30TOF         COMPLETION SECTION OFFSET
*          BZ    BEGIN05                 NO COMPLETION SECTION
*          AR    R10,R6                  COMPLETION SECTION ADDRESS
*          USING SMF30S04,R10
*          DROP  R10
* BEGIN05  ICM   R10,15,SMF30COF         PROCESSOR SECTION OFFSET
*          BZ    BEGIN06                 NO PROCESSOR SECTION
*          AR    R10,R6                  PROCESSOR SECTION ADDRESS
*          USING SMF30S05,R10
*          DROP  R10
* BEGIN06  ICM   R10,15,SMF30EOF         EXCP SECTION OFFSET
*          BZ    BEGIN07                 NO EXCP SECTION
*          AR    R10,R6                  EXCP SECTION ADDRESS
*          USING SMF30S06,R10
*          DROP  R10
* BEGIN07  ICM   R10,15,SMF30AOF         ACCOUNTING SECTION OFFSET
*          BZ    BEGIN08                 NO ACCOUNTING SECTION
*          AR    R10,R6                  ACCOUNTING SECTION ADDRESS
*          USING SMF30S07,R10
*          DROP  R10
* BEGIN08  ICM   R10,15,SMF30ROF         STORAGE SECTION OFFSET
*          BZ    BEGIN09                 NO STORAGE SECTION
*          AR    R10,R6                  STORAGE SECTION ADDRESS
*          USING SMF30S08,R10
*          DROP  R10
* BEGIN09  ICM   R10,15,SMF30POF         PERFORMANCE SECTION OFFSET
*          BZ    BEGIN10                 NO PERFORMANCE SECTION
*          AR    R10,R6                  PERFORMANCE SECTION ADDRESS
*          USING SMF30S09,R10
*          DROP  R10
* BEGIN10  ICM   R10,15,SMF30OOF         OPERATOR SECTION OFFSET
*          BZ    JLOGBRCH                NO OPERATOR SECTION
*          AR    R10,R6                  OPERATOR SECTION ADDRESS
*          USING SMF30S10,R10
*          DROP  R10
JLOGBRCH NOP   JLOGOUT                 BRANCH IF PARM=JOBLOG
JLOGSW   EQU   JLOGBRCH+1,1            SET TO X'FO' IF PARM=JOBLOG
         MVC   SMF30LEN,INPUT1+82      MOVE LENGTH TO OUTPUT
         XC    SMF30SEG,SMF30SEG       CLEAR SEGMENT DESCRIPTOR
         PUT   OUTPUT1,SMFRCD          WRITE OUTPUT RECORD
         CLI   SUBTYPE,5               WAS THIS JOB TERMINATION?
         BNE   MAINLINE                GO CLEAN UP TABLE
         B     CLEANTBL                GO CLEAN UP TABLE
JLOGOUT  MVC   OUTJJBN,JBLGJBN         JOB NAME TO OUTPUT
         UNPK  OUTJRSD,SMF30RSD        READER START DATE OUTPUT
         L     R1,SMF30RST             GET READER START TIME
         CVD   R1,DBLWD                CONVERT TO PACKED
         UNPK  OUTJRST,DBLWD           READER START TIME TO OUTPUT
         OI    OUTJRST+7,C'0'          CLEAR SIGN
         UNPK  OUTJDTE,SMF30DTE        RECORD CREATE DATE OUTPUT
         ICM   R1,15,SMF30TME          GET RECORD CREATE TIME
         CVD   R1,DBLWD                CONVERT TO PACKED
         UNPK  OUTJTME,DBLWD           CREATE TIME TO OUTPUT
         OI    OUTJTME+7,C'0'          CLEAR SIGN
         MVC   OUTJSID,SMF30SID        SYSTEM ID TO OUTPUT
         SR    R1,R1                   CLEAR FOR INSERT
         IC    R1,SUBTYPE              GET SUBTYPE
         CVD   R1,DBLWD                CONVERT TO PACKED
         UNPK  OUTJSTP,DBLWD           SMF TYPE 30 SUB-TYPE TO OUTPUT
         OI    OUTJSTP+1,C'0'
         MVC   OUTJPGM,SMF30PGM        PROGRAM NAME TO OUTPUT
         MVC   OUTJSTM,SMF30STM        STEP NAME TO OUTPUT
         MVC   OUTJUIF,SMF30UIF        USER ID TO OUTPUT
         MVC   OUTJJNM,SMF30JNM        JES2 JOB NUMBER TO OUTPUT
         MVC   OUTJUSR,SMF30USR        PROGRAMMER NAME TO OUTPUT
         MVI   OUTJFLG,C' '            CLEAR PRINT FLAG
         MVC   OUTJDDN,BLANKS          CLEAR DDNAME
         MVC   OUTJDSN,BLANKS          CLEAR DSNAME
         CLC   SMF30DTE,JBLGDTE        WAS IT THIS STEP?
         BL    PUTJLOG                 NO
         BH    SETJFLG                 YES
         CLC   SMF30TME,JBLGTME        WAS IT THIS STEP?
         BL    PUTJLOG                 NO
SETJFLG  MVI   OUTJFLG,C'*'            SHOW THIS STEP FOR INPUT
         MVC   OUTJDDN,JBLGDDN         PRINT DDNAME
         MVC   OUTJDSN,JBLGDSN         PRINT DSNAME
         MVI   JBLGDTE,X'FF'           FORCE HI CREATE DATE
PUTJLOG  DS    0H                      OUTPUT STEP
         CLI   OUTJFLG,C'*'            WAS THIS THE STEP?  !!!!!!!!!!!!
         BNE   MAINLINE                NO                  !!!!!!!!!!!!
         PUT   OUTPUT1,OUTJLOG         WRITE OUTPUT RECORD
*        CLI   SUBTYPE,5               WAS THIS JOB TERMINATION?!!!!!!!
*        BNE   MAINLINE                NO                  !!!!!!!!!!!!
CLEANTBL L     R1,JBLGTBND             POINT TO LAST ENTRY
         C     R1,JBLGTBLA             WAS THIS THE LAST ENTRY?
         BE    EOJ                     YES
         MVC   JBLGNTRY,0(R1)          NO, MOVE LAST ENTRY HERE
         MVI   0(R1),X'FF'             SET NEW TABLE STOPPER
         SH    R1,JBLGLENG             BACK UP TO NEXT-TO-LAST ENTRY
         ST    R1,JBLGTBND             SET NEW END ADDRESS
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1,,READER1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
DBLWD    DS    D                       WORK AREA
BLANKS   DC    CL135' '                BLANKS
JBLGTBLA DC    A(JLOGAREA)             JOBLOG TABLE AREA ADDRESS
JBLGTBND DC    A(JLOGAREA)             JOBLOG TABLE END ADDRESS
RETCODE  DC    F'0'                    RETURN CODE
JBLGLENG DC    AL2(L'JBLGNTRY)         LENGTH OF TABLE ENTRY
SUBTYPE  DC    XL1'00'                 SMF TYPE 30 SUB-TYPE
*
OUTJLOG  DS    0CL147                  OUTPUT RECORD FOR PARM=JOBLOG
OUTJFLG  DC    CL1' '                    FLAG FOR THIS STEP
OUTJJBN  DC    CL8'        '             JOB NAME
OUTJRSD  DC    CL6'      '               READER START DATE
OUTJRST  DC    CL8'        '             READER START TIME
OUTJDTE  DC    CL6'      '               CREATE DATE
OUTJTME  DC    CL8'        '             CREATE TIME
OUTJSID  DC    CL4'    '                 SMF SYSTEM IDENTIFIER
OUTJSTP  DC    CL2'  '                   TYPE 30 SUB-TYPE
OUTJPGM  DC    CL8'        '             PROGRAM NAME
OUTJSTM  DC    CL8'        '             STEP NAME
OUTJUIF  DC    CL8'        '             USERID
OUTJJNM  DC    CL8'        '             JES2 JOB NUMBER
OUTJUSR  DC    CL20' '                   PROGRAMMER NAME
OUTJDDN  DC    CL8' '                    DDNAME
OUTJDSN  DC    CL44' '                   DATASET NAME
*
HEADER1  DS    0CL147                  OUTPUT RECORD FOR PARM=JOBLOG
         DC    CL1'F'                    FLAG FOR THIS STEP
         DC    CL8'        '             JOB NAME
         DC    CL6' RDR  '               READER START DATE
         DC    CL8' RDR    '             READER START TIME
         DC    CL6' REC  '               CREATE DATE
         DC    CL8' REC    '             CREATE TIME
         DC    CL4'SMF '                 SMF SYSTEM IDENTIFIER
         DC    CL2'ST'                   TYPE 30 SUB-TYPE
         DC    CL8'        '             PROGRAM NAME
         DC    CL8'        '             STEP NAME
         DC    CL8'        '             USERID
         DC    CL8' JES2   '             JES2 JOB NUMBER
         DC    CL20' '                   PROGRAMMER NAME
         DC    CL8' '                    DDNAME
         DC    CL44' '                   DATASET NAME
*
HEADER2  DS    0CL147                  OUTPUT RECORD FOR PARM=JOBLOG
         DC    CL1'L'                    FLAG FOR THIS STEP
         DC    CL8' JOB    '             JOB NAME
         DC    CL6' STRT'                READER START DATE
         DC    CL8' STRT   '             READER START TIME
         DC    CL6' CREA'                CREATE DATE
         DC    CL8' CREATE '             CREATE TIME
         DC    CL4'SYST'                 SMF SYSTEM IDENTIFIER
         DC    CL2'UY'                   TYPE 30 SUB-TYPE
         DC    CL8' PROGRAM'             PROGRAM NAME
         DC    CL8' STEP   '             STEP NAME
         DC    CL8' USER   '             USERID
         DC    CL8' JOB    '             JES2 JOB NUMBER
         DC    CL20' PROGRAMMER'         PROGRAMMER NAME
         DC    CL8' '                    DDNAME
         DC    CL44' DATASET'            DATASET NAME
*
HEADER3  DS    0CL147                  OUTPUT RECORD FOR PARM=JOBLOG
         DC    CL1'G'                    FLAG FOR THIS STEP
         DC    CL8' NAME   '             JOB NAME
         DC    CL6' DATE '               READER START DATE
         DC    CL8' TIME   '             READER START TIME
         DC    CL6' DATE '               CREATE DATE
         DC    CL8' TIME   '             CREATE TIME
         DC    CL4'ID  '                 SMF SYSTEM IDENTIFIER
         DC    CL2'BP'                   TYPE 30 SUB-TYPE
         DC    CL8' NAME   '             PROGRAM NAME
         DC    CL8' NAME   '             STEP NAME
         DC    CL8' ID     '             USERID
         DC    CL8' NBR    '             JES2 JOB NUMBER
         DC    CL20' NAME'               PROGRAMMER NAME
         DC    CL8' DDNAME '             DDNAME
         DC    CL44' NAME'               DATASET NAME
*
HEADER4  DS    0CL147                  OUTPUT RECORD FOR PARM=JOBLOG
         DC    1C'_'                     FLAG FOR THIS STEP
         DC    8C'.'                     JOB NAME
         DC    6C'_'                     READER START DATE
         DC    8C'.'                     READER START TIME
         DC    6C'_'                     READER START DATE
         DC    8C'.'                     READER START TIME
         DC    4C'_'                     SMF SYSTEM IDENTIFIER
         DC    2C'.'                     TYPE 30 SUB-TYPE
         DC    8C'_'                     PROGRAM NAME
         DC    8C'.'                     STEP NAME
         DC    8C'_'                     USERID
         DC    8C'.'                     JES2 JOB NUMBER
         DC    20C'_'                    PROGRAMMER NAME
         DC    8C'.'                     DDNAME
         DC    44C'_'                    DATASET NAME
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
READER1  DCB   DSORG=PS,MACRF=GL,DDNAME=READER1,EODAD=ENDCTL
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
*
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
JLOGRECD DSECT ,                        INPUT (FROM SMFSTR14)
JLOGJBN  DS    CL8                          JOBNAME
JLOGRSD  DS    CL6                          READER START DATE
JLOGRST  DS    CL8                          READER START TIME
JLOGDTE  DS    CL6                        CREATION DATE
JLOGTME  DS    CL8                        CREATION TIME
JLOGSID  DS    CL4                        SYSTEM ID
JLOGRTY  DS    CL2                        SMF RECORD TYPE
JLOGVOLS DS    CL6                        VOLSER OF SMF TAPE
JLOGDDN  DS    CL8                        DDNAME
JLOGDSN  DS    CL44                       DSNAME
*
JBLGTBL  DSECT ,                        JOBLOG TABLE DESCRIPTOR
JBLGNTRY DS    0CL76                      JOBLOG TABLE ENTRY
JBLGJBN  DS    CL8                          JOB NAME
JBLGRST  DS    XL4                          READER START TIME
JBLGRSD  DS    PL4                          READER START DATE
JBLGTME  DS    XL4                          RECORD CREATE TIME
JBLGDTE  DS    PL4                          RECORD CREATE DATE
JBLGDDN  DS    CL8                          DDNAME
JBLGDSN  DS    CL44                         DSNAME
*
*        SMF RECORD DSECT
*
SMFRECD  DSECT ,                       *** S M F D A T A R E C O R D S
SMF30LEN DS    H                       RECORD LENGTH
SMF30SEG DS    H                       SEGMENT DESCRIPTOR
SMF30FLG DS    XL1                     0=MVT, 2=MVS
SMF30RTY DS    XL1                     RECORD TYPE
SMF30TME DS    XL4                     TIME 1/100 SEC
SMF30DTE DS    PL4                     DATE 00YYDDD
SMF30SID DS    CL4                     SYSTEM ID
SMF30WID DS    CL4                     SUBSYSTEM IDENTIFIER
         DS    XL2
SMF30SOF DS    XL4                     OFFSET TO SUBSYSTEM SECTION
SMF30SLN DS    XL2                     SUBSYSTEM SECTION LENGTH
SMF30SON DS    XL2                     NUMBER OF SUBSYSTEM SECTIONS
SMF30IOF DS    XL4                     OFFSET TO IDENTIFICATION SECT
SMF30ILN DS    XL2                     LENGTH OF IDENTIFICATION SECT
SMF30ION DS    XL2                     NUMBER OF IDENTIFICATION SECTS
SMF30UOF DS    XL4                     OFFSET TO UNIT RECORD SECT
SMF30ULN DS    XL2                     LENGTH OF UNIT RECORD SECT
SMF30UON DS    XL2                     NUMBER OF UNIT RECORD SECTS
SMF30TOF DS    XL4                     OFFSET TO COMPLETION SECTION
SMF30TIN DS    XL2                     LENGTH OF COMPLETION SECTION
SMF30TON DS    XL2                     NUMBER OF COMPLETION SECTIONS
SMF30COF DS    XL4                     OFFSET TO PROCESSOR SECTION
SMF30CLN DS    XL2                     LENGTH OF PROCESSOR SECTION
SMF30CON DS    XL2                     NUMBER OF PROCESSOR SECTIONS
SMF30AOF DS    XL4                     OFFSET TO ACCOUNTING SECTION
SMF30ALN DS    XL2                     LENGTH OF ACCOUNTING SECTION
SMF30AON DS    XL2                     NUMBER OF ACCOUNTING SECTIONS
SMF30ROF DS    XL4                     OFFSET TO STORAGE SECTION
SMF30RLN DS    XL2                     LENGTH OF STORAGE SECTION
SMF30RON DS    XL2                     NUMBER OF STORAGE SECTIONS
SMF30POF DS    XL4                     OFFSET TO PERFORMANCE SECT
SMF30PLN DS    XL2                     LENGTH OF PERFORMANCE SECT
SMF30PON DS    XL2                     NUMBER OF PERFORMANCE SECTS
SMF30OOF DS    XL4                     OFFSET TO OPERATOR SECTION
SMF30OLN DS    XL2                     LENGTH OF OPERATOR SECTION
SMF30OON DS    XL2                     NUMBER OF OPERATOR SECTIONS
SMF30EOF DS    XL4                     OFFSET TO EXCP SECTION
SMF30ELN DS    XL2                     LENGTH OF EXCP SECTION
SMF30EON DS    XL2                     NUMBER OF EXCP SECTIONS
SMF30EOR DS    XL2                     NUMBER OF EXCP SECTS IN
*                                       SUBSEQUENT RECORDS
*****************************************************************
*          SUBSYSTEM SECTION                                    *
*****************************************************************
SMF30S01 DSECT ,                       TYPE 30 SUBSYSTEM SECTION
SMF30TYP DS    XL2                     SUB TYPE IDENTIFICATION
*                                        1 = JOB START
*                                        2 = INTERVAL
*                                        3 = STEP TERMINATION
*                                        4 = STEP TOTAL
*                                        5 = JOB TERMINATION
*                                        6 = SYSTEM A.S. INIT
SMF30RS1 DS    XL2
SMF30RVN DS    CL2                     RECORD REVISION NUMBER
SMF30PNM DS    CL8                     SUBSYSTEM OR PRODUCT NAME
*****************************************************************
*           IDENTIFICATION SECTION                              *
*****************************************************************
SMF30S02 DSECT ,                       TYPE 30 IDENTIFICATION SECTION
SMF30JBN DS    CL8                     JOB OR SESSION NAME
SMF30PGM DS    CL8                     PROGRAM NAME
SMF30STM DS    CL8                     STEP NAME
SMF30UIF DS    CL8                     USER IDENTIFICATION
SMF30JNM DS    CL8                     JES2 ASSIGNED JOB NUMBER
SMF30STN DS    XL2                     STEP NUMBER
SMF30CLS DS    CL1                     JOB CLASS
         DS    XL1
SMF30PGN DS    XL2                     JOB PERFORMANCE GROUP NUMBER
SMF30JPT DS    XL2                     JES2 INPUT PRIORITY
SMF30AST DS    XL4                     DEVICE ALLOCATION START TIME
SMF30PPS DS    XL4                     PROBLEM PROGRAM START TIME
SMF30SIT DS    XL4                     INITIATOR SELECT TIME
SMF30STD DS    PL4                     INITIATOR SELECT DATE 00YYDDDF
SMF30RST DS    XL4                     READER START TIME
SMF30RSD DS    PL4                     READER START DATE 00YYDDDF
SMF30RET DS    XL4                     READER END TIME
SMF30RED DS    PL4                     READER END DATE 00YYDDDF
SMF30USR DS    CL20                    PROGRAMMER'S NAME
SMF30GRP DS    CL8                     RACF GROUP ID
SMF30RUD DS    CL8                     RACF USER ID
SMF30TID DS    CL8                     RACF TERMINAL ID
*****************************************************************
*           UNIT RECORD (I/O ACTIVITY) SECTION                  *
*****************************************************************
SMF30S03 DSECT ,                       TYPE 30 UNIT RECORD SECTION
SMF30INP DS    XL4                     NBR DD *, DD DATA CARDS
SMF30TEP DS    XL4                     TOTAL BLOCKS TRANSFERRED
SMF30TPT DS    XL4                     ZERO (TSO TPUTS)
SMF30TGT DS    XL4                     ZERO (TSO TGETS)
SMF30RDR DS    XL1                     READER DEVICE CLASS
SMF30RDT DS    XL1                     READER DEVICE TYPE
SMF30RV4 DS    XL2                     RESERVED
*****************************************************************
*           COMPLETION SECTION                                  *
*****************************************************************
SMF30S04 DSECT ,                       TYPE 30 COMPLETION SECTION
SMF30SCC DS    XL2                     STEP COMPLETION CODE
*                                        IF ABEND:
*                                           80=USER ABEND
*                                           7F=ABEND CODE
*                                        IF NORMAL COMPLETION:
*                                           FF=R15 AT TERMINATION
SMF30STI DS    XL2                     STEP TERMINATION INDICATOR
*                                        20=CANCELED BY IEFUJI
*                                        10=CANCELED BY IEFUSI
*                                        08=CANCELED BY IEFACTRT
*                                        04=STEP TO BE RESTARTED
*                                        02=ABEND
*                                        01=FLUSHED
*                                        IN BYTE 2:
*                                        80=EXCP COUNTS INCORRECT
*                                        40=PREV INTV REC NOT WRT
*****************************************************************
*           PROCESSOR ACCOUNTING SECTION                        *
*****************************************************************
SMF30S05 DSECT ,                       TYPE 30 PROCESSOR SECTION
SMF30PTY DS    XL2                     ASCB DISPATCHING PRIORITY
SMF30RV3 DS    XL2                     RESERVED
SMF30CPT DS    XL4                     TCB CPU TIME, SEC/100
SMF30CPS DS    XL4                     SRB CPU TIME, SEC/100
SMF30ICU DS    XL4                     INIT TCB CPU TIME, SEC/100
SMF30ISB DS    XL4                     INIT SRB CPU TIME, SEC/100
*****************************************************************
*           EXCP SECTION                                        *
*****************************************************************
SMF30S06 DSECT ,                       TYPE 30 EXCP SECTION
SMF30DEV DS    XL1                     DEVICE CLASS
SMF30UTP DS    XL1                     UNIT TYPE
SMF30CUA DS    XL2                     CHANNEL AND UNIT ADDRESS
SMF30DDN DS    CL8                     DDNAME
SMF30BLK DS    XL4                     BLOCK COUNT (NBR EXCPS)
SMF30BSZ DS    XL2                     MAX BLOCK SIZE, IGNORE 80
SMF30RV2 DS    XL2                     RESERVED
*****************************************************************
*           ACCOUNTING SECTION                                  *
*****************************************************************
SMF30S07 DSECT ,                       TYPE 30 ACCOUNTING SECTION
SMF30ACL DS    XL1                     LENGTH, INCLUDING THIS
SMF30ACT DS    0C                      JOB OR STEP ACCOUNTING FLD
*****************************************************************
*           STORAGE AND PAGING SECTION                          *
*****************************************************************
SMF30S08 DSECT ,                       TYPE 30 STORAGE SECTION
SMF30RGN DS    XL2                     REGION SIZE REQUESTED
SMF30SFL DS    XL1                     80=V=R SPECIFIED
SMF30SPK DS    XL1                     F0=PROTECT KEY
SMF30PRV DS    XL2                     STORAGE USED FM BOT PVT
SMF30SYS DS    XL2                     STORAGE USED FM TOP PVT
SMF30PGI DS    XL4                     NON-VIO, NON-SWAP PAGE-INS
SMF30PGO DS    XL4                     NON-VIO, NONSWAP PAGE-OUTS
SMF30REC DS    XL4                     NON-VIO RECLAIMS
SMF30NSW DS    XL4                     ADDRESS SPACE SWAPS
SMF30PSI DS    XL4                     PAGES SWAPPED IN
SMF30PSO DS    XL4                     PAGES SWAPPED OUT
SMF30VPI DS    XL4                     VIO PAGE-INS
SMF30VPO DS    XL4                     VIO PAGE-OUTS
SMF30VPR DS    XL4                     VIO RECLAIMS
SMF30CPI DS    XL4                     COMMON AREA PAGE-INS
SMF30CPR DS    XL4                     COMMON AREA RECLAIMS
SMF30LPI DS    XL4                     LPA PAGE-INS
SMF30LPR DS    XL4                     LPA RECLAIMS
SMF30PST DS    XL4                     STOLEN PAGES
SMF30PSC DS    XL8                     CPU PAGE-SECONDS
*****************************************************************
*           PERFORMANCE SECTION                                 *
*****************************************************************
SMF30S09 DSECT ,                       TYPE 30 PERFORMANCE SECTION
SMF30SRV DS    XL4                     TOTAL SERVICE UNITS
SMF30CSU DS    XL4                     CPU SERVICE UNITS
SMF30SRB DS    XL4                     SRB SERVICE UNITS
SMF30IO  DS    XL4                     I/O SERVICE UNITS
SMF30MSO DS    XL4                     MSO SERVICE UNITS
SMF30TAT DS    XL4                     TRANSACTION ACTIVE TIME
SMF30TET DS    XL4                     RESERVED
SMF30RES DS    XL4                     TRANSACTION RESIDENCY TIME
SMF30TRS DS    XL4                     NUMBER OF TRANSACTIONS
*****************************************************************
*           OPERATOR SECTION                                    *
*****************************************************************
SMF30S10 DSECT ,                       TYPE 30 OPERATOR SECTION
SMF30PDM DS    XL4                     NON-SPECIFIC DASD MOUNTS
SMF30PRD DS    XL4                     SPECIFIC DASD MOUNTS
SMF30PTM DS    XL4                     NON-SPECIFIC TAPE MOUNTS
SMF30TPR DS    XL4                     SPECIFIC TAPE MOUNTS
SMF30MTM DS    XL4                     NON-SPECIFIC MSS MOUNTS
SMF30MSR DS    XL4                     SPECIFIC MSS MOUNTS
*
SMFSTR30 CSECT
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
         DS    0F
SMFRCD   DS    CL32768                 SMF RECORD INPUT AREA
*
*        INPUT AREA FOR SMF RECORD
*
         DS    0F
JLOGAREA DS    1000CL(L'JBLGNTRY)      AREA FOR JOBLOG TABLE
         DC    X'FF'                   END OF TABLE
         END

         TITLE 'PDSDIRLN - PRINT FULL DIRECTORY ENTRY OF SOURCE PDS'
PDSDIRLN CSECT
***********************************************************************
* PROGRAM TO READ THE DIRECTORY IN AN INPUT PDS NOT CREATED BY THE    *
* LINKAGE EDITOR. OUTPUT IS A FILE OF RECORDS CONTAINING ALL FIELDS   *
* IN EACH DIRECTORY ENTRY INCLUDING MEMBER NAME, VERSION, MODLEVEL,   *
* CREATION DATE, CHANGE DATE, TIME IN HOURS AND MINUTES, SIZE, INITIAL*
* SIZE, MOD, AND ID.                                                  *
*                                                                     *
* A DSNAME MAY BE GIVEN IN THE PARM PARAMETER OF THE EXEC STATEMENT.  *
* WHEN A DSNAME IS SUPPLIED, IT IS MOVED INTO EACH OUTPUT RECORD.     *
*                                                                     *
* AUTHOR R. THORNTON   JUL 1998                                       *
***********************************************************************
         USING PDSDIRLN,R3             ESTABLISH ADDRESSABILITY
         STM   R14,R12,12(R13)         SAVE CALLING PROG REGISTERS
         LR    R3,R15                  INITIALIZE BASE REG
         ST    R13,SAVE+4              SAVE RETURN POINTER
         LA    R15,SAVE                GET THIS PGM SAVE ADDR
         ST    R15,8(R13)              STORE FORWARD POINTER
         LR    R13,R15                 INITIALIZE SAVE AREA POINTER
         B     INITIAL                 BRANCH AROUND CONSTANTS
         DC    CL8'PDSDIRLN'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
SAVE     DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
*        PROCESS PARM FIELD: SAVE DSNAME, IF SPECIFIED                *
***********************************************************************
INITIAL  L     R2,0(R1)                GET PARM FIELD ADDR
         LH    R1,0(R2)                GET PARM LENGTH
         LTR   R1,R1                   ANY PARM DATA?
         BZ    OPENS                   NO, CONTINUE
         CH    R1,MAXPARM              VALID PARM LENGTH?
         BNH   PARMOK                  YES
BADPARM  WTO   'PDSDIRLN: INVALID PARM ON EXEC STATEMENT',ROUTCDE=11
         ABEND 111,DUMP                TERMINATE IN ERROR
PARMOK   BCTR  R1,0                    COMPUTE LENGTH CODE
         EX    R1,PARMVC               SAVE PARM DATA
         B     OPENS                   GO OPEN FILES
PARMVC   MVC   DSNAME(1),2(R2)         EXECUTED MOVE
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT,PDS)   OPEN FILES
*********************************************************************
*        MAINLINE: READ DIRECTORY BLOCKS, EXTRACT DIRECTORY ENTRIES,*
*        FORMAT AND WRITE TO THE SYSPRINT  FILE                     *
*********************************************************************
MAINLINE LH    R12,REMBYTS             GET NBR BYTES REMAINING IN BLOCK
         CH    R12,ELEVEN              ANY MORE ENTRIES IN BLOCK
         BNL   GETADR                  YES
NEXREC   GET   PDS                     READ DIRECTORY BLOCK
         LR    R12,R1                  GET BLOCK ADDRESS
         MVC   REMBYTS,0(R12)          MOVE NBR BYTES IN BLOCK
         CLC   REMBYTS,ZERO            ANY BYTES IN BLOCK
         BE    EOJ                     NO, EOF
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         CLI   0(R12),X'F9'            END OF DIRECTORY DATA?
         BH    EOJ                     YES
         BAS   R9,FORMPRT              GO FORMAT AND PRINT
         NI    11(R12),CLRBITS         CLEAR ALIAS, TTR BITS
         SR    R11,R11                 CLEAR WORK REG
         IC    R11,11(R12)             GET NBR USER HALFWORDS
         SLL   R11,1                   MULTIPLY BY 2
         LA    R11,12(R11)             CALCULATE RECORD LENGTH
         LH    R12,REMBYTS             GET NBR BYTES REMAINING
         SR    R12,R11                 SUBTRACT THIS ENTRY LENGTH
         BP    STREM                   ANY LEFT
         SR    R12,R12                 NO, SET TO ZERO
STREM    STH   R12,REMBYTS             STORE REMAINING BYTES
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         B     MAINLINE                GET NEXT DIRECTORY ENTRY
*********************************************************************
*        FORMPRT FORMATS THE DIRECTORY ENTRY AND WRITES IT TO THE   *
*        SYSPRINT FILE. R12 POINTS TO THE DIRECTORY ENTRY.          *
*********************************************************************
FORMPRT  ST    R9,FP9                  SAVE EXIT ADDRESS
         MVC   PMEMBER,0(R12)          MEMBER NAME TO OUTPUT
         NI    11(R12),CLRBITS         CLEAR ALIAS, TTR BITS
         SR    R11,R11                 CLEAR WORK REG
         IC    R11,11(R12)             GET NBR USER HALFWORDS
         CH    R11,H15                 15 HALFWORDS USER DATA PRESENT?
         BNE   FPPRT                   NO, JUST PRINT
         UNPK  PVERMOD(3),12(2,R12)    UNPACK VERSION
         UNPK  PVERMOD+3(3),13(2,R12)  UNPACK MOD LEVEL
         MVI   PVERMOD+5,C' '          CLEAR TRASH
         MVZ   PVERMOD(5),FZONES       CLEAR ZONES
         MVI   PVERMOD+2,C'.'          SET DELIMITER
         UNPK  PCRDATE,17(3,R12)       UNPACK CREATION DATE
         OI    PCRDATE+4,C'0'          CLEAR SIGN
         UNPK  PCHGDATE,21(3,R12)      UNPACK CHANGE DATE
         OI    PCHGDATE+4,C'0'         CLEAR SIGN
         UNPK  PTIME(3),24(2,R12)      UNPACK HOUR
         UNPK  PTIME+3(3),25(2,R12)    UNPACK MINUTE
         MVZ   PTIME(5),FZONES         CLEAR ZONES
         MVI   PTIME+2,C':'            SET DELIMITER
         MVI   PTIME+5,C' '            CLEAR TRASH
         MVC   PSIZE,ED5               SET EDIT WORD
         SR    R1,R1                   CLEAR FOR INSERT
         ICM   R1,B'0011',26(R12)      GET SIZE
         CVD   R1,DBLWD                CONVERT TO PACKED
         ED    PSIZE,DBLWD+5           EDIT SIZE
         OI    PSIZE+5,C'0'            CLEAR SIGN
         MVC   PINIT,ED5               SET EDIT WORD
         SR    R1,R1                   CLEAR FOR INSERT
         ICM   R1,B'0011',28(R12)      GET INIT
         CVD   R1,DBLWD                CONVERT TO PACKED
         ED    PINIT,DBLWD+5           EDIT INIT
         OI    PINIT+4,C'0'            CLEAR SIGN
         MVC   PID,32(R12)             ID TO OUTPUT
FPPRT    MVC   PDSNAME,DSNAME          DSNAME TO OUTPUT
         BAS   R9,PRINT                WRITE THE SYSPRINT RECORD
FPXIT    L     R9,FP9                  GET EXIT ADDRESS
         BR    R9                      EXIT
*********************************************************************
*        PRINT WRITES THE DATA LINE IN THE PRT AREA TO SYSPRINT     *
*        FILE, THEN CLEARS THE PRT AREA TO SPACES                   *
*********************************************************************
PRINT    TRT   PMEMBER(1),VALCHARS     VALID MEMBER NAME?
         BNZ   PRTCLEAR                NO, DROP THE RECORD
         PUT   SYSPRINT,PRT            WRITE DATA TO THE SYSPRINT FILE
PRTCLEAR MVC   PRT,BLANKS              CLEAR PRT AREA
         BR    R9                      EXIT
*********************************************************************
*        END OF JOB                                                 *
*********************************************************************
EOJ      CLOSE (SYSPRINT,,PDS)         CLOSE FILES
         L     R13,SAVE+4              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*********************************************************************
*        DATA AREAS                                                 *
*********************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
FP9      DS    A                       FORMPRT EXIT ADDRESS
REMBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ELEVEN   DC    H'11'                   CONSTANT 11
H15      DC    H'15'                   CONSTANT 15
ZERO     DC    H'0'                    CONSTANT ZERO
MAXPARM  DC    H'44'                   MAXIMUM PARM LENGTH
RECADDR  DS    A                       BLOCK ADDRESS
ED5      DC    XL6'402020202020'       EDIT WORD
FZONES   DC    CL5'00000'              CONSTANT ZEROS
CLRBITS  EQU   X'1F'                   AND MASK
DSNAME   DC    CL44' '                 DSNAME FROM EXEC PARM PARAMETER
BLANKS   DC    CL80' '                 BLANKS
*
PRT      DS    0CL98                   DATA AREA FOR SYSPRINT RECORDS
PMEMBER  DC    CL8' '                    MEMBER NAME
         DC    CL1' '
PVERMOD  DC    CL5' '                    VV.MM
         DC    CL1' '
PCRDATE  DC    CL5' '                    CREATION DATE YYDDD
         DC    CL1' '
PCHGDATE DC    CL5' '                    CHANGE DATE YYDDD
         DC    CL1' '
PTIME    DC    CL5' '                    TIME HH:MM
PSIZE    DC    CL6' '                    SIZE NNNNN
PINIT    DC    CL6' '                    INIT NNNNN
         DC    CL1' '
PID      DC    CL8' '                    ID OF LAST USER MAKING CHANGES
         DC    CL1' '
PDSNAME  DC    CL44' '                   DSNAME FROM PARM STATEMENT
*
VALCHARS DC    256X'FF'                VALID JOB NAME START CHARS
         ORG   VALCHARS+C'$'
         DC    X'00'
         ORG   VALCHARS+C'@'
         DC    X'00'
         ORG   VALCHARS+C'#'
         DC    X'00'
         ORG   VALCHARS+C'A'
         DC    9X'00'
         ORG   VALCHARS+C'J'
         DC    9X'00'
         ORG   VALCHARS+C'S'
         DC    8X'00'
         ORG
*********************************************************************
*        FILE DEFINITIONS                                           *
*********************************************************************
PDS      DCB   DSORG=PS,MACRF=GL,DDNAME=PDS,RECFM=F,                   X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=98
*********************************************************************
*        REGISTER EQUATES                                           *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

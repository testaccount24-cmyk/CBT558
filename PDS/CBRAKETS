         TITLE '*****CBRAKETS: CONVERT C PROGRAM BRACKETS *****'
CBRAKETS SVLNK R3
         PRINT GEN
***********************************************************************
* CONVERT THE VARIANT CHARACTERS IN A C SOURCE PROGRAM TO THE VALUES  *
* USED BY THE C COMPILER. AT PRESENT:                                 *
*                                                                     *
*               C COMPILER       REFLECTIONS                          *
*               --------------   -------------------                  *
* LEFT BRACKET  X'AD' Y-UMLAUT   X'BA' LEFT BRACKET                   *
*                                                                     *
* RIGHT BRACKET X'BD' QUOTE      X'BB' RIGHT BRACKET                  *
*                                                                     *
* CIRCUMFLEX    X'5F' CIRCUMFLEX X'B0' CIRCUMFLEX                     *
*                                                                     *
* TO SATISFY THE COMPILER, X'BA' IS CONVERTED TO X'AD' FOR THE LEFT   *
* BRACKET, X'BB' IS CONVERTED TO X'BD' FOR THE RIGHT BRACKET, AND     *
* X'B0' IS CONVERTED TO X'5F' FOR THE CIRCUMFLEX (HAT).               *
*                                                                     *
* AUTHOR R THORNTON AUG 2000                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
         OPEN  (SYSOUT,OUTPUT,SYSIN)   OPEN FILES
***********************************************************************
* MAINLINE PROCESSING: READ-TRANSLATE-WRITE THROUGH ENTIRE INPUT FILE.*
***********************************************************************
MAINLINE GET   SYSIN,RECORD            READ SYSIN RECORD
         TR    RECORD,XLATE            TRANSLATE BRACKETS
         PUT   SYSOUT,RECORD           WRITE TRANSLATED RECORD
         B     MAINLINE                LOOP THRU SYSIN FILE.
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (SYSIN,,SYSOUT)         CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
RECORD   DS    CL80                    RECORD STORAGE AREA
XLATE    DS    0D                      TRANSLATION TABLE
         DC    XL16'000102030405060708090A0B0C0D0E0F' 00-0F
         DC    XL16'101112131415161718191A1B1C1D1E1F' 10-1F
         DC    XL16'202122232425262728292A2B2C2D2E2F' 20-2F
         DC    XL16'303132333435363738393A3B3C3D3E3F' 30-3F
         DC    XL16'404142434445464748494A4B4C4D4E4F' 40-4F
         DC    XL16'505152535455565758595A5B5C5D5E5F' 50-5F
         DC    XL16'606162636465666768696A6B6C6D6E6F' 60-6F
         DC    XL16'707172737475767778797A7B7C7D7E7F' 70-7F
         DC    XL16'808182838485868788898A8B8C8D8E8F' 80-8F
         DC    XL16'909192939495969798999A9B9C9D9E9F' 90-9F
         DC    XL16'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF' A0-AF
         DC    XL16'5FB1B2B3B4B5B6B7B8B9ADBDBCBDBEBF' B0-BF B0, BA, BB
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF' C0-CF
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF' D0-DF
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF' E0-EF
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF' F0-FF
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,                        X
               RECFM=FB,LRECL=80
         LTORG
         END

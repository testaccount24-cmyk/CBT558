         PRINT NOGEN
         TITLE '*****GETGDG: GET GDG INFO FOR CALLER ****'
GETGDG SVLNK R3
*
***********************************************************************
* READS CATALOG INFORMATION FOR THE (N) VERSION OF A GDG AND RETURNS  *
* THE GENERATION NUMBER TO THE CALLER. USES THE LOCATE AND CAMLST     *
* MACROS TO OBTAIN CATALOG DATA. (SEE SC26-4555, MANAGING CATALOGS,   *
* PAGES 232-238).                                                     *
*                                                                     *
* CALLER PASSES A 44 BYTE AREA CONTAINING THE DATASET NAME LEFT       *
* JUSTIFIED AND PADDED TO THE RIGHT WITH BLANKS. THE DATASET NAME     *
* SHOULD END IN THE PARENTHESIZED GENERATION NUMBER DESIRED, FOR      *
* EXAMPLE: NTM.NETMAN.R410.TABLE(0). THE SECOND PARAMETER PASSED IS A *
* FOUR-BYTE FIELD WHICH WILL CONTAIN THE CURRENT GENERATION NUMBER IN *
* ZONED DECIMAL IF THE CATALOG SEARCH WAS SUCCESSFUL, OTHERWISE IT    *
* WILL CONTAIN HEX FF'S (HIGH VALUES) TO INDICATE FAILURE.            *
*                                                                     *
* AUTHOR R THORNTON SEP 1990                                          *
***********************************************************************
***********************************************************************
* GET THE DATASET NAME FROM THE CALLER'S AREA, ISSUE LOCATE, AND      *
* SEND BACK RESULTS                                                   *
***********************************************************************
         LM    R11,R12,0(R1)           POINT TO USER AREAS
         MVC   DSNAME,0(R11)           MOVE DSNAME
         LOCATE CAMLST                 GET CATALOG DATA
         ST    R15,SAVER15             SAVE RETURN CODE
         ST    R0,SAVER0               SAVE ADDITIONAL DATA
         LTR   R15,R15                 DID LOCATE WORK O.K?
         BNZ   MOVERR                  NO
         LA    R11,DSNAME+41           LAST POSSIBLE V00 POINT
         LA    R10,DSNAME+7            FIRST POSSIBLE V00 POINT
CHKV00   CLC   0(3,R11),=C'V00'        FOUND END OF GNNNNV00?
         BE    GETGENO                 YES
         BCTR  R11,0                   NO, BACK UP 1
         CR    R10,R11                 EXHAUSTED FIELD?
         BL    CHKV00                  NO, CONTINUE
         B     MOVERR                  YES, ERROR
GETGENO  SH    R11,=H'4'               BACK UP 4 BYTES
         MVZ   NUMTST,0(R11)           MOVE ZONES FOR NUMERIC TEST
         CLC   NUMTST,ZEROS            GENERATION NBR IS NUMERIC?
         BNE   MOVERR                  NO, ERROR
         MVC   0(4,R12),0(R11)         MOVE GENERATION NBR TO CALLER
         B     RETURN                  AND RETURN TO CALLER
MOVERR   MVC   0(4,R12),HIVALUS        RETURN HIGH VALUES ERROR INDIC
         B     RETURN                  AND RETURN
RETURN   L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
CAMLST   CAMLST NAME,DSNAME,,LOCAREA   CATALOG READ MACRO
DSNAME   DC    CL44'NTM.NETMAN.R410.TABLE(0)' GDG DSN TO FIND
LOCAREA  DS    0D                      AREA FOR RETURNED VOLUME INFO
         DS    265C
SAVER15  DS    F                       R15 AFTER LOCATE
SAVER0   DS    F                       R0 AFTER LOCATE
NUMTST   DC    CL4'0000'               NUMERIC TEST AREA
ZEROS    DC    CL4'0000'               ZEROS FOR NUMERIC TEST
HIVALUS  DC    XL4'FFFFFFFF'           HIGH VALUES FOR ERROR INDICATION
         END

         PRINT NOGEN
PRTPCH   SVLNK R3
         L     R6,0(R1)                GET PARM FIELD ADDRESS
         LA    R6,2(R6)                GET PARM DATA ADDRESS
         MVC   REQUESTS,0(R6)          MOVE PARMS TO WORKING STORAGE
         OPEN  INPUT1                  OPEN INPUT FILE
         RDJFCB INPUT1                 READ THE JFCB FOR INPUT1
         CLOSE INPUT1                  CLOSE INPUT FILE
         LA    R1,LIBNAME              POINT TO HEADING LIBRARY NAME
         LA    R2,JFCBDSNM             POINT TO DSNAME IN JFCB
         LA    R15,44                  MAX LOOP VALUE
MVDSN    MVC   0(1,R1),0(R2)           MOVE A DSNAME BYTE
         CLI   0(R1),C' '              NAME ENDED?
         BE    CKPRT                   YES
         LA    R1,1(R1)                NO, STEP OVER IN LIBRARY NAME
         LA    R2,1(R2)                STEP OVER IN JFCB NAME
         BCT   R15,MVDSN               LOOP TO MOVE THE DSNAME
         MVI   0(R1),C' '              SPACE AFTER 44-BYTE NAME
CKPRT    CLI   PRT,YES                 PRINTOUT WANTED
         BNE   CKPUN                   NO
         OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT
CKPUN    CLI   PUN,YES                 PUNCH DESIRED
         BNE   CKCRD                   NO
         OPEN  (SYSOUT,OUTPUT)         OPEN PUNCH
CKCRD    CLI   MEM,CRD                 MEMBER NAMES IN CARDS
         BNE   OPNDIR                  NO
         OPEN  SYSIN                   YES, OPEN CARD FILE
         B     OPMEM                   GO CHECK FURTHER
OPNDIR   CALL  READDIR,OPEN            OPEN DIRECTORY FILE
         CLI   MEM,DIR                 DIRECTORY LIST DESIRED
         BE    DIRLST                  YES, GO TO ROUTINE
OPMEM    CALL  READMEM,OPEN            OPEN MEMBER FILE
         MVC   LRECL,82(R1)            SAVE LRECL
         MVC   RECFM,36(R1)            SAVE RECFM
*
*
*        ROUTINE TO READ MEMBERS FOR OUTPUT WHEN MEM=C OR MEM=A
*
*
NEWMEM   CLI   MEM,CRD                 MEMBER NAMES IN CARDS
         BE    READCRD                 YES
         CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
NEWHDG   MVC   HDGMEM,FINDNAM          NAME TO HEADING
         MVI   MEMHDR,1                INDICATE HEADER
         MVI   OUTCC,TRIPSP            SET TRIPLE SPACING
         MVC   OUTCC(81),MEMHDG        NEW MEMBER HEADING
         CLI   SEP,PAG                 NEW PAGE EACH MEMBER
         BNE   HDG                     NO
         MVI   OUTCC,SKTO1             SET SKIP-TO-1 COMMAND
HDG      BAL   R9,OUTPUT               PRINT/PUNCH HEADER
         MVI   OUTCC,TRIPSP            SET TRIPLE SPACING
         CALL  READMEM,FIND            LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BNE   READMEM                 YES
         MVC   NOTNAM,MEMNAME          NAME TO MESSAGE
         MVC   OUTREC,NOTFND           MESSAGE TO USER
         BAL   R9,OUTPUT               GO TO OUTPUT ROUTINE
         B     NEWMEM                  TRY NEXT MEMBER
READMEM  CALL  READMEM,(READ,MEMREC)   READ NEXT RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         LA    R1,MEMREC               GET RECORD ADDRESS
         SR    R2,R2                   CLEAR REG
         ICM   R2,3,LRECL              GET LRECL
         TM    RECFM,X'80'             FIXED LENGTH RECORDS
         BO    BEGNMOV                 YES
         SR    R2,R2                   NO, CLEAR REG
         ICM   R2,3,0(R1)              VARIABLE, GET RDW LGTH
         SH    R2,=H'4'                DEDUCT 4 FOR RDW
         LA    R1,4(R1)                STEP PAST RDW
BEGNMOV  CH    R2,=H'80'               OVER 80 BYTES LEFT?
         BNH   FINLMOV                 NO
         MVC   OUTREC,0(R1)            MOVE 80 BYTES
         LA    R1,80(R1)               STEP TO NEXT 80
         SH    R2,=H'80'               DEDUCT 80 BYTES
         STM   R1,R2,SAV12             SAVE REGS ACROSS OUTPUT
         BAL   R9,OUTPUT               GO TO OUTPUT ROUTINE
         LM    R1,R2,SAV12             RESTORE REGS
         B     BEGNMOV                 CONTINUE THRU RECORD
FINLMOV  LTR   R2,R2                   ANY LEFT TO MOVE?
         BZ    READMEM                 NO
         BCTR  R2,0                    DEDUCT 1 FOR MOVE LENGTH
         MVC   OUTREC,BLANX            CLEAR OUTPUT AREA
         EX    R2,LASTMOVE             MOVE LAST PART
         BAL   R9,OUTPUT               GO TO OUTPUT ROUTINE
         B     READMEM                 GO READ AGAIN
LASTMOVE MVC   OUTREC(1),0(R1)         EXECUTED MOVE
READCRD  L     R12,CDADR               GET NEXT CARD COLUMN ADDR
         CLI   0(R12),BLANK            IS IT BLANK
         BNE   SCAN                    NO
REREAD   GET   SYSIN,CARD              READ NEXT CARD
         CLI   CARD,C'*'               COMMENT CARD
         BE    REREAD                  YES, SKIP IT
         LA    R12,CARD                GET CARD ADDR
         ST    R12,CDADR               STORE CARD ADDR
         B     READCRD                 RECHECK BLANK CARD
SCAN     LA    R11,FINDNAM             GET FIND LIST NAME ADDR
         MVC   FINDNAM,BLANX           CLEAR NAME TO BLANKS
         LA    R10,8                   GET MAX LENGTH
CKEND    CLI   0(R12),BLANK            BLANK COL = END OF NAME
         BE    STR12                   YES, FINISHED
         CLI   0(R12),COMMA            COMMA ALSO END OF NAME
         BE    ADD1                    YES, STEP PAST
         MVC   0(1,R11),0(R12)         MOVE CHARACTER TO FIND NAME
         LA    R11,1(R11)              TO NEXT COL IN FIND NAME
         LA    R12,1(R12)              TO NEXT COL IN CARD
         BCT   R10,CKEND               LOOP THRU NAME
ADD1     LA    R12,1(R12)              STEP PAST COMMA
STR12    ST    R12,CDADR               SAVE CARD ADDRESS
         B     NEWHDG                  GO TO NEXT MEMBER HEADING
*
*
*
DIRLST   EQU   *                       *** DIRECTORY LISTING ROUTINE **
         MVI   DIRHDG,SKTO1            SET SKIP-TO-1 COMMAND
         MVC   OUTCC(81),DIRHDG        DIRECTORY HEADING TO OUTPUT
         BAL   R9,OUTPUT               GO WRITE IT
         MVI   OUTCC,TRIPSP            SET LINE SPACING TO TRIPLE SPACE
NEXLIN   LA    R7,OUTREC               GET FIRST BYTE ADDR IN OUTPUT
         LA    R8,OUTREC+79            GET ADDR OF OUTPUT END
RDIRCTY  CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF DIRECTORY
         BE    EOJ                     YES, END OF JOB
         MVC   0(8,R7),MEMNAME         NAME TO OUTPUT RECORD
         LA    R7,12(R7)               STEP TO NEXT OUTPUT LOCATION
         CR    R7,R8                   PAST END OF OUTPUT
         BL    RDIRCTY                 NO, CONTINUE
         BAL   R9,OUTPUT               YES, WRITE OUTPUT
         B     NEXLIN                  START NEXT LINE
*
*
*
OUTPUT   EQU   *                       *** OUTPUT ROUTINE ***
         CLI   PRT,YES                 PRINTED OUTPUT
         BNE   CKPUNCH                 NO
         PUT   SYSPRINT,OUTCC          WRITE PRINT LINE
CKPUNCH  CLI   PUN,YES                 PUNCHED OUTPUT
         BNE   CLROUT                  NO
         CLI   MEMHDR,1                THIS A HEADER
         BNE   PUTWRT                  NO
         MVI   MEMHDR,0                RESET HEADER INDIC
         MVI   OUTCC,SGLSPAC           BLANK TO CONTROL CHAR
         MVC   OUTREC,OUTCC            CLEAR OUTPUT RECORD
         MVC   OUTREC(12),=C'./ ADD NAME=' OUTPUT HEADER LITERAL
         MVC   OUTREC+12(8),FINDNAM      MEMBER NAME TO HEADER
PUTWRT   PUT   SYSOUT,OUTREC           PUNCH OUTPUT RECORD
CLROUT   MVI   OUTCC,SGLSPAC           SET SINGLE SPACE CONTROL
         MVC   OUTREC,OUTCC            CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
*
*
*
EOJ      EQU   *                       END OF JOB ROUTINE
         BAL   R9,OUTPUT               WRITE LAST RECORD
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*                      *** STORAGE AND FILES ***
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
NOTFND   DS    0CL80
         DC    CL7'MEMBER'
NOTNAM   DS    CL8
         DC    CL65' '
MEMHDG   DC    C'-'                    CONTROL CHARACTER FOR HEADER
         DC    X'00'
         DC    17C'*'
         DC    CL12'MEMBER NAME'
HDGMEM   DS    CL8                     MEMBER NAME
         DC    C' IN'
DIRHDG   DC    C' LIBRARY '
LIBNAME  DS    CL1
         DC    71C'*'
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
MEMREC   DS    CL1688                  MEMBER RECORD
LRECL    DS    H                       LRECL FROM INPUT1 DCB
RECFM    DS    XL1                     RECFM FROM INPUT1 DCB
MEMHDR   DC    X'00'                   MEMBER HEADER INDIC
OUTCC    DS    CL1                     CONTROL CHAR FOR PRINTING
OUTREC   DS    CL80                    OUTPUT RECORD
CDADR    DC    A(CARD)                 NEXT CARD ADDRESS
CARD     DC    CL80' '                 CARD WITH MEMBER NAMES
         DC    C' '                    SCAN STOPPER
YES      EQU   C'Y'
ALL      EQU   C'A'
DIR      EQU   C'D'
CRD      EQU   C'C'
SKTO1    EQU   C'1'
SGLSPAC  EQU   C' '
TRIPSP   EQU   C'-'
EOF      EQU   X'FF'
PAG      EQU   C'P'
BLANK    EQU   C' '
FIVE     DC    H'5'
BLANX    DC    CL80' '
SAV12    DS    2F
COMMA    EQU   C','
REQUESTS DS    0CL4                    DESCRIPTION OF PARM FIELD
PRT      DS    CL1                     PRINT REQUEST, Y=YES
PUN      DS    CL1                     PUNCH REQUEST, Y=YES
MEM      DS    CL1                     MEMBER REQUEST, A=ALL, C=FROM CA
*                                      RDS, D=DIRECTORY ONLY
SEP      DS    CL1                     MEMBER SEP, P=BY PAGE
JFCB     DC    CL176'00'               JFCB FOR INPUT1
         ORG   JFCB                    DATASET NAME
JFCBDSNM DS    CL44                    LIBRARY NAME
         DS    CL132
         ORG
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
INPUT1   DCB   DSORG=PO,DDNAME=INPUT1,MACRF=R,EXLST=JFCBADDR
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,LRECL=81,BLKSIZE=81
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,                        X
               RECFM=FB,LRECL=80
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80,EODAD=EOJ
         END

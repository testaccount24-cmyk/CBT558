         TITLE '***** STRIP RECORDS FROM AN SMF FILE *****'
         PRINT NOGEN
SMFSTRIP SVLNK R3,R4
**********************************************************************
*  STRIP ALL TYPE 14 (INPUT DATASET), TYPE 15 (OUTPUT DATASET),      *
*  TYPE 17 (SCRATCH DATASET), AND TYPE 18 (RENAME DATASET) SMF       *
*  TO THE OUTPUT FILE. INPUT1 DEFINES THE SMF INPUT FILE, WHICH IS   *
*  HARD-CODED TO BE RECFM=VBS. THE OUTPUT1 DATASET IS HARD-CODED TO  *
*  BE RECFM=VB,LRECL=32716, AND BLKSIZE=32720.                       *
*  THE DATASET NAME TO BE STRIPPED IS ENTERED IN THE PARM FIELD OF   *
*  THE EXECUTE STATEMENT.                                            *
*  AUTHOR R. THORNTON SEP 1989                                       *
**********************************************************************
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
         L     R2,0(R1)                POINT TO PARM DATA
         LH    R9,0(R2)                GET PARM LENGTH
         LTR   R9,R9                   ANY PARM DATA?
         BZ    NOPARM                  NO, ERROR
         CH    R9,H44                  MORE THAN 44 BYTES?
         BH    TOOLONG                 YES
         BCTR  R9,0                    COMPUTE LENGTH CODE
         EX    R9,MOVPARM              MOVE PARM DATA
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
         B     MAINLINE                GO TO MAINLINE
MOVPARM  MVC   COMPDSN(1),2(R2)        EXECUTED MOVE
NOPARM   WTO   'NO PARM FIELD: ENTER DATASET NAME IN PARM',ROUTCDE=11
         ABEND 777,DUMP
TOOLONG  WTO   'PARM DATASET NAME TOO LONG, ERROR',ROUTCDE=11
         ABEND 888,DUMP
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
MAINLINE GET   INPUT1,SMFHDR           GET INPUT AREA ADDRESS
         CLI   SMFRTY,14               TYPE 14 (INPUT DATASET)?
         BE    GOT1415                 YES
         CLI   SMFRTY,15               TYPE 15 (OUTPUT DATASET)?
         BE    GOT1415                 YES
         CLI   SMFRTY,17               TYPE 17 (SCRATCH DATASET)?
         BE    GOT17                   YES
         CLI   SMFRTY,18               TYPE 18 (RENAME DATASET)?
         BE    GOT18                   YES
         B     MAINLINE                NO, DROP IT
GOT1415  CLC   SMF14DSN,=CL44'USER02.FOCUS.MULTI'
         BE    PUT                      GOT IN/OUT DATASET
         B     MAINLINE                 NO, SKIP IT
GOT17    CLC   SMF17DSN,=CL44'USER02.FOCUS.MULTI'
         BE    PUT                      GOT SCRATCH DATASET
         B     MAINLINE                 NO, SKIP IT
GOT18    CLC   SMF17DSN,=CL44'USER02.FOCUS.MULTI'
         BE    PUT                      GOT RENAME DATASET
         CLC   SMF18DSN,=CL44'USER02.FOCUS.MULTI'
         BE    PUT                      GOT RENAME DATASET
         B     MAINLINE                 NO, SKIP IT
PUT      MVC   SMFLEN,INPUT1+82         MOVE LENGTH TO OUTPUT
         XC    SMFSEG,SMFSEG            CLEAR RDW BYTES 2-3
         PUT   OUTPUT1,SMFRCD           WRITE OUTPUT RECORD
         B     MAINLINE                 GO READ AGAIN
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
RETCODE  DC    F'0'                    RETURN CODE
H44      DC    H'44'                   CONSTANT 44
COMPDSN  DC    CL44' '                 DESIRED DATASET NAME
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
SMFRCD   DS    0CL32768                SMF RECORD INPUT AREA
SMFLEN   DS    H                       RECORD DESCRIPTOR WORD
SMFSEG   DS    H                       LAST 2 BYTES OF RDW
SMFHDR   DS    0X                      START OF SMF HEADER DATA
SMFFLG   DS    XL1                     4=XA, 8=ESA
SMFRTY   DS    XL1                     RECORD TYPE
SMFTME   DS    XL4                     TIME 1/100 SEC
SMFDTE   DS    PL4                     DATE 00YYDDDF
SMFSID   DS    CL4                     SYSTEM ID
SMFJBN   DS    CL8                     JOB NAME
SMFRST   DS    XL4                     READER START TIME
SMFRSD   DS    PL4                     READER START DATE
SMF17DSN EQU   SMFRCD+44,44            TYPE 17,18 DATASET NAME
SMF18DSN EQU   SMFRCD+88,44            TYPE 18 NEW DSN
SMF14DSN EQU   SMFRCD+68,44            TYPE 14,15 DATASET NAME
SMF30JOB EQU   SMFRCD+120,8            TYPE 30 JOB NAME
SMF30RST EQU   SMFRCD+184,4            TYPE 30 READER START TIME
         DS    CL32768
*
*        JOB LOG NUMBER TABLE
*
TABLE    DC    256XL17'00'              JOBLOG TABLE
         END

         TITLE '*** DUPLMEMB: REPORT DUPLICATE MEMBERS ***'
DUPLMEMB CSECT
*********************************************************************
* READS A FILE OF DIRECTORY ENTRY RECORDS SORTED IN MEMBER NAME     *
* ORDER. THE DIRECTORY RECORDS WILL HAVE BEEN CREATED BY MULTIPLE   *
* PDSDIRB RUNS AGAINST DIFFERENT LIBRARIES SO THAT DUPLICATE MEMBER *
* NAMES MAY EXIST.                                                  *
*                                                                   *
* THE FILE IS READ SEQUENTIALLY, AND ONE RECORD IS WRITTEN FOR EACH *
* MEMBER NAME REPRESENTED. AN INDICATION IS MADE ON THE LISTING TO  *
* SHOW EVERY DATASET IN WHICH EACH MEMBER RESIDES.                  *
*                                                                   *
* SPECIALIZE FOR EACH USE.                                          *
* AUTHOR R THORNTON APR 2002                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING DUPLMEMB,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'DUPLMEMB'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
         GET   INPUT1,INREC            READ FIRST INPUT RECORD
         B     MLFILL                  GO START A MEMBER
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,INREC            READ INPUT RECORD
         CLC   OUTMEMBR,INMEMBER       SAME MEMBER NAME?
         BE    MLSAME                  YES
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
MLFILL   MVC   OUTREC,BLANKS           CLEAR THE RECORD
         MVC   OUTLIBS,DOTS            FILL LIBRARY COLUMNS WITH DOTS
         MVC   OUTMEMBR,INMEMBER       MOVE MEMBER NAME
MLSAME   TRT   INNUMBER,ISNUM          IS NUMBER FIELD NUMERIC?
         BNZ   MAINLINE                NO, DROP IT
         PACK  DBLWD,INNUMBER          PACK THE NUMBER
         CVB   R1,DBLWD                CONVERT IT TO BINARY
         LTR   R1,R1                   ANY LIBRARY NUMBER?
         BZ    MAINLINE                NO, DROP THE RECORD
         CHI   R1,15                   VALID LIBRARY NUMBER?
         BH    MAINLINE                NO, DROP THE RECORD
         LA    R15,OUTLIBS(R1)         POINT TO THE LIBRARY BYTE
         STC   R1,0(R15)               STORE THE LIBRARY NUMBER
         NI    0(R15),X'0F'            CLEAR THE ZONE
         TR    0(1,R15),TRCHAR         MAKE IT PRINTABLE
         MHI   R1,46                   MULTIPLY BY NAME ENTRY LENGTH
         LA    R14,LIBNAMES(R1)        POINT TO LIBRARY NAME ENTRY
         MVC   0(1,R14),0(R15)         MOVE IN LIBRARY NUMBER
         MVC   2(44,R14),INDSNAME      COPY LIBRARY NAME
         B     MAINLINE                LOOP THROUGH INPUT FILE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      PUT   OUTPUT1,OUTREC          WRITE LAST RECORD
         LA    R11,16                  NUMBER OF LIBRARY NAME ENTRIES
         LA    R12,LIBNAMES            POINT TO FIRST ENTRY
LIBLUP   MVC   OUTREC(46),0(R12)       MOVE IN LIBRARY NUMBER & NAME
         PUT   OUTPUT1,OUTREC          WRITE LIBRARY LINE
         LA    R12,L'LIBNAMES(R12)     STEP TO NEXT ENTRY
         BCT   R11,LIBLUP              LOOP TO PRINT THEM ALL
EOJ2     CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DC    F'0'                    RETURN CODE
*
TRCHAR   DC    CL16'0123456789ABCDEF'  TRANSLATE HEX TO PRINTABLE
ISNUM    DC    256X'FF'                CHECK FOR UNSIGNED ZONED DECIMAL
         ORG   ISNUM+C'0'
         DC    10X'00'
         ORG
LIBNAMES DC    16CL46' '               LIBRARY NAME TABLE
BLANKS   DC    CL80' '                 BLANKS
DOTS     DC    CL16' ...............'  DOTS
*
INREC    DS    0CL80                   INPUT RECORD
INMEMBER DS    CL8                       MEMBER NAME
         DS    CL15
INNUMBER DS    CL3                       FILE NUMBER
         DS    CL1
INDSNAME DS    CL44                      DATASET NAME
         DS    CL9
*
OUTREC   DS    0CL80                   OUTPUT RECORD
OUTMEMBR DC    CL8' '                    MEMBER NAME
OUTLIBS  DS    0CL16                     LIBRARIES CONTAINING
OUTNL    DC    CL1' '                        NOT A LIBRARY
OUTASMP  DC    CL1' '                        ASM LIBRARY
OUTCOBP  DC    CL1' '                        COBOL LIBRARY
OUTCPGM  DC    CL1' '                        C LIBRARY
OUTEXEC  DC    CL1' '                        EXEC LIBRARY
OUTJCLX  DC    CL1' '                        JCL LIBRARY
OUTDATA  DC    CL1' '                        DATA LIBRARY
OUTINST  DC    CL1' '                        INSTRUCT LIBRARY
OUTCLST  DC    CL1' '                        CLIST LIBRARY
OUTCLAS  DC    CL1' '                        CLASS LIBRARY
OUTCBCL  DC    CL1' '                        COBCLASS LIBRARY
OUTCCCL  DC    CL1' '                        CCLASS LIBRARY
OUTDPC2  DC    CL1' '                        DUMPCLAS2 LIBRARY
OUTMEMY  DC    CL1' '                        MEMORY LIBRARY
OUTMSGS  DC    CL1' '                        MSGS LIBRARY
OUTPLIB  DC    CL1' '                        PLIB LIBRARY
         DC    CL56' '
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
         LTORG
DUPLMEMB CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** VTOCPRNT - PRINT DISK PACK VTOC LISTING ***'
VTOCPRNT CSECT
         REQU
         USING VTOCPRNT,R3
***********************************************************************
* THIS PROGRAM PRINTS THE FILE CREATED BY PROGRAM VTOCDATA AFTER IT IS*
* SORTED IN THE DESIRED ORDER.                                        *
* R THORNTON OCT 1991
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS
         LR    R3,R15                  INITIALIZE BASE REGISTER
         LA    R15,SAVE                GET SAVE AREA ADDRESS
         ST    R15,8(R13)              STORE FORWARD POINTER
         ST    R13,SAVE+4              STORE BACK POINTER
         LR    R13,R15                 INITIALIZE SAVE POINTER
         OPEN  (INPUT1,INPUT,PRINT1,OUTPUT) OPEN FILES
         BAS   R9,HEADING              WRITE FIRST HEADING
*
*
*
MAINLINE GET   INPUT1,PRTLINE          READ A PRINT LINE
         BAS   R9,PRINT                PRINT IT
         B     MAINLINE                LOOP TO PRINT ALL LINES
*
*
*
PRINT    PUT   PRINT1,PRTLINE          WRITE A PRINT LINE
         AP    LINECT,P1               ADD 1 TO LINE COUNTER
         CP    LINECT,ENDOPAGE         AT END OF PAGE?
         BLR   R9                      NO, RETURN
*
*
*
HEADING  AP    PAGENO,P1               ADD 1 TO PAGE COUNTER
         MVC   HDRPAGE,EDITMASK        EDIT PAGE
         ED    HDRPAGE,PAGENO             NUMBER TO HEADING
         PUT   PRINT1,HEADER1          WRITE FIRST HEADER LINE
         PUT   PRINT1,HEADER2          WRITE SECOND HEADER LINE
         PUT   PRINT1,SPACER           WRITE SPACER LINE
         ZAP   LINECT,P5               RESET LINE COUNTER
         BR    R9                      RETURN TO CALLER
*
*
*
EOJ      CLOSE (INPUT1,,PRINT1)        CLOSE OUTPUT FILES
         L     R13,SAVE+4              GET CALLER'S SAVE AREA ADDRESS
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         SR    R15,R15                 RETURN CODE = 0
         BR    R14                     RETURN TO CALLER
*
*
*
*                       *** DATA, WORK, FILE, AND STORAGE AREAS ***
*
         LTORG
*
SAVE     DS    18F                     REGISTER SAVE AREA
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
ENDOPAGE DC    PL2'58'                 END OF PAGE LINE COUNT
PAGENO   DC    PL2'0'                  PAGE COUNTER
EDITMASK DC    X'40202020'             EDIT MASK FOR PAGE NUMBER
P1       DC    PL1'1'                  CONSTANT 1
P5       DC    PL1'5'                  CONSTANT 5
*
HEADER1  DS    0CL133                  HEADER LINE 1
         DC    CL1'1'                  SKIP TO HEAD OF FORM
         DC    CL39'VTOCDATA'
         DC    CL20'DISK VOLUME CONTENTS'
         DC    CL39' '
         DC    CL4'PAGE'
HDRPAGE  DS    CL4                     PAGE NUMBER
         DC    CL26' '
*
HEADER2  DS    0CL133                  HEADER LINE 2
         DC    C'0'                    DOUBLESPACE
         DC    CL44'DATASET NAME'      DATASET NAME
         DC    CL1' '
         DC    CL6'VOLSER'             VOLUME SERIAL NUMBER
         DC    CL1' '
         DC    CL6'CREATE'             DATE CREATED
         DC    CL1' '
         DC    CL6'LSTREF'             DATE LAST REFERENCED
         DC    CL1' '
         DC    CL7'TRKALLO'            TRACKS ALLOCATED
         DC    CL1' '
         DC    CL7'TRKUSED'            TRACKS USED
         DC    CL1' '
         DC    CL3'FMT'                RECORD FORMAT
         DC    CL1' '
         DC    CL2'OR'                 DATASET ORGANIZATION
         DC    CL1' '
         DC    CL6' LRECL'             LOGICAL RECORD LENGTH
         DC    CL1' '
         DC    CL6'BLKSIZ'             BLOCKSIZE
         DC    CL1' '
         DC    CL3'EXT'                NUMBER OF EXTENTS
         DC    CL26' '
*
SPACER   DS    CL133' '                SPACE LINE AFTER HEADING
*
PRTLINE  DS    0CL133                  PRINT LINE OUTPUT
PRTCC    DC    C' '                    CARRIAGE CONTROL CHARACTER
PRT      DC    CL132' '                PRINT LINE
DSNAME   DS    CL44                    DATASET NAME
         DS    CL1
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
         DS    CL1
CREDATE  DS    CL6                     DATE CREATED
         DS    CL1
LREFDATE DS    CL6                     DATE LAST REFERENCED
         DS    CL1
TRKALLOC DS    CL7                     TRACKS ALLOCATED
         DS    CL1
TRKUSED  DS    CL7                     TRACKS USED
         DS    CL1
RECFM    DS    CL3                     RECORD FORMAT
         DS    CL1
DSORG    DS    CL2                     DATASET ORGANIZATION
         DS    CL1
LRECL    DS    CL6                     LOGICAL RECORD LENGTH
         DS    CL1
BLKSIZE  DS    CL6                     BLOCKSIZE
         DS    CL1
NUMEXTS  DS    CL3                     NUMBER OF EXTENTS
         DS    CL26
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,                        X
               RECFM=FB,LRECL=133,EODAD=EOJ
*
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
         END

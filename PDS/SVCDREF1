         TITLE '*** SVCDREF1: REFORMAT SVCDUMP INFORMATION ***'
         PRINT NOGEN
SVCDREF1 SVLNK R3
***********************************************************************
* REFORMATS THE SPP.SVCDUMP.DATA FILE OF INFORMATION ON SVCDUMPS FOR  *
* PROCESSING BY OTHER SVCD PROGRAMS. SEE PDS.JCL(SVCDJOB).            *
*                                                                     *
* AUTHOR R THORNTON SEP 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION                                                      *
***********************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
* MAINLINE PROCESSING: REFORMAT THE 80-BYTE RECORDS INPUT TO A SINGLE *
* OUTPUT RECORD FOR EACH DUMP. THE HIGHEST DUMP NUMBER FOUND IS KEPT  *
* FOR EOJ PROCESSING.                                                 *
***********************************************************************
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         CLI   INRECD,C'*'             COMMENT?
         BE    MAINLINE                YES, SKIP IT
         CLC   INRECD(72),BLANKS       BLANK RECORD?
         BE    MAINLINE                YES, SKIP IT
         CLI   INRECD,C' '             FIRST RECORD OF NEW DUMP?
         BE    SUBSEQ                  NO
         CLI   PROGRESS,0              RECORD IN PROGRESS?
         BE    NEWDUMP                 NO
         PUT   OUTPUT1,OTRECD          WRITE OUTPUT RECORD
         MVI   PROGRESS,0              RESET IN PROGRESS INDICATOR
NEWDUMP  MVC   OTDMPNBR,INDMPNBR       DUMP NBR
         PACK  TESTNBR,OTDMPNBR        PACK FOR COMPARE
         CP    HIGHNBR,TESTNBR         THIS HIGHEST NUMBER SO FAR?
         BNL   MOVEDSN                 NO
         PACK  HIGHNBR,OTDMPNBR        YES, SAVE IT
MOVEDSN  MVC   OTDSNAME,INDSNAME       DSNAME ABBREVIATION, LAST DUMP
         MVC   OTLSTDAT,INLSTDAT       YYMMDD DATE OF LAST DUMP
         MVC   OTLSTTIM,INLSTTIM       HHMM TIME OF LAST DUMP
         MVC   OTFSTDAT,INFSTDAT       MMDD DATE OF 1ST DUMP
         MVC   OTTOTAL,INTOTAL         TOTAL DUMPS
         MVC   OTADUMPS,INADUMPS       TOTAL SYSA DUMPS
         MVC   OTBDUMPS,INBDUMPS       TOTAL SYSB DUMPS
         MVC   OTREPDAT,INREPDAT       PROBLEM REPORT DATE MMDD
         MVC   OTIBMNBR,INIBMNBR       IBM PROBLEM NUMBER
         MVC   OTPTFNBR,INPTFNBR       FIXING PTF NUMBER
         MVC   OTAPLYA,INAPLYA         DATE PTF ON SYSA MMDD
         MVC   OTAPLYB,INAPLYB         DATE PTF ON SYSB MMDD
         MVC   OTTITLE,BLANKS          CLEAR DUMP TITLE
         MVC   OTNOTE,BLANKS           CLEAR NOTE
         MVI   PROGRESS,1              SHOW PART 1 COMPLETE
         B     MAINLINE                GO READ NEXT RECORD
SUBSEQ   CLI   PROGRESS,0              ANYTHING IN PROGRESS?
         BE    MAINLINE                NO, SKIP THIS RECORD
         CLI   PROGRESS,1              THIS FIRST LINE OF TITLE?
         BNE   SUBSEQ2                 NO
         MVC   OTTITLE(67),INRECD+5    YES, MOVE START OF TITLE
         MVI   PROGRESS,2              SHOW TITLE BEGUN
         B     MAINLINE                GO READ NEXT RECORD
SUBSEQ2  CLC   INRECD+5(6),=CL6'NOTE: ' THIS A NOTE?
         BE    GOTNOTE                 YES
         CLI   PROGRESS,2              JUST MOVED FIRST LINE?
         BNE   MAINLINE                NO, SKIP THE RECORD
         MVC   OTTITLE+67(49),INRECD+5 ELSE MOVE REST OF TITLE
         MVI   PROGRESS,3              SHOW 2ND TITLE LINE MOVED
         B     MAINLINE                GO READ NEXT RECORD
GOTNOTE  CLI   PROGRESS,2              AT LEAST ONE LINE OF TITLE?
         BL    MAINLINE                NO, DISCARD RECORD
         MVC   OTNOTE,INRECD+5         MOVE NOTE
         MVI   PROGRESS,4              SHOW NOTE WRITTEN
         B     MAINLINE                GO READ NEXT RECORD
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLI   PROGRESS,0              ANY OUTPUT RECORD TO WRITE?
         BE    EOJHIGH                 NO
         PUT   OUTPUT1,OTRECD          WRITE DUMP NUMBER RECORD
EOJHIGH  AP    HIGHNBR,P1              INCREMENT HIGHEST DUMP NBR FOUND
         XC    OTRECD,OTRECD           CLEAR OUTPUT RECORD
         UNPK  OTDMPNBR,HIGHNBR        MOVE IN NEXT AVAILABLE DUMP NBR
         PUT   OUTPUT1,OTRECD          WRITE DUMP NUMBER RECORD
         CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
TESTNBR  DC    PL3'0'                  WORK FIELD
HIGHNBR  DC    PL3'0'                  HIGHEST DUMP NUMBER FOUND
PROGRESS DC    X'00'                   RECORD IN PROGRESS INDICATOR
BLANKS   DC    CL120' '                BLANKS
P1       DC    PL1'1'                  CONSTANT 1
*
INRECD   DS    0CL80                   INPUT RECORD
INDMPNBR DS    CL4                      DUMP NBR IF 1ST CARD OF DUMP
         DS    CL1
INDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
         DS    CL1
INLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
         DS    CL1
INLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
         DS    CL1
INFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
         DS    CL1
INTOTAL  DS    CL4                      TOTAL DUMPS
         DS    CL1
INADUMPS DS    CL4                      TOTAL SYSA DUMPS
         DS    CL1
INBDUMPS DS    CL4                      TOTAL SYSB DUMPS
         DS    CL1
INREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
         DS    CL1
INIBMNBR DS    CL5                      IBM PROBLEM NUMBER
         DS    CL1
INPTFNBR DS    CL7                      FIXING PTF NUMBER
         DS    CL1
INAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
         DS    CL1
INAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
         DS    CL8
*
OTRECD   DS    0CL236                  OUTPUT RECORD
OTDMPNBR DS    CL4                      DUMP NBR
OTDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
OTLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
OTLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
OTFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
OTTOTAL  DS    CL4                      TOTAL DUMPS
OTADUMPS DS    CL4                      TOTAL SYSA DUMPS
OTBDUMPS DS    CL4                      TOTAL SYSB DUMPS
OTREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
OTIBMNBR DS    CL5                      IBM PROBLEM NUMBER
OTPTFNBR DS    CL7                      FIXING PTF NUMBER
OTAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
OTAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
OTTITLE  DS    CL116                    DUMP TITLE
OTNOTE   DS    CL60                     NOTE
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=236
         END

         TITLE '*** ENDVREFM: REFORMAT SYSTEM OWNER LIST ***'
***********************************************************************
* READ ENDEVOR REPORT RUN NIGHTLY AND WRITTEN TO DISK TO WRITE A FILE *
* OF PROGRAM-TO-ENDEVOR-SYSTEM RECORDS. THE OUTPUT FILE WILL BE SORTED*
* BY ENDEVOR SYSTEM AND SUBSYSTEM AND IS THEN PROCESSED BY PROGRAM    *
* ENDVRMTCH TO ADD THE SYSTEM OWNER'S NAME.                           *
* AUTHOR R THORNTON NOV 1994                                          *
***********************************************************************
*
ENDVREFM SVLNK R3,R4
OPENS    OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1)
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE MVI   GOODREC,1               INDICATE GOOD RECORD
         BAS   R9,GETIN                READ INPUT RECORD
         BAS   R9,CHKRECD              CHECK RECORD
         CLI   GOODREC,0               BAD RECORD?
         BE    WRITBAD                 YES
         BAS   R9,PROCESS              PROCESS THE RECORD
         CLI   GOODREC,0               BAD RECORD?
         BE    WRITBAD                 YES
         BAS   R9,PUTGOOD              WRITE GOOD RECORD
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         B     MAINLINE                LOOP THROUGH INPUT FILE
WRITBAD  B     MAINLINE                LOOP THROUGH INPUT FILE
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
CHKRECD  CLI   CC,C'1'                 THIS A SKIP-TO-HOF?
         BE    TOSS                    YES
         CLC   TYPE,=CL8'COBOL'        TYPE ITSELF?
         BE    CKELEM                  YES
         CLC   TYPE,=CL8'ASSEM'        TYPE ITSELF?
         BNE   TOSS                    YES
CKELEM   CLC   ELEMENT,BLANKS          ANY ELEMENT GIVEN?
         BE    TOSS                    NO
         CLC   ELEMENT,=CL8'ELEMENT'   ELEMENT ITSELF?
         BE    TOSS                    YES
         CLC   ENVIRON,=CL8'ENVIRON'   ENVIRON ITSELF?
         BE    TOSS                    YES
         CLC   B01,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B02,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B03,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B04,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B05,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B06,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B07,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B08,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B09,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B10,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B11,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B12,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B13,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         CLC   B14,BLANKS              BLANK FIELD IS BLANK?
         BNE   TOSS                    NO
         BR    R9                      RETURN TO CALLER
TOSS     MVI   GOODREC,0               INDICATE BAD RECORD
         BR    R9                      GO READ ANOTHER RECORD
***********************************************************************
*             PROCESS THE INPUT RECORD, BUILD OUTREC                  *
***********************************************************************
PROCESS  CLC   TYPE,BLANKS             ANY TYPE SPECIFIED?
         BE    TSTSUBS                 NO
         MVC   OUTYPE,TYPE             YES, MOVE IT TO OUTPUT
TSTSUBS  CLC   SUBSYS,BLANKS           ANY SUBSYSTEM SPECIFIED?
         BE    TSTSYS                  NO
         MVC   OUTSUBSY,SUBSYS         YES, MOVE IT TO OUTPUT
TSTSYS   CLC   SUBSYS,BLANKS           ANY SYSTEM SPECIFIED?
         BE    TSTENV                  NO
         MVC   OUTSYSTM,SYSTEM         YES, MOVE IT TO OUTPUT
TSTENV   CLC   ENVIRON,BLANKS          ANY ENVIRONMENT SPECIFIED?
         BE    MOVEREST                NO
         MVC   OUTENV,ENVIRON          YES, MOVE IT TO OUTPUT
MOVEREST MVC   OUTELEM,ELEMENT         MOVE ELEMENT TO OUTPUT
         MVI   GOODREC,1               INDICATE GOOD RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         AP    INRECS,=P'1'            ADD 1 TO INPUT COUNT
         BR    R9                      EXIT
***********************************************************************
*             WRITE GOOD OUTPUT RECORD                                *
***********************************************************************
PUTGOOD  PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         AP    GDRECS,=P'1'            ADD 1 TO INPUT COUNT
         BR    R9                      EXIT
***********************************************************************
*             END OF JOB ROUTINE                                      *
***********************************************************************
EOJ      MVC   PRTCNT,=X'402020206B2020206B202020' EDIT WORD
         ED    PRTCNT,GDRECS           COUNTER TO PRINT LINE
         PUT   PRINT1,TOTALINE         WRITE TOTAL LINE
         MVC   PRTID,=C'INPUT'         COUNTER ID
         MVC   PRTCNT,=X'402020206B2020206B202020' EDIT WORD
         ED    PRTCNT,INRECS           COUNTER TO PRINT LINE
         PUT   PRINT1,TOTALINE         WRITE TOTAL LINE
         CLOSE (OUTPUT1,,INPUT1,,PRINT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 RETURN CODE = 0
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
INRECS   DC    PL5'0'                  INPUT RECORD COUNT
GDRECS   DC    PL5'0'                  GOOD RECORDS WRITTEN
BLANKS   DC    CL133' '                 BLANKS
GOODREC  DC    X'00'                    1 = GOOD RECORD
*
INREC    DS    0CL133                   INPUT RECORD
CC       DS    CL1                        CARRIAGE RETURN BYTE
ELEMENT  DS    CL8                        ELEMENT (PROGRAM) NAME
B01      DS    CL4                        BLANK FIELD
ENVIRON  DS    CL8                        ENVIRONMENT NAME
B02      DS    CL2                        BLANK FIELD
SYSTEM   DS    CL8                        SYSTEM NAME
B03      DS    CL2                        BLANK FIELD
SUBSYS   DS    CL8                        SUBSYSTEM NAME
B04      DS    CL1                        BLANK FIELD
TYPE     DS    CL8                        TYPE
B05      DS    CL1                        BLANK FIELD
STAGE    DS    CL6                        STAGE: ID AND SEQ
B06      DS    CL1                        BLANK FIELD
VVDOTLL  DS    CL5                        VERSION AND LEVEL
B07      DS    CL3                        BLANK FIELD
PROCGRUP DS    CL8                        PROCESSOR GROUP
B08      DS    CL2                        BLANK FIELD
BASEDATE DS    CL7                        BASE DATE
B09      DS    CL1                        BLANK FIELD
BASEUSER DS    CL8                        BASE USER ID
B10      DS    CL2                        BLANK FIELD
CURRDATE DS    CL7                        CURRENT DATE DDMMMYY
B11      DS    CL1                        BLANK FIELD
CURRUSER DS    CL8                        CURRENT USER ID
B12      DS    CL1                        BLANK FIELD
LASTGNDT DS    CL7                        LAST GEN DATE
B13      DS    CL1                        BLANK FIELD
LSTGNUID DS    CL8                        LAST GEN USERID
B14      DS    CL2                        BLANK FIELD
DELTATYP DS    CL1                        DELTA TYPE
B15      DS    CL3                        BLANK FIELD
*
OUTREC   DS    0CL80                   OUTPUT RECORD
OUTELEM  DS    CL8                     ELEMENT NAME
         DC    CL1' '
OUTYPE   DC    CL8' '                  ELEMENT TYPE
         DC    CL1' '
OUTENV   DC    CL8' '                  ENVIRONMENT NAME
         DC    CL1' '
OUTSYSTM DC    CL8' '                  SYSTEM NAME
         DC    CL1' '
OUTSUBSY DC    CL8' '                  SUBSYSTEM NAME
         DC    CL36' '
*
TOTALINE DS    0CL80
PRTID    DC    CL5' '
         DC    C' RECORDS'
PRTCNT   DC    CL12' '
         DC    CL80' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=133
         LTORG
ENDVREFM CSECT
         END

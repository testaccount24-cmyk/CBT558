         TITLE '*** FREEPOOL: FREEPOOL FOR AN EXTERNAL DCB ***'
***********************************************************************
* ISSUES A FREEPOOL FOR A DCB WHICH IS LOCATED ELSEWHERE. AN INITIAL  *
* CALL IS USED TO LOCATE THE DCB, BY PASSING THE DDNAME AS A          *
* PARAMETER. THE FILE MUST BE OPEN FOR THE INITIAL LOCATION CALL TO   *
* WORK. IF ANY ERROR OCCURS, AN EXPLANATORY MESSAGE IS WRITTEN, AND   *
* THE PROGRAM TERMINATES WITH A USER ABEND.                           *
*                                                                     *
* AFTER THE INITIAL CALL, THIS ROUTINE SHOULD BE CALLED AFTER EACH    *
* TIME THE FILE IS CLOSED IN ORDER TO FREE THE BUFFER POOL OBTAINED   *
* WHEN THE FILE WAS LAST OPENED.                                      *
*                                                                     *
* CALLING EXAMPLE (FROM A COBOL PROGRAM):                             *
*  1. AFTER THE FILE IS OPENED FOR THE FIRST TIME, AND BEFORE IT IS   *
*     CLOSED. NOTE THAT THE DDNAME PARAMETER MUST BE AN 8-CHARACTER   *
*     STRING, LEFT JUSTIFIED, AND WITH TRAILING BLANKS, IF NEEDED,    *
*     TO FILL THE FIELD. EXAMPLE OF INITIAL LOCATE CALL:              *
*                                                                     *
*              05  LOCATE-CODE    PIC X VALUE 'L'.                    *
*              05  FREEPOOL-CODE  PIC X VALUE 'F'.                    *
*              05  DD-NAME        PIC X(8) VALUE 'PENDIN  '.          *
*                                                                     *
*                                                                     *
*              CALL FREEPOOL USING LOCATE-CODE, DD-NAME.              *
*                                                                     *
*  2. AFTER EACH TIME THE FILE IS CLOSED:                             *
*                                                                     *
*              CALL FREEPOOL USING FREEPOOL-CODE.                     *
*                                                                     *
* AUTHOR R THORNTON SEPTEMBER 1995                                    *
***********************************************************************
*
FREEPOOL CSECT
***********************************************************************
*        PROCESS PARM FIELD                                           *
***********************************************************************
         USING FREEPOOL,R15
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  USE R3 AS BASE REG
         DROP  R15
         USING FREEPOOL,R3
         LA    R15,SAVEAREA            POINT TO MY SAVE AREA
         ST    R15,8(R13)              SET FORWARD SAVE PTR (LSA)
         ST    R13,SAVEAREA+4          SET BACKWARD SAVE PTR (HSA)
         LR    R13,R15                 R13 NOW POINTS TO MY SAVE
         B     PARMS                   BRANCH AROUND SAVE AREA
SAVEAREA DS    9D                      REGISTER SAVE AREA
***********************************************************************
*        PROCESS PARM FIELD                                           *
***********************************************************************
PARMS    L     R2,0(R1)                POINT TO 1ST PARM (L OR F)
         CLI   0(R2),C'L'              THIS THE INITIAL LOCATE CALL?
         BE    LOCATE                  YES
         CLI   0(R2),C'F'              THIS A FREEPOOL CALL?
         BE    FREEIT                  YES
BADPARM  WTO   'FREEPOOL: MISSING OR INVALID PARAMETER, L OR F',       X
               ROUTCDE=11
         ABEND 111,DUMP                ABEND WITH U111
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
LOCATE   L     R12,4(R1)               POINT TO THE DDNAME (2ND PARM)
         MVC   DDNAME,0(R12)           SAVE THE DDNAME
         L     R1,16                   GET CVT ADDRESS
         L     R1,0(R1)                GET CVTTCBP
         L     R1,0(R1)                GET TCB ADDRESS
         ST    R1,TCBADDR              SAVE TCB ADDRESS
         L     R11,12(R1)              GET TIOT ADDRESS
         LA    R1,24(R11)              GET ADDRESS OF 1ST DD ENTRY
         SR    R2,R2                   CLEAR WORK REG
CKLSTDD  CLI   0(R1),0                 LAST TIOT DD ENTRY
         BNE   CKFOUND                 NO
         WTO   'FREEPOOL: DDNAME PASSED NOT IN TIOT',ROUTCDE=11
         ABEND 222,DUMP                ABEND WITH U222
CKFOUND  CLC   4(8,R1),DDNAME          FOUND THE TIOT DD ENTRY?
         BE    USEDD                   YES, USE IT
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         B     CKLSTDD                 GO CK NEXT DD
USEDD    SR    R1,R11                  COMPUTE TIOT OFFSET TO ENTRY
         STH   R1,TIOTOFST             SAVE TIOT DD ENTRY OFFSET
         L     R1,TCBADDR              GET THE TCB ADDRESS
         L     R1,8(R1)                POINT TO THE FIRST DEB
CKLAST   LTR   R1,R1                   ANY MORE DEB'S?
         BNZ   PNTDCB                  YES
         WTO   'FREEPOOL: THE FILE IS NOT OPEN AT LOCATE CALL',        X
               ROUTCDE=11
         ABEND 333,DUMP                TERMINATE WITH U333 ABEND
PNTDCB   L     R2,24(R1)               POINT TO THE DCB
         CLC   40(2,R2),TIOTOFST       DCB TIOT OFFSET MATCHES?
         BE    GOTDCB                  YES
         L     R1,4(R1)                NO, POINT TO THE NEXT DEB
         B     PNTDCB                  AND GO CHECK AGAIN
GOTDCB   ST    R2,DCBADDR              SAVE DCB ADDRESS
         B     RETURN                  AND RETURN TO CALLER
***********************************************************************
*             ISSUE A FREEPOOL FOR THE FILE                           *
***********************************************************************
FREEIT   L     R12,DCBADDR             POINT TO THE DCB
         LTR   R12,R12                 ANY ADDRESS STORED?
         BNZ   ISSUFP                  YES
         WTO   'FREEPOOL: FREE CALL ISSUED BEFORE LOCATE',ROUTCDE=11
         ABEND 444,DUMP                TERMINATE WITH U444 ABEND
ISSUFP   FREEPOOL 0(R12)               FREE THE BUFFER POOL
***********************************************************************
*             RETURN CONTROL TO CALLER                                *
***********************************************************************
RETURN   L     R13,SAVEAREA+4          POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         SR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN TO CALLER
***********************************************************************
*             WORKING STORAGE SECTION                                 *
***********************************************************************
DDNAME   DC    CL8' '                  DDNAME
TCBADDR  DC    A(0)                    ADDRESS OF TCB
DCBADDR  DC    A(0)                    ADDRESS OF DCB
TIOTOFST DC    H'0'                    OFFSET TO TIOT DD ENTRY
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '    ***** VTOCLIST - STRIP DISK PACK VTOCS *****'
VTOCLIST CSECT
         REQU
         USING VTOCLIST,R3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* THIS PROGRAM SEARCHES IT'S TIOT FOR DD CARDS WHOSE NAMES BEGIN WITH
* VTOC. WHEN FOUND, THE DDNAME IS MOVED TO THE VTOC DCB DDNAME FIELD,
* AND THE JFCB IS READ. THE DSNAME FIELD OF THE JFCB IS SET TO ALL
* HEX 04'S TO PERMIT OPENING THE VTOC, AS THE FORMAT 4 DSCB DEFINES THE
* VTOC EXTENTS. THE VTOC IS THEN READ AS FOLLOWS: THE FORMAT 4 DSCB
* IS READ TO OBTAIN THE DEVICE CHARACTERISTICS, THEN THE DATA SET
* DSCB'S ARE READ AND USED TO LOCATE THEIR ASSOCIATED FORMAT 2 AND 3
* DSCB'S.  AN OUTPUT DATASET IS CREATED CONTAINING ONE RECORD PER
* DATASET ON THE VOLUME.  EACH RECORD CONTAINS THE FOLLOWING
* INFORMATION:  DATASET NAME, VOLUME SERIAL NUMBER, DATE CREATED, DATE
* LAST REFERENCED, TRACKS ALLOCATED, TRACKS USED, RECFM, DSORG, LRECL,
* BLOCKSIZE, AND NUMBER OF EXTENTS.
* MAY, 1983
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS
         LR    R3,R15                  INITIALIZE BASE REGISTER
         LA    R15,SAVE                GET SAVE AREA ADDRESS
         ST    R15,8(R13)              STORE FORWARD POINTER
         ST    R13,SAVE+4              STORE BACK POINTER
         LR    R13,R15                 INITIALIZE SAVE POINTER
         L     R1,0(R1)                GET PARM FIELD ADDRESS
         CH    R1,ZEROS                ANY PARM FIELD CODED
         BE    OPENS                   NO
         MVC   PARM,2(R1)              SAVE PARM FIELD
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT) OPEN OUTPUT FILES
         L     R1,16                   GET CVT ADDRESS
         L     R1,0(R1)                GET CVTTCBP
         L     R1,0(R1)                GET TCB ADDRESS
         L     R1,12(R1)               GET TIOT ADDRESS
         LA    R1,24(R1)               GET ADDRESS OF 1ST DD ENTRY
         ST    R1,TIOTDD               SAVE TIOT DD ENTRY ADDRESS
*
*
*
NEXVOL   EQU   *                       *** INITIALIZE FOR NEXT VOLUME *
         L     R1,TIOTDD               GET TIOT DD ENTRY ADDRESS
         SR    R2,R2                   CLEAR WORK REG
CKLSTDD  CLI   0(R1),0                 LAST TIOT DD ENTRY
         BE    EOJ                     YES
         CLC   4(4,R1),=C'VTOC'        DDNAME BEGINS WITH VTOC
         BE    USEDD                   YES, USE IT
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         B     CKLSTDD                 GO CK NEXT DD
USEDD    MVC   VTOC+40(8),4(R1)        MOVE DNAME IN TIOT TO DCB
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         ST    R1,TIOTDD               SAVE IT'S ADDRESS
         XC    JFCBDSNM,JFCBDSNM       CLEAR JFCB DSNAME FOR CHECK
         RDJFCB VTOC                   READ THE JFCB
         CLC   JFCBDSNM(8),ZEROS       WAS JFCB READ
         BNE   CKSERNO                 YES, DSNAME NOT ZEROS
         MVC   PRT(16),=C'NO JFCB READ FOR'
MOVDD    MVC   PRT+21(8),VTOC+40       DDNAME TO MESSAGE
         BAL   R9,PRINT                GO PRINT THE MESSAGE
         B     NEXVOL                  GO START NEXT VOLUME
CKSERNO  CLC   JFCBVOLS(8),=CL8' '     SERIAL NBR IN DD CARD OMITTED
         BNE   DS4NAME                 NO, O.K.
         MVC   PRT(20),=C'NO VOL SER IN DD FOR'
         B     MOVDD                   GO MOVE DDNAME
DS4NAME  MVI   JFCBDSNM,4              DATA SET NAME FOR FMT 4 DSCB
         MVC   JFCBDSNM+1(43),JFCBDSNM DATA SET NAME FOR FORMAT 4 DSC
         MVI   JFCBTSDM,X'08'          SUPPRESS JFCB REWRITE
         OPEN  VTOC,TYPE=J             OPEN USING MODIFIED JFCB
         L     R1,VTOC+44              GET DEB ADDRESS
         MVC   NEXCCHH,38(R1)          MOVE BEGIN CCHH TO NEXT RECADR
         MVI   NEXREC,1                BEGIN RECORD IS 1
         MVC   RNAME,JFCBVOLS          RNAME FOR ENQ IS VOLSER
         ENQ   (QNAME,RNAME,S,6,SYSTEM) ENQ ON VTOC
SEQREAD  OI    READCMD+4,X'40'         SET CHAIN FLAG TO READ COUNT
         MVC   VTSRCH,NEXRECA          MOVE IN NEXT REC ADDR
         BAL   R9,EXCPRTN              GO READ THE RECORD
         CLI   DS1FMTID,C'4'           IS IT FORMAT 4
         BE    FORMAT4                 YES
         CLI   DS1FMTID,C'1'           IS IT FORMAT 1
         BNE   SEQREAD                 NO, GO READ NEXT
         MVC   DSNAME,DS1DSNAM         MOVE DSNAME TO SORT FIELD
*
GETCDATE SR    R5,R5                   CREATE DATE
         IC    R5,DS1CREDT             CONVERT DATE TO DECIMAL
         CVD   R5,DBLWORD              AND MOVE TO OUTPUT RECORD
         MVO   CREDATE(2),DBLWORD+6(2) IN THE FORMAT 'YYDDDF'
         SR    R5,R5
         ICM   R5,3,DS1CREDT+1
         CVD   R5,DBLWORD
         MVC   CREDATE+1(2),DBLWORD+6
         OI    CREDATE+2,X'0F'
GETRDATE SR    R5,R5                    LAST REFERENCED DATE
         IC    R5,DS1REFD               CONVERT DATE TO DECIMAL
         CVD   R5,DBLWORD               AND MOVE TO OUTPUT RECORD
         MVO   LREFDATE(2),DBLWORD+6(2) IN THE FORMAT 'YYDDDF'
         SR    R5,R5
         ICM   R5,3,DS1REFD+1
         CVD   R5,DBLWORD
         MVC   LREFDATE+1(2),DBLWORD+6
         OI    LREFDATE+2,X'0F'
*
GETDSORG TM    DS1DSORG,X'40'     IS IT SEQUENTIAL?
         BNO   CHKPO              NO, CHECK FOR PARTITIONED
         MVC   DSORG,=C'PS'       YES, MOVE 'PS' TO PRINT LINE
         B     GETRECFM           GO GET RECFM
CHKPO    TM    DS1DSORG,X'02'     IS IT PARTITIONED?
         BNO   CHKDA              NO, CHECK FOR DIRECT ACCESS
         MVC   DSORG,=C'PO'       YES, MOVE 'PO' TO PRINT LINE
         B     GETRECFM           GO GET RECFM
CHKDA    TM    DS1DSORG,X'20'     IS IT DIRECT ACCESS?
         BNO   CHKIS              NO, CHECK FOR ISAM
         MVC   DSORG,=C'DA'       YES, MOVE 'DA' TO PRINT LINE
         B     GETRECFM           GO GET RECFM
CHKIS    TM    DS1DSORG,X'80'     IS IT ISAM?
         BNO   CHKVS              NO, CHECK FOR VSAM
         MVC   DSORG,=C'IS'       YES, MOVE 'IS' TO PRINT LINE
         B     GETRECFM           GO GET RECFM
CHKVS    TM    DS1DSORG+1,X'08'   IS IT VSAM?
         BNO   QMARKS             NO, GO MOVE '??'
         MVC   DSORG,=C'VS'       YES, MOVE 'VS' TO PRINT LINE
         B     GETRECFM           GO GET RECFM
QMARKS   MVC   DSORG,=C'??'       MOVE '??' TO PRINT LINE
*
GETRECFM MVC   RECFM,=CL3' '      CLEAR RECFM FIELD
         TM    DS1RECFM,X'C0'     IS IT UNDEFINED?
         BNO   CHKFIX             NO, CHECK FOR FIXED
         MVI   RECFM,C'U'         YES, MOVE 'U' TO PRINT LINE
         B     CHKBLK             GO CHECK FOR BLOCKED
CHKFIX   TM    DS1RECFM,X'80'     IS IT FIXED?
         BNO   CHKVAR             NO, CHECK FOR VARIABLE
         MVI   RECFM,C'F'         YES, MOVE 'F' TO PRINT LINE
         B     CHKBLK             GO CHECK FOR BLOCKED
CHKVAR   TM    DS1RECFM,X'40'     IS IT VARIABLE?
         BNO   QMARK              NO, GO MOVE '?'
         MVI   RECFM,C'V'         YES, MOVE 'V' TO PRINT LINE
         B     CHKBLK             GO CHECK FOR BLOCKED
QMARK    MVI   RECFM,C'?'         MOVE '?' TO PRINT LINE
CHKBLK   LA    R6,RECFM+1         GET @ OF NEXT RECFM PRINT POSITION
         TM    DS1RECFM,X'10'     IS IT BLOCKED?
         BNO   CHKSPAN            NO, CHECK FOR SPANNED
         MVI   0(R6),C'B'         YES, MOVE 'B' TO PRINT LINE
         LA    R6,1(R6)           BUMP TO NEXT RECFM PRINT POSITION
CHKSPAN  TM    DS1RECFM,X'08'     IS IT SPANNED?
         BNO   CHKASA             NO, CHECK FOR ASA
         MVI   0(R6),C'S'         YES, MOVE 'S' TO PRINT LINE
         B     GETLRECL           GO GET LRECL
CHKASA   TM    DS1RECFM,X'04'     IS IT ASA?
         BNO   GETLRECL           NO, GO GET LRECL
         MVI   0(R6),C'A'         YES, MOVE 'A' TO PRINT LINE
*
GETLRECL MVC   LRECL,DS1LRECL          MOVE LRECL TO OUTPUT
         MVC   BLKSIZE,DS1BLKL         MOVE BLKSIZE TO OUTPUT
         MVC   NUMEXTS,DS1NOEPV        NUMBER OF EXTENTS TO OUTPUT
         MVC   SAVETTR,DS1LSTAR        SAVE LAST TRACK USED TTR
F1EXTS   MVI   EXTABLE,X'00'           CLEAR EXTENT TABLE
         MVC   EXTABLE+1(159),EXTABLE
         LA    R7,EXTABLE              GET @ OF EXTENT TABLE
         LA    R8,DS1EXT1              GET @ OF 1ST EXTENT IN FORMAT 1
         LA    R5,3                    SET BCT REG TO 3
         BAL   R9,MOVEXTS              GO MOVE F1 EXTENTS TO TABLE
*
CHKPTRDS CLC   DS1PTRDS,ZEROS          POINTER TO NEXT DSCB ZERO
         BE    GETSPACE                YES, NO FORMAT 2 OR 3
         MVC   VTSRCH,DS1PTRDS         MOVE POINTER TO IOB SEARCH FIELD
         MVI   READCMD+4,0             TURN OFF CHAIN FLAG
         BAL   R9,EXCPRTN              GO READ THE RECORD
         CLI   DS1FMTID,C'2'           IS IT A FORMAT 2
         BE    CHKPTRDS                YES
CKFMT3   CLI   DS1FMTID,C'3'           IS IT FORMAT 3
         BNE   ERROR                   NO, GO TO ERROR ROUTINE
         MVC   DS3FMTID(90),DS3ADEXT   MAKE EXTENTS CONTIGUOUS
         LA    R8,DS3EXTNT             LOAD @ OF 1ST EXTENT FIELD
         LA    R5,13                   SET UP BCT REGISTER FOR MAX
         BAL   R9,MOVEXTS              MOVE EXTENTS TO TABLE
GETSPACE BAL   R9,CALCSPCE             CALCULATE TRKS ALLOC AND USED
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         B     SEQREAD                 GO READ NEXT DSCB
*
FORMAT4  EQU   *
         MVC   TRKCYL,DS4DEVSZ+2       SAVE TRACKS PER CYLINDER
         MVC   VOLSER,JFCBVOLS         MOVE VOLSER TO OUTPUT RECORD
         B     SEQREAD                 GO READ NEXT DSCB
*
ERROR    MVC   PRT(27),=C'INCORRECT CHAIN POINTER FOR'
         MVC   PRT+28(44),DSNAME       MOVE DSNAME TO MESSAGE
         BAL   R9,PRINT                PRINT MESSAGE
         B     SEQREAD                 GO READ NEXT DSCB
*
*
MOVEXTS  ST    R9,SAVER9               SAVE RETURN ADDRESS
EXTUSED  CLI   0(R8),X'00'             IF EXTENT NOT USED
         BE    RETURN                  BRANCH TO RETURN
         MVC   0(10,R7),0(R8)          ELSE, MOVE EXTENT INFO TO TABLE
         LA    R7,10(R7)               BUMP TO NEXT TABLE ENTRY
         LA    R8,10(R8)               BUMP TO NEXT EXTENT
         BCT   R5,EXTUSED              REPEAT LOOP
RETURN   L     R9,SAVER9               LOAD RETURN ADDRESS
         BR    R9                      RETURN
*
*
CALCSPCE EQU   *
         ST    R9,SAVER9          SAVE RETURN ADDRESS
         LA    R10,EXTABLE        GET ADDRESS OF EXTENT TABLE
         USING EXTENT,R10
         LA    R11,16             SET BCT REGISTER TO 16
         SR    R12,R12            CLEAR REG 12
SPCELOOP CLI   EXTYP,X'00'        IF EXTENT NOT USED
         BE    ENDEXT             BRANCH TO ENDEXT
         SR    R7,R7              ELSE, CLEAR REG 7
         ICM   R7,3,EXTLOLIM      GET LOW CYLINDER
         SR    R8,R8              CLEAR REG 8
         ICM   R8,3,EXTUPLIM      GET HIGH CYLINDER
         SR    R8,R7              SUBTRACT FOR NUM CYL IN EXTENT
         MH    R8,TRKCYL          MULTIPLY BY TRKS/CYL
         AR    R12,R8             ADD TO TOTAL TRACKS
         SR    R7,R7              CLEAR REG 7
         ICM   R7,3,EXTLOLIM+2    GET LOW TRACK
         SR    R8,R8              CLEAR REG 8
         ICM   R8,3,EXTUPLIM+2    GET HIGH TRACK
         LA    R8,1(R8)           ADD '1' TO HIGH TRACK
         SR    R8,R7              SUBTRACT FOR NUMBER OF TRACKS
         AR    R12,R8             ADD TO TOTAL TRACKS
         LA    R10,10(R10)        BUMP TO NEXT TABLE ENTRY
         BCT   R11,SPCELOOP       REPEAT LOOP (UP TO 16 TIMES)
ENDEXT   EQU   *
         DROP  R10
         SR    R10,R10            CLEAR REG 10
         ICM   R10,3,SAVETTR      GET LAST TRACK USED
         CLI   SAVETTR+2,X'00'    IF LAST RECORD IS ZERO
         BE    NOADD              DON'T ADD '1' TO TRACKS USED
         LA    R10,1(R10)         ADD '1' TO NUMBER OF TRACKS USED
NOADD    EQU   *
         ST    R10,TRKUSED        SAVE TRKS USED TO OUTPUT RECORD
         ST    R12,TRKALLOC       SAVE TRKS ALLOC TO OUTPUT RECORD
         L     R9,SAVER9          LOAD RETURN ADDRESS
         BR    R9                 RETURN
*
*
EXCPRTN  EXCP  VTOCIOB                 CALL O/S FOR READ
         WAIT  ECB=VTECB               WAIT FOR READ
         CLI   VTECB,X'7F'             ANY ERROR
         BCR   8,R9                    NO, RETURN
         CLI   VTECB,X'42'             EXTENT VIOLATION
         BNE   IOERR                   NO, GO TO ERROR ROUTINE
         DEQ   (QNAME,RNAME,6,SYSTEM)  RELEASE VTOC
         CLOSE VTOC                    END OF VTOC, CLOSE
         B     NEXVOL                  GO OPEN NEXT VOLUME
IOERR    ABEND 104,DUMP
*
*
*
PRINT    EQU   *
         PUT   PRINT1,PRTLINE
         MVC   PRT,PRTCC               CLEAR PRINT LINE
         L     R1,ERRCOD               GET ERROR INDICATOR
         LA    R1,4(R1)                INCREMENT ERROR
         ST    R1,ERRCOD               STORE UPDATED ERROR INDIC
         CLC   PARM,=C'DUMP'           USER WANTS DUMP
         BCR   7,R9                    NO, RETURN TO CALLER
         ABEND 999,DUMP
*
*
*
EOJ      EQU   *                       *** END OF JOB ***
         CLOSE (OUTPUT1,,PRINT1)       CLOSE OUTPUT FILES
         L     R13,SAVE+4              GET CALLER'S SAVE AREA ADDRESS
         MVC   16(4,R13),ERRCOD        ERROR INDICATOR TO R15 IN SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         BR    R14                     RETURN TO CALLER
*
*
*
*                       *** DATA, WORK, FILE, AND STORAGE AREAS ***
*
         LTORG
*
SAVE     DS    18F                     REGISTER SAVE AREA
SAVER9   DS    F                       BAL REGISTER SAVE AREA
*
CMDCHN   DS    0D                      COMMAND CHAIN TO READ VTOC
         CCW   X'31',VTSRCH,X'40',5    SEARCH ID EQUAL
         CCW   8,*-8,0,0               NOT FOUND, REPEAT SEARCH
READCMD  CCW   X'0E',DSCB,X'40',140    READ KEY AND DATA INTO DSCB
         CCW   X'92',NEXCNT,0,8        READ COUNT OF NEXT RECORD, M-T
NEXCNT   DS    0D                      COUNT RECORD OF NEXT RECORD
NEXRECA  DS    0XL5                    CCHHR ADDRESS OF NEXT RECORD
NEXCCHH  DS    XL4                     CCHH ADDRESS OF NEXT RECORD
NEXREC   DS    XL1                     RECORD NBR ON TRACK
         DS    XL3                     KEY LENG AND DATA LENG
ERRCOD   DC    F'0'                    ERROR INDICATOR
QNAME    DC    CL8'SYSVTOC'            QNAME TO ENQ ON VTOC
RNAME    DS    CL6                     RNAME = VOLUME SERIAL NBR
*
DBLWORD  DS    D                       CVD WORKAREA
TRKCYL   DS    H                       TRACKS PER CYLINDER
*
ZEROS    DC    2F'0'
PARM     DC    C'    '                 PARM FIELD ON JOB CARD
SAVETTR  DS    XL3                     SAVE AREA FOR LAST TRACK USED
*
EXTABLE  DS    16CL10                  EXTENT TABLE
*
         DS    0F
OUTREC   DS    0CL74                   OUTPUT RECORD
DSNAME   DS    CL44                    DATASET NAME
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
CREDATE  DS    PL3                     DATE CREATED
LREFDATE DS    PL3                     DATE LAST REFERENCED
TRKALLOC DS    F                       TRACKS ALLOCATED
TRKUSED  DS    F                       TRACKS USED
RECFM    DS    CL3                     RECORD FORMAT
DSORG    DS    CL2                     DATASET ORGANIZATION
LRECL    DS    XL2                     LOGICAL RECORD LENGTH
BLKSIZE  DS    XL2                     BLOCKSIZE
NUMEXTS  DS    XL1                     NUMBER OF EXTENTS
*
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
TIOTDD   DS    F                       ADDRESS OF NEXT DD ENTRY IN TIOT
*
PRTLINE  DS    0CL133                  PRINT LINE OUTPUT
PRTCC    DC    C' '                    CARRIAGE CONTROL CHARACTER
PRT      DC    CL132' '                PRINT LINE
*
         JFCBDEF DSECT=NO
*
VTOC     DCB   DDNAME=VTOC,MACRF=E,EXLST=JFCBADDR
*
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,RECFM=FB
*
PRINT1   DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=133,BLKSIZE=133,       X
               DDNAME=PRINT1
*
DSCB     DS    0CL140
FMT1     IECSDSL1 (1)
         ORG   DSCB
FMT3     IECSDSL1 (3)
         ORG   DSCB+44
FMT4     IECSDSL1 (4)
*
EXTENT   DSECT
EXTYP    DS    XL1                EXTENT TYPE, X'00'=UNUSED
EXTSEQ   DS    XL1                SEQUENCE
EXTLOLIM DS    XL4                LOWER LIMIT CCHH
EXTUPLIM DS    XL4                UPPER LIMIT CCHH
*
         END

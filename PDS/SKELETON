         TITLE '*** SKELETON:                          ***'
SKELETON CSECT
*********************************************************************
*                                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING SKELETON,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'SKELETON'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        PROCESS PARM FIELD                                         *
*********************************************************************
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),H0              ANY PARM DATA?
         BE    OPENS                   NO, CONTINUE
         CLC   0(2,R2),MAXPARM         VALID PARM LENGTH?
         BNH   PARMOK                  YES
BADPARM  WTO   'SKELETON: INVALID PARM ON EXEC STATEMENT',ROUTCDE=11
         ABEND 111,DUMP
PARMOK   MVC   PARMDATA,2(R2)          SAVE PARM DATA
*********************************************************************
*        OPEN FILES, PRINT HEADING                                  *
*********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT,INPUT1) OPEN FILES
         BAS   R9,HEADING              PRINT FIRST HEADING
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE BAS   R9,GETIN                READ INPUT RECORD
         MVC   OUTREC,INREC            COPY RECORD TO OUTPUT
         BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         B     MAINLINE                LOOP THROUGH INPUT FILE
*********************************************************************
*             READ INPUT FILE SUBROUTINE                            *
*********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         AP    INPCOUNT,P1             INCREMENT COUNTER
         BR    R9                      RETURN TO CALLER
*********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                        *
*********************************************************************
PUTOUT   PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         AP    OUTCOUNT,P1             INCREMENT COUNTER
         BR    R9                      RETURN TO CALLER
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         AP    LINECT,P1               INCREMENT LINE COUNTER
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         CP    LINECT,ENDPAGE          PAST END OF PAGE?
         BNHR  R9                      NO, RETURN
*********************************************************************
*             HEADING SUBROUTINE                                    *
*********************************************************************
HEADING  ST    R9,H9                   SAVE RETURN ADDRESS
         ZAP   LINECT,P0               RESET LINE COUNTER
         AP    PAGECT,P1               INCREMENT PAGE COUNTER
         MVC   PAGENBR,PAGEDIT         PAGE NUMBER
         ED    PAGENBR,PAGECT              TO HEADER LINE
         MVC   PRTLINE,HEADER1         PRINT HEADER
         BAS   R9,PRINT                    LINE 1
         MVC   PRTLINE,HEADER2         PRINT HEADER
         BAS   R9,PRINT                    LINE 2
         MVI   PCC,C'0'                DOUBLE SPACE NEXT DATA LINE
         L     R9,H9                   GET RETURN ADDRESS
         BR    R9                      RETURN
*********************************************************************
*             WTO FROM A MODIFIABLE AREA                            *
*********************************************************************
WTOEXP   MVC   MSGVRBL,=C'VARIABLE DATA' MOVE VARIABLE DATA TO MESSAGE
         LA    R1,MSGWTO               POINT TO MESSAGE
         SVC   35                      ISSUE WTO
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      MVC   PRT(12),TOTLEDIT        EDIT MASK
         ED    PRT(12),INPCOUNT        EDIT INPUT COUNTER
         MVC   PRT+13(12),=CL12'RECORDS READ' IDENTIFY COUNT
         BAS   R9,PRINT                PRINT COUNT
         MVC   PRT(12),TOTLEDIT        EDIT MASK
         ED    PRT(12),OUTCOUNT        EDIT OUTPUT COUNTER
         MVC   PRT+13(15),=CL15'RECORDS WRITTEN' IDENTIFY COUNT
         BAS   R9,PRINT                PRINT COUNT
         CLOSE (OUTPUT1,,PRINT1,,INPUT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
*
*
H9       DS    A                       HEADING RETURN ADDRESS
RETCODE  DS    F                       RETURN CODE
H0       DC    H'0'                    HEX ZERO
MAXPARM  DC    AL2(L'PARMDATA)         MAX PARM DATA LENGTH
P0       DC    P'0'                    PACKED 0
P1       DC    P'1'                    PACKED 1
LINECT   DS    PL2                     LINE COUNTER
PAGECT   DC    PL3'0'                  PAGE COUNTER
ENDPAGE  DC    P'57'                   MAX LINES PER PAGE
INPCOUNT DC    PL5'0'                  INPUT RECORD COUNTER
OUTCOUNT DC    PL5'0'                  OUTPUT RECORD COUNTER
PAGEDIT  DC    XL7'4020206B202020'     EDIT MASK FOR PAGE NBR
TOTLEDIT DC    XL12'402020206B2020206B202120' EDIT MASK FOR TOTAL
PARMDATA DS    CL8                     PARM DATA FROM EXEC
BLANKS   DC    CL133' '                BLANKS
*
HEADER1  DS    0CL133                  HEADER LINE 1
         DC    CL1'1'                  CARRIAGE CONTROL
         DC    CL47' '
         DC    CL37'CENTERED LITERAL OF SOME KIND .......'
         DC    CL37' '
         DC    CL4'PAGE'
PAGENBR  DS    CL7                     PAGE NUMBER
*
HEADER2  DS    0CL133                  HEADER LINE 2
         DC    CL1'0'                  CARRIAGE CONTROL
         DC    CL132' '
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DS    CL132
*
INREC    DS    0CL80                    INPUT RECORD
         DS    CL80
*
OUTREC   DS    0CL80                    OUTPUT RECORD
         DS    CL80
*
MSGWTO   DS    0F                      WTO LENGTH ERROR MESSAGE
         DC    AL2(MSGEND-MSGWTO)      AREA LENGTH
         DC    X'8000'                 MCS FLAGS
MSGLEAD  DC    CL40' '                 LEADING MESSAGE TEXT
MSGVRBL  DS    CL13                    VARIABLE MESSAGE DATA
MSGTRAIL DC    CL125' '                TRAILING MESSAGE TEXT
MSGEND   EQU   * END OF TEXT
         DC    X'0200'                 DESC=7
         DC    X'0020'                 ROUTCDE=11
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=80
         LTORG
SKELETON CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

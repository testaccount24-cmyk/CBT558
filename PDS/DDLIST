         PRINT NOGEN
DDLIST   SVLNK R3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   THIS PROGRAM PRINTS ALL ALLOCATED DATASETS FOR A TSO USER. FOR EACH
*   ALLOCATED DATASET, THE FILE NAME, DATASET NAME, DISPOSITION, AND
*   VOLUME SERIAL NUMBER IS PRINTED. OUTPUT IS WRITTEN TO SYSRINT. TO
*   LOCATE THE INFORMATION, THE TCB IS LOCATED VIA THE TCBWORDS IN THE
*   CVT. THE JSCB IS FOUND USING THE TCB. A POINTER TO THE DSAB QDB IS
*   OBTAINED FROM THE JSCB. THE DSAB QDB CONTAINS THE ADDRESSES OF THE
*   FIRST AND LAST DSAB'S. THE DSAB HAS POINTERS TO THE TIOT DD ENTRY
*   AND THE SIOT. THE TIOTDD ENTRY CONTAINS THE DDNAME AND A POINTER
*   TO THE UCB. THE UCB CONTAINS THE OLUME SERIAL NUMBER. THE SIOT
*   POINTS TO THE JFCB, WHICH HAS THE DSNAME, MEMBER NAME, AND DISP
*   FIELDS.
*
*
*  **********     ******
*  *ABSOLUTE*---->* CVT*
*  *HEX '10'*     ******      ******
*  **********     *+180*----->*JSCB*
*                 ******      ******      ******
*                             *+320*----->* QDB*      (FIRST)
*                             ******      ******      ******
*                                         *+12 *----->*DSAB*
*                                         ******      ******
*                                         *+16 *--|
*                                         ******  |   (LAST)
*                                                 |   ******
*                                                 |-->*DSAB*
*                                                     ******
*   ******
*   *DSAB*
*   ******                   ********
*   *+16 *------------------>*TIOTDD*
*   ******                   ********
*   *+64 *-----|             *  +4  * = DDNAME
*   ******     |             *      *
*              |             ********      *****
*              |             *  +16 *----->*UCB*
*              |             ********      *****
*              |                           *+28* = VOLUME SERIAL NBR
*              |                           *****
*              |
*              |             ******
*              |------------>*SIOT*
*                            ******
*                            * +4 * = DDNAME
*                            ******
*                            *    *      ******
*                            *+156*----->*JFCB*
*                            ******      ******
*                                        * +0 * = DSNAME
*                                        ******
*                                        * +44* = MEMBER NAME
*                                        ******
*                                        * +87* = DISPOSITION FLAGS
*                                        ******
*
*  AUTHOR R THORNTON AUG 1980
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         OPEN  (SYSPRINT,OUTPUT)       OPEN LISTING FILE
         L     R1,16                   POINT TO CVT
         L     R1,0(R1)                POINT TO TCB WORDS
         L     R1,0(R1)                POINT TO MY TCB
         L     R1,180(R1)              POINT TO JSCB
         L     R1,320(R1)              POINT TO DSAB QDB
         L     R12,16(R1)              POINT TO LAST DSAB
         L     R11,12(R1)              POINT TO 1ST DSAB
NEXTDSAB L     R10,64(R11)             POINT TO SIOT
         L     R10,156(R10)            POINT TO JFCB
         L     R9,16(R11)              POINT TO TIOT DD ENTRY
         L     R8,16(R9)               POINT TO UCB
         LA    R7,PRTLINE              POINT TO PRINT LINE
         MVC   PRTLINE,BLANX           CLEAR PRINT LINE
         CLI   4(R9),C' '              ANY DD NAME?
         BE    DISPOSTN                NO
STRTDDN  MVC   FSTRT,=C'DD('           DELIMIT DDNAME
         MVC   DDNAME,4(R9)            DDNAME TO PRINT
         LA    R7,DDNAME               POINT TO DDNAME
FDDRHE   CLI   0(R7),C' '              FOUND RIGHT END?
         BE    DDRPRN                  YES
         LA    R7,1(R7)                NO, STEP TO NEXT BYTE
         B     FDDRHE                  CONTINUE SEARCH
DDRPRN   MVI   0(R7),C')'              MOVE FINAL PAREN
DISPOSTN TM    87(R10),X'08'           SHR?
         BO    SHRDS                   YES
         TM    87(R10),X'C0'           NEW?
         BO    NEWDS                   YES
         TM    87(R10),X'80'           MOD?
         BO    MODDS                   YES
         MVC   DISP,=C'OLD'            NO, OLD
         B     STRTVOL                 CONTINUE
SHRDS    MVC   DISP,=C'SHR'            SHOW SHARED
         B     STRTVOL                 CONTINUE
NEWDS    MVC   DISP,=C'NEW'            SHOW NEW
         B     STRTVOL                 CONTINUE
MODDS    MVC   DISP,=C'MOD'            SHOW MOD
STRTVOL  CLI   28(R8),C'A'             ANY VOLSER?
         BL    STRTDSN                 NO, GO TO DSNAME
         MVC   VOLSTRT,=C'VOL('        YES, DELIMIT VOL FIELD
         MVC   VOLSER,28(R8)           VOLUME SERIAL TO PRINT
         MVI   VOLEND,C')'             ENDING PAREN FOR VOL
STRTDSN  MVC   DASTRT,=C'DSN('         DELIMIT DSNAME
         MVC   DSNAME,0(R10)           DSNAME TO PRINT
         LA    R7,DSNAME               POINT TO DSNAME FIELD
FDSNRHE  CLI   0(R7),C' '              FOUND RIGHT END?
         BE    DSNRPRN                 YES
         LA    R7,1(R7)                NO, STEP TO NEXT
         B     FDSNRHE                 CONTINUE SEARCH
DSNRPRN  MVI   0(R7),C')'              FINAL PAREN ON DA
         CLI   44(R10),C' '            ANY MEMBER NAME?
         BE    PRINTIT                 NO
         MVI   0(R7),C'('              MEMBER NAME PAREN
         MVC   1(8,R7),44(R10)         MOVE MEMBER NAME
         LA    R7,1(R7)                POINT TO MEMBER NAME
FMBRRHE  CLI   0(R7),C' '              FOUND RIGHT END?
         BE    MBRRPRN                 YES
         LA    R7,1(R7)                NO, STEP TO NEXT BYTE
         B     FMBRRHE                 CONTINUE SEARCH
MBRRPRN  MVI   0(R7),C')'              FINAL MEMBER PAREN
         MVI   1(R7),C')'              FINAL DSNAME PAREN
PRINTIT  PUT   SYSPRINT,PRTLINE        WRITE TO PRINT
         CR    R11,R12                 WAS THIS LAST DSAB?
         BE    EOJ                     YES
         L     R11,4(R11)              NO, POINT TO NEXT DSAB
         B     NEXTDSAB                GO PROCESS NEXT DSAB
*
*
*
EOJ      CLOSE SYSPRINT                CLOSE PRINT FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE
         LM    R14,R12,12(R13)         RESTORE REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
LASTDA   DC    A(PRTLINE)
BLANX    DC    CL150' '
PRTLINE  DS    0CL150
FSTRT    DS    CL3
DDNAME   DS    CL8
FEND     DS    CL1
         DS    CL1
DISP     DS    CL3
         DS    CL1
VOLSTRT  DS    CL4
VOLSER   DS    CL6
VOLEND   DS    CL1
         DS    CL1
DASTRT   DS    CL4
DSNAME   DS    CL44
DAEND    DS    CL1
MBRSTRT  DS    CL1
MEMBER   DS    CL8
MBREND   DS    CL1
         DS    CL100
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=F,LRECL=80,BLKSIZE=80
         END

         TITLE 'PDSCHECK: CHECK PDS DIRECTORY FOR ERRORS'
PDSCHECK SVLNK R3
***********************************************************************
* READS THE DIRECTORY FILE OUTPUT BY PROGRAM PDSINFO AFTER IT IS      *
* SORTED IN TTR (HIGH) AND ALIAS-OF (LOW) ORDER. WHEN A MEMBER WITH   *
* BLANK ALIAS-OF IS READ, ITS TTR MUST NOT EQUAL THE LAST TTR READ.   *
* THE MEMBER NAME AND TTR ARE STORED AS LAST NAME AND LAST TTR FOR    *
* EACH MEMBER WITH NO ALIAS-OF NAME. WHEN ALIAS-OF IS NOT BLANK, THE  *
* TTR MUST EQUAL THE LAST TTR STORED, AND THE ALIAS-OF NAME MUST EQUAL*
* THE LAST NAME STORED. ERRORS ARE PRINTED.                           *
*                                                                     *
* AUTHOR R. THORNTON   JAN 1993 USING MODULENG AND IHAPDS             *
***********************************************************************
*
         OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
*
MAINLINE GET   INPUT1,DIRRECD          READ DIRECTORY RECORD
         CLI   PNAME,C'A'              MEMBER RECORD?
         BL    MAINLINE                NO, DROP IT
         CLI   PALIAS,C' '             ALIAS-OF NAME IS BLANK?
         BNE   GOTALIAS                NO, PROCESS ALIAS
         CLC   PTTR,LASTTTR            SAME TTR AS LAST MEMBER?
         BNE   SAVELAST                NO, O.K.
         MVC   DUPNAME,LASTNAME       PROVIDE DUPLICATE NAME
         MVC   PRT,DUPTTR              PRINT ERROR
         BAS   R9,PRINT                  MESSAGE
         MVC   PRT,DIRRECD            PRINT ERROR
         BAS   R9,PRINT                  RECORD
         BAS   R9,PRINT                PRINT BLANK LINE
SAVELAST MVC   LASTNAME,PNAME          SAVE MEMBER NAME
         MVC   LASTTTR,PTTR               AND TTR
         B     MAINLINE                GO READ AGAIN
GOTALIAS CLC   PALIAS,LASTNAME         ALIAS-OF SAME AS LAST MEMBER?
         BE    CHEKTTR                 YES
MISSED   MVC   PRT,MISSMAIN            PRINT ERROR
         BAS   R9,PRINT                  MESSAGE
         MVC   PRT,DIRRECD            PRINT ERROR
         BAS   R9,PRINT                  RECORD
         BAS   R9,PRINT               PRINT BLANK LINE
         B     MAINLINE                GO READ AGAIN
CHEKTTR  CLC   PTTR,LASTTTR            TTR SAME AS MAIN MEMBER?
         BE    MAINLINE                YES, GOOD ALIAS
         B     MISSED                  MISSING MAIN MEMBER ERROR
*
*
*
PRINT    PUT   PRINT1,PRT              PRINT A LINE
         MVI   PRT,C' '                BEGIN CLEAR
         MVC   PRT+1(79),PRT           FINISH CLEAR
         BR    R9                      RETURN
*
*
*
EOJ      BAL   R9,PRINT                PRINT LAST LINE
         CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
LASTTTR  DC    CL6' '                  LAST TTR OF A NON-ALIAS
LASTNAME DC    CL8' '                  MEMBER NAME OF LAST NON-ALIAS
DUPTTR   DC    CL33'MEMBER BELOW HAS THE SAME TTR AS '
DUPNAME  DS    CL8
         DC    CL39' '
MISSMAIN DC    CL49'MAIN MEMBER NAME FOR THIS ALIAS DOES NOT EXIST AT'
         DC    CL31' THIS TTR.'
*
*
DIRRECD  DS    0CL80                   DIRECTORY RECORD FROM PDSINFO
PNAME    DS    CL8                     MODULE NAME
         DS    CL1
PLENG    DS    CL6                     MODULE LENGTH
         DS    CL1
PTTR     DS    CL6                     TTR TO MODULE
         DS    CL1
PALIAS   DS    CL8                     ALIAS-OF NAME
         DS    CL1
PREFR    DS    CL4                     REFRESHABLE
         DS    CL1
PRENT    DS    CL4                     REENTRANT
         DS    CL1
PREUS    DS    CL4                     REUSABLE
         DS    CL1
PEXEC    DS    CL4                     EXECUTABLE
         DS    CL29
*
PRT      DC    CL80' '
*
         LTORG
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,RECFM=FB,               X
               LRECL=80,EODAD=EOJ
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,RECFM=FB,               X
               LRECL=80
         END

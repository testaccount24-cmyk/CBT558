         TITLE '*** TIME: TEST NEW TIME MACRO RESULTS ***'
TIME CSECT
*********************************************************************
* ISSUE TIME MACRO OLD WAY AND NEW WAY AND COMPARE RESULTS.         *
*                                                                   *
* AUTHOR R THORNTON FEB 1999                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING TIME,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'TIME'               PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN PRINT FILE                                            *
*********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT)       OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE TIME  DEC                     ISSUE OLD TIME MACRO
         ST    R1,OLDTIME              SAVE DATE RETURNED
         UNPK  CNVOLD,OLDTIME(5)       CONVERT TO UNPACKED
         TR    CNVOLD,TRCHAR-C'0'      MAKE PRINTABLE
         MVI   OLDGBG,C' '             CLEAR GARBAGE
         PUT   SYSPRINT,OLDMSG         PRINT RESULT
         TIME  DEC,NEWTIME,LINKAGE=SYSTEM  ISSUE NEW TIME MACRO
         UNPK  CNVNEW(9),NEWTIME(5)    CONVERT TO UNPACKED
         UNPK  CNVNEW+8(9),NEWTIME+4(5) CONVERT TO UNPACKED
         UNPK  CNVNEW+16(9),NEWTIME+8(5) CONVERT TO UNPACKED
         UNPK  CNVNEW+24(9),NEWTIME+12(5) CONVERT TO UNPACKED
         TR    CNVNEW,TRCHAR-C'0'      MAKE PRINTABLE
         MVI   CNVNEW+32,C' '          CLEAR GARBAGE
         PUT   SYSPRINT,NEWMSG         PRINT RESULT
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE SYSPRINT                CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 RC=0
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
OLDTIME  DC    F'0'                    DATE FROM OLD TIME MACRO
NEWTIME  DC    CL16' '                 DATE FROM NEW TIME MACRO
TRCHAR   DC    C'0123456789ABCDEF'     CHARACTER TRANSLATE TABLE
*
OLDMSG   DS    0CL80                   OLD TIME MESSAGE
         DC    C'THIS IS THE RESULT FROM THE OLD TIME MACRO: '
CNVOLD   DC    CL9' '
         ORG CNVOLD+8
OLDGBG   DS    CL1
         ORG
         DC    CL80' '
NEWMSG   DS    0CL80                   NEW TIME MESSAGE
         DC    C'THIS IS THE RESULT FROM THE NEW TIME MACRO: '
CNVNEW   DC    CL16' '
         ORG
         DC    CL80' '
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         LTORG
TIME CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

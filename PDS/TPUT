         TITLE '    T P U T    I N T E R F A C E'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    TPUT - WRITES A MESSAGE TO THE TERMINAL.                       *
*     INVOCATION:  CALL TPUT(VSTR,RC);                              *
*       WHERE  VSTR IS A VARYING CHARACTER STRING CONTAINING THE    *
*                   OUTPUT MESSAGE.                                 *
*       AND    RC   IS A FIXED BIN(15) VARIABLE, USED TO RECEIVE    *
*                   THE TPUT RETURN CODE.                           *
*     ENTRY DECLARATION:                                            *
*       DCL  TPUT ENTRY(CHAR(X) VAR, FIXED BIN(15));                *
*       THE CURRENT LENGTH OF VSTR IS USED AS THE TPUT BUFFER       *
*           LENGTH.                                                 *
*                                                                   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
TPUT    CSECT
*                                                                   *
*        R  E  G  I  S  T  E  R  S                                  *
*                                                                   *
R0       EQU   0
R1       EQU   1
RW2      EQU   6
RW3      EQU   7
RSDV     EQU   8
RPARM    EQU   9
RBSE     EQU   10
RW1      EQU   11
R13      EQU   13
R15      EQU   15
         SAVE (14,12),,*
         LR    RBSE,R15
         USING TPUT,RBSE
         ST    R13,SA+4            SAVE REG 13
         LR    RW1,R13
         LA    R13,SA
         ST    R13,8(RW1)
         LR    RPARM,R1
         L     RSDV,0(RPARM)
         USING DSDV,RSDV
         SR    R0,R0               CLEAR OUT REG ZERO
TP1A     LH    R0,SDVCSL           BUFFER SIZE=CURRENT STRING LNTH
         IC    RW2,=X'00'          FLAGS=TPUT,WAIT,NOHOLD,EDIT,NOBREAK
         L     RW3,SDVADDR
TP1B     SLL   RW3,8               COMMON SETUP FOR TPUT
         SLDL  RW2,24
         LR    R1,RW2
         TPUT  (1),(0),R
         L     RW1,4(RPARM)
         STH   R15,0(RW1)          SET RETURN CODE
RET      L     R13,4(R13)
         RETURN (2,12)
DPARM    DSECT
PL1      DS    F
PL2      DS    F
PL3      DS    F
DSDV     DSECT
SDVADDR  DS    F
SDVMSL   DS    H
SDVCSL   DS    H
TPUT    CSECT
SA       DS    18F                 SAVE AREA
HWHLD    DS    H
         END

          PRINT NOGEN
          TITLE '       PRINT ALL MEMBERS OF A PDS'
LIBPRNT   SVLNK R3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   THIS PROGRAM PRINTS ALL, OR SELECTED MEMBERS OF THE PDS DEFINED BY
* THE INPUT1 DD CARD. PRINTED OUTPUT IS DIRECTED TO THE SYSPRINT DD
* CARD, AND RECFM=FBA,LRECL=81 IS HARD-CODED IN THE PROGRAM. CONTROL
* CARDS MAY BE USED TO SELECT MEMBERS TO BE PRINTED. WHEN NO CONTROL
* CARDS ARE FOUND, ALL PDS MEMBERS ARE PRINTED, IN THE ORDER FOUND
* IN THE DIRECTORY. EACH MEMBER WILL BEGIN ON A NEW PAGE. CONTROL
* CARDS ARE ENTERED AFTER A SYSIN DD * DD CARD, AND ARE FORMATTED
* BEGINNING IN COLUMN 1 WITH A MEMBER NAME. MULTIPLE MEMBER NAMES
* MAY BE ENTERED ON A SINGLE CARD BY PLACING A COMMA AFTER THE
* NAME AND ADDING THE NEXT NAME IN THE FOLLOWING COLUMN. THERE
* MUST BE NO IMBEDDED SPACES IN THE CONTROL CARD BETWEEN NAMES, AS
* CONTROL CARD SCAN STOPS WHEN THE FIRST BLANK COLUMN IS FOUND.
* ANY NUMBER OF CONTROL CARDS MAY BE ENTERED.
*    AUTHOR R THORNTON    9/79
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN PRINT, CONTROL CARD FILES
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
         CALL  READMEM,OPEN            OPEN MEMBER FILE
*
*
*        ROUTINE TO READ MEMBERS FOR OUTPUT
*
*
NEWMEM   CLI   MEM,CRD                 MEMBER NAMES IN CARDS
         BE    READCRD                 YES
         CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
NEWHDG   MVI   OUTCC,SKTO1             SET SKIP TO HEAD OF FORM
         CALL  READMEM,FIND            LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BNE   READMEM                 YES
         MVC   NOTNAM,MEMNAME          NAME TO MESSAGE
         MVC   OUTCC(81),NOTFND        MESSAGE TO USER
         BAL   R9,OUTPUT               GO TO OUTPUT ROUTINE
         B     NEWMEM                  TRY NEXT MEMBER
READMEM  CALL  READMEM,(READ,OUTREC)   READ NEXT RECORD IN MEMBER
         CLI   OUTREC,EOF              IS IT EOF
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         BAL   R9,OUTPUT               GO TO OUTPUT ROUTINE
         B     READMEM                 NO
READCRD  L     R12,CDADR               GET NEXT CARD COLUMN ADDR
         CLI   0(R12),BLANK            IS IT BLANK
         BNE   SCAN                    NO
REREAD   GET   SYSIN,CARD              READ NEXT CARD
         MVI   CARDEOF+1,C'0'          SET EOF SWITCH
         LA    R12,CARD                GET CARD ADDR
         ST    R12,CDADR               STORE CARD ADDR
         B     READCRD                 RECHECK BLANK CARD
SCAN     LA    R11,FINDNAM             GET FIND LIST NAME ADDR
         MVC   FINDNAM,BLANX           CLEAR NAME TO BLANKS
         LA    R10,8                   GET MAX LENGTH
CKEND    CLI   0(R12),BLANK            BLANK COL = END OF NAME
         BE    STR12                   YES, FINISHED
         CLI   0(R12),COMMA            COMMA ALSO END OF NAME
         BE    ADD1                    YES, STEP PAST
         MVC   0(1,R11),0(R12)         MOVE CHARACTER TO FIND NAME
         LA    R11,1(R11)              TO NEXT COL IN FIND NAME
         LA    R12,1(R12)              TO NEXT COL IN CARD
         BCT   R10,CKEND               LOOP THRU NAME
ADD1     LA    R12,1(R12)              STEP PAST COMMA
STR12    ST    R12,CDADR               SAVE CARD ADDRESS
         B     NEWHDG                  GO TO NEXT MEMBER HEADING
*
*
*
OUTPUT   EQU   *                       *** OUTPUT ROUTINE ***
         PUT   SYSPRINT,OUTCC          WRITE PRINT LINE
         MVI   OUTCC,SGLSPAC           BLANK TO CONTROL CHAR
         MVC   OUTREC,OUTCC            CLEAR OUTPUT RECORD
         BR    R9                      EXIT
*
*
*
CARDEOF  EQU   *                       END OF JOB ROUTINE
         NOP   EOJ                     SET TO BRANCH IF ANY CARDS READ
         MVI   MEM,ALL                 SHOW ALL MEMBERS TO BE PROCESSED
         B     NEWMEM                  GO READ DIRECTORY
*
*
*
EOJ      EQU   *                       END OF JOB ROUTINE
         CLOSE (SYSPRINT,,SYSIN)       WRITE LAST RECORD
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*                      *** STORAGE AND FILES ***
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
NOTFND   DS    0CL80
         DC    CL8'1MEMBER'
NOTNAM   DS    CL8
         DC    CL65' NOT FOUND IN INPUT1 PDS'
OUTCC    DS    CL1                     CONTROL CHAR FOR PRINTING
OUTREC   DS    CL80                    OUTPUT RECORD
CDADR    DC    A(CARD)                 NEXT CARD ADDRESS
CARD     DC    CL80' '                 CARD WITH MEMBER NAMES
         DC    C' '                    SCAN STOPPER
ALL      EQU   C'A'
CRD      EQU   C'C'
SKTO1    EQU   C'1'
SGLSPAC  EQU   C' '
EOF      EQU   X'FF'
BLANK    EQU   C' '
BLANX    DC    CL80' '
COMMA    EQU   C','
MEM      DC    CL1'C'                  MEMBERS: A=ALL, C=FROM CARDS
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=81
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,                         X
               RECFM=F,LRECL=80,BLKSIZE=80,EODAD=CARDEOF
         END

         PRINT NOGEN
         TITLE '*****STRIPLID: GET USERID/NAME *****'
STRIPLID SVLNK R3
*
***********************************************************************
* STRIP USERID AND NAME INFORMATION FROM THE ACF2.LID.LIST DATASET    *
* AUTHOR R THORNTON SEP 1990                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES AND PRINT FIRST PAGE HEADING             *
***********************************************************************
*
         OPEN  (OUTPUT1,OUTPUT,INPUT1)  OPEN FILES
*
***********************************************************************
* MAINLINE PROCESSING: READ A LID RECORD. IF IT HAS A USERID, WRITE   *
* AN OUTPUT RECORD CONTAINING THE LAST NAME, FIRST NAME, MIDDLE       *
* INITIAL, AND USERID.                                                *
***********************************************************************
*
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         MVC   OUTRECD,BLANKS          CLEAR OUTPUT RECORD
         MVC   HOLDNBR,BLANKS          CLEAR WORK AREA
         CLI   INB1,C' '               VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLC   INB2,BLANKS             VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLI   INB3,C' '               VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLI   INB4,C' '               VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLI   INB5,C' '               VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLI   INB6,C' '               VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLC   INB7,BLANKS             VALID RECORD?
         BNE   MAINLINE                NO, DROP IT
CHKUID   CLC   INUSERID,BLANKS         ANY USER ID?
         BE    MAINLINE                NO, DROP RECORD
         MVC   OUTUSER,INUSERID        USER ID
         TRT   INPHONE,VALFONE         VALID TELEPHONE NBR?
         BNZ   MAINLINE                NO, HAS INVALID CHARACTERS
         LA    R1,HOLDNBR+11           RHE WORK AREA
         LA    R2,INPHONE+11           RHE INCOMING PHONE NBR
         LA    R15,12                  LOOP COUNT
PHCK1    CLI   0(R2),C'0'              NUMERIC DIGIT?
         BL    PHBKUP                  NO
         MVC   0(1,R1),0(R2)           YES, MOVE IT
         BCTR  R1,0                    BACK UP IN WORK
PHBKUP   BCTR  R2,0                    BACK UP IN INCOMING PHONE NBR
         BCT   R15,PHCK1               LOOP TO RT JUST PHONE NBR
         CLC   HOLDNBR,BLANKS          ANY TELEPHONE NUMBER?
         BE    MAINLINE                NO, DROP RECORD
         MVC   OUTWPHON(6),HOLDNBR+2   AREA CODE AND C.O.
         MVI   OUTWPHON+6,C'-'         SEPARATOR
         MVC   OUTWPHON+7(4),HOLDNBR+8 LAST 4 DIGITS
         CLI   OUTWPHON,C' '           ANY AREA CODE?
         BNE   TESTNAME                YES, MUST BE COMPLETE
         CLC   OUTWPHON(3),BLANKS      NO, THEN ALL THREE MUST BE BLK
         BNE   MAINLINE                ELSE INVALID TELEPHONE NBR
         CLI   OUTWPHON+3,C' '         IS CENTRAL OFFICE PREFIX HERE?
         BE    MAINLINE                NO, INVALID TELEPHONE NUMBER
TESTNAME TRT   INNAME,VALNAME          NAME FIELD VALID?
         BNZ   MAINLINE                NO, HAS INVALID CHARACTERS
NMCK1    TRT   INNAME,FINDALFA         LOCATE FIRST BYTE
         BZ    MAINLINE                NO NAME, SKIP RECORD
         LR    R14,R1                  COPY START ADDRESS
         LA    R1,INNAME+20            END OF INCOMING NAME FIELD+1
         EX    R14,EXTRT               LOCATE END OF LAST NAME+1
         LR    R15,R1                  SAVE ADDRESS OF END+1
         SR    R1,R14                  COMPUTE LENGTH OF LAST NAME
         BCTR  R1,0                    THEN LENGTH CODE
         CH    R1,=H'14'               TOO LONG?
         BNH   MVLNM                   NO
         LA    R1,14                   YES, SET MAX LENGTH
MVLNM    EX    R1,EXMVLNM              MOVE LAST NAME
         TRT   1(20,R15),FINDALFA      FIND LEFT END OF FIRST NAME
         BZ    WRITEOUT                NO FIRST NAME FOUND
         C     R1,=A(INNAME+19)        PAST END OF NAME FIELD?
         BH    WRITEOUT                YES, NO FIRST NAME
         LR    R14,R1                  COPY FIRST NAME ADDRESS
         LA    R1,INNAME+20            END OF INCOMING NAME FIELD+1
         EX    R14,EXTRT               LOCATE END OF FIRST NAME+1
         LR    R15,R1                  COPY ADDRESS OF END+1
         SR    R1,R14                  COMPUTE LENGTH OF FIRST NAME
         BCTR  R1,0                    THEN LENGTH CODE
         CH    R1,=H'11'               TOO LONG?
         BNH   MVFNM                   NO
         LA    R1,11                   YES, SET MAX LENGTH
MVFNM    EX    R1,EXMVFNM              MOVE FIRST NAME
         TRT   1(20,R15),FINDALFA      FIND MIDDLE INITIAL
         BZ    WRITEOUT                NO MIDDLE INITIAL FOUND
         C     R1,=A(INNAME+19)        PAST END OF FIELD?
         BH    WRITEOUT                YES, NO MIDDLE INITIAL
         MVC   OUTMI,0(R1)             MOVE MIDDLE INITIAL
WRITEOUT MVI   OUTORIG,C'1'            SOURCE IDENTIFIER
         CLC   OUTLNAME,BLANKS         ANY LAST NAME?
         BE    MAINLINE                NO, DROP RECORD
         PUT   OUTPUT1,OUTRECD         WRITE THE RECORD
         B     MAINLINE                GO GET NEXT RECORD
EXTRT    TRT   1(1,R14),FINDNOAL       TRT TO FIND NON-ALPHABETIC
EXMVLNM  MVC   OUTLNAME(1),0(R14)      MOVE LAST NAME
EXMVFNM  MVC   OUTFNAME(1),0(R14)      MOVE LAST NAME
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
HOLDNBR  DS    CL12
BLANKS   DC    CL256' '
FINDALFA DC    XL256'00'
         ORG   FINDALFA+C'A'
         DC    9X'FF'
         ORG   FINDALFA+C'J'
         DC    9X'FF'
         ORG   FINDALFA+C'S'
         DC    8X'FF'
         ORG
FINDNOAL DC    256X'FF'
         ORG   FINDNOAL+C'A'
         DC    9X'00'
         ORG   FINDNOAL+C'J'
         DC    9X'00'
         ORG   FINDNOAL+C'S'
         DC    8X'00'
         ORG
VALFONE  DC    256X'FF'
         ORG   VALFONE+C'('
         DC    X'00'
         ORG   VALFONE+C')'
         DC    X'00'
         ORG   VALFONE+C'-'
         DC    X'00'
         ORG   VALFONE+C' '
         DC    X'00'
         ORG   VALFONE+C'0'
         DC    10X'00'
         ORG
VALNAME  DC    256X'FF'
         ORG   VALNAME+C' '
         DC    X'00'
         ORG   VALNAME+C','
         DC    X'00'
         ORG   VALNAME+C'.'
         DC    X'00'
         ORG   VALNAME+C'A'
         DC    9X'00'
         ORG   VALNAME+C'J'
         DC    9X'00'
         ORG   VALNAME+C'S'
         DC    8X'00'
         ORG
*
OUTRECD  DS    0CL80                   OUTPUT RECORD
OUTUSER  DS    CL8                     USER IS
         DS    CL1
OUTLNAME DS    CL15                    LAST NAME
         DS    CL1
OUTFNAME DS    CL12                    FIRST NAME
         DS    CL1
OUTMI    DS    CL1                     MIDDLE INITIAL
         DS    CL1
OUTWPHON DS    CL11                    WORK PHONE NBR
         DS    CL1
OUTHPHON DS    CL11                    HOME PHONE
         DS    CL1
OUTBLDG  DS    CL10                    BUILDING/LOCATION
         DS    CL1
OUTMLDRP DS    CL3                     MAIL DROP
         DS    CL1
OUTORIG  DS    CL1                     RECORD ORIGIN = 1
*
INRECD   DS    0CL200                  INPUT RECORD
INB1     DS    CL1
INUSERID DS    CL8                     USER ID
INB2     DS    CL13
INACFGRP DS    CL5                     ACF2 GROUP
INUSER2  DS    CL8                     USER ID AGAIN
INB3     DS    CL1
INNAME   DS    CL20                    LAST, FIRST MI
INB4     DS    CL1
INSOCSEC DS    CL9                     SOCIAL SEC NBR
INB5     DS    CL1
INCSTCTR DS    CL2                     COST CENTER
INB6     DS    CL1
INPHONE  DS    CL12                    PHONE NBR WITH W/O DASHES
INB7     DS    CL118
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ               X
               RECFM=FB,LRECL=200
         END

         TITLE '***** STRIP ABEND INFO FROM SMF FILE *****'
         PRINT NOGEN
SMFTSO CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFTSO,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFTSO'             PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
* FOR GENERIC SMF TYPE 30 RECORD PROCESSING SEE SMFSTR30             *
*                                                                    *
*  AUTHOR R. THORNTON APR 1986                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
         USING SMFRECD,R6
MAINLINE GET   INPUT1,SMFRCD+4         GET INPUT AREA ADDRESS
         LA    R6,SMFRCD               POINT TO INPUT AREA
         MVC   SMF30LEN,INPUT1+82      MOVE LENGTH TO OUTPUT
         XC    SMF30SEG,SMF30SEG       CLEAR SEGMENT DESCRIPTOR
         CLI   SMF30RTY,30             TYPE 30 RECORD?
         BNE   MAINLINE                NO, SKIP IT
BEGIN01  ICM   R12,15,SMF30SOF         SUBSYSTEM SECTION OFFSET
         BZ    MAINLINE                NO SUBSYSTEM SECTION
         AR    R12,R6                  SUBSYSTEM SECTION ADDRESS
         USING SMF30S01,R12
         CLI   SMF30TYP+1,5            JOB COMPLETION RECORD?
         BNE   MAINLINE                NO, SKIP IT
         DROP  R12
BEGIN02  ICM   R12,15,SMF30IOF         IDENTIFICATION SECT OFFSET
         BZ    MAINLINE                NO IDENTIFICATION SECTION
         AR    R12,R6                  IDENTIFICATION SECT ADDRESS
         USING SMF30S02,R12
         CLC   SMF30JBN(4),TSOUSR      TSO SESSION?
         BNE   MAINLINE                NO, SKIP IT
         MVC   OTJOBN,SMF30JBN         JOB NAME TO PRINT
         MVC   OTRSD,SMF30RSD          READER START DATE
         MVC   OTRST,SMF30RST          READER START TIME
         MVC   OTPGMN,SMF30PGM         PROGRAM NAME TO PRINT
         MVC   OTPGMR,SMF30USR         PROGRAMMER'S NAME TO PRINT
         MVC   OTLOD,SMF30DTE          LOGOFF DATE
         MVC   OTLOT,SMF30TME          LOGOFF TIME
         DROP  R12
BEGIN04  ICM   R12,15,SMF30TOF         COMPLETION SECTION OFFSET
         BZ    MAINLINE                NO IDENTIFICATION SECTION
         AR    R12,R6                  IDENTIFICATION SECT ADDRESS
         USING SMF30S04,R12
         MVC   OTSCC,BLANX             CLEAR COMPLETION CODE
         TM    SMF30STI,2              DID IT ABEND?
         BZ    BEGIN05                 NO
         UNPK  WORK,SMF30SCC(3)        UNPACK INTO WORK AREA
         TR    WORK,TRCHAR-C'0'        MAKE PRINTABLE
         MVC   OTSCC+1(3),WORK+1       MOVE CODE TO OUTPUT
         MVI   OTSCC,C'U'              SET USER ABEND IDENTIFIER
         TM    SMF30SCC,X'80'          USER ABEND CODE?
         BO    BEGIN05                 YES, CONTINUE
         MVI   OTSCC,C'S'              SET SYSTEM ABEND IDENTIFIER
         DROP  R12
BEGIN05  ICM   R12,15,SMF30COF         PROCESSOR ACCOUNTING OFFSET
         BZ    MAINLINE                NO ACCOUNTING SECTION
         AR    R12,R6                  ACCOUNTING SECT ADDRESS
         USING SMF30S05,R12
         L     R1,SMF30CPT             STEP TIME UNDER TCB
         A     R1,SMF30CPS             ADD STEP TIME UNDER SRB
         STCM  R1,15,OTCPUTM           SAVE CPU TIME
         DROP  R12
WRITEREC PUT   OUTPUT1,OTREC           WRITE OUTPUT RECORD
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
RETCODE  DC    F'0'                    RETURN CODE
WORK     DS    XL5                     WORK AREA
TSOUSR   DC    CL4'USER'               TSO USER ID PREFIX
BLANX    DC    CL4'    '               BLANKS
TRCHAR   DC    CL16'0123456789ABCDEF'  CHARACTER TRANSLATE
*
OTREC    DS    0CL60                   OUTPUT RECORD
OTJOBN   DS    CL8                     JOB NAME
OTRSD    DS    PL4                     READER START DATE, 00YYDDDF
OTRST    DS    XL4                     READER START TIME, SEC/100
OTSCC    DS    CL4                     SYSTEM ABEND CODE
OTCPUTM  DS    XL4                     CPU TIME IN SEC/100
OTPGMN   DS    CL8                     PROGRAM NAME
OTPGMR   DS    CL20                    PROGRAMMER'S NAME
OTLOD    DS    PL4                     LOGOFF DATE, 00YYDDDF
OTLOT    DS    XL4                     LOGOFF TIME, SEC/100
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
*
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
*
*
*        SMF RECORD DSECT
*
SMFRECD  DSECT ,                       *** S M F D A T A R E C O R D S
SMF30LEN DS    H                       RECORD LENGTH
SMF30SEG DS    H                       SEGMENT DESCRIPTOR
SMF30FLG DS    XL1                     0=MVT, 2=MVS
SMF30RTY DS    XL1                     RECORD TYPE
SMF30TME DS    XL4                     TIME 1/100 SEC
SMF30DTE DS    PL4                     DATE 00YYDDD
SMF30SID DS    CL4                     SYSTEM ID
SMF30WID DS    CL4                     SUBSYSTEM IDENTIFIER
         DS    XL2
SMF30SOF DS    XL4                     OFFSET TO SUBSYSTEM SECTION
SMF30SLN DS    XL2                     SUBSYSTEM SECTION LENGTH
SMF30SON DS    XL2                     NUMBER OF SUBSYSTEM SECTIONS
SMF30IOF DS    XL4                     OFFSET TO IDENTIFICATION SECT
SMF30ILN DS    XL2                     LENGTH OF IDENTIFICATION SECT
SMF30ION DS    XL2                     NUMBER OF IDENTIFICATION SECTS
SMF30UOF DS    XL4                     OFFSET TO UNIT RECORD SECT
SMF30ULN DS    XL2                     LENGTH OF UNIT RECORD SECT
SMF30UON DS    XL2                     NUMBER OF UNIT RECORD SECTS
SMF30TOF DS    XL4                     OFFSET TO COMPLETION SECTION
SMF30TIN DS    XL2                     LENGTH OF COMPLETION SECTION
SMF30TON DS    XL2                     NUMBER OF COMPLETION SECTIONS
SMF30COF DS    XL4                     OFFSET TO PROCESSOR SECTION
SMF30CLN DS    XL2                     LENGTH OF PROCESSOR SECTION
SMF30CON DS    XL2                     NUMBER OF PROCESSOR SECTIONS
SMF30AOF DS    XL4                     OFFSET TO ACCOUNTING SECTION
SMF30ALN DS    XL2                     LENGTH OF ACCOUNTING SECTION
SMF30AON DS    XL2                     NUMBER OF ACCOUNTING SECTIONS
SMF30ROF DS    XL4                     OFFSET TO STORAGE SECTION
SMF30RLN DS    XL2                     LENGTH OF STORAGE SECTION
SMF30RON DS    XL2                     NUMBER OF STORAGE SECTIONS
SMF30POF DS    XL4                     OFFSET TO PERFORMANCE SECT
SMF30PLN DS    XL2                     LENGTH OF PERFORMANCE SECT
SMF30PON DS    XL2                     NUMBER OF PERFORMANCE SECTS
SMF30OOF DS    XL4                     OFFSET TO OPERATOR SECTION
SMF30OLN DS    XL2                     LENGTH OF OPERATOR SECTION
SMF30OON DS    XL2                     NUMBER OF OPERATOR SECTIONS
SMF30EOF DS    XL4                     OFFSET TO EXCP SECTION
SMF30ELN DS    XL2                     LENGTH OF EXCP SECTION
SMF30EON DS    XL2                     NUMBER OF EXCP SECTIONS
SMF30EOR DS    XL2                     NUMBER OF EXCP SECTS IN
*                                       SUBSEQUENT RECORDS
*****************************************************************
*          SUBSYSTEM SECTION                                    *
*****************************************************************
SMF30S01 DSECT ,                       TYPE 30 SUBSYSTEM SECTION
SMF30TYP DS    XL2                     SUB TYPE IDENTIFICATION
*                                        1 = JOB START
*                                        2 = INTERVAL
*                                        3 = STEP TERMINATION
*                                        4 = STEP TOTAL
*                                        5 = JOB TERMINATION
*                                        6 = SYSTEM A.S. INIT
SMF30RS1 DS    XL2
SMF30RVN DS    CL2                     RECORD REVISION NUMBER
SMF30PNM DS    CL8                     SUBSYSTEM OR PRODUCT NAME
*****************************************************************
*           IDENTIFICATION SECTION                              *
*****************************************************************
SMF30S02 DSECT ,                       TYPE 30 IDENTIFICATION SECTION
SMF30JBN DS    CL8                     JOB OR SESSION NAME
SMF30PGM DS    CL8                     PROGRAM NAME
SMF30STM DS    CL8                     STEP NAME
SMF30UIF DS    CL8                     USER IDENTIFICATION
SMF30JNM DS    CL8                     JES2 ASSIGNED JOB NUMBER
SMF30STN DS    XL2                     STEP NUMBER
SMF30CLS DS    CL1                     JOB CLASS
         DS    XL1
SMF30PGN DS    XL2                     JOB PERFORMANCE GROUP NUMBER
SMF30JPT DS    XL2                     JES2 INPUT PRIORITY
SMF30AST DS    XL4                     DEVICE ALLOCATION START TIME
SMF30PPS DS    XL4                     PROBLEM PROGRAM START TIME
SMF30SIT DS    XL4                     INITIATOR SELECT TIME
SMF30STD DS    PL4                     INITIATOR SELECT DATE 00YYDDDF
SMF30RST DS    XL4                     READER START TIME
SMF30RSD DS    PL4                     READER START DATE 00YYDDDF
SMF30RET DS    XL4                     READER END TIME
SMF30RED DS    PL4                     READER END DATE 00YYDDDF
SMF30USR DS    CL20                    PROGRAMMER'S NAME
SMF30GRP DS    CL8                     RACF GROUP ID
SMF30RUD DS    CL8                     RACF USER ID
SMF30TID DS    CL8                     RACF TERMINAL ID
*****************************************************************
*           UNIT RECORD (I/O ACTIVITY) SECTION                  *
*****************************************************************
SMF30S03 DSECT ,                       TYPE 30 UNIT RECORD SECTION
SMF30INP DS    XL4                     NBR DD *, DD DATA CARDS
SMF30TEP DS    XL4                     TOTAL BLOCKS TRANSFERRED
SMF30TPT DS    XL4                     ZERO (TSO TPUTS)
SMF30TGT DS    XL4                     ZERO (TSO TGETS)
SMF30RDR DS    XL1                     READER DEVICE CLASS
SMF30RDT DS    XL1                     READER DEVICE TYPE
SMF30RV4 DS    XL2                     RESERVED
*****************************************************************
*           COMPLETION SECTION                                  *
*****************************************************************
SMF30S04 DSECT ,                       TYPE 30 COMPLETION SECTION
SMF30SCC DS    XL2                     STEP COMPLETION CODE
*                                        IF ABEND:
*                                           80=USER ABEND
*                                           7F=ABEND CODE
*                                        IF NORMAL COMPLETION:
*                                           FF=R15 AT TERMINATION
SMF30STI DS    XL2                     STEP TERMINATION INDICATOR
*                                        20=CANCELED BY IEFUJI
*                                        10=CANCELED BY IEFUSI
*                                        08=CANCELED BY IEFACTRT
*                                        04=STEP TO BE RESTARTED
*                                        02=ABEND
*                                        01=FLUSHED
*                                        IN BYTE 2:
*                                        80=EXCP COUNTS INCORRECT
*                                        40=PREV INTV REC NOT WRT
*****************************************************************
*           PROCESSOR ACCOUNTING SECTION                        *
*****************************************************************
SMF30S05 DSECT ,                       TYPE 30 PROCESSOR SECTION
SMF30PTY DS    XL2                     ASCB DISPATCHING PRIORITY
SMF30RV3 DS    XL2                     RESERVED
SMF30CPT DS    XL4                     TCB CPU TIME, SEC/100
SMF30CPS DS    XL4                     SRB CPU TIME, SEC/100
SMF30ICU DS    XL4                     INIT TCB CPU TIME, SEC/100
SMF30ISB DS    XL4                     INIT SRB CPU TIME, SEC/100
*****************************************************************
*           EXCP SECTION                                        *
*****************************************************************
SMF30S06 DSECT ,                       TYPE 30 EXCP SECTION
SMF30DEV DS    XL1                     DEVICE CLASS
SMF30UTP DS    XL1                     UNIT TYPE
SMF30CUA DS    XL2                     CHANNEL AND UNIT ADDRESS
SMF30DDN DS    CL8                     DDNAME
SMF30BLK DS    XL4                     BLOCK COUNT (NBR EXCPS)
SMF30BSZ DS    XL2                     MAX BLOCK SIZE, IGNORE 80
SMF30RV2 DS    XL2                     RESERVED
*****************************************************************
*           ACCOUNTING SECTION                                  *
*****************************************************************
SMF30S07 DSECT ,                       TYPE 30 ACCOUNTING SECTION
SMF30ACL DS    XL1                     LENGTH, INCLUDING THIS
SMF30ACT DS    0C                      JOB OR STEP ACCOUNTING FLD
*****************************************************************
*           STORAGE AND PAGING SECTION                          *
*****************************************************************
SMF30S08 DSECT ,                       TYPE 30 STORAGE SECTION
SMF30RGN DS    XL2                     REGION SIZE REQUESTED
SMF30SFL DS    XL1                     80=V=R SPECIFIED
SMF30SPK DS    XL1                     F0=PROTECT KEY
SMF30PRV DS    XL2                     STORAGE USED FM BOT PVT
SMF30SYS DS    XL2                     STORAGE USED FM TOP PVT
SMF30PGI DS    XL4                     NON-VIO, NON-SWAP PAGE-INS
SMF30PGO DS    XL4                     NON-VIO, NONSWAP PAGE-OUTS
SMF30REC DS    XL4                     NON-VIO RECLAIMS
SMF30NSW DS    XL4                     ADDRESS SPACE SWAPS
SMF30PSI DS    XL4                     PAGES SWAPPED IN
SMF30PSO DS    XL4                     PAGES SWAPPED OUT
SMF30VPI DS    XL4                     VIO PAGE-INS
SMF30VPO DS    XL4                     VIO PAGE-OUTS
SMF30VPR DS    XL4                     VIO RECLAIMS
SMF30CPI DS    XL4                     COMMON AREA PAGE-INS
SMF30CPR DS    XL4                     COMMON AREA RECLAIMS
SMF30LPI DS    XL4                     LPA PAGE-INS
SMF30LPR DS    XL4                     LPA RECLAIMS
SMF30PST DS    XL4                     STOLEN PAGES
SMF30PSC DS    XL8                     CPU PAGE-SECONDS
*****************************************************************
*           PERFORMANCE SECTION                                 *
*****************************************************************
SMF30S09 DSECT ,                       TYPE 30 PERFORMANCE SECTION
SMF30SRV DS    XL4                     TOTAL SERVICE UNITS
SMF30CSU DS    XL4                     CPU SERVICE UNITS
SMF30SRB DS    XL4                     SRB SERVICE UNITS
SMF30IO  DS    XL4                     I/O SERVICE UNITS
SMF30MSO DS    XL4                     MSO SERVICE UNITS
SMF30TAT DS    XL4                     TRANSACTION ACTIVE TIME
SMF30TET DS    XL4                     RESERVED
SMF30RES DS    XL4                     TRANSACTION RESIDENCY TIME
SMF30TRS DS    XL4                     NUMBER OF TRANSACTIONS
*****************************************************************
*           OPERATOR SECTION                                    *
*****************************************************************
SMF30S10 DSECT ,                       TYPE 30 OPERATOR SECTION
SMF30PDM DS    XL4                     NON-SPECIFIC DASD MOUNTS
SMF30PRD DS    XL4                     SPECIFIC DASD MOUNTS
SMF30PTM DS    XL4                     NON-SPECIFIC TAPE MOUNTS
SMF30TPR DS    XL4                     SPECIFIC TAPE MOUNTS
SMF30MTM DS    XL4                     NON-SPECIFIC MSS MOUNTS
SMF30MSR DS    XL4                     SPECIFIC MSS MOUNTS
*
SMFTSO CSECT
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
SMFRCD   DS    CL32768                 SMF RECORD INPUT AREA
         END

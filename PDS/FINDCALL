         TITLE 'FIND SUBROUTINE CALLS'
FINDCALL SVLNK R3
*******************************************************************
* FIRST, A CONTROL FILE CONTAINING MEMBER NAMES IS READ AND STORED*
* FOR SEARCHES. NEXT, EACH DIRECTORY ENTRY OF THE INPUT PDS FILE  *
* IS READ USING THE READDIR SUBROUTINE. A FIND MACRO LOCATES THE  *
* MEMBER, WHICH IS THEN READ USING BPAM READS. THE CESD RECORDS   *
* ARE PROCESSED TO FIND ALL EXTERNAL REFERENCES MATCHING ANY ENTRY*
* IN THE CONTROL TABLE. THE OUTPUT FILE CONTAINS ONE RECORD PER   *
* FOR EACH EXTERNAL REFERENCE MATCHING A CONTROL TABLE ENTRY.     *
* AUTHOR R THORNTON (BASED ON FINDSUBR)                           *
*******************************************************************
*
*******************************************************************
* INITIALIZATION: OPEN FILES AND LOAD THE CONTROL TABLE           *
*******************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1,,READER1) OPEN FILES
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
         LA    R12,CTLTBL              POINT TO CONTROL TABLE
READCTL  GET   READER1,0(R12)          READ NEXT CONTROL REC INTO TBL
         LA    R12,8(R12) STEP TO NEXT ENTRY
         C     R12,ENDCTL PAST END OF ALLOCATED TABLE?
         BL    READCTL NO, CONTINUE LOADING
EOFCTL   MVC   0(8,R12),TBLSTOP NO MORE CONTROL RECORDS
*******************************************************************
* MAINLINE: READ DIRECTORY ENTRY, AT END OF DIRECTORY, TERMINATE  *
*           PROCESSING. FOR EACH DIRECTORY, ISSUE A FIND MACRO TO *
*           POSITION TO THE MEMBER, THEN READ IT USING BPAM READS.*
*           FOR EACH CESD RECORD READ, SEARCH FOR EXTERNAL REFER- *
*           ENCES.
*******************************************************************
MAINLINE CALL  READDIR,(READ,DIRENTRY) READ DIRECTORY ENTRY
         CLI   DIRENTRY,X'FF'          IS IT EOF
         BE    EOJ                     YES
         MVC   MEMBER,DIRNAME          MEMBER NAME TO OUTPUT RECORD
         FIND  INPUT1,DIRTTR,C         FIND THE MEMBER
         LTR   R15,R15                 FOUND O.K.
         BZ    READMEM                 YES
         WTO   'FINDCALL: ERROR RETURNED BY FIND MACRO',ROUTCDE=11
         ABEND  111,DUMP               TERMINATE IN ERROR
READMEM  READ  DECB,SF,INPUT1,INREC,248 READ LOAD MODULE RECORD
         CHECK DECB                    WAIT FOR COMPLETION
         CLI   RECDID,X'20'            IS IT A CESD RECORD
         BE    GETSIZE                 YES, GO PROCESS
         TM    RECDID,X'01'            IS IT A CONTROL RECORD
         BO    MAINLINE                YES, GO GET NEXT MEMBER
         B     READMEM                 NO, READ NEXT LOAD MODULE RECORD
GETSIZE  LA    R5,ESDATA               GET ADDR OF 1ST ESD ENTRY
         LH    R2,COUNT                GET LENGTH OF ESD DATA
         SRL   R2,4                    DIVIDE BY 16 BYTES PER ENTRY
CKTYPE   TM    8(R5),X'0F'             THIS A CSECT ENTRY?
         BNZ   CK4ESD                  NO, GO CHECK FOR ESD
         MVC   CSECT,0(R5)             YES, SAVE CSECT NAME
         B     NEXTESD                 CONTINUE
CK4ESD   TM    8(R5),X'02'             X'02' IS ESD ENTRY AN EXTERNAL R
         BNO   NEXTESD                 NO, IGNORE IT
         LA    R12,CTLTBL              POINT TO CONTROL TABLE
CKTBLND  CLI   0(R12),X'FF'            END OF TABLE?
         BE    NEXTESD                 YES
         CLC   0(8,R12),0(R5)          THIS EXTERNAL SYMBOL IN CTLTBL?
         BE    OKOUT                   YES
         LA    R12,8(R12)              NO, ADVANCE ONE ENTRY
         B     CKTBLND                 CONTINUE TABLE SEARCH
OKOUT    MVC   SUBRTN,0(R5)            EXTERNAL SYMBOL TO OUTPUT
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT
NEXTESD  BCT   R2,STEPESD              LOOP THRU RECORD
         B     READMEM                 AT END GO READ AGAIN
STEPESD  LA    R5,16(R5)               STEP TO NEXT ESD ENTRY
         B     CKTYPE                  CONTINUE PROCESSING
*******************************************************************
* END OF JOB: CLOSE FILES AND TERMINATE PROCESSING                *
*******************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1,,READER1) YES,CLOSE FILES
         CALL  READDIR,CLOSE           CLOSE DIRECTORY FILE
         L     R13,4(R13)              GET CALLERS SAVE ADDR
         LM    R14,R12,12(R13)         RESTORE HIS REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*******************************************************************
* DATA, CONSTANT, AND FILE AREAS                                  *
*******************************************************************
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
CLOSE    DC    C'C'                    CLOSE PARAMETER
DIRENTRY DS    0CL11                   *** DIRECTORY ENTRY FROM READDIR
DIRNAME  DS    CL8                     MEMBER NAME
DIRTTR   DS    XL3                     TTR OF MEMBER ON LIBRARY
         DC    X'00'
*
*
*
CTLRECD  DS    CL80
TBLSTOP  DC    8X'FF'
ENDCTL   DC    A(CTLEND)
*
*
*
OUTREC   DS    0CL80                   *** OUTPUT RECORD ***
MEMBER   DC    CL8' '                  CALLING MODULE NAME
         DC    CL1' '
CSECT    DC    CL8' '                  CALLING MODULE NAME
         DC    CL1' '
SUBRTN   DC    CL8' '                  CALLED SUBROUTINE NAME
         DC    CL63' '
*
*
*
         DS    0H
INREC    DS    0CL248                  *** LOAD MODULE RECORD ***
RECDID   DS    XL1                     RECORD ID: (IN HEX)
*                20=CESD RECORD, 40=SYM RECORD, 10=SCATTER TRANSLATION,
*                01=CONTROL RECORD, 05=CONTROL RECORD BEFORE LAST TEXT
*                RECORD OF OVERLAY SEGMENT, 0D=CONTROL RECORD BEFORE
*                LAST TEXT RECORD OF MODULE (EOM), 02=RLD RECORD,
*                06=LAST RECORD OF SEGMENT, 0E=LAST RECORD OF MODULE
*                03=CONTROL AND RLD RECORD (FOLLOWED BY TEXT RECORD),
*                07=CONTROL AND RLD RECORD FOLLOWED BY LAST TEXT
*                RECORD OF SEGMENT (EOS), 0F=CONTROL AND RLD RECORD
*                FOLLOWED BY LAST TEXT RECORD OF MODULE (EOM),
*                80=CSECT IDENTIFICATION RECORD
         DS    XL3                     RESERVED, ZEROS
ESDID1   DS    XL2                     ESD ID OF 1ST ESD ITEM
COUNT    DS    CL2                     NBR BYTES OF ESD DATA
ESDATA   DS    0CL16                   *** ESD DATA ELEMENT ***
SYMBOL   DS    CL8                     EXTERNAL NAME (ZEROS IF NULL)
TYPE     DS    XL1                     SYMBOL TYPE: (X MAY BE ANY BITS)
*                X0=SD, X3=LR, X4=PC, X5=CM, X6=PR,
*                07=NULL, X2=ER, XA=WX, 1X=DELETE,
*                1X=REPLACE, 2X=INSERT, 4X=CHAIN, 8X=MAP
ADDRESS  DS    XL3                     ADDRESS OF SYMBOL IN MODULE. ZER
SEGNO    DS    XL1                     SEGMENT NBR IN WHICH SYMBOL APPE
*                                        ZERO FOR ER WX NULL
LENGTH   DS    0XL3                    LENGTH IF SD PC CM OR PR
*                                        ZERO IF WX ER NULL
*                                        X'06'=NEVER CALL
ID       DS    XL2                     ID IF TYPE IS LR
         DS    XL1
         DS    14CL16                  ROOM FOR 14 ADDITIONAL ESD ENTRI
*******************************************************************
* FILE DEFINITIONS                                                *
*******************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PO,MACRF=R,DDNAME=INPUT1,EODAD=MAINLINE,          X
               RECFM=U
READER1  DCB   DSORG=PS,MACRF=GM,DDNAME=READER1,EODAD=EOFCTL,          X
               RECFM=FB,LRECL=80
*******************************************************************
* CONTROL FILE TABLE OF MEMBER NAMES WANTED                       *
*******************************************************************
         LTORG
CTLTBL   DC    8X'FF'
         DS    500CL8
CTLEND   DC    8X'FF'
         DS    CL80
         END

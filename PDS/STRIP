         TITLE '*** STRIP:                          ***'
STRIP CSECT
*********************************************************************
* STRIPS RECORDS FROM THE INPUT1 FILE AND WRITES OUTPUT TO THE      *
* OUTPUT1 FILE. SPECIALIZE FOR EACH USE.                            *
*                                                                   *
* AUTHOR R THORNTON JUN 2000                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING STRIP,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'STRIP'              PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES, INITIALIZE                                     *
*********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,OUTPUT2,OUTPUT,PRINT1,OUTPUT,INPUT1)
         MVC   OUT1REC,BLANKS       CLEAR OUTPUT1 RECORD AREA
         MVC   OUT2REC,BLANKS       CLEAR OUTPUT2 RECORD AREA
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,INREC         READ NEXT RECORD
         CLI   INREC,C'*'           COMMENT?
         BE    MAINLINE             YES, SKIP IT
         CLC   INJCLNM,BLANKS       ANY JCL NAME?
         BE    MLCALL               NO
         MVC   SAVEJCL,INJCLNM      YES, SAVE THE NAME
MLCALL   BAS   R9,SCANDATA          GO PROCESS THE DATA
         B     MAINLINE             CONTINUE
*********************************************************************
*             SCAN AND PROCESS THE INPUT DATA                       *
*********************************************************************
SCANDATA ST    R9,SD9               SAVE RETURN ADDRESS
         LA    R12,INDATA           POINT TO INDATA START
SDFPGMNM TRT   0(80,R12),TRTSTRT    SEARCH FOR PROGRAM NAME
         BZ    SDXIT                NONE FOUND, EXIT
         C     R1,INEND             FOUND WITHIN INREC?
         BNL   SDXIT                NO, EXIT
         ST    R1,PGMSTAD           SAVE PROGRAM START ADDRESS
         LA    R12,1(R1)            STEP PAST PROGRAM NAME START
SDFPGMND TRT   0(80,R12),TRTEND     LOOK FOR END OF NAME
         BZ    SDXIT                EXIT IF NOT FOUND
         C     R1,INEND             FOUND WITHIN INREC?
         BNL   SDXIT                NO, EXIT
         ST    R1,PGMNDAD           YES, SAVE END OF NAME ADDRESS
         CLI   0(R1),C'F'           AT AN 'F'?
         BNE   SDPGATND             NO
         CLC   0(5,R1),=CL5'FILE5'  THIS A FILE PARAMETER?
         BE    SDBADFL              YES
         LA    R12,1(R1)            NO, STEP PAST THE F
         B     SDFPGMND             CONTINUE
SDBADFL  MVC   PRT(20),=CL20'MISSING LEFT PAREN: '
         MVC   PRT+20(80),INREC     INREC TO PRINT
         BAS   R9,PRINT             GO PRINT THE LINE
SDPGATND LM    R10,R11,PGMADRS      GET PROGRAM NAME ADDRESSES
         SR    R11,R10              COMPUTE LENGTH
         CH    R11,=H'8'            NAME TOO LONG?
         BNH   SDSAVPGM             NO
         MVC   PRT(15),=CL15'NAME TOO LONG: '
         MVC   PRT+15(80),INREC     MOVE IN INREC
         BAS   R9,PRINT             GO PRINT
         B     SDXIT                AND EXIT
SDSAVPGM MVC   SAVEPGM,BLANKS       CLEAR FIELD
         BCTR  R11,0                COMPUTE MOVE LENGTH CODE
         EX    R11,SDMVPGM          MOVE PROGRAM NAME
         L     R12,PGMNDAD          GET END OF NAME ADDRESS
         CLI   0(R12),C'('          FOLLOWED BY FILE NUMBER?
         BE    SDFILNO              YES
         CLI   0(R12),C'F'          FOLLOWED BY "FILE5"
         BE    SDFILNO              YES
         BAS   R9,UTILNAME          NO, VENDOR SOFTWARE
         B     SDFPGMNM             GO GET NEXT NAME
SDFILNO  BAS   R9,PROGRAM           LOCALLY WRITTEN PROGRAM
         B     SDFPGMNM             GO GET NEXT NAME
SDXIT    L     R9,SD9               GET RETURN ADDRESS
         BR    R9                   RETURN TO CALLER
SD9      DC    A(0)                 RETURN ADDRESS
SDMVPGM  MVC   SAVEPGM(1),0(R10)    SAVE PROGRAM NAME
*********************************************************************
*             PROCESS LOCALLY WRITTEN PROGRAMS                      *
*********************************************************************
PROGRAM  ST    R9,PG9               SAVE RETURN ADDRESS
         CLI   0(R12),C'('          AT LEFT PARENTHESIS?
         BNE   PGATF                NO
         LA    R12,1(R12)           YES, STEP PAST IT
PGATF    CLC   0(5,R12),=CL5'FILE5' IS IT VALID FILE NUMBER?
         BNE   PGFLBAD              NO
         CLI   7(R12),C')'          VALID FILE NUMBER?
         BNE   PGFLBAD              NO
         CLI   8(R12),C','          VALID TERMINATOR?
         BE    PGOK                 YES
         CLI   8(R12),C' '          VALID TERMINATOR?
         BE    PGOK                 YES
PGFLBAD  MVC   PRT(20),=CL20'INVALID FILENUMBER: '
         MVC   PRT+20(80),INREC     MOVE INREC
         BAS   R9,PRINT             PRINT ERROR
         LA    R12,INEND            POINT PAST INREC
         B     PGXIT                AND EXIT
PGOK     MVC   OUT1FILE(7),0(R12)   PROGRAM NAME
         MVC   OUT1PGM,SAVEPGM      MOVE PROGRAM NAME
         MVC   OUT1JCL,SAVEJCL      MOVE JCL MEMBER NAME
         PUT   OUTPUT1,OUT1REC      WRITE OUTPUT1 RECORD
         MVC   OUT1REC,BLANKS       CLEAR OUTPUT1 RECORD AREA
         LA    R12,8(R12)           STEP PAST THE FILENAME
PGXIT    L     R9,PG9               GET RETURN ADDRESS
         BR    R9                   RETURN TO CALLER
PG9      DC    A(0)                 RETURN ADDRESS
*********************************************************************
*             PROCESS VENDOR SUPPLIED PROGRAMS                      *
*********************************************************************
UTILNAME ST    R9,UN9               SAVE RETURN ADDRESS
         MVC   OUT2PGM,SAVEPGM      MOVE PROGRAM NAME
         MVC   OUT2JCL,SAVEJCL      MOVE JCL MEMBER NAME
         PUT   OUTPUT2,OUT2REC      WRITE OUTPUT2 RECORD
         MVC   OUT2REC,BLANKS       CLEAR OUTPUT2 RECORD AREA
UNXIT    L     R9,UN9               GET RETURN ADDRESS
         BR    R9                   RETURN TO CALLER
UN9      DC    A(0)                 RETURN ADDRESS
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    ST    R9,PR9                  SAVE RETURN ADDRESS
         PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
PRXIT    L     R9,PR9                  GET RETURN ADDRESS
         BR    R9                      NO, RETURN
PR9      DC    A(0)                    RETURN ADDRESS
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (OUTPUT1,,OUTPUT2,,PRINT1,,INPUT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DS    F                       RETURN CODE
PGMADRS  DS    0F                   PROGRAM NAME ADDRESSES
PGMSTAD  DC    A(0)                   PROGRAM NAME START ADDRESS
PGMNDAD  DC    A(0)                   PROGRAM NAME END ADDRESS
BLANKS   DC    CL133' '                BLANKS
SAVEJCL  DS    CL8                  JCL MEMBER NAME
SAVEPGM  DS    CL8                  PROGRAM NAME
TRTSTRT  DC    256X'00'             TO FIND START OF PGM NAME
         ORG   TRTSTRT+C'@'
         DC    X'FF'
         ORG   TRTSTRT+C'#'
         DC    X'FF'
         ORG   TRTSTRT+C'$'
         DC    X'FF'
         ORG   TRTSTRT+C'A'
         DC    9X'FF'
         ORG   TRTSTRT+C'J'
         DC    9X'FF'
         ORG   TRTSTRT+C'S'
         DC    8X'FF'
         ORG   TRTSTRT+C'0'
         DC    10X'FF'
         ORG
*
TRTEND   DC    256X'00'             TO FIND PROGRAM NAME END
         ORG   TRTEND+C' '
         DC    X'FF'
         ORG   TRTEND+C','
         DC    X'FF'
         ORG   TRTEND+C'('
         DC    X'FF'
         ORG   TRTEND+C'F'
         DC    X'FF'
         ORG
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    CL132
*
         DS    0F
INREC    DS    0CL80                INPUT1 RECORD
INJCLNM  DS    CL8                     JCL MEMBER NAME
         DS    CL11                    " EXECUTES  "
INDATA   DS    CL61                    STRING DATA
INEND    DC    A(*)
*
         DS    0F
OUT1REC  DS    0CL80                OUTPUT1 RECORD
OUT1FILE DS    CL7                     "FILE5NN"
         DS    CL1                     BLANK
OUT1PGM  DS    CL8                     PROGRAM NAME
         DS    CL1                     BLANK
OUT1JCL  DS    CL8                     JCL MEMBER NAME
         DS    CL55
*
         DS    0F
OUT2REC  DS    0CL80                OUTPUT2 RECORD
OUT2PGM  DS    CL8                     PROGRAM NAME
         DS    CL1
OUT2JCL  DS    CL8                     JCL MEMBER NAME
         DS    CL63
*
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
OUTPUT2  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT2,                       X
               RECFM=FB,LRECL=80
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
         LTORG
STRIP CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

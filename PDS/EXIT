         MACRO
&L       EXIT  &LENGTH=72,&RENT=N,&RC=(15),&TRANSP=N
.*
.*      EXIT - ASSEMBLER PROGRAM EPILOG - REENTRANT/NON-REENTRANT
.*
.*      MACRO BY DAVID R. BIVENS - 04/17/79
.*
         LCLC  &T
         AIF   (0 EQ K'&L).NOLBL
&L       DS    0H
.NOLBL   ANOP
*        DECHAIN SAVE AREAS
         L     13,4(13)
&T       SETC  T'&RC
         AIF   ('(' EQ '&RC'(1,1)).RCREG
         AIF   ('&T' NE 'F' AND '&T' NE 'A').NOTF
         L     15,&RC
         AGO   .GOTRC
.NOTF    AIF   ('&T' NE 'H' AND '&T' NE 'Y').NOTH
         LH    15,&RC
         AGO   .GOTRC
.NOTH    AIF   ('&T' NE 'N' AND '&T' NE 'U').NOTU
         LA    15,&RC
         AGO   .GOTRC
.NOTU    AIF   (1 NE L'&RC).NOTIC
         IC    15,&RC
         AGO   .GOTRC
.NOTIC   ANOP
         MNOTE *,'RETURN CODE TYPE IGNORED - &RC'
         L     15,16(13)           *** UNPREDICTABLE RETURN CODE ***
         AGO   .GOTRC
.RCREG   ANOP
&T       SETC  '&RC'(2,K'&RC-2)
         LR    15,&T
         AGO   .GOTRC
.GOTRC   ANOP
         AIF   ('Y' EQ '&TRANSP').NOSAVE
         STM   15,1,16(13)         SAVE R0,R1,R15.  INCLUDES RET CODE.
         AGO   .RCSET
.NOSAVE  ANOP
         ST    15,16(13)           STORE RETURN CODE FOR LATER USE.
.RCSET   AIF   ('Y' EQ '&RENT').FREE
.LOAD    ANOP
         LM    14,12,12(13)        RESTORE CALLERS REGS AND RET CODE.
         BR    14                  EXIT...
         MEXIT
.FREE    ANOP
         L     1,8(13)             ADDRESS OF OUR SAVE AREA.
         SR    0,0                 INDICATE SUBPOOL 0.
&T       SETC  T'&LENGTH
         AIF   ('F' NE '&T' AND 'A' NE '&T').TRYH
         L     0,&LENGTH
         AGO   .GOTLEN
.TRYH    AIF   ('H' NE '&T' AND 'Y' NE '&T').TRYN
         LH    0,&LENGTH
         AGO   .GOTLEN
.TRYN    AIF   ('N' NE '&T' AND 'U' NE '&T').TRYZ
         LA    0,&LENGTH
         AGO   .GOTLEN
.TRYZ    ANOP
         MNOTE 4,'UNSUPPORTED LENGTH DATA TYPE - &LENGTH - 72 ASSUMED'
         LA    0,72                ***** ASSUMED SAVE AREA LENGTH *****
.GOTLEN  ANOP
*        FREEMAIN R,LV=(0),A=(1)
         FREEMAIN R,LV=(0),A=(1)
         AGO   .LOAD
         MEND

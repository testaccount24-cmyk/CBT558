         PRINT NOGEN
         TITLE '*****DELEJSPC: DELETE EJECT AND SPACE STATEMENTS ***'
DELEJSPC CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY ENTRY POINT ADDRESS
         USING DELEJSPC,R3             TELL ASSEMBLER R3 IS BASE
         LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              FORWARD POINTER TO CALLER'S SAVE
         ST    R13,SAVE+4              BACK POINTER TO MY SAVE
         LR    R13,R15                 MY SAVE AREA ADDRESS TO R13
         B     BEGIN                   BRANCH START INITIALIZATION
         DC    CL8'DELEJSPC'           PROGRAM NAME CONSTANT
SAVE     DS    18F                     MY REGISTER SAVE AREA
*
***********************************************************************
* READS AN ASSEMBLER SOURCE PROGRAM, LOCATES ALL EJECT AND SPACE      *
* STATEMENTS AND DELETES THEM FROM THE PROGRAM. ALL REMAINING         *
* STATEMENTS ARE COPIED WITHOUT CHANGE.                               *
*                                                                     *
* AUTHOR R THORNTON MAY 1989                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES
***********************************************************************
*
BEGIN    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*
***********************************************************************
* MAINLINE PROCESSING: READ SOURCE FILE RECORD. IF NOT EJECT OR       *
* SPACE, COPY IT TO OUTPUT.                                           *
***********************************************************************
*
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         CLI   INRECD,C' '             COLUMN 1 BLANK?
         BNE   WRITOUT                 YES, GO WRITE IT
         TRT   INRECD,FNDNONBK         NO, FIND FIRST NON-BLANK
         BZ    WRITOUT                 NONE FOUND, GO WRITE IT
         CLC   0(6,R1),=CL6'EJECT '    IS IT AN EJECT STATEMENT?
         BE    MAINLINE                YES, DROP IT
         CLC   0(6,R1),=CL6'SPACE '    IS IT A SPACE STATEMENT?
         BE    MAINLINE                YES, DROP IT
WRITOUT  PUT   OUTPUT1,INRECD          COPY RECORD TO OUTPUT
         B     MAINLINE                GO READ NEXT RECORD
*
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
FNDNONBK DC    256X'FF'                TRT TABLE TO SCAN NON-BLANK
         ORG   FNDNONBK+C' '
         DC    X'00'                   DON'T STOP AT BLANKS
         ORG
*
INRECD   DS    CL80                    INPUT RECORD
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

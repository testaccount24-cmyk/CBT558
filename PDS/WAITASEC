         TITLE '*****WAITASEC: WAIT CALLER SPECIFIED TIME *****'
****************************************************************Y2000C
* 08/11/1997 CB0PJKR REVIEWED CODE. RENOVATION WAS NOT REQUIRED Y2000C
****************************************************************Y2000C
WAITASEC AMODE 31
WAITASEC RMODE ANY
WAITASEC SVLNK R3,,,SUB
*
***********************************************************************
* CALLER PASSES AN UNSIGNED ZONED DECIMAL FIELD CONTAINING THE DESIRED*
* TIME TO WAIT. THE FIELD CONTAINS THE HOURS, MINUTES, SECONDS, AND   *
* HUNDREDTHS OF SECONDS TO WAIT. IN COBOL THE FIELD FORMAT IS:        *
*                                                                     *
*     05 WAIT-TIME.                                                   *
*        10 WAIT-HOURS            PIC 99 VALUE '00'.                  *
*        10 WAIT-MINUTES          PIC 99 VALUE '00'.                  *
*        10 WAIT-SECONDS          PIC 99 VALUE '05'.                  *
*        10 WAIT-HUNDREDTHS       PIC 99 VALUE '00'.                  *
*                                                                     *
* THE CALLER IS FREE TO SPECIFY ANY DESIRED TIME UP TO 23:59:59.99.   *
* IF THE INPUT FIELD IS NON-NUMERIC, HOURS GREATER THAN 23, MINUTES   *
* GREATER THAN 59, OR SECONDS GREATER THAN 59, AN ARBITRARY 5 SECOND  *
* WAIT WILL BE ISSUED WITH NO NOTIFICATION TO THE CALLER. RETURN CODE *
* IS ALWAYS ZERO FROM THIS SUBROUTINE.                                *
*                                                                     *
* AUTHOR R THORNTON JAN 1995                                          *
***********************************************************************
*                       **** MAINTENANCE CHANGES ****                 *
* REMOVED MODIFIED THE SVLNK MACRO TO ELIMINATE USE OF THE GETMAIN FOR*
* SAVEAREA, WHICH RESULTED IN S80A ABENDS AFTER MANY CALLS. 2/11/2002 *
* BY R THORNTON                                                       *
*                                                                     *
*                                                                     *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: GET NUMBER OF SECONDS TO WAIT FROM CALLER           *
***********************************************************************
*
         L     R1,0(R1)                POINT TO PARM FIELD
         MVC   WAITTIME,0(R1)          MOVE IT TO WORK AREA
         TRT   WAITTIME,CHKZONED       IS IT UNPACKED DECIMAL?
         BNZ   BADTIME                 NO
         CLC   WAITTIME(2),=C'23'      VALID HOURS?
         BH    BADTIME                 NO
         CLC   WAITTIME+2(2),=C'59'    VALID MINUTES?
         BH    BADTIME                 NO
         CLC   WAITTIME+4(2),=C'59'    VALID SECONDS?
         BH    BADTIME                 NO
         CLC   WAITTIME,=ZL8'00000000' VALID TIME (> ZERO)
         BH    WAITABIT                YES, GO WAIT
BADTIME  MVC WAITTIME,DEFAULT          BAD TIME, SET 5-SECOND DEFAULT
WAITABIT STIMER WAIT,DINTVL=WAITTIME   WAIT SPECIFIED TIME
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
         DS    0D
WAITTIME DC    ZL8'00000000'           WAIT TIME HHMMSSTH
DEFAULT  DC    ZL8'00000500'           DEFAULT TIME
CHKZONED DC    256X'FF'                TRT TABLE TO VALIDATE DECIMAL
         ORG   CHKZONED+C'0'
         DC    XL10'00'                DECIMAL DIGITS O.K.
         ORG
         END

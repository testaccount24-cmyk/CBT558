         TITLE '*** MSMCHMST: PREPARE ENDEVOR MASTER FILE ***'
MSMCHMST CSECT
*********************************************************************
* READS THE DATASET TEST.ENDEVOR.MASTER AND STRIPS OUT ALL ELEMENT  *
* RECORDS FOR PROGRAMS. THE RESULTING FILE IS SORTED IN ELEMENT     *
* NAME ORDER TO BE READ BY PROGRAM MSMCHRPT.                        *
*                                                                   *
* PURPOSE IS TO PROVIDE ENDEVOR INFORMATION FOR PROGRAMS ON THE     *
* MISMATCH REPORT.                                                  *
*                                                                   *
* AUTHOR R THORNTON JULY 2001                                       *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING MSMCHMST,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'MSMCHMST'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES, PRINT HEADING GET TABLE STORAGE                *
*********************************************************************
OPENS    OPEN  (ELEMENTS,OUTPUT,ENDVMSTR) OPENS
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   ENDVMSTR,ENDVRECD       READ ENDEVOR MASTER RECORD
         CLI   EMELEM,C' '             IS THERE AN ELEMENT NAME?
         BE    MAINLINE                NO, DROP THE RECORD
         CLI   EMENV,C' '              ANY ENVIRONMENT?
         BE    MAINLINE                NO, DROP THE RECORD
         CLI   EMSYS,C' '              ANY SYSTEM?
         BE    MAINLINE                NO, DROP THE RECORD
         CLI   EMSUB,C' '              ANY SUBSYSTEM?
         BE    MAINLINE                NO, DROP THE RECORD
         CLI   EMTYPE,C' '             ANY TYPE?
         BE    MAINLINE                NO, DROP THE RECORD
         CLI   EMSTAGE,C' '            ANY STAGE?
         BE    MAINLINE                NO, DROP THE RECORD
         CLC   EMBLK1,BLANKS           BLANKS WHERE EXPECTED?
         BNE   MAINLINE                NO, DROP THE RECORD
         CLC   EMBLK2,BLANKS           BLANKS WHERE EXPECTED?
         BNE   MAINLINE                NO, DROP THE RECORD
         CLC   EMBLK3,BLANKS           BLANKS WHERE EXPECTED?
         BNE   MAINLINE                NO, DROP THE RECORD
         CLC   EMBLK4,BLANKS           BLANKS WHERE EXPECTED?
         BNE   MAINLINE                NO, DROP THE RECORD
         CLC   EMBLK5,BLANKS           BLANKS WHERE EXPECTED?
         BNE   MAINLINE                NO, DROP THE RECORD
         LA    R1,TYPETABL             YES, POINT TO TYPE TABLE
MLCKEND  CLI   0(R1),X'FF'             END OF TYPE TABLE?
         BE    MAINLINE                YES, SKIP UNDESIRED TYPE
         CLC   EMTYPE,0(R1)            RECORD TYPE IS IN TABLE?
         BE    MLCLEAR                 YES, WANT THIS ONE.
         LA    R1,8(R1)                NO, STEP TO NEXT
         B     MLCKEND                 CONTINUE SEARCH
MLCLEAR  MVC   EMREST,BLANKS           CLEAR END OF RECORD
         PUT   ELEMENTS,ELEMRECD       WRITE ELEMENTS FILE RECORD
         B     MAINLINE                CONTINUE THROUGH THE INPUT FILE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (ELEMENTS,,ENDVMSTR)    CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DS    F'0'                    RETURN CODE
BLANKS   DC    CL80' '                 BLANKS
*
TYPETABL DS    0CL8                    ENDEVOR TYPE TABLE
         DC    CL8'COBOL   '
         DC    CL8'ASSEM   '
         DC    CL8'COMPOSIT'
         DC    CL8'CLIST   '
         DC    CL8'CLISTS  '
         DC    CL8'EZYTRIEV'
         DC    CL8'EZCOMPIL'
         DC    CL8'MACRO   '
         DC    CL8'SOURCE  '
         DC    CL8'FOCUS   '
         DC    CL8'ASSMTAB '
         DC    CL8'SAS     '
         DC    CL8'REXX    '
         DC    CL8'OBJECT  '
         DC    CL8'LINKEDIT'
         DC    CL8'COMMANDS'
         DC    CL8'OVERLAY '
         DC    CL8'SOVERLAY'
         DC    X'FF'
*
ENDVRECD DS    0CL133                  ENDEVOR MASTER RECORD (INPUT)
         DS    CL1                       CARRIAGE CONTROL BYTE
ELEMRECD DS    0CL80                   ELEMENTS RECORD (OUTPUT)
EMELEM   DS    CL8                       ENDEVOR ELEMENT NAME
EMBLK1   DS    CL4
EMENV    DS    CL8                       ENDEVOR ENVIRONMENT
EMBLK2   DS    CL2
EMSYS    DS    CL8                       ENDEVOR SYSTEM
EMBLK3   DS    CL2
EMSUB    DS    CL8                       ENDEVOR SUBSYSTEM
EMBLK4   DS    CL1
EMTYPE   DS    CL8                       ENDEVOR TYPE
EMBLK5   DS    CL1
EMSTAGE  DS    CL1                       ENDEVOR STAGE
EMREST   DS    CL81
*
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
ENDVMSTR DCB   DSORG=PS,MACRF=GM,DDNAME=ENDVMSTR,EODAD=EOJ,            X
               RECFM=FBA,LRECL=133
ELEMENTS DCB   DSORG=PS,MACRF=PM,DDNAME=ELEMENTS,                      X
               RECFM=FB,LRECL=80
         LTORG
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

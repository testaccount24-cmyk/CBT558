         TITLE '*** PDSVREPL: SAMPLE PROGRAM TO ADD A PDS MEMBER ***'
PDSVREPL CSECT
***********************************************************************
* USES BPAM WITH MACROS OPEN, BLDL, WRITE, CHECK, STOW, CLOSE, AND DCB*
* TO ADD OR REPLACE MEMBERS IN AN EXISTING RECFM=VB PDS.              *
*                                                                     *
* THE SYSIN FILE IS READ TO OBTAIN A MEMBER NAME. BLDL IS USED TO SEE *
* IF THE MEMBER ALREADY EXISTS. A NEW DIRECTORY ENTRY IS BUILT USING  *
* DATA FROM THE CURRENT DIRECTORY ENTRY IF IT EXISTS. A DATA BLOCK IS *
* WRITTEN WHICH CONTAINS A CLIST THAT WILL DISPLAY A MESSAGE TO ANY   *
* USER SAYING THE CLIST HAS BEEN ARCHIVED. A STOW IS THEN ISSUED TO   *
* EITHER ADD OR REPLACE THE CURRENT DIRECTORY ENTRY. ACTION REPEATS   *
* WHILE SYSIN CONTAINS MEMBER RECORDS. ANY ERROR CAUSES ABEND WITH AN *
* EXPLANATORY WTO.                                                    *
*                                                                     *
* AUTHOR R THORNTON AUGUST 1998                                       *
***********************************************************************
*
***********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS           *
***********************************************************************
         USING PDSVREPL,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'PDSVREPL'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
OPENS    OPEN  (PDSOUT,OUTPUT,SYSPRINT,OUTPUT,SYSIN) OPEN FILES
         TM    PDSOUT+36,X'50'          RECFM=VB?
         BO    CHECKBS                  YES
         WTO   'PDSOUT MUST HAVE RECFM=VB',ROUTCDE=11
ABEND111 ABEND 111,DUMP                 TERMINATE IN ERROR
CHECKBS  CLC   PDSOUT+62(2),OUTBDW      BLKSIZE SUFFICIENT?
         BNL   INITLIZE                 YES
         LH    R1,OUTBDW                GET OUTPUT BLOCK LENGTH
         CVD   R1,DBLWD                 CONVERT TO PACKED
         ED    BBKSIZE,DBLWD+5          EDIT BLKSIZE TO MESSAGE
         LA    R1,BADBKSZ               POINT TO MESSAGE
         SVC   35                       ISSUE WTO
         B     ABEND111                GO ABEND
INITLIZE STCK  DBLWD                   GET SYSTEM TOD CLOCK VALUE
         STCKCONV STCKVAL=DBLWD,CONVVAL=DATETIME GET TIME&DATE
         MVO   DATEPART,DATEPART       SHIFT DATE LEFT .5 BYTES
         OI    DATEPART+3,X'0F'        SET PACKED DECIMAL SIGN
         MVC   STOWCRDT,DATEPART       SET CREATION DATE
         MVC   STOWCHDT,DATEPART       SET CHANGED DATE
         MVC   STOWTIME,TIMEHH         SET CHANGED TIME HHMM
         MVC   SAVESTLS,STOWLIST       SAVE DEFAULT STOW LIST
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE BAS   R9,GETMEMNM             READ MEMBER NAME RECORD
         BAS   R9,BLDIRENT             GO BUILD DIRECTORY ENTRY
         BAS   R9,WRITMEMB             GO WRITE NEW MEMBER
         BAS   R9,STOWIT               GO CREATE NEW DIRECTORY ENTRY
         B     MAINLINE                LOOP TO PROCESS ALL MEMBERS
***********************************************************************
* BUILD DIRECTORY ENTRY: USE BLDL TO READ ANY EXISTING ENTRY. IF      *
* AN EXISTING MEMBER IS FOUND, THE DIRECTORY ENTRY IS USED IN BUILDING*
* THE ENTRY FOR THE NEW MEMBER. IF NOT, THE NEW ENTRY IS BUILT FROM   *
* SCRATCH.                                                            *
***********************************************************************
BLDIRENT MVC   BLDLNAME,MEMBNAME       MEMBER NAME TO BLDL LIST
         MVC   BLDLUDTA,STOWUDTA       INITIALIZE USER DATA
         BLDL  PDSOUT,BLDLIST          READ DIRECTORY ENTRY
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   NEWENTRY                NO
         MVI   EXIST,1                 YES, INDICATE EXISTING MEMBER
         MVC   STOWNAME,MEMBNAME       USE CONTROL CARD NAME
         MVI   STOWC,X'0F'             ALIAS/NOTES/USR DATA LENG
         MVC   STOWUDTA,BLDLUDTA       USER DATA
         IC    R1,STOWMLVL             INCREMENT
         LA    R1,1(R1)                   MODIFICATION
         STC   R1,STOWMLVL                   LEVEL
         MVC   STOWCHDT,DATEPART       TODAY'S DATE
         MVC   STOWTIME,TIMEHH         TIME (HHMM)
         MVC   STOWSIZE,=AL2(17)       NEW MEMBER SIZE
         MVC   STOWID,=CL8'BC0THOR'    NEW CHANGER ID
         BR    R9                      EXIT
NEWENTRY MVI   EXIST,0                 INDICATE NEW MEMBER
         MVC   STOWLIST(L'SAVESTLS),SAVESTLS USE DEFAULT STOW LIST
         MVC   STOWNAME,MEMBNAME       USE CONTROL CARD NAME
         BR    R9                      EXIT
***********************************************************************
* READ THE CONTROL FILE TO OBTAIN MEMBER NAME TO BE USED              *
***********************************************************************
GETMEMNM GET   SYSIN,CTLREC            READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
WRITMEMB MVC   OUTNAME,MEMBNAME        MEMBER NAME TO OUTPUT AREA
         WRITE WRITDECB,SF,PDSOUT,OUTBLOCK WRITE BLOCK TO OUTPUT
         CHECK WRITDECB                AWAIT COMPLETION
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             PRINT SUBROUTINE                                        *
***********************************************************************
PRINT    PUT   SYSPRINT,PRTLINE        WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      EXIT
***********************************************************************
* CREATE A DIRECTORY ENTRY FOR THIS NEW MEMBER USING THE STOW MACRO   *
***********************************************************************
STOWIT   CLI   EXIST,1                 IS THERE AN EXISTING MEMBER?
         BE    STOWREP                 YES
         STOW  PDSOUT,STOWLIST,A       NO, ADD DIRECTORY ENTRY
         LTR   R15,R15                 SUCCESSFUL?
         BZR   R9                      YES, EXIT
         B     STOWERRS                NO, ERROR
STOWREP  STOW  PDSOUT,STOWLIST,R       REPLACE DIRECTORY ENTRY
         LTR   R15,R15                 SUCCESSFUL?
         BZR   R9                      YES, EXIT
STOWERRS STM   R15,R1,SAVE3R           SAVE R15, R0, R1
         MVC   BADSTOWN,MEMBNAME       MEMBER NAME TO MESSAGE
         LA    R1,BADSTOW              POINT TO MESSAGE
         SVC   35                      ISSUE WTO
         ABEND 222,DUMP                TERMINATE IN ERROR
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (PDSOUT,,SYSPRINT,,SYSIN) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
*
DATETIME DS    0D                      CONVERTED DATE/TIME
TIMEHH   DS    XL1                       HOUR (HH)
TIMEMM   DS    XL1                       MINUTE (MM)
TIMESEC  DS    XL1                       SECONDS (SS)
TIMEFRAC DS    XL3                       DECIMAL FRACTION (6 PLACES)
         DS    XL2                       ZEROS
DATEPART DS    PL4                       DATE (YYYYDDD)
         DS    XL4                       RESERVED
*
SAVE3R   DS    3A                      REG SAVE
*
RETCODE  DS    F                       RETURN CODE
PRTLINE  DC    CL133' '                PRINT LINE
BLANKS   DC    CL133' '                BLANKS
EXIST    DC    X'00'                   01=MEMBER EXISTS IN PDS
*
BLDLIST  DS    0H                      BLDL LIST
         DC    H'1',H'76'               ONE MEMBER, 76 BYTES MAX
BLDLNAME DS    CL8                      MEMBER NAME
BLDLTTR  DS    XL3                      MEMBER TTR
BLDLKZ   DS    XL2                      FLAGS
BLDLC    DS    XL1                      ALIAS, NBR NOTES, USERLENG
BLDLUDTA DS    CL62                     USER DATA
*
STOWLIST DS    0H                      DIRECTORY ENTRY FOR NEW MEMBER
STOWNAME DC    CL8' '                   MEMBER NAME
         DC    XL3'00'                  MEMBER TTR (SET BY BPAM)
STOWC    DC    XL1'0F'                  ALIAS, NBR NOTES, USERLENG
STOWUDTA DS    0CL30                    USER DATA
STOWVER  DC    XL1'01'                   VERSION
STOWMLVL DC    XL1'00'                   MODIFICATION LEVEL
         DC    XL2'0000'                 ?????
STOWCRDT DC    PL4'0'                    CREATION DATE
STOWCHDT DC    PL4'0'                    LAST CHANGED DATE
STOWTIME DC    XL2'0000'                 LAST CHANGED TIME (HHMM)
STOWSIZE DC    AL2(17)                   NUMBER RECORDS IN MEMBER
STOWINIT DC    AL2(17)                   INITIAL NBR RECORDS
STOWMODS DC    XL2'0000'                 NUMBER MODIFICATIONS
STOWID   DC    CL8'BC0THOR'              LAST USERID
         DC    CL2'  '                   ????
STOWEND  EQU   *
*
SAVESTLS DS    CL(STOWEND-STOWLIST)      SAVE DEFAULT STOW LIST
*
CTLREC   DS    0CL80                   CONTROL RECORD
MEMBNAME DS    CL8                       MEMBER NAME
         DS    CL72
*
OUTBLOCK DS    0F                       OUTPUT BLOCK (VARIABLE BLOCKED)
OUTBDW   DC    AL2(ENDBLK-OUTBLOCK),AL2(0) BDW
OUTRDW1  DC    AL2(ENDREC1-OUTRDW1),AL2(0) RDW FOR RECORD 1
REC1     DC    C'PROC 0'
ENDREC1  EQU   *
OUTRDW2  DC    AL2(ENDREC2-OUTRDW2),AL2(0) RDW FOR RECORD 2
REC2     DC    C'/**********************************************/'
ENDREC2  EQU   *
OUTRDW3  DC    AL2(ENDREC3-OUTRDW3),AL2(0) RDW FOR RECORD 3
REC3     DC    C'/* PURPOSE: ARCHIVAL MESSAGE                  */'
ENDREC3  EQU   *
OUTRDW4  DC    AL2(ENDREC4-OUTRDW4),AL2(0) RDW FOR RECORD 4
REC4     DC    C'/* AUTHOR: R THORNTON                         */'
ENDREC4  EQU   *
OUTRDW5  DC    AL2(ENDREC5-OUTRDW5),AL2(0) RDW FOR RECORD 5
REC5     DC    C'/* DIVISION: ISD, TECHNICAL SERVICES          */'
ENDREC5  EQU   *
OUTRDW6  DC    AL2(ENDREC6-OUTRDW6),AL2(0) RDW FOR RECORD 6
REC6     DC    C'/* DATE: 08/06/1998                           */'
ENDREC6  EQU   *
OUTRDW7  DC    AL2(ENDREC7-OUTRDW7),AL2(0) RDW FOR RECORD 7
REC7     DC    C'/**********************************************/'
ENDREC7  EQU   *
OUTRDW8  DC    AL2(ENDREC8-OUTRDW8),AL2(0) RDW FOR RECORD 8
REC8     DC    C'WRITE ******************************************'
ENDREC8  EQU   *
OUTRDW9  DC    AL2(ENDREC9-OUTRDW9),AL2(0) RDW FOR RECORD 9
REC9     DC    C'WRITE ******************************************'
ENDREC9  EQU   *
OUTRDW10 DC    AL2(ENDREC10-OUTRDW10),AL2(0) RDW FOR RECORD 10
REC10    DC    C'WRITE *** THE '
OUTNAME  DC    CL8' '
         DC    C' CLIST WAS MOVED TO    ***'
ENDREC10 EQU   *
OUTRDW11 DC    AL2(ENDREC11-OUTRDW11),AL2(0) RDW FOR RECORD 11
REC11    DC    C'WRITE *** ARCHIVE PDS SYS2.CLIST ON 08/06/98 ***'
ENDREC11 EQU   *
OUTRDW12 DC    AL2(ENDREC12-OUTRDW12),AL2(0) RDW FOR RECORD 12
REC12    DC    C'WRITE *** TO DETERMINE WHETHER IT IS BEING   ***'
ENDREC12 EQU   *
OUTRDW13 DC    AL2(ENDREC13-OUTRDW13),AL2(0) RDW FOR RECORD 13
REC13    DC    C'WRITE *** USED. PLEASE NOTIFY DICK THORNTON  ***'
ENDREC13 EQU   *
OUTRDW14 DC    AL2(ENDREC14-OUTRDW14),AL2(0) RDW FOR RECORD 14
REC14    DC    C'WRITE *** AT 354-4829 IF YOU NEED TO USE THIS***'
ENDREC14 EQU   *
OUTRDW15 DC    AL2(ENDREC15-OUTRDW15),AL2(0) RDW FOR RECORD 15
REC15    DC    C'WRITE *** CLIST AND WISH TO HAVE IT RESTORED.***'
ENDREC15 EQU   *
OUTRDW16 DC    AL2(ENDREC16-OUTRDW16),AL2(0) RDW FOR RECORD 16
REC16    DC    C'WRITE ******************************************'
ENDREC16 EQU   *
OUTRDW17 DC    AL2(ENDREC17-OUTRDW17),AL2(0) RDW FOR RECORD 17
REC17    DC    C'WRITE ******************************************'
ENDREC17 EQU   *
ENDBLK   EQU   *
*
BADBKSZ  DS    0F                      WTO LENGTH ERROR MESSAGE
         DC    AL2(BBKEND-BADBKSZ)     AREA LENGTH
         DC    X'8000'                 MCS FLAGS
         DC    C'PDSOUT BLKSIZE MUST BE AT LEAST'
BBKSIZE  DC    X'402020202020'         EDIT WORD FOR BLOCK SIZE
BBKEND   EQU   * END OF TEXT
         DC    X'0200'                 DESC=7
         DC    X'0020'                 ROUTCDE=11
*
BADSTOW  DS    0F                      WTO STOW ERROR MESSAGE
         DC    AL2(BSTEND-BADSTOW)     AREA LENGTH
         DC    X'8000'                 MCS FLAGS
         DC    C'STOW FAILED FOR MEMBER '
BADSTOWN DC    CL8' '                  MEMBER NAME
BSTEND   EQU   * END OF TEXT
         DC    X'0200'                 DESC=7
         DC    X'0020'                 ROUTCDE=11
*
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PDSOUT   DCB   DSORG=PO,MACRF=W,DDNAME=PDSOUT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FBA,LRECL=80
         LTORG
PDSVREPL CSECT
***********************************************************************
*             REGISTER EQUATES                                        *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

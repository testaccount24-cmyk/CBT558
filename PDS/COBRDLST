         TITLE '*****COBRDLST: PROCESS COBREAD LISTING *****'
COBRDLST SVLNK R3
         PRINT NOGEN
*
***********************************************************************
* READS THE LISTING FILE OUTPUT FROM COBREAD AND WRITES 3 FILES:      *
*   OUTPUT1: CONTAINS ONE RECORD FOR EACH MEMBER THAT CONTAINS ANY    *
*            COBOL CSECT COMPILED WITH NORES. EACH RECORD HAS THE     *
*            MEMBER NAME, "NORES", AND TYPES OF COBOL CSECTS FOUND,   *
*            "VS" AND/OR "C2"                                         *
*   OUTPUT2: CONTAINS ONE RECORD FOR EACH VS COBOL CSECT. EACH RECORD *
*            CONTAINS MEMBER NAME, CSECT NAME, "NORES" OR "     " AND *
*            "VS".                                                    *
*   OUTPUT3: CONTAINS ONE RECORD FOR EACH NON-COBOL CSECT. EACH RECORD*
*            CONTAINS MEMBER NAME, CSECT NAME, AND "NON-COBOL"        *
* AUTHOR R THORNTON SEP 1998                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
*
         OPEN  (OUTPUT1,OUTPUT,OUTPUT2,OUTPUT,OUTPUT3,OUTPUT,INPUT1)
*
***********************************************************************
* MAINLINE PROCESSING: READ COBREAD LISTING FILE. WITHIN EACH MEMBER  *
* LOOK FOR CSECTS CONTAINING NORES. IF FOUND, WRITE AN OUTPUT RECORD  *
***********************************************************************
MAINLINE GET   INPUT1,INREC            READ NEXT INPUT RECORD
         CLI   INREC,C'1'              THIS FIRST LINE OF HEADER?
         BNE   CHECKMEM                NO
SKIPHDR  GET   INPUT1,INREC            READ PAST HEADING
         CLC   INREC(5),=CL5' ----'    LAST LINE OF HEADER?
         BE    MAINLINE                YES, GET NEXT RECORD
         B     SKIPHDR                 NO, READ NEXT HEADER LINE
CHECKMEM CLI   IN1MEM,C' '             THIS A NEW MEMBER?
         BE    CHECKCS                 NO
         CLC   OUTNORES,BLANKS         MEMBER HAD NORES CSECT?
         BE    NEWMEM                  NO
         TRT   OUTMEM(1),NATNLCH       VALID MEMBER NAME?
         BNZ   NEWMEM                  NO, DROP RECORD
         TRT   OUTMEM+1(7),VALCH       VALID MEMBER NAME?
         BNZ   NEWMEM                  NO, DROP RECORD
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
NEWMEM   MVC   OUTREC,BLANKS           CLEAR OUTPUT RECORD
         MVC   OUTMEM,IN1MEM           MOVE IN MEMBER NAME
         B     MAINLINE                GO READ AGAIN
CHECKCS  CLC   IN2CS,=CL3'CS-'         THIS A CSECT RECORD?
         BNE   MAINLINE                NO, DROP IT
         CLC   IN2NOTA,=CL26'NOT A KNOWN COBOL LANGUAGE'
         BE    NOTCOB                  BRANCH IF NOT COBOL
         CLC   IN2VER,=CL2'VS'         VS COBOL?
         BE    ISVSCBL                 YES
         CLC   IN2VER,=CL2'V4'         VS COBOL RELEASE 4?
         BNE   CHKCOB2                 NO
ISVSCBL  MVC   OUTVS,IN2VER            YES, MOVE COBOL VERSION
         MVC   OU2REC,BLANKS           CLEAR OUTPUT2 RECORD
         MVC   OU2MEM,OUTMEM           COPY MEMBER NAME
         MVC   OU2CSECT,IN2CSECT       COPY CSECT NAME
         MVC   OU2NORES,IN2RES         COPY RES/NORES OPTION
         CLC   OU2NORES,=CL7'NOT-RES'  IS IT NORES?
         BNE   MOV2VER                 NO
         MVC   OU2NORES,=CL5'NORES'    YES, MOVE NORES
MOV2VER  MVC   OU2VER,IN2VER           COPY COBOL VERSION
         TRT   OU2MEM(1),NATNLCH       VALID MEMBER NAME?
         BNZ   CHKNORES                NO, DROP RECORD
         TRT   OU2MEM+1(7),VALCH       VALID MEMBER NAME?
         BNZ   CHKNORES                NO, DROP RECORD
         PUT   OUTPUT2,OU2REC          WRITE OUTPUT2 RECORD
         B     CHKNORES                CONTINUE
CHKCOB2  CLC   IN2VER,=CL2'C2'         COBOL II?
         BNE   BADVER                  NO
         MVC   OUTC2,IN2VER            YES, MOVE COBOL VERSION
         B     CHKNORES                CONTINUE
BADVER   MVC   OUTBAD,=C'??'           SHOW UNKNOWN COBOL VERSION
CHKNORES CLC   IN2RES,=CL7'NOT-RES'    IS IT A NORES CSECT?
         BNE   MAINLINE                NO
         MVC   OUTNORES,=CL5'NORES'    YES, SHOW NORES
         B     MAINLINE                GO READ NEXT RECORD
NOTCOB   MVC   OU3MEM,OUTMEM           COPY MEMBER NAME
         MVC   OU3CSECT,IN2CSECT       COPY CSECT NAME
         TRT   OU2MEM(1),NATNLCH       VALID MEMBER NAME?
         BNZ   MAINLINE                NO, DROP RECORD
         TRT   OU2MEM+1(7),VALCH       VALID MEMBER NAME?
         BNZ   MAINLINE                NO, DROP RECORD
         PUT   OUTPUT3,OU3REC          WRITE OUTPUT3 RECORD
         B     MAINLINE                GO READ NEXT RECORD
*
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,OUTPUT2,,OUTPUT3,,INPUT1) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
DBLWD    DS    D                       WORK AREA
H0       DC    H'0'                    CONSTANT ZERO
H6       DC    H'6'                    CONSTANT 6
H11      DC    H'11'                   CONSTANT 11
CTR      DC    H'0'                    ILB COUNTER
BLANKS   DC    CL80' '                 BLANKS
*
INREC    DS    0CL133                  INPUT RECORD
IN1REC   DS    0CL133                    MEMBER RECORD
         DS    CL2
IN1MEM   DS    CL8                         MEMBER NAME
         DS    CL1
IN1TTRC  DS    CL8                         TTRC
         DS    CL1
IN1LATTR DS    CL4                         L-ATTR
         DS    CL1
IN1LGTH  DS    CL6                         LOAD MODULE LENGTH
         DS    CL1
IN1EP    DS    CL6                         ENTRY POINT OFFSET
         DS    CL1
IN1LDATE DS    CL6                         LINKEDIT DATE YY.DDD
         DS    CL7' RMODE '
IN1RMODE DS    CL3                         ANY, 24, OR 31
         DS    CL78
         ORG   IN1REC
IN2REC   DS    0CL133                    CSECT RECORD
         DS    CL5
IN2CS    DS    CL3    COL6               "CS-"
IN2CSECT DS    CL8    COL9               CSECT NAME
         DS    CL1
IN2LENG  DS    CL6    COL18              CSECT LENGTH
         DS    CL2
IN2DATE  DS    CL8    COL26              DATE COMPILED MM/DD/YY
         DS    CL1
IN2TIME  DS    CL8    COL35              TIME COMPILED HH.MM.SS
         DS    CL5' '
IN2NOTA  DS    0CL26  COL48              "NOT A KNOWN COBOL LANGUAGE"
IN2DATA  DS    CL8'DATA-31 '
IN2DTA31 DS    CL2                       "NO" OR "  "
         DS    CL3
IN2STFD  DS    CL3    COL61              "YES" OR "   "
         DS    CL3
IN2TEST  DS    CL3    COL67              "YES" OR "   "
         DS    CL2
IN2RES   DS    CL7    COL72              "NOT-RES" OR "       "
         DS    CL1
IN2EJFS  DS    CL7    COL80
         DS    CL1
IN2OBDY  DS    CL6    COL88
         DS    CL1
IN2OPTIM DS    CL7    COL95
         DS    CL2
IN2VER   DS    CL2    COL104     "VS" "C2" OR "  "
         DS    CL2
IN2COUAW DS    CL5    COL107
         DS    CL1
IN2TRCMP DS    CL5    COL113
         DS    CL1
IN2CASS  DS    CL7    COL 119
         DS    CL4
IN2OPST  DS    CL4    COL130
         ORG
*
OUTREC   DS    0CL80                   OUTPUT1 RECORD
OUTMEM   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
OUTNORES DC    CL5' '                    COMPILE RES/NORES OPTION
         DC    CL1' '
OUTVS    DC    CL2' '                    VS = CONTAINS VS COBOL
         DC    CL1' '
OUTC2    DC    CL2' '                    C2 = CONTAINS COBOL II
         DC    CL1' '
OUTBAD   DC    CL2' '                    ?? = UNIDENTIFIABLE COBOL
         DC    CL57' '
*
OU2REC   DS    0CL80                   OUTPUT2 RECORD
OU2MEM   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
OU2CSECT DC    CL8' '                    CSECT NAME
         DC    CL1' '
OU2VER   DC    CL2' '                    COBOL VERSION
         DC    CL1' '
OU2NORES DC    CL5' '                    RES/NORES OPTION
         DC    CL54' '
*
OU3REC   DS    0CL80                   NON-COBOL OUTPUT3 RECORD
OU3MEM   DS    CL8                       MEMBER NAME
         DC    CL1' '
OU3CSECT DS    CL8                       CSECT NAME
         DC    CL63' NON-COBOL'
*
NATNLCH  DC    256X'FF'      NATIONAL CHARACTERS
         ORG   NATNLCH+C' '    BLANK
         DC    X'00'
         ORG   NATNLCH+C'$'    DOLLAR SIGN
         DC    X'00'
         ORG   NATNLCH+C'#'    POUNDS SIGN
         DC    X'00'
         ORG   NATNLCH+C'@'    AT SIGN
         DC    X'00'
         ORG   NATNLCH+C'A'    ALPHABET A-I
         DC    9X'00'
         ORG   NATNLCH+C'J'    ALPHABET J-R
         DC    9X'00'
         ORG   NATNLCH+C'S'    ALPHABET S-Z
         DC    8X'00'
         ORG
VALCH    DC    256X'FF'        VALID NAME CHARACTERS AFTER 1ST
         ORG   VALCH+C' '      BLANK
         DC    X'00'
         ORG   VALCH+C'$'      DOLLAR SIGN
         DC    X'00'
         ORG   VALCH+C'-'      HYPHEN
         DC    X'00'
         ORG   VALCH+C'#'      POUNDS SIGN
         DC    X'00'
         ORG   VALCH+C'@'      AT SIGN
         DC    X'00'
         ORG   VALCH+C'A'      ALPHABET A-I
         DC    9X'00'
         ORG   VALCH+C'J'      ALPHABET J-R
         DC    9X'00'
         ORG   VALCH+C'S'      ALPHABET S-Z
         DC    8X'00'
         ORG   VALCH+C'0'      DIGITS 0-9
         DC    10X'00'
         ORG
*
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
OUTPUT2  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT2,                       X
               RECFM=FB,LRECL=80
OUTPUT3  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT3,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=133
         END

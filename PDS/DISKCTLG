         TITLE '***** CHECK IF DATASETS IN VTOC ARE CATALOGED *****'
DISKCTLG SVLNK R3
***********************************************************************
* THE JFCB IS READ FOR DDNAME VTOC. THE DSNAME FIELD OF THE JFCB IS   *
* SET TO HEX 04'S TO CAUSE THE VTOC TO BE ALLOCATED. THE VTOC IS READ,*
* AND FOR EACH FORMAT 1 DSCB, A LOCATE MACRO IS ISSUED TO CHECK IF    *
* THE DATASET IS CATALOGED OR NOT. ONE LINE IS OUTPUT PER DATASET     *
* SHOWING WHETHER THE DSNAME IS CATALOGED, AND IF IT IS ON THE VOLUME *
* BEING CHECKED.                                                      *
*                                                                     *
* AUTHOR R. THORNTON   APR 1992                                       *
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         XC    JFCBDSNM,JFCBDSNM       CLEAR JFCB DSNAME FOR CHECK
         RDJFCB VTOC                   READ THE JFCB
         CLC   JFCBDSNM(8),ZEROS       WAS JFCB READ
         BNE   SETVTOC                 YES, DSNAME NOT ZEROS
         MVC   PRTLINE(16),=C'NO JFCB READ FOR VTOC'
         PUT   SYSPRINT,PRTLINE        PRINT THE MESSAGE
         B     EOJ                     TERMINATE
SETVTOC  MVI   JFCBDSNM,4              DATA SET NAME FOR FMT 4 DSCB
         MVC   JFCBDSNM+1(43),JFCBDSNM DATA SET NAME FOR FORMAT 4 DSC
         MVI   JFCBTSDM,X'08'          SUPPRESS JFCB REWRITE
         OPEN  VTOC,TYPE=J             OPEN USING MODIFIED JFCB
         L     R1,VTOC+44              GET DEB ADDRESS
         MVC   NEXCCHH,38(R1)          MOVE BEGIN CCHH TO NEXT RECADR
         MVI   NEXREC,1                BEGIN RECORD IS 1
         L     R15,32(R1)              POINT TO THE UCB
         MVC   VOLSER,28(R15)          SAVE VOLUME SERIAL NUMBER
         PUT   SYSPRINT,HDRLINE        WRITE HEADER LINE
SEQREAD  CLI   VTECB,X'42'             EXTENT VIOLATION
         BE    EOJ                     YES, END OF VTOC
         L     R5,CURREC               POINT TO CURRENT RECORD
         C     R5,LASTREC              ANY MORE RECORDS IN MEMORY?
         BNH   CKFORMAT                YES, GO PROCESS
         LA    R5,DSCB00               POINT TO 1ST DSCB AREA
         ST    R5,CURREC               STORE AS CURRENT RECORD
         MVC   VTSRCH,NEXRECA          MOVE IN NEXT REC ADDR
         EXCP  VTOCIOB                 CALL O/S FOR READ
         WAIT  ECB=VTECB               WAIT FOR READ
         CLI   VTECB,X'7F'             ANY ERROR
         BE    CKFORMAT                NO, GO CHECK
         CLI   VTECB,X'42'             EXTENT VIOLATION
         BE    SETLAST                 YES, GO SET LAST RECORD ADDR
IOERROR  MVC   PRTLINE(22),=C'I/O ERROR READING VTOC'
         PUT   SYSPRINT,PRTLINE        WRITE ERROR MESSAGE
         ABEND 104,DUMP
SETLAST  L     R15,VTNXCCW             POINT TO NEXT CCW
         SH    R15,=H'8'               BACK UP TO LAST CCW EXEC
         C     R15,FSTCCW              ANY READS EXECUTED?
         BNH   EOJ                     NO
         MVC   LASTREC+1(3),1(R15)     RESET LAST RECORD ADDRESS
         USING DSCBFMT1,R5
CKFORMAT CLI   DS1FMTID,C'1'           IS IT FORMAT 1
         BNE   STEPNEXT                NO STEP TO NEXT DSCB
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         MVC   DSNAME,DS1DSNAM         MOVE DSNAME TO PRINT LINE
         SR    R15,R15                 CLEAR WORK REG
         IC    R15,DS1CREDT            GET CREATE YEAR
         CVD   R15,DBLWD               CONVERT TO PACKED
         UNPK  CRDATE(2),DBLWD+6(2)    UNPACK TO PRINT
         OI    CRDATE+1,C'0'           CLEAR SIGN
         MVI   CRDATE+2,C'/'           DELIMITER
         ICM   R15,3,DS1CREDT+1        GET CREATE DAY
         CVD   R15,DBLWD               CONVERT TO PACKED
         UNPK  CRDATE+3(3),DBLWD+6(2)  UNPACK TO PRINT
         OI    CRDATE+5,C'0'           CLEAR SIGN
         SR    R15,R15                 CLEAR WORK REG
         IC    R15,DS1REFD             GET LAST REFERENCED DATE
         CVD   R15,DBLWD               CONVERT TO PACKED
         UNPK  LRDATE(2),DBLWD+6(2)    UNPACK TO PRINT
         OI    LRDATE+1,C'0'           CLEAR SIGN
         MVI   LRDATE+2,C'/'           DELIMITER
         ICM   R15,3,DS1REFD+1         GET LAST REFERENCED DAY
         CVD   R15,DBLWD               CONVERT TO PACKED
         UNPK  LRDATE+3(3),DBLWD+6(2)  UNPACK TO PRINT
         OI    LRDATE+5,C'0'           CLEAR SIGN
         LOCATE CHKCTLG                READ CATALOG INFORMATION
         LTR   R15,R15                 ANY DATA FOUND?
         BNZ   NOCTLG                  NO
         CLC   VOLCNT,=H'1'            ONLY ONE VOLUME RETURNED?
         BNE   MULTVOL                 NO, MORE THAN 1
         MVC   CATVOL,VOLVOL           MOVE CATALOGED VOLSER
         CLC   CATVOL,VOLSER           CATALOGED ON THIS VOLUME?
         BE    WRITLINE                YES
         B     NOCTLG                  NO, SET FLAG
MULTVOL  MVC   CATVOL,=CL6'*MULT*'     SHOW MULTIPLE VOLUMES
NOCTLG   MVI   PERR,C'*'               SHOW CATALOG EXCEPTION
         ST    R15,DBLWD               SAVE RETURN CODE
         UNPK  PRETCOD(9),DBLWD(5)     UNPACK RETURN CODE
         MVI   PRETCOD+8,C' '          CLEAR TRASH BYTE
         TR    PRETCOD,TRCHAR-C'0'     MAKE PRINTABLE
WRITLINE PUT   SYSPRINT,PRTLINE        PRINT IT
STEPNEXT LA    R5,140(R5)              POINT TO NEXT DSCB
         ST    R5,CURREC               SAVE NEXT ADDRESS
         B     SEQREAD                 GO READ AGAIN
         DROP  R5
**********************************************************************
*        END OF JOB                                                  *
**********************************************************************
EOJ      EQU   *                       *** END OF JOB ***
         CLOSE (VTOC,,SYSPRINT)        CLOSE OUTPUT FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDRESS
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
**********************************************************************
*            *** DATA, WORK, FILE, AND STORAGE AREAS ***             *
**********************************************************************
*
CMDCHN   DS    0D                      COMMAND CHAIN TO READ VTOC
         CCW   X'31',VTSRCH,X'40',5    SEARCH ID EQUAL
         CCW   8,*-8,0,0               NOT FOUND, REPEAT SEARCH
READCMD  CCW   X'8E',DSCB00,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB01,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB02,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB03,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB04,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB05,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB06,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB07,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB08,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB09,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB10,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB11,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB12,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB13,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB14,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB15,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB16,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB17,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB18,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB19,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB20,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB21,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB22,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB23,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB24,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB25,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB26,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB27,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB28,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB29,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB30,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB31,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB32,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB33,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB34,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB35,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB36,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB37,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB38,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB39,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB40,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB41,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB42,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB43,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB44,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB45,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB46,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB47,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB48,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB49,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB50,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB51,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB52,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB53,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB54,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB55,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB56,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB57,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB58,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB59,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB60,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB61,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB62,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB63,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB64,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB65,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB66,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB67,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB68,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB69,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB70,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB71,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB72,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB73,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB74,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB75,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB76,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB77,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB78,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB79,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB80,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB81,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB82,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB83,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB84,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB85,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB86,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB87,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB88,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB89,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB90,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB91,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB92,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB93,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB94,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB95,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB96,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB97,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB98,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'8E',DSCB99,X'40',140  READ KEY AND DATA INTO DSCB
         CCW   X'92',NEXCNT,0,8        READ COUNT OF NEXT RECORD, M-T
*
*
*
FSTCCW   DC    A(READCMD)              @ 1ST READ CCW
CURREC   DC    A(DSCB99+140)           @ CURRENT VTOC RECORD
LASTREC  DC    A(DSCB99)               @ LAST VTOC RECORD
NEXCNT   DS    0D                      COUNT RECORD OF NEXT RECORD
NEXRECA  DS    0XL5                    CCHHR ADDRESS OF NEXT RECORD
NEXCCHH  DS    XL4                     CCHH ADDRESS OF NEXT RECORD
NEXREC   DS    XL1                     RECORD NBR ON TRACK
         DS    XL3                     KEY LENG AND DATA LENG
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
DBLWD    DS    D                       DOUBLEWORD WORK AREA
ZEROS    DC    D'0'
VOLSER   DS    CL6                     DISK VOLUME SERIAL NUMBER
BLANKS   DC    CL80' '                 BLANKS
TRCHAR   DC    CL16'0123456789ABCDEF'  TRANSLATE TO MAKE PRINTABLE
*
*
*
CHKCTLG  CAMLST NAME,DSNAME,,VOLAREA   CAMLST TO READ CATALOG DATA
VOLAREA  DS    0D                      VOLUME LIST RETURNED BY LOCATE
VOLCNT   DS    H                       NUMBER OF VOLUMES RETURNED
VOLNTRY  DS    0CL12                   VOLUME ENTRY (1 OF 20 MAX)
VOLDEV   DS    CL4                       DEVICE TYPE
VOLVOL   DS    CL6                       VOLUME SERIAL NUMBER
VOLDSSEQ DS    H                         DATASET SEQUENCE NBR
         DS    19CL12                  REMAINING 19 ENTRY FIELDS
         DS    CL80
*
*
*
PRTLINE  DS    0CL80                   PRINT LINE OUTPUT
PERR     DS    CL1                     CATALOG ERROR = *
DSNAME   DS    CL44                    DSNAME
         DS    CL1
CATVOL   DS    CL6                     VOLUME SERIAL FROM CATALOG
         DS    CL1
CRDATE   DS    CL6                     CREATION DATE
         DS    CL1
LRDATE   DS    CL6                     LAST REFERENCED DATE
         DS    CL1
         DS    CL5
PRETCOD  DS    CL8                     RETURN CODE FROM LOCATE
*
*
*
HDRLINE  DS    0CL80                   HEADING LINE OUTPUT
         DC    CL1' '                  CATALOG ERROR BYTE
         DC    CL44'DATASET NAME'      DSNAME
         DC    CL1' '
         DC    CL6'CATVOL '            VOLUME SERIAL FROM CATALOG
         DC    CL1' '
         DC    CL6'CRDATE '            CREATION DATE
         DC    CL1' '
         DC    CL6'LRDATE '            LAST REFERENCED DATE
         DC    CL1' '
         DC    CL5' '
         DC    CL8'LOCRETCD'
*
*
*
         JFCBDEF DSECT=NO
VTOC     DCB   DDNAME=VTOC,MACRF=E,EXLST=JFCBADDR
*
*
*
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
SYSPRINT DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,DDNAME=SYSPRINT
         DSCBDEF FMT2=NO,FMT3=NO,FMT4=NO,FMT5=NO,FMT6=NO,DSECT=YES
         LTORG
DSCB00   DS    CL140
DSCB01   DS    CL140
DSCB02   DS    CL140
DSCB03   DS    CL140
DSCB04   DS    CL140
DSCB05   DS    CL140
DSCB06   DS    CL140
DSCB07   DS    CL140
DSCB08   DS    CL140
DSCB09   DS    CL140
DSCB10   DS    CL140
DSCB11   DS    CL140
DSCB12   DS    CL140
DSCB13   DS    CL140
DSCB14   DS    CL140
DSCB15   DS    CL140
DSCB16   DS    CL140
DSCB17   DS    CL140
DSCB18   DS    CL140
DSCB19   DS    CL140
DSCB20   DS    CL140
DSCB21   DS    CL140
DSCB22   DS    CL140
DSCB23   DS    CL140
DSCB24   DS    CL140
DSCB25   DS    CL140
DSCB26   DS    CL140
DSCB27   DS    CL140
DSCB28   DS    CL140
DSCB29   DS    CL140
DSCB30   DS    CL140
DSCB31   DS    CL140
DSCB32   DS    CL140
DSCB33   DS    CL140
DSCB34   DS    CL140
DSCB35   DS    CL140
DSCB36   DS    CL140
DSCB37   DS    CL140
DSCB38   DS    CL140
DSCB39   DS    CL140
DSCB40   DS    CL140
DSCB41   DS    CL140
DSCB42   DS    CL140
DSCB43   DS    CL140
DSCB44   DS    CL140
DSCB45   DS    CL140
DSCB46   DS    CL140
DSCB47   DS    CL140
DSCB48   DS    CL140
DSCB49   DS    CL140
DSCB50   DS    CL140
DSCB51   DS    CL140
DSCB52   DS    CL140
DSCB53   DS    CL140
DSCB54   DS    CL140
DSCB55   DS    CL140
DSCB56   DS    CL140
DSCB57   DS    CL140
DSCB58   DS    CL140
DSCB59   DS    CL140
DSCB60   DS    CL140
DSCB61   DS    CL140
DSCB62   DS    CL140
DSCB63   DS    CL140
DSCB64   DS    CL140
DSCB65   DS    CL140
DSCB66   DS    CL140
DSCB67   DS    CL140
DSCB68   DS    CL140
DSCB69   DS    CL140
DSCB70   DS    CL140
DSCB71   DS    CL140
DSCB72   DS    CL140
DSCB73   DS    CL140
DSCB74   DS    CL140
DSCB75   DS    CL140
DSCB76   DS    CL140
DSCB77   DS    CL140
DSCB78   DS    CL140
DSCB79   DS    CL140
DSCB80   DS    CL140
DSCB81   DS    CL140
DSCB82   DS    CL140
DSCB83   DS    CL140
DSCB84   DS    CL140
DSCB85   DS    CL140
DSCB86   DS    CL140
DSCB87   DS    CL140
DSCB88   DS    CL140
DSCB89   DS    CL140
DSCB90   DS    CL140
DSCB91   DS    CL140
DSCB92   DS    CL140
DSCB93   DS    CL140
DSCB94   DS    CL140
DSCB95   DS    CL140
DSCB96   DS    CL140
DSCB97   DS    CL140
DSCB98   DS    CL140
DSCB99   DS    CL140
         END

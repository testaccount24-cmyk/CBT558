         TITLE '*****COPY: COPY ONE FILE TO ANOTHER *****'
COPY SVLNK R3
         PRINT NOGEN
*
***********************************************************************
* COPY ONE FILE OF ONE RECFM/LRECL/BLKSIZE TO ANOTHER OF THE SAME OR  *
* DIFFERENT ATTRIBUTES. PROGRAM SHOULD BE RE-SPECIALIZED FOR EACH USE.*
* AUTHOR R THORNTON APR 1992                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
*
         OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
SKIPCOMT BAL   R14,READ2               READ INPUT2
         CLC   IN2REC+1(17),=CL17'SUMMARY OF GROUPS'
         BE    SKIPCOMT                YES
         BAL   R14,READ1               READ FIRST INPUT1 RECORD
*
***********************************************************************
* MAINLINE PROCESSING: LOCATE NEXT INPUT2 GROUP AND PROCESS IT.       *
***********************************************************************
*
MAINLINE BAL   R14,READ2               GO READ INPUT2 FILE
         CLC   IN2REC+1(20),BLANKS     POSSIBLE PROGRAM CONTINUATION?
         BNE   MLCKGRP                 NO
         CLI   INPROGS,1               NOW PROCESSING A PROGRAM LIST?
         BE    MLGOTPGM                YES
MLCKGRP  MVI   INPROGS,0               TURN OFF PROGRAM IN PROGRESS
         CLC   IN2REC+1(12),=CL12'GROUP NAME: ' GROUP NAME HEADER?
         BE    MLGOTGRP                YES
         CLC   IN2REC+1(10),=CL10'PROGRAMS: ' PROGRAMS HEADER?
         BE    MLGOTPGM                YES
         CLC   IN2REC+1(17),=CL17'OBJECTS IN GROUPS' OBJECTS/GRP HDR?
         BNE   MAINLINE                NO, SKIP THIS RECORD
         CLC   IN1GROUP,=CL8'ZRW08'    PROCESSING ZRW08?
         BE    EOJ                     YES, O.K.
         MVC   RETCODE,=F'8'           SET RETURN CODE = 8
         MVC   PRTLINE+1(24),=CL24'ALL GROUPS MISSED AFTER '
         MVC   PRTLINE+24(8),IN1GROUP  GROUP NAME TO MESSAGE
         BAL   R14,PRINT               PRINT THE MESSAGE
         B     EOJ                     AND QUIT
MLGOTGRP MVC   LASTGRP,IN2REC+13       SAVE NEW GROUP NUMBER
         CLC   IN1GROUP,IN2REC+13      INPUT2 GROUP MATCHES INPUT1?
         BNL   MAINLINE                INPUT1 IS HIGH OR EQUAL
         CLI   IN1PROG,C' '            PROGRAM FOUND FOR IN1GROUP?
         BNE   MLREAD1                 YES
         MVC   PRTLINE+1(28),=CL28'NO PROGRAMS FOUND FOR GROUP '
         MVC   PRTLINE+28(8),IN1GROUP  GROUP NAME TO MESSAGE
         BAL   R14,PRINT               PRINT EMPTY GROUP MESSAGE
         MVC   RETCODE,=F'8'           SET RETURN CODE = 8
MLREAD1  BAL   R14,READ1               GO READ NEXT INPUT1 RECORD
         B     MAINLINE                CONTINUE
MLGOTPGM MVI   INPROGS,1               SHOW PROGRAM LIST IN PROGRESS
         CLC   IN1GROUP,LASTGRP        INPUT2 GROUP MATCHES INPUT1?
         BNE   MAINLINE                NO, IGNORE IT
         LA    R6,IN2REC+21            POINT TO 1ST PROGRAM NAME
MLTESTPG CLI   0(R6),C' '              ANY NAME?
         BE    MLENDPGM                NO, THIS LINE FINISHED
         MVC   IN1PROG,0(R6)           MOVE IN PROGRAM NAME
         BAL   R14,WRITE1              AND WRITE THE RECORD
         LA    R6,10(R6)               STEP TO NEXT PROGRAM NAME
         B     MLTESTPG                CONTINUE
MLENDPGM CLI   IN2REC+101,C' '         WAS THERE A NAME IN POS 102?
         BE    MAINLINE                NO
         MVI   INPROGS,1               SHOW PROGRAM LIST IN PROGRESS
         B     MAINLINE                NO
***********************************************************************
*        READ NEXT RECORD FROM INPUT1 FILE                            *
***********************************************************************
READ1    ST    R14,RD1SAVE             SAVE RETURN ADDRESS
         GET   INPUT1,IN1REC           READ NEXT INPUT1 RECORD
R1XIT    L     R14,RD1SAVE             GET RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
RD1SAVE  DC    A(0)                    RETURN ADDRESS
***********************************************************************
*        READ NEXT RECORD FROM INPUT2 FILE                            *
***********************************************************************
READ2    ST    R14,RD2SAVE             SAVE RETURN ADDRESS
R2GET    GET   INPUT2,IN2REC           READ NEXT INPUT2 RECORD
         CLC   IN2REC(12),=CL12'1CICS/ESA   ' HEADER LINE?
         BE    R2GET                   YES, SKIP IT
         CLC   IN2REC+1(132),BLANKS    BLANK LINE?
         BE    R2GET                   YES, SKIP IT
R2XIT    L     R14,RD2SAVE             GET RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
EOF2RTN  WTO   'EARLY END ON INPUT2',ROUTCDE=11
         ABEND 777,DUMP                TERMINATE IN ERROR
RD2SAVE  DC    A(0)                    RETURN ADDRESS
***********************************************************************
*        WRITE NEXT OUTPUT1 RECORD FROM IN1REC                        *
***********************************************************************
WRITE1   ST    R14,WT1SAVE             SAVE RETURN ADDRESS
         PUT   OUTPUT1,IN1REC          WRITE THE RECORD
W1XIT    L     R14,WT1SAVE             GET RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
WT1SAVE  DC    A(0)                    RETURN ADDRESS
***********************************************************************
*        PRINT A LINE FROM PRTLINE                                    *
***********************************************************************
PRINT    ST    R14,PSAVE               SAVE RETURN ADDRESS
         PUT   PRINT1,PRTLINE          WRITE THE RECORD
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
PXIT     L     R14,PSAVE               GET RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
PSAVE    DC    A(0)                    RETURN ADDRESS
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (PRINT1,,OUTPUT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         L     R14,12(R13)             GET CALLER'S RETURN ADDRESS
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R12)          RESTORE CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
RETCODE  DC    F'0'                    END OF JOB RETURN CODE
INPROGS  DC    X'00'                   EXPECTING PROGRAM NAME CONTIN
LASTGRP  DC    CL8' '                  LAST GROUP NAME READ
*
IN1REC   DS    0CL80                   INPUT1/OUTPUT RECORD AREA
IN1RGN   DS    CL8                       REGION NAME
         DS    CL1
IN1GROUP DS    CL8                       GROUP NAME
         DS    CL1
IN1PROG  DS    CL8                       PROGRAM NAME
         DS    CL1
IN1REMK  DS    CL53                      REMARKS
*
IN2REC   DS    CL133                   INPUT2 RECORD AREA
*
BLANKS   DC    CL133' '                BLANKS
*
PRTLINE  DC    CL132' '                BLANKS
*
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=132
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOF2RTN,          X
               RECFM=FB,LRECL=133
         LTORG
         END

         TITLE '***** CAMLST: OBTAIN/CAMLST TO OBTAIN VOLSERS ***'
         PRINT NOGEN
CAMLST   SVLNK R3,R4
OPENS    OPEN  (SYSPRINT,OUTPUT)      OPEN FILES
*
***********************************************************************
*   MAINLINE ROUTINE.                                                 *
***********************************************************************
*
MAINLINE LOCATE BYDSN                  READ CATALOG FOR SPEC DSNAME
         LTR   R15,R15                 SUCCESSFUL CATALOG READ?
         BZ    GOTCAT                  YES
         MVC   PRT,NOCTLG              NO, MESSAGE TO PRINT
         BAL   R9,PRINT                TELL USER NOT AVAILABLE
         ABEND 111,DUMP                AND EXIT
GOTCAT   SR    R11,R11                 CLEAR FOR INSERT
         ICM   R11,3,VOLCNT            GET VOLSER COUNT
         LA    R12,VOLVOL              POINT TO 1ST VOLSER
CATLINE  LA    R9,9                    VOLSERS PER LINE
         LA    R10,PRT                 POINT TO PRINT LINE
CATMOVE  MVC   0(6,R10),VOLVOL-VOLNTRY(R12) MOVE A VOLSER
         LA    R12,L'VOLNTRY(R12)      TO NEXT VOLSER IN VOLAREA
         LA    R10,8(R10)              TO NEXT PRINT LOCATION
         BCT   R11,CATDEC              DECREMENT VOLSER COUNT
         BAL   R9,PRINT                PRINT LAST LINE
         B     EOJ                     AND QUIT
CATDEC   BCT   R9,CATMOVE              DECREMENT VOLSER/LINE COUNT
         BAL   R9,PRINT                PRINT THIS LINE
         B     CATLINE                 GO START NEXT LINE
*
***********************************************************************
*           END OF JOB ROUTINE                                        *
***********************************************************************
*
EOJ      CLOSE (SYSPRINT)              CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        PRINT A LINE                                                 *
***********************************************************************
*
PRINT    PUT   SYSPRINT,PRT            WRITE PRINT LINE
         MVC   PRT,BLANX               CLEAR PRINT LINE
         BR    R9                      EXIT
*
***********************************************************************
*                                                                     *
*        DATA AREAS                                                   *
*                                                                     *
***********************************************************************
*
NOCTLG   DC    CL80'UNABLE TO OBTAIN CATALOG INFORMATION FOR DSNAME'
*
BYDSN    CAMLST NAME,DSNAME,,VOLAREA READ CATALOG LIST
VOLAREA  DS    0D                      VOLUME LIST RETURNED BY LOCATE
VOLCNT   DS    H                       NUMBER OF VOLUMES RETURNED
VOLNTRY  DS    0CL12                   VOLUME ENTRY (1 OF 20 MAX)
VOLDEV   DS    XL4                       DEVICE TYPE
VOLVOL   DS    CL6                       VOLUME SERIAL NUMBER
         DS    H
         DS    19CL(L'VOLNTRY)         REMAINING 19 ENTRY FIELDS
         DS    CL80
PRT      DC    CL80' '                 PRINT AREA
BLANX    DC    CL80' '                 CONSTANT BLANKS
DSNAME   DC    CL44'BC0THOR.NONVSAM.MANYVOL' DSNAME FOR CAMLST/LOCATE
*
***********************************************************************
*                                                                     *
*        PRINT AND CONTROL FILES                                      *
*                                                                     *
***********************************************************************
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         END

         PRINT NOGEN
         TITLE '*** CHGJOBNM: CHANGE JOB CARD JOBNAME ***'
CHGJOBNM SVLNK R3
*
***********************************************************************
* SCAN A JCL PDS AND MODIFY JOB CARDS.                                *
*                                                                     *
* THE SYSIN DATASET IS USED TO ENTER AN 8-CHARACTER JOBNAME, WHICH IS *
* MOVED TO COLUMNS 3-10 OF THE FIRST RECORD IN EACH MEMBER OF THE     *
* PDS IDENTIFIED BY THE INPUT1 DD STATEMENT. SYSIN MUST BE RECFM=FB,  *
* LRECL=80, WHICH IS HARD-CODED. THE INPUT1 PDS IS HARD-CODED AS      *
* RECFM=FB,LRECL=80                                                   *
* THE INPUT PDS IS MODIFIED DIRECTLY WITH THE NEW JOBNAME, SO A COPY  *
* OF THE PDS SHOULD BE MADE BEFORE THE RUN TO INSURE AGAINST LOSS OF  *
* DATA. ONLY ONE CHANGE (MAXIMUM) IS MADE PER MEMBER, SO IF THERE ARE *
* MULTIPLE JOB CARDS IN A MEMBER, ONLY THE FIRST OCCURRENCE WILL BE   *
* CHANGED. THE FIRST RECORD IS ASSUMED TO BE A JOB CARD, AND NO TESTS *
* ARE MADE TO INSURE THAT IT IS A JOB CARD.                           *
*                                                                     *
* AUTHOR R THORNTON APR 1990                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN, READ, CLOSE THE OLD-NEW JCL FILE.             *
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN CONTROL CARD AND LISTING
         GET   SYSIN,JOBNMCRD          READ NEW JOBNAME
ENDCARDS CLOSE SYSIN                   CLOSE JOBNAME FILE
*
***********************************************************************
* INITIALIZATION: OPEN THE JCL PDS FOR DIRECTORY AND MEMBER RECORD    *
*                 PROCESSING BY READDIR AND UPDMEM.                   *
***********************************************************************
*
         CALL  READDIR,OPEN            OPEN PDS FOR DIRECTORY READS
         CALL  UPDMEM,OPEN             OPEN PDS FOR MEMBER PROCESSING
*
***********************************************************************
* MAINLINE PROCESSING: LOCATE THE NEXT MEMBER OF THE INPUT1 PDS,      *
* POSITION TO IT, AND READ THE FIRST RECORD. ASSUME IT IS A JOB CARD  *
* AND REPLACE THE JOBNAME WITH THAT ENTERED VIA CONTROL CARD. REWRITE *
* THE JOB CARD AND ADVANCE TO THE NEXT MEMBER.                        *
***********************************************************************
*
NEWMEM   CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
         CALL  UPDMEM,FIND             LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BE    NEWMEM                  NO, TRY NEXT MEMBER
CALLREAD CALL  UPDMEM,(READ,MEMREC)    READ NEXT RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         MVC   MEMREC+2(8),NEWJOBNM    REPLACE WITH NEW ACCT NBR
         CALL  UPDMEM,(UPDATE,MEMREC)  UPDATE THE RECORD
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVC   PRTIMAG,MEMREC          MEMBER RECORD TO PRINT REC
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
         B     NEWMEM                  GO GET NEXT MEMBER
*
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
*
EOJ      CLOSE (SYSIN,,SYSPRINT)       CLOSE FILES
         CALL  UPDMEM,CLOSE            CLOSE PDS MEMBER ACCESS
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
NUMCK    DC    CL7'0000000'            NUMERIC CHECK AREA
NUMZROS  DC    CL7'0000000'            NUMERIC CHECK CONSTANT
EOF      EQU   X'FF'                   EOF INDICATOR
*
JOBNMCRD DS    0CL80                   JOBNAME RECORD
NEWJOBNM DC    CL8'USER020W'           NEW JOBNAME
         DS    CL72
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ REQUEST CODE
UPDATE   DC    C'U'                    UPDATE REQUEST CODE
CLOSE    DC    C'C'                    CLOSE REQUEST CODE
*
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
*
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
*
MEMREC   DS    CL80                    MEMBER RECORD
*
PRTREC   DS    0CL80                   OUTPUT RECORD
PRTMEM   DC    CL8' '                  MEMBER NAME
PRTIMAG  DC    CL72' '                 IMAGE OF INPUT RECORD
*
TRTSCH   DC    256X'00'                 TRT TABLE FOR SCANNING
*
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80,EODAD=ENDCARDS
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
*
         LTORG
         END

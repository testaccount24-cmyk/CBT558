         TITLE '*** RENAME AND SCRATCH PDS MEMBERS ***'
         PRINT NOGEN
RENSCR   SVLNK R3
         OPEN  (READER1,,LIBRARY,UPDAT,PRINT1,OUTPUT)
         RDJFCB LIBRARY                GET JFCB FOR DSNAME
         MVI   PCC,C'*'                FILL PRINT LINE
         MVC   PRT,PCC                 WITH ASTRICES
         MVI   PCC,TRIPLSP             SET SKIP 3 LINES
         LA    R1,JFCB                 @ 1ST BYTE OF DSNAME
         LA    R2,44                   MAX DSNAME LENGTH
CKDSRHE  CLI   0(R1),BLANK             AT RIGHT HAND END
         BE    ATDSRHE                 YES
         LA    R1,1(R1)                STEP TO NEXT DSNAME CHAR
         BCT   R2,CKDSRHE              LOOP TO FIND RHE
ATDSRHE  BCTR  R1,0                    BACK UP TO RHE
         LA    R2,JFCB                 GET DSNAME LHE ADDRESS
         SR    R1,R2                   COMPUTE DSNAME LENGTH CODE
         STC   R1,MVDSN+1              SET MOVE LENGTH
         SRL   R1,1                    DIVIDE LENGTH BY 2
         LA    R12,PRT+65              @ PRTLINE CENTER
         SR    R12,R1                  BACK UP DSNAME/2 BYTES
MVDSN    MVC   0(1,R12),0(R2)          DSNAME TO HEADER
         BAL   R9,PRTRTN               PRINT HEADER
         MVI   PCC,BLANK
         MVC   PRT,PCC                 CLEAR PRINT LINE
         MVI   PCC,DBLSPAC             SET DOUBLE SPACING
*
*
*
MAINLINE NOP   *                       *** MAINLINE ROUTINE ***
         GET   READER1,CARD            READ A CONTROL CARD
         MVI   PCC,DBLSPAC             SET PRINT DOUBLE SPACE
         MVC   PRT(L'CARD),CARD        MOVE CARD TO PRINT
         BAL   R9,PRTRTN               PRINT CARD
         CLI   TYPE,SCRATCH            SCRATCH CARD
         BE    SCRTCH                  YES
         CLI   TYPE,RENAME             RENAME CARD
         BE    RENAM                   YES
INVTYPE  MVC   PRT(L'BADTYPE),BADTYPE  ERROR MESSAGE
         BAL   R9,PRTRTN               GO PRINT MESSAGE
         B     MAINLINE                GO READ NEXT CARD
*
*
*
SCRTCH   NOP   *                       *** SCRATCH MEMBERS ***
         CLC   NAMES,BLANX             ANY NAMES LEFT IN CARD
         BE    MAINLINE                NO, BACK TO MAINLINE
         MVC   OLDNAME,BLANX           CLEAR NAME FIELD
         LA    R11,OLDNAME             @ NAME FIELD
         LA    R12,NAMES               @ CARD NAMES FIELD
         LA    R10,8                   MAX LENGTH OF NAME
SCRFLHE  CLI   0(R12),BLANK            COLUMN IS BLANK
         BNE   SCRATLHE                NO, AT LEFT END
         LA    R12,1(R12)              STEP TO NEXT COLUMN
         B     SCRFLHE                 LOOP TO FIELD LEFT END
SCRATLHE MVC   0(1,R11),0(R12)         MOVE NAME CHARACTER
         MVI   0(R12),BLANK            BLANK CLEAR IT IN CARD
         LA    R11,1(R11)              STEP TO NEXT NAME LOCATION
         LA    R12,1(R12)              STEP TO NEXT CARD LOCATION
         CLI   0(R12),BLANK            CARD COLUMN IS BLANK
         BE    SCRENDNM                YES, END OF NAME
         CLI   0(R12),COMMA            COMMA IN CARD
         BNE   SCRATLHE                NO, CONTINUE NAME MOVE
         MVI   0(R12),BLANK            YES, CLEAR COMMA
SCRENDNM BAL   R9,SCRSTOW              GO ISSUE STOW TO SCRATCH
         B     SCRTCH                  CONTINUE CARD PROCESSING
*
*
*
RENAM    NOP   *                       *** RENAME MEMBER ROUTINE ***
         CLC   NAMES,BLANX             CARD NAME FIELD IS BLANK
         BE    MAINLINE
         MVC   OLDNEW,BLANX            CLEAR NAMES FOR STOW
         LA    R11,NAMES               @ NAMES IN CARD
RENFLHE  CLI   0(R11),BLANK            CARD COLUMN IS BLANK
         BNE   RENATLHE                NO
         LA    R11,1(R11)              STEP TO NEXT COLUMN
         B     RENFLHE                 LOOK FOR LEFT END
RENATLHE LA    R12,OLDNAME             @ OLD NAME FIELD
         LA    R10,8                   MAX NAME SIZE
RENMVO   MVC   0(1,R12),0(R11)         NAME CHARACTER TO OLDNAME
         MVI   0(R11),BLANK            CLEAR CARD COLUMN
         LA    R11,1(R11)              STEP TO NEXT CARD COL
         LA    R12,1(R12)              STEP TO NEXT NAME FIELD
         CLI   0(R11),COMMA            AT SEPARATOR COMMA
         BE    RENEND1                 YES
         BCT   R10,RENMVO              LOOP TO MOVE OLD NAME
RENCDERR MVC   PRT(L'RENCDER),RENCDER  MOVE ERROR MESSAGE
         BAL   R9,PRTRTN               GO PRINT ERROR
         B     MAINLINE                SKIP REST OF CARD
RENEND1  MVI   0(R11),BLANK            CLEAR CARD COLUMN
         LA    R11,1(R11)              STEP TO NEXT CARD COLUMN
         LA    R12,NEWNAME             @ NEWNAME FIELD
         LA    R10,8                   MAX LENGTH OF NAME
         CLI   0(R11),BLANK            1ST CHARACTER IS BLANK
         BE    RENCDERR                YES
RENMVNW  MVC   0(1,R12),0(R11)         MOVE NEWNAME CHARACTER
         MVI   0(R11),BLANK            CLEAR CARD COLUMN
         LA    R11,1(R11)              STEP TO NEXT CARD COLUMN
         LA    R12,1(R12)              STEP TO NEXT NAME CHARACTER
         CLI   0(R11),BLANK            AT FOLLOWING BLANK
         BE    RENEND2                 YES, END OF 2-NAME GROUP
         BCT   R10,RENMVNW             NO, CONTINUE MOVE
         B     RENCDERR                OVER MAX NBR CHARACTERS
RENEND2  BAL   R9,RENSTOW              GO ISSUE RENAME STOW
         B     RENAM                   CONTINUE CARD PROCESSING
*
*
*
PRTRTN   NOP   *                       *** PRINT ROUTINE ***
         PUT   PRINT1,PRINT            WRITE PRINT LINE
         AP    LINECT,PONE             ADD 1 TO LINE COUNTER
         CLI   PCC,SGLSPAC             WAS IT SINGLE SPACED
         BE    CKENDPG                 YES
         AP    LINECT,PONE             ADD 1 MORE FOR DOUBLE SPACING
CKENDPG  MVI   PCC,SGLSPAC             SET SINGLE SPACING
         MVC   PRT,PCC                 CLEAR PRINT LINE
         CP    LINECT,MAXLINES         AT END OF PAGE
         BNH   PRTXIT                  NO
         ZAP   LINECT,=P'0'            RESET LINECT TO 0
         MVI   PCC,SKPCH1              SET SKIP TO HEAD OF FORM
         PUT   PRINT1,PRINT            SKIP TO TOP
         MVI   PCC,SGLSPAC             SET SINGLE SPACING
PRTXIT   BR    R9                      RETURN TO CALLER
*
*
*
RENSTOW  NOP   *                       *** ISSUE STOW AND PRINT RESULT
        ST    R9,RENSAV9              SAVE RETURN ADDR
         STOW  LIBRARY,OLDNEW,C        RENAME OLD NAME TO NEW NAME
         C     R15,MAXCODE             RETURN CODE VALID
         BNH   RMSGTBL(R15)            YES, GO TO BRANCH TABLE
RINVALID MVC   PRT(L'RINVCODE),RINVCODE MESSAGE TO PRINT
         B     RERR                    SET ERROR AND PRINT
RMSGTBL  B     RMSG00                  RETURN CODE 0
         B     RMSG04                  RETURN CODE 04
         B     RMSG08                  RETURN CODE 08
         B     RMSG0C                  RETUCN CODE 0C
         B     RMSG10                  RETURN CODE 10
         B     RMSG14                  RETURN CODE 14
         B     RMSG18                  RETURN CODE 18
RMSG00   MVC   PRT+20(L'RMESG00),RMESG00 MESSAGE TO PRINT
         B     RPRT                    GO PRINT
RMSG04   MVC   PRT+20(L'RMESG04),RMESG04 MESSAGE TO PRINT
         B     RERR                    GO SET ERROR AND PRINT
RMSG08   MVC   PRT+20(L'RMESG08),RMESG08 MESSAGE TO PRINT
         B     RERR                    SET ERROR AND PRINT
RMSG0C   MVC   PRT+20(L'RMESG0C),RMESG0C MESSAGE TO PRINT
         B     RERR                    SET ERROR AND PRINT
RMSG10   MVC   PRT+20(L'RMESG10),RMESG10 MESSAGE TO PRINT
         B     RERR                    SET ERROR AND PRINT
RMSG14   MVC   PRT+20(L'RMESG14),RMESG14 MESSAGE TO PRINT
         B     RERR                    SET ERROR AND PRINT
RMSG18   MVC   PRT+20(L'RMESG18),RMESG18 MESSAGE TO PRINT
RERR     LA    R1,4                    GET ERROR CODE
         ST    R1,RTNCODE              SET RETURN CODE
RPRT     MVC   PRT(8),OLDNAME          OLDNAME TO MESSAGE
         MVI   PRT+9,DASH              HYPHEN TO MESSAGE
         MVC   PRT+11(8),NEWNAME       NEWNAME TO MESSAGE
         BAL   R9,PRTRTN               GO PRINT MESSAGE
        L     R9,RENSAV9              GET RETURN ADDR
         BR    R9                      RETURN TO CALLER
*
*
*
SCRSTOW  NOP   *                       ***ISSUE STOW TO SCRATCH MEMBER
        ST    R9,SCRSAV9              SAVE RETURN ADDR
         STOW  LIBRARY,OLDNEW,D        STOW TO DELETE MEMBER
         LTR   R0,R0                   ANY I/O ERROR
         BZ    MSGSCR                  NO
         LA    R15,X'10'               SET I/O ERROR CODE
MSGSCR   C     R15,MAXCODE             RETURN CODE VALID
         BNH   SMSGTBL(R15)            YES, GO TO BRANCH TABLE
SCRINV   MVC   PRT(L'SINVCODE),SINVCODE MESSAGE TO PRINT
         B     SERR                    SET ERROR AND PRINT
SMSGTBL  B     SMSG00                  RETURN CODE 00
         B     SCRINV                  RETURN CODE 04
         B     SMSG08                  RETURN CODE 08
         B     SCRINV                  RETURN CODE 0C
         B     SMSG10                  RETURN CODE 10
         B     SMSG14                  RETURN CODE 14
         B     SMSG18                  RETURN CODE 18
SMSG00   MVC   PRT+9(L'SMESG00),SMESG00 MESSAGE TO PRINT
         B     SPRT                    PRINT MESSAGE
SMSG08   MVC   PRT+9(L'SMESG08),SMESG08 MESSAGE TO PRINT
         B     SERR                    SET ERROR AND PRINT
SMSG10   MVC   PRT+9(L'SMESG10),SMESG10 MESSAGE TO PRINT
         B     SERR                    SET ERROR AND PRINT
SMSG14   MVC   PRT+9(L'SMESG14),SMESG14 MESSAGE TO PRINT
         B     SERR                    SET ERROR AND PRINT
SMSG18   MVC   PRT+9(L'SMESG18),SMESG18 MESSAGE TO PRINT
         B     SERR                    SET ERROR AND PRINT
SERR     LA    R1,4                    GET ERROR CODE
         ST    R1,RTNCODE              SET ERROR RETURN CODE
SPRT     MVC   PRT(8),OLDNAME          MEMBER NAME TO MSG
         BAL   R9,PRTRTN               PRINT MESSAGE
        L     R9,SCRSAV9              GET RETURN ADDR
         BR    R9                      RETURN TO CALLER
*
*
*
EOJ      NOP   *                       *** END OF JOB ROUTINE ***
         CLOSE (READER1,,PRINT1,,LIBRARY) CLOSE FILES USED
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE
         L     R14,12(R13)             GET RETURN ADDRESS
         L     R15,RTNCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          GET CALLER'S REGS
         BR    R14                     RETURN CONTROL
*
*
*
         TITLE '*** RENSCR DATA AND WORK AREAS ***'
PRINT    DS    0CL133                  PRINT AREA
PCC      DS    CL1                     CARRIAGE CONTROL BYTE
DBLSPAC  EQU   C'0'                    DOUBLE SPACE CHARACTER
SGLSPAC  EQU   C' '                    SINGLE SPACE CHARACTER
SKPCH1   EQU   C'1'                    SKIP TO CHANNEL 1 CHARACTER
TRIPLSP  EQU   C'-'                    TRIPLE SPACE CHARACTER
PRT      DS    0CL132                  PRINT TEXT AREA
         DS    CL132
*
*
*
CARD     DS    0CL80                   CONTROL CARD AREA
TYPE     DS    CL1                     REQUEST TYPE CODE
SCRATCH  EQU   C'S'                    CODE FOR SCRATCH
RENAME   EQU   C'R'                    CODE FOR RENAME
NAMES    DS    CL70                    MEMBER NAMES TO PROCESS
COL72    DS    CL1                     CARD COLUMN 72
         DS    CL8                     CARD COLUMNS 73-80 NOT USED
*
*
*
*                        *** EQUATES ***
DASH     EQU   C'-'                    HYPHEN
COMMA    EQU   C','                    COMMA
BLANK    EQU   C' '                    BLANK
*
*
*
*                        *** MESSAGES ***
RENCDER  DC    C'INVALID RENAME CARD FORMAT. REMAINDER IGNORED'
BADTYPE  DC    C'INVALID TYPE CODE. CARD IGNORED'
SINVCODE DC    C'SCRATCH UNSUCCESSFUL, INVALID RETURN CODE FROM STOW'
SMESG00  DC    C'SUCCESSFUL SCRATCH OF MEMBER'
SMESG08  DC    C'UNSUCCESSFUL SCRATCH, MEMBER NOT FOUND'
SMESG10  DC    C'UNSUCCESSFUL SCRATCH, I/O ERROR DETECTED'
SMESG14  DC    C'UNSUCCESSFUL SCRATCH, DCB NOT OPEN FOR OUTPUT'
SMESG18  DC    C'UNSUCCESSFUL SCRATCH, INSUFFICIENT VIRTUAL STORAGE'
RINVCODE DC    C'RENAME UNSUCCESSFUL, INVALID RETURN CODE FROM STOW'
RMESG00  DC    C'MEMBER SUCCESSFULLY RENAMED'
RMESG04  DC    C'RENAME UNSUCCESSFUL, NEWNAME ALREADY IN FILE'
RMESG08  DC    C'RENAME UNSUCCESSFUL, OLDNAME COULD NOT BE FOUND'
RMESG0C  DC    C'RENAME UNSUCCESSFUL, NO DIRECTORY SPACE AVAILABLE'
RMESG10  DC    C'RENAME UNSUCCESSFUL, PERMANENT I/O ERROR DETECTED'
RMESG14  DC    C'RENAME UNSUCCESSFUL, DCB NOT OPEN FOR OUTPUT'
RMESG18  DC    C'RENAME UNSUCCESSFUL, INSUFFICIENT VIRTUAL STORAGE'
*
*
*
*                        *** MISCELLANEOUS WORK AREAS ***
MAXCODE  DC    F'24'                   MAXIMUM STOW RETURN CODE
MAXLINES DC    PL2'57'                 MAX LINES PER PAGE
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
PONE     DC    P'1'                    CONSTANT 1
BLANX    DC    CL133' '                FIELD OF BLANKS
RTNCODE  DC    F'0'                    RETURN CODE
RENSAV9  DS    F                       RENSTOW SAVE AREA
SCRSAV9  DS    F                       SCRSTOW SAVE AREA
OLDNEW   DS    0CL16                   STOW PARAMETER LIST
OLDNAME  DS    CL8                     OLD MEMBER NAME
NEWNAME  DS    CL8                     NEWMEMBER NAME
JFCAD    DS    0F                      EXIT LIST FOR RDJFCB
         DC    X'87'                   RDJFCB EXIT, END OF LIST
         DC    AL3(JFCB)               AREA FOR JFCB
JFCB     DS    CL176                   AREA TO HOLD JFCB
*
*
*
*                        *** DATA CONTROL BLOCKS ***
READER1  DCB   DSORG=PS,DDNAME=READER1,MACRF=GM,                       X
               RECFM=FB,LRECL=80,EODAD=EOJ
PRINT1   DCB   DSORG=PS,DDNAME=PRINT1,MACRF=PM,                        X
               RECFM=FBA,LRECL=133
LIBRARY  DCB   DSORG=PO,DDNAME=LIBRARY,MACRF=(R,W),EXLST=JFCAD
         END

FNDALLMC SVLNK R3
***********************************************************************
* THIS PROGRAM SCANS AN ASSEMBLER SOURCE PROGRAM FOR MACROS, EXPLICIT *
* SVC USAGE, AND COPY STATEMENTS.                                     *
*                                                                     *
* EACH INPUT RECORD IS SCANNED TO LOCATE THE OPERATION CODE MNEMONIC  *
* MNEMONIC. IF THE MNEMONIC FOUND IS 'SVC' OR 'COPY', A FURTHER       *
* SCAN IS MADE TO EXTRACT THE OPERAND. IF NOT SVC OR COPY, AN OP-CODE *
* TABLE LOOK-UP IS PERFORMED TO SEE IF THE MNEMONIC IS AN INSTRUCTION.*
* WHEN THE MNEMONIC IS NOT FOUND IN THE INSTRUCTION TABLE, A MACRO IS *
* ASSUMED AND AN OUTPUT RECORD IS CREATED WHICH CONTAINS THE MEMBER   *
* NAME, TYPE (MACRO, SVC, OR COPY), AND MNEMONIC. SVC AND COPY RECORDS*
* ALSO CONTAIN THE STATEMENT OPERAND.                                 *
* AUTHOR R THORNTON JULY 2000 (BASED ON PROGRAM FINDMAC)              *
***********************************************************************
**********************************************************************
* INITIALIZATION: OPEN FILES                                         *
**********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN FILES
**********************************************************************
* MAINLINE: READ INPUT SOURCE PROGRAM TO EXHAUSTION. FOR EACH INPUT  *
* RECORD, CALL SCAN ROUTINE TO SEARCH FOR NON-IBM MACROS. IF ANY ARE *
* FOUND, CALL THE PRINT ROUTINE TO WRITE AN OUTPUT RECORD.           *
**********************************************************************
MAINLINE GET   SYSIN,INCARD            READ ASSEMBLER SOURCE RECORD
         CLI   CONTIN,1                THIS A CONTINUATION?
         BE    MAINLINE                YES, GO READ NEXT RECORD
         CLI   INCARD,C' '             ANY LABEL
         BE    NOLBL                   NO
         CLI   INCARD,C'*'             IS IT A COMMENT?
         BE    MAINLINE                YES, GO READ NEXT RECORD
         CLC   INCARD(2),=C'.*'        IS IT A COMMENT
         BE    MAINLINE                YES, GO READ NEXT RECORD
         TRT   INCARD(9),FINDBLNK      LOCATE BLANK AFTER LABEL
         BZ    MAINLINE                DROP RECORD IF NOT FOUND
NOLBL    TRT   1(72,R1),FINDNONB       LOCATE MNEMONIC
         BZ    MAINLINE                DROP RECORD IF NOT FOUND
         C     R1,ENDIN                PAST END OF RECORD?
         BNL   MAINLINE                YES, SKIP THE RECORD
         LR    R14,R1                  SAVE MNEMONIC START ADDR
         TRT   1(72,R1),FINDBLNK       FIND NEXT BLANK
         BZ    MAINLINE                DROP RECORD IF NOT FOUND
         C     R1,ENDIN                PAST END OF RECORD?
         BNL   MAINLINE                YES, SKIP THE RECORD
MVOPERCD MVC   OUTOPER,BLANX           CLEAR MNEMONIC FIELD
         MVC   OUTOPND,BLANX           CLEAR OPERAND FIELD
         LR    R15,R1                  SAVE POINTER PAST MNEMONIC
         SR    R1,R14                  COMPUTE LENGTH
         BCTR  R1,0                    COMPUTE LENGTH CODE
         LTR   R1,R1                   NEGATIVE LENGTH CODE?
         BM    MAINLINE                YES, DROP THE RECORD
         CH    R1,=H'7'                OVER 8 BYTES?
         BH    MAINLINE                YES, SKIP IT
         EX    R1,MVOPCD               O.K., MOVE THE MNEMONIC
         CLC   OUTOPER,=CL8'SVC'       IS IT AN EXPLICIT SVC?
         BE    SVCCOPY                 YES
         CLC   OUTOPER,=CL8'COPY'      IS IT A COPY STATEMENT?
         BE    SVCCOPY                 YES
CKOPCD   LA    R11,OCTBL               OP CODE TABLE ADDRESS
OPLKUP   CLC   OUTOPER,0(R11)          FOUND OP CODE IN TBL
         BE    MAINLINE                YES, SKIP THE CARD
         LA    R11,8(R11)              STEP TO NEXT ENTRY
         CLI   0(R11),X'FF'            END OF TABLE
         BNE   OPLKUP                  NO, CONTINUE SEARCH
         MVC   OUTYPE,=CL5'MACRO'      SET TYPE
MVLINE   PUT   SYSPRINT,OUTREC         WRITE RECORD
         B     MAINLINE                TO NEXT CARD
SVCCOPY  MVC   OUTYPE,OUTOPER          SET OUTPUT TYPE
         TRT   1(72,R15),FINDNONB      POINT TO OPERAND START
         BZ    MAINLINE                COULDN'T FIND IT
         C     R2,ENDIN                PAST END OF CARD?
         BNL   MAINLINE                YES, GO TO NEXT CARD
         LR    R14,R1                  COPY OPERAND START
         TRT   1(72,R1),FINDBLNK       FIND NEXT BLANK
         BZ    MAINLINE                DROP RECORD IF NOT FOUND
         C     R1,ENDIN                PAST END OF RECORD?
         BNL   MAINLINE                YES, SKIP THE RECORD
         MVC   OUTOPND,BLANX           CLEAR OPERAND FIELD
         SR    R1,R14                  COMPUTE LENGTH
         BCTR  R1,0                    COMPUTE LENGTH CODE
         LTR   R1,R1                   NEGATIVE LENGTH CODE?
         BM    MAINLINE                YES, DROP THE RECORD
         CH    R1,=H'7'                OVER 8 BYTES?
         BH    MAINLINE                YES, SKIP IT
         EX    R1,MVOPND               O.K., MOVE THE MNEMONIC
         B     MVLINE                  GO WRITE OUTPUT
MVOPCD   MVC   OUTOPER(1),0(R14)       MOVE MNEMONIC (EXECUTED)
MVOPND   MVC   OUTOPND(1),0(R14)       MOVE OPERAND (EXECUTED)
**********************************************************************
* EOJ: CLOSE FILES AND RETURN CONTROL.                               *
**********************************************************************
EOJ      CLOSE (SYSIN,,SYSPRINT)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE POINTER
         LM    R14,R12,12(R13)         RESTORE HIS REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
**********************************************************************
* DATA AND FILE AREAS                                                *
**********************************************************************
FIRSTSW  DC    X'01'                   FIRST TIME SWITCH
TESTNUM  DC    CL6'000000'             WORK FIELD
BLANX    DC    CL80' '                 BLANKS
CONTIN   DC    X'00'                   CONTINUED CARD INDIC
ENDIN    DC    A(INCONT)               ADDR OF LAST INPUT DATA COLUMN
*
INCARD   DS    0CL80                   INPUT CARD
         DC    CL71' '
INCONT   DC    CL1' '                  CONTINUATION COLUMN
INSEQ    DC    CL8' '                  SEQUENCE NUMBER
*
OUTREC   DS    0CL80                   OUTPUT RECORD
MEMBER   DC    CL8' '
         DC    C' '
OUTYPE   DS    CL5                     MACRO OR SVC
         DC    C' '
OUTOPER  DS    CL8                     MNEMONIC OR SVC NBR
         DC    C' '
OUTOPND  DC    CL9' '
         DC    CL31' '
         DC    CL8' '
         DC    CL8' '
*
*
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
*
*
*
FINDBLNK DC    256X'00'           TRT TABLE TO SCAN FOR BLANK
         ORG   FINDBLNK+C' '
         DC    X'FF'
         ORG
*
FINDNONB DC    256X'FF'           TRT TABLE TO SCAN FOR NON-BLANK
         ORG   FINDNONB+C' '
         DC    X'00'
         ORG
         LTORG
*
*
*
OCTBL    DS    0CL8              TABLE OF INSTRUCTIONS MNEMONICS,
         DC    CL8'A'               EXTENDED MNEMONICS, AND
         DC    CL8'ACTR'            ASSEMBLER INSTRUCTIONS
         DC    CL8'AD'
         DC    CL8'ADR'
         DC    CL8'AE'
         DC    CL8'AER'
         DC    CL8'AGO'
         DC    CL8'AH'
         DC    CL8'AIF'
         DC    CL8'AL'
         DC    CL8'ALR'
         DC    CL8'AMODE'
         DC    CL8'ANOP'
         DC    CL8'AP'
         DC    CL8'AR'
         DC    CL8'AREAD'
         DC    CL8'AU'
         DC    CL8'AUR'
         DC    CL8'AW'
         DC    CL8'AWR'
         DC    CL8'AXR'
         DC    CL8'B'
         DC    CL8'BAKR'
         DC    CL8'BAL'
         DC    CL8'BALR'
         DC    CL8'BAS'
         DC    CL8'BASR'
         DC    CL8'BASSM'
         DC    CL8'BC'
         DC    CL8'BCR'
         DC    CL8'BCT'
         DC    CL8'BCTR'
         DC    CL8'BE'
         DC    CL8'BER'
         DC    CL8'BH'
         DC    CL8'BHR'
         DC    CL8'BL'
         DC    CL8'BLR'
         DC    CL8'BM'
         DC    CL8'BM'
         DC    CL8'BMR'
         DC    CL8'BMR'
         DC    CL8'BNE'
         DC    CL8'BNER'
         DC    CL8'BNH'
         DC    CL8'BNHR'
         DC    CL8'BNL'
         DC    CL8'BNLR'
         DC    CL8'BNM'
         DC    CL8'BNM'
         DC    CL8'BNMR'
         DC    CL8'BNMR'
         DC    CL8'BNO'
         DC    CL8'BNO'
         DC    CL8'BNOR'
         DC    CL8'BNOR'
         DC    CL8'BNP'
         DC    CL8'BNPR'
         DC    CL8'BNZ'
         DC    CL8'BNZ'
         DC    CL8'BNZR'
         DC    CL8'BNZR'
         DC    CL8'BO'
         DC    CL8'BO'
         DC    CL8'BOR'
         DC    CL8'BOR'
         DC    CL8'BP'
         DC    CL8'BPR'
         DC    CL8'BR'
         DC    CL8'BSM'
         DC    CL8'BXH'
         DC    CL8'BXLE'
         DC    CL8'BZ'
         DC    CL8'BZ'
         DC    CL8'BZR'
         DC    CL8'BZR'
         DC    CL8'C'
         DC    CL8'CCW'
         DC    CL8'CCW0'
         DC    CL8'CCW1'
         DC    CL8'CD'
         DC    CL8'CDR'
         DC    CL8'CDS'
         DC    CL8'CE'
         DC    CL8'CER'
         DC    CL8'CFC'
         DC    CL8'CH'
         DC    CL8'CL'
         DC    CL8'CLC'
         DC    CL8'CLCL'
         DC    CL8'CLI'
         DC    CL8'CLM'
         DC    CL8'CLR'
         DC    CL8'CNOP'
         DC    CL8'COM'
         DC    CL8'COPY'
         DC    CL8'CP'
         DC    CL8'CPYA'
         DC    CL8'CR'
         DC    CL8'CS'
         DC    CL8'CSCH'
         DC    CL8'CSECT'
         DC    CL8'CVB'
         DC    CL8'CVD'
         DC    CL8'CXD'
         DC    CL8'D'
         DC    CL8'DC'
         DC    CL8'DD'
         DC    CL8'DDR'
         DC    CL8'DE'
         DC    CL8'DER'
         DC    CL8'DP'
         DC    CL8'DR'
         DC    CL8'DROP'
         DC    CL8'DS'
         DC    CL8'DSECT'
         DC    CL8'DXD'
         DC    CL8'DXR'
         DC    CL8'EAR'
         DC    CL8'ED'
         DC    CL8'EDMK'
         DC    CL8'EJECT'
         DC    CL8'END'
         DC    CL8'ENTRY'
         DC    CL8'EPAR'
         DC    CL8'EQU'
         DC    CL8'EREG'
         DC    CL8'ESAR'
         DC    CL8'ESTA'
         DC    CL8'EX'
         DC    CL8'EXTRN'
         DC    CL8'GBLA'
         DC    CL8'GBLB'
         DC    CL8'GBLC'
         DC    CL8'HDR'
         DC    CL8'HER'
         DC    CL8'HSCH'
         DC    CL8'IAC'
         DC    CL8'IC'
         DC    CL8'ICM'
         DC    CL8'ICTL'
         DC    CL8'IPK'
         DC    CL8'IPM'
         DC    CL8'IPTE'
         DC    CL8'ISEQ'
         DC    CL8'ISKE'
         DC    CL8'IVSK'
         DC    CL8'L'
         DC    CL8'LA'
         DC    CL8'LAE'
         DC    CL8'LAM'
         DC    CL8'LASP'
         DC    CL8'LCDR'
         DC    CL8'LCER'
         DC    CL8'LCLA'
         DC    CL8'LCLB'
         DC    CL8'LCLC'
         DC    CL8'LCR'
         DC    CL8'LCTL'
         DC    CL8'LD'
         DC    CL8'LDR'
         DC    CL8'LE'
         DC    CL8'LER'
         DC    CL8'LH'
         DC    CL8'LM'
         DC    CL8'LNDR'
         DC    CL8'LNER'
         DC    CL8'LNR'
         DC    CL8'LOCTR'
         DC    CL8'LPDR'
         DC    CL8'LPER'
         DC    CL8'LPR'
         DC    CL8'LPSW'
         DC    CL8'LR'
         DC    CL8'LRA'
         DC    CL8'LRDR'
         DC    CL8'LRER'
         DC    CL8'LTDR'
         DC    CL8'LTER'
         DC    CL8'LTORG'
         DC    CL8'LTR'
         DC    CL8'LURA'
         DC    CL8'M'
         DC    CL8'MACRO'
         DC    CL8'MC'
         DC    CL8'MD'
         DC    CL8'MDR'
         DC    CL8'ME'
         DC    CL8'MEND'
         DC    CL8'MER'
         DC    CL8'MEXIT'
         DC    CL8'MH'
         DC    CL8'MHELP'
         DC    CL8'MNOTE'
         DC    CL8'MP'
         DC    CL8'MR'
         DC    CL8'MSCH'
         DC    CL8'MSTA'
         DC    CL8'MVC'
         DC    CL8'MVCDK'
         DC    CL8'MVCIN'
         DC    CL8'MVCK'
         DC    CL8'MVCL'
         DC    CL8'MVCP'
         DC    CL8'MVCS'
         DC    CL8'MVCSK'
         DC    CL8'MVI'
         DC    CL8'MVN'
         DC    CL8'MVO'
         DC    CL8'MVZ'
         DC    CL8'MXD'
         DC    CL8'MXDR'
         DC    CL8'MXR'
         DC    CL8'N'
         DC    CL8'NC'
         DC    CL8'NI'
         DC    CL8'NOP'
         DC    CL8'NOPR'
         DC    CL8'NR'
         DC    CL8'O'
         DC    CL8'OC'
         DC    CL8'OI'
         DC    CL8'OPSYN'
         DC    CL8'OR'
         DC    CL8'ORG'
         DC    CL8'PACK'
         DC    CL8'PALB'
         DC    CL8'PC'
         DC    CL8'POP'
         DC    CL8'PR'
         DC    CL8'PRINT'
         DC    CL8'PT'
         DC    CL8'PTLB'
         DC    CL8'PUNCH'
         DC    CL8'PUSH'
         DC    CL8'RCHP'
         DC    CL8'REPRO'
         DC    CL8'RMODE'
         DC    CL8'RRBE'
         DC    CL8'RSCH'
         DC    CL8'S'
         DC    CL8'SAC'
         DC    CL8'SAL'
         DC    CL8'SAR'
         DC    CL8'SCHM'
         DC    CL8'SCK'
         DC    CL8'SCKC'
         DC    CL8'SD'
         DC    CL8'SDR'
         DC    CL8'SE'
         DC    CL8'SER'
         DC    CL8'SETA'
         DC    CL8'SETB'
         DC    CL8'SETC'
         DC    CL8'SH'
         DC    CL8'SIE'
         DC    CL8'SIGP'
         DC    CL8'SL'
         DC    CL8'SLA'
         DC    CL8'SLDA'
         DC    CL8'SLDL'
         DC    CL8'SLL'
         DC    CL8'SLR'
         DC    CL8'SP'
         DC    CL8'SPACE'
         DC    CL8'SPKA'
         DC    CL8'SPM'
         DC    CL8'SPT'
         DC    CL8'SPX'
         DC    CL8'SR'
         DC    CL8'SRA'
         DC    CL8'SRDA'
         DC    CL8'SRDL'
         DC    CL8'SRL'
         DC    CL8'SRP'
         DC    CL8'SSAR'
         DC    CL8'SSCH'
         DC    CL8'SSKE'
         DC    CL8'SSM'
         DC    CL8'ST'
         DC    CL8'STAM'
         DC    CL8'STAP'
         DC    CL8'START'
         DC    CL8'STC'
         DC    CL8'STCK'
         DC    CL8'STCKC'
         DC    CL8'STCM'
         DC    CL8'STCPS'
         DC    CL8'STCRW'
         DC    CL8'STCTL'
         DC    CL8'STD'
         DC    CL8'STE'
         DC    CL8'STH'
         DC    CL8'STIDP'
         DC    CL8'STM'
         DC    CL8'STNSM'
         DC    CL8'STOSM'
         DC    CL8'STPT'
         DC    CL8'STPX'
         DC    CL8'STSCH'
         DC    CL8'STURA'
         DC    CL8'SU'
         DC    CL8'SUR'
         DC    CL8'SVC'
         DC    CL8'SW'
         DC    CL8'SWR'
         DC    CL8'SXR'
         DC    CL8'TAR'
         DC    CL8'TB'
         DC    CL8'TITLE'
         DC    CL8'TM'
         DC    CL8'TPI'
         DC    CL8'TPROT'
         DC    CL8'TR'
         DC    CL8'TRACE'
         DC    CL8'TRT'
         DC    CL8'TS'
         DC    CL8'TSCH'
         DC    CL8'UNPK'
         DC    CL8'UPT'
         DC    CL8'USING'
         DC    CL8'VA'
         DC    CL8'VACD'
         DC    CL8'VACDR'
         DC    CL8'VACE'
         DC    CL8'VACER'
         DC    CL8'VACRS'
         DC    CL8'VACSW'
         DC    CL8'VAD'
         DC    CL8'VADQ'
         DC    CL8'VADR'
         DC    CL8'VADS'
         DC    CL8'VAE'
         DC    CL8'VAEQ'
         DC    CL8'VAER'
         DC    CL8'VAES'
         DC    CL8'VAQ'
         DC    CL8'VAR'
         DC    CL8'VAS'
         DC    CL8'VC'
         DC    CL8'VCD'
         DC    CL8'VCDQ'
         DC    CL8'VCDR'
         DC    CL8'VCDS'
         DC    CL8'VCE'
         DC    CL8'VCEQ'
         DC    CL8'VCER'
         DC    CL8'VCES'
         DC    CL8'VCOVM'
         DC    CL8'VCQ'
         DC    CL8'VCR'
         DC    CL8'VCS'
         DC    CL8'VCVM'
         DC    CL8'VCZVM'
         DC    CL8'VDD'
         DC    CL8'VDDQ'
         DC    CL8'VDDR'
         DC    CL8'VDDS'
         DC    CL8'VDE'
         DC    CL8'VDEQ'
         DC    CL8'VDER'
         DC    CL8'VDES'
         DC    CL8'VL'
         DC    CL8'VLBIX'
         DC    CL8'VLCDR'
         DC    CL8'VLCER'
         DC    CL8'VLCR'
         DC    CL8'VLCVM'
         DC    CL8'VLD'
         DC    CL8'VLDQ'
         DC    CL8'VLDR'
         DC    CL8'VLE'
         DC    CL8'VLEL'
         DC    CL8'VLELD'
         DC    CL8'VLELE'
         DC    CL8'VLEQ'
         DC    CL8'VLER'
         DC    CL8'VLH'
         DC    CL8'VLI'
         DC    CL8'VLID'
         DC    CL8'VLIE'
         DC    CL8'VLINT'
         DC    CL8'VLM'
         DC    CL8'VLMD'
         DC    CL8'VLMDQ'
         DC    CL8'VLMDR'
         DC    CL8'VLME'
         DC    CL8'VLMEQ'
         DC    CL8'VLMER'
         DC    CL8'VLMQ'
         DC    CL8'VLMR'
         DC    CL8'VLNDR'
         DC    CL8'VLNER'
         DC    CL8'VLNR'
         DC    CL8'VLPDR'
         DC    CL8'VLPER'
         DC    CL8'VLPR'
         DC    CL8'VLQ'
         DC    CL8'VLR'
         DC    CL8'VLVCA'
         DC    CL8'VLVCU'
         DC    CL8'VLVM'
         DC    CL8'VLY'
         DC    CL8'VLYD'
         DC    CL8'VLYE'
         DC    CL8'VLZDR'
         DC    CL8'VLZER'
         DC    CL8'VLZR'
         DC    CL8'VMAD'
         DC    CL8'VMADQ'
         DC    CL8'VMADS'
         DC    CL8'VMAE'
         DC    CL8'VMAEQ'
         DC    CL8'VMAES'
         DC    CL8'VMCD'
         DC    CL8'VMCDR'
         DC    CL8'VMCE'
         DC    CL8'VMCER'
         DC    CL8'VMD'
         DC    CL8'VMDQ'
         DC    CL8'VMDR'
         DC    CL8'VMDS'
         DC    CL8'VME'
         DC    CL8'VMEQ'
         DC    CL8'VMER'
         DC    CL8'VMES'
         DC    CL8'VMNSD'
         DC    CL8'VMNSE'
         DC    CL8'VMQ'
         DC    CL8'VMR'
         DC    CL8'VMRRS'
         DC    CL8'VMRSV'
         DC    CL8'VMS'
         DC    CL8'VMSD'
         DC    CL8'VMSDQ'
         DC    CL8'VMSDS'
         DC    CL8'VMSE'
         DC    CL8'VMSEQ'
         DC    CL8'VMSES'
         DC    CL8'VMXAD'
         DC    CL8'VMXAE'
         DC    CL8'VMXSD'
         DC    CL8'VMXSE'
         DC    CL8'VN'
         DC    CL8'VNQ'
         DC    CL8'VNR'
         DC    CL8'VNS'
         DC    CL8'VNVM'
         DC    CL8'VO'
         DC    CL8'VOQ'
         DC    CL8'VOR'
         DC    CL8'VOS'
         DC    CL8'VOVM'
         DC    CL8'VRCL'
         DC    CL8'VRRS'
         DC    CL8'VRSV'
         DC    CL8'VRSVC'
         DC    CL8'VS'
         DC    CL8'VSD'
         DC    CL8'VSDQ'
         DC    CL8'VSDR'
         DC    CL8'VSDS'
         DC    CL8'VSE'
         DC    CL8'VSEQ'
         DC    CL8'VSER'
         DC    CL8'VSES'
         DC    CL8'VSLL'
         DC    CL8'VSPSD'
         DC    CL8'VSQ'
         DC    CL8'VSR'
         DC    CL8'VSRL'
         DC    CL8'VSRRS'
         DC    CL8'VSRSV'
         DC    CL8'VSS'
         DC    CL8'VST'
         DC    CL8'VSTD'
         DC    CL8'VSTE'
         DC    CL8'VSTH'
         DC    CL8'VSTI'
         DC    CL8'VSTID'
         DC    CL8'VSTIE'
         DC    CL8'VSTK'
         DC    CL8'VSTKD'
         DC    CL8'VSTKE'
         DC    CL8'VSTM'
         DC    CL8'VSTMD'
         DC    CL8'VSTME'
         DC    CL8'VSTVM'
         DC    CL8'VSTVP'
         DC    CL8'VSVMM'
         DC    CL8'VTVM'
         DC    CL8'VX'
         DC    CL8'VXEL'
         DC    CL8'VXELD'
         DC    CL8'VXELE'
         DC    CL8'VXQ'
         DC    CL8'VXR'
         DC    CL8'VXS'
         DC    CL8'VXVC'
         DC    CL8'VXVM'
         DC    CL8'VXVMM'
         DC    CL8'VZPSD'
         DC    CL8'WXTRN'
         DC    CL8'X'
         DC    CL8'XC'
         DC    CL8'XI'
         DC    CL8'XR'
         DC    CL8'ZAP'
INITEND  DC    X'FF'
         END

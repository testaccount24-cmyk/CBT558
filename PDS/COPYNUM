         TITLE '*** COPYNUM: COPY AND NUMBER PAGES    ***'
COPYNUM CSECT
*********************************************************************
* READS AN INPUT FILE OF RECFM=FBA,LRECL=133 AND WRITES IT OUT AS   *
* RECFM=FBA,LRECL=151. EACH INPUT RECORD IS LEFT-JUSTIFIED IN THE   *
* OUTPUT RECORD. A PAGE NUMBER AND LINE NUMBER IS PRINTED AT THE    *
* RIGHT SIDE OF EACH LINE. PAGE NUMBER IS ADVANCED FOR EACH LINE    *
* HAVING A '1' IN THE CARRIAGE CONTROL POSITION.                    *
*                                                                   *
* AUTHOR R THORNTON NOV 1999                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING COPYNUM,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'COPYNUM'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES, PRINT HEADING                                  *
*********************************************************************
START    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,INREC            READ INPUT RECORD
         CLI   INREC,C'1'              THIS A NEW PAGE?
         BNE   EDTLINE                 NO
         AP    PAGENO,P1               YES, INCREMENT PAGE NUMBER
         MVC   OUTPGNO,PAGEDIT         MOVE IN EDIT WORD
         ED    OUTPGNO,PAGENO          PAGE NUMBER TO PRINT LINE
         ZAP   LINENO,P0               ZERO OUT LINE NUMBER
EDTLINE  AP    LINENO,P1               INCREMENT LINE NUMBER
         UNPK  OUTLNNO,LINENO          UNPACK LINE NUMBER TO OUTPUT
         MVI   OUTLNNO,C' '            CLEAR LEADING ZERO
         OI    OUTLNNO+2,C'0'          CLEAR SIGN
PUTOUT   PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         B     MAINLINE                LOOP THRU INPUT FILE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DS    F                       RETURN CODE
P0       DC    P'0'                    CONSTANT ZERO
P1       DC    P'1'                    PACKED 1
LINENO   DC    PL2'0'                  LINE NUMBER COUNTER
PAGENO   DC    PL3'0'                  PAGE COUNTER
PAGEDIT  DC    XL7'4020206B202020'     EDIT MASK FOR PAGE NBR
*
OUTREC   DS    0CL151                 OUTPUT RECORD
INREC    DS    CL133                    INPUT RECORD
         DC    CL1' '                   TRAILING SPACES
         DC    CL4'PAGE'                LITERAL "PAGE"
OUTPGNO  DS    CL7                      PAGE NUMBER
         DC    CL3' LN'                 TRAILING SPACES
OUTLNNO  DS    CL3                     LINE NUMBER
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FBA,LRECL=151
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=133
         LTORG
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

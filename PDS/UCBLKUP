         TITLE '***** UCB LOOKUP EXAMPLE *****'
         PRINT NOGEN
UCBLKUP  CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING UCBLKUP,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'UCBLKUP'            PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
         L     R1,0(R1)                POINT TO EXEC PARM DATA
         LH    R15,0(R1)               GET PARM FIELD LENGTH
         CH    R15,H4                  4 BYTES OF PARM DATA?
         BE    MVPARM                  YES
         WTO   'UCBLKUP: MISSING OR INVALID 4-BYTE SYS ID IN PARM',    X
               ROUTCDE=11
         ABEND 777,DUMP
MVPARM   MVC   OUTID,2(R1)             YES, MOVE ID TO OUTPUT AREA
*
**********************************************************************
* RUNS THE UCB HALFWORD ADDRESS TABLE, SELECTS UCBS FOR DASD DEVICES,*
* AND CREATES A FILE OF DATA ABOUT EACH ONE FOUND. THE EXEC CARD PARM*
* FIELD CAN BE USED TO ENTER A SYSTEM IDENTIFIER, IF DESIRED. IF IT  *
* IS ENTERED, IT WILL BE WRITTEN INTO EACH OUTPUT RECORD.            *
*  AUTHOR R. THORNTON DEC 1985                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
INIT     OPEN  (OUTPUT1,OUTPUT)        OPEN FILES
         USING PSA,R0
         L     R1,FLCCVT               POINT TO THE CVT
         DROP R0
         USING CVT,R1
         L     R12,CVTILK1             POINT TO THE UCB LOOKUP TABLE
         LA    R12,0(R12)              CLEAR HI-ORDER
         ST    R12,ILK1                SAVE LOOKUP TABLE ADDRESS
         L     R12,CVTILK2             POINT TO THE UCB ADDRESS TABLE
         LA    R12,0(R12)              CLEAR HI-ORDER
         ST    R12,ILK2                SAVE HALFWORD ADDRESS TABLE ADDR
         BCTR  R12,0                   BACK UP
         BCTR  R12,0                           2 BYTES
         DROP  R1
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
         USING UCBHWTB,R12
NEXTUCB  LA    R12,L'UCBHWAD(R12)      ADVANCE TABLE POINTER
         SR    R11,R11                 CLEAR FOR INSERT
         ICM   R11,3,UCBHWAD           PICK UP UCB ADDRESS
         BZ    NEXTUCB                 IGNORE ZERO ADDRESSES
         C     R11,UCBTBEND            END OF UCB ADDRESS TABLE?
         BE    EOJ                     YES
         USING UCB,R11
         CLI   UCBDVCLS,UCB3DACC       DIRECT ACCESS DEVICE?
         BNE   NEXTUCB                 NO
         TM    UCBSTAT,UCBONLI         IS IT ONLINE (I.E. REAL)?
         BZ    NEXTUCB                 NO
         BAL   R9,FINDHWAD             GET PRIM/ALT CUU NAMES
         MVC   ODEVTYPE,LIT23051       ASSUME 2305 MODEL 1
         CLI   UCBUNTYP,UCB23051       IS IT 2305 MODEL 1?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT23052       ASSUME 2305 MODEL 2
         CLI   UCBUNTYP,UCB23052       IS IT 2305 MODEL 2?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT2314        ASSUME 2314
         CLI   UCBUNTYP,UCB2314        IS IT 2314?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT33301       ASSUME 3330 MODEL 1
         CLI   UCBUNTYP,UCB33301       IS IT 3330 MODEL 1?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT33302       ASSUME 3330 MODEL 2
         CLI   UCBUNTYP,UCB33302       IS IT 3330 MODEL 2?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT3340        ASSUME 3340?
         CLI   UCBUNTYP,UCB3340        IS IT 3340?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT3350        ASSUME 3350?
         CLI   UCBUNTYP,UCB3350        IS IT 3350?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT3375        ASSUME 3375?
         CLI   UCBUNTYP,UCB3375        IS IT 3375?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LIT3380        ASSUME 3380?
         CLI   UCBUNTYP,UCB3380        IS IT 3380?
         BE    MVVOLS                  YES
         MVC   ODEVTYPE,LITQUES        NONE OF THESE, UNKNOWN TYPE
MVVOLS   MVC   OVOLSER,UCBVOLI         VOLUME SERIAL TO OUTPUT
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         B     NEXTUCB
*
***********************************************************************
*        FIND UCB HALFWORD TABLE ENTRY FOR THIS UCB USING THE EBCDIC  *
*        "UCBNAME" FIELD, AND THE LOOKUP TABLES POINTED TO BY THE CVT *
*        FIELDS CVTILK1 AND CVTILK2. IF THE ADDRESS FOUND IS THAT NOW *
*        IN R12, THIS IS THE PRIMARY (AND PERHAPS ONLY) DEVICE NAME.  *
*        IF A DIFFERENT HALFWORD TABLE ENTRY IS FOUND, THE UCB EBCDIC *
*        NAME IS THE PRIMARY DEVICE NAME, BUT THIS HALFWORD TABLE     *
*        ENTRY REPRESENTS AN ALTERNATE DEVICE NAME. TO DETERMINE THE  *
*        ALTERNATE NAME, THE FIRST CHARACTER OF THE UCBNAME IS VARIED *
*        FROM 0 TO F AS THE LOOKUP IS RECALCULATED UNTIL THE RESULT IS*
*        THE ADDRESS IN R12. AT THAT POINT, THE CUU ADDRESS USED IA   *
*        TAKEN AS THE ALTERNATE DEVICE NAME.                          *
***********************************************************************
*
FINDHWAD MVC   OPCUU,UCBNAME           SET UP PRIMARY DEVICE NAME
         MVC   OCUU,LITQUES            INDICATE UNKNOWN DEV NAME
         MVC   VARYCH,XZROS            SET TRIAL CHANNEL NUMBER TO 0
         MVI   CUNBR,0                 SET HI-BYTE OF C.U. NBR TO 0
         MVC   CUNBR+1(1),UCBNAME+1    CONTROL UNIT NUMBER TO SAVE
         TM    CUNBR+1,C'0'            C.U. NBR NUMERIC?
         BO    STRPZN                  YES
         IC    R1,CUNBR+1              NO, ALPHA
         LA    R1,9(R1)                CONVERT TO HEX
         STC   R1,CUNBR+1              SAVE RESULT
STRPZN   NI    CUNBR+1,X'0F'           CLEAR GARBAGE
         MVI   UNITNBR,0               SET HI-BYTE OF UNIT NBR TO 0
         MVC   UNITNBR+1(1),UCBNAME+2  UNIT NUMBER TO SAVE
         TM    UNITNBR+1,C'0'          UNIT NBR NUMERIC?
         BO    STRZON                  YES
         IC    R1,UNITNBR+1            NO, ALPHA
         LA    R1,9(R1)                CONVERT TO HEX
         STC   R1,UNITNBR+1            SAVE RESULT
STRZON   NI    UNITNBR+1,X'0F'         CLEAR GARBAGE
TRYCU    LH    R1,VARYCH               LOAD CHANNEL NUMBER
         A     R1,ILK1                 POINT TO CHANNEL INDEX BYTE
         SR    R2,R2                   CLEAR FOR INSERT
         IC    R2,0(R1)                PICK UP CHANNEL INDEX VALUE
         AH    R2,CUNBR                ADD CONTROL UNIT NUMBER
         SLL   R2,1                    MULTIPLY BY 2
         A     R2,ILK1                 POINT TO CTL UNIT INDEX
         LH    R1,0(R2)                PICK UP INDEX
         AH    R1,UNITNBR              ADD UNIT NUMBER
         SLL   R1,1                    MULTIPLY BY 2
         A     R1,ILK2                 POINT TO HALFWORD TABLE ENTRY
         CR    R1,R12                  IS IT THE SAME ENTRY IN R12?
         BE    FORMALT                 YES
         LH    R1,VARYCH               GET CURRENT CHANNEL VALUE
         CH    R1,H15                  END OF CHANNEL RANGE?
         BNL   HWXIT                   YES, ENTRY NOT FOUND
         LA    R1,1(R1)                INCREMENT CHANNEL NUMBER
         STH   R1,VARYCH               UPDATE CHANNEL NUMBER
         B     TRYCU                   GO TRY THIS ONE
FORMALT  LH    R1,VARYCH               PICK UP CHANNEL NBR
         SLL   R1,4                    MOVE CHANNEL TO HIGH 4 BITS
         AH    R1,CUNBR                ADD CONTROL UNIT NBR
         STH   R1,VARYCH               STORE TO CONVERT
         UNPK  OCUU(3),VARYCH+1(2)     UNPACK
         TR    OCUU(2),TRTBL-C'0'      MAKE PRINTABLE
         MVC   OCUU+2(1),OPCUU+2       UNIT ADDRESS TO ALT NAME
HWXIT    BR    R9                      RETURN
*
*
***********************************************************************
*        PRINT SUBROUTINE                                             *
***********************************************************************
*
OUTPUT   PUT   OUTPUT1,OUTREC          WRITE THE OUTPUT RECORD
         BR    R9                      RETURN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE OUTPUT1                 CLOSE FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
OUTREC   DS    0CL21                   OUTPUT RECORD
OVOLSER  DS    CL6                     VOLUME SERIAL NUMBER
OUTID    DS    CL4                     SYSTEM ID
OPCUU    DS    CL3                     PRIMARY CUU ADDRESS
OCUU     DS    CL3                     CHANNEL-UNIT ADDRESS (EBCDIC)
ODEVTYPE DS    CL5                     DEVICE TYPE (EBCDIC)
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
ILK1     DS    A                       POINTER TO FIRST UCB TABLE
ILK2     DS    A                       POINTER TO UCB HALFWORD TABLE
VARYCH   DC    H'0'                    CHANNEL NBR USED BY FINDHWAD
CUNBR    DC    H'0'                    CONTROL UNIT NBR FOR FINDHWAD
UNITNBR  DC    H'0'                    UNIT NBR FOR FINDHWAD
H15      DC    H'15'                   CONSTANT 15
H4       DC    H'4'                    CONSTANT 4
RETCODE  DC    F'0'                    RETURN CODE
UCBTBEND DC    X'0000FFFF'             EOT INDICATOR
TRTBL    DC    CL16'0123456789ABCDEF'  TRANSLATE TABLE FOR PRINT HEX
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
XZROS    DC    XL4'00'                 CONSTANT ZEROS
LIT23051 DC    CL5'23051'              LITERAL FOR 2305 MODEL 1
LIT23052 DC    CL5'23052'              LITERAL FOR 2305 MODEL 2
LIT2314  DC    CL5'2314 '              LITERAL FOR 2314/2319
LIT33301 DC    CL5'33301'              LITERAL FOR 3330 MODEL 1
LIT33302 DC    CL5'33302'              LITERAL FOR 3330 MODEL 11
LIT3340  DC    CL5'3340 '              LITERAL FOR 3340
LIT3350  DC    CL5'3350 '              LITERAL FOR 3350
LIT3375  DC    CL5'3375 '              LITERAL FOR 3375
LIT3380  DC    CL5'3380 '              LITERAL FOR 3380
LITQUES  DC    CL5'?????'              LITERAL FOR UNKNOWN
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=21
         LTORG
*
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
*
PSA      DSECT ,                       DEFINES LOWEST 4K OF MEMORY
FLCCVT   EQU   PSA+16,4                POINTER TO THE CVT
*
CVT      DSECT ,                       COMMUNICATION VECTOR TABLE
CVTILK1  EQU   CVT+36                  PTR TO UCB LOOKUP TABLE
CVTILK2  EQU   CVT+40                  PTR TO UCB HALFWORD ADDR TBL
*                                      ADDRESS LIST PORTION OF THE
*                                      UCB LOOKUP TABLE
*
UCBHWTB  DSECT ,                       UCB HALFWORD ADDRESS TABLE
UCBHWAD  DS    H                       UCB ADDRESS
*                                      X'0000' = HOLE IN TABLE, IGNORE
*                                      X'FFFF' = END OF TABLE
*
UCB      DSECT ,                       UNIT CONTROL BLOCK
UCBFL5   EQU   UCB+1,1                 FLAG BYTE
UCBALTCU EQU   X'02'                     DVC HAS ALT CUU ADDRESS
UCBALTPH EQU   X'01'                     DVC HAS AN ALT PATH
UCBSTAT  EQU   UCB+3,1                 DEVICE STATUS
UCBONLI  EQU   X'80'                     DVC IS ONLINE
UCBRESV  EQU   X'20'                     MOUNT STATUS IS RESERVED
UCBALOC  EQU   X'08'                     DVC IS ALLOCATED
UCBPRES  EQU   X'04'                     MOUNT STATUS IS PERM RES
UCBCHAN  EQU   UCB+4,2                 BINARY CUU ADDR LAST STRTD OPN
UCBFLB   EQU   UCB+7,1                 I/O SUPERVISOR FLAG BYTE B
UCBRESVH EQU   X'10'                     DEVICE IS RESERVED
UCBLCI   EQU   UCB+10,1                *32=INDEX TO LCHTAB
UCBWGT   EQU   UCB+12,1                FLAGS
UCBPUB   EQU   X'20'                     DVC WILL BE ALLOC FOR PUBLIC
UCBNAME  EQU   UCB+13,3                UNIT NAME (CUU) IN EBCDIC
UCBTBYT1 EQU   UCB+16,1                DEVICE TYPE: MODEL BITS
UCBD1600 EQU   X'04'                     1600 BPI
UCBD6250 EQU   X'02'                     6250 BPI
UCBTBYT2 EQU   UCB+17,1                DEVICE TYPE: OPTION FLAGS
UCBDUDN1 EQU   X'20'                     DUAL DENSITY 800/1600 BPI
UCBDUDN2 EQU   X'10'                     DUAL DENSITY 1600/6250 BPI
UCBRVDEV EQU   X'08'                     VIRTUAL DEVICE
UCBDVCLS EQU   UCB+18,1                DEVICE CLASS
UCB3TAPE EQU   X'80'                     TAPE
UCB3COMM EQU   X'40'                     COMMUNICATIONS
UCB3CTC  EQU   X'41'                     CHANNEL-TO-CHANNEL ADAPTER
UCB3DACC EQU   X'20'                     DIRECT ACCESS
UCB3DISP EQU   X'10'                     DISPLAY
UCB3UREC EQU   X'08'                     UNIT RECORD
UCB3CHAR EQU   X'04'                     CHARACTER READER
UCBUNTYP EQU   UCB+19,1                DEVICE CODE
UCB23051 EQU   X'06'                     2305 MODEL 1
UCB23052 EQU   X'07'                     2305 MODEL 2
UCB2314  EQU   X'08'                     2314/2319
UCB33301 EQU   X'09'                     3330 MODEL 1
UCB33302 EQU   X'0D'                     3330 MODEL 11
UCB3340  EQU   X'0A'                     3340
UCB3350  EQU   X'0B'                     3350
UCB3375  EQU   X'0C'                     3375
UCB3380  EQU   X'0E'                     3380
UCBVTOC  EQU   UCB+24,4                TTR0 OF VTOC IF DIRECT ACCESS
UCBVOLI  EQU   UCB+28,6                VOLUME SERIAL (TAPE, D.A.)
UCBSTAB  EQU   UCB+34,1                VOLUME STATUS (TAPE AND D.A.)
UCBBPRV  EQU   X'10'                     PRIVATE USE STATUS
UCBBPUB  EQU   X'08'                     PUBLIC USE STATUS
UCBBSTR  EQU   X'04'                     STORAGE USE STATUS
UCBBNUL  EQU   X'01'                     CONTROL VOL (HAS CATALOG)
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

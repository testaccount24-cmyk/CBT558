      TITLE '**** PRINT THE HEAVY HITTER LIST ****'
         PRINT NOGEN
SMFSTEP3 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFSTEP3,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFSTEP3'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
*  READ OUTPUT FROM SMFSTEP2 AND THE LIST OF PROGRAM NAMES TO BE     *
*  OUTPUT, NORMALLY THE OUTPUT OF PDSDIR. THE SMFSTEP2 RECORDS ARE   *
*  ONLY WRITTEN OUT IF THE PROGRAM NAME IS IN THE PDSDIR FILE AND    *
*  TOTAL CPU TIME IS GREATER THAN 2.00 SECONDS.                      *
*  AUTHOR R. THORNTON APR 1994                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
OPENS    OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1,,INPUT2)
         BAS   R9,HEADING              PRINT FIRST HEADING
         L     R0,TBLENGTH             GET TABLE STORAGE LENGTH
         STORAGE OBTAIN,LENGTH=(R0),SP=1 GET TABLE STORAGE
         ST    R1,TBLSTRT              SAVE TABLE ADDRESS
         LR    R12,R1                  POINT TO TABLE
         ZAP   TOTPGMS,P0              RESET COUNTER
LOADPGMS GET   INPUT2                  READ A PROGRAM NAME RECORD
         MVC   0(8,R12),0(R1)          STORE NAME IN TABLE
         LA    R12,8(R12)              ADVANCE IN TABLE
         AP    TOTPGMS,P1              INCREMENT COUNTER
         CP    TOTPGMS,MAXPGMS         PAST END OF GOTTEN STORAGE?
         BL    LOADPGMS                LOOP TO LOAD PROGRAM NAME TBL
EOF2     ST    R12,TBLEND              SAVE TABLE END ADDRESS
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
MAINLINE GET   INPUT1,INPREC           GET SMFSTEPT RECORD
         CLC   INPTCPU,MINCPU          AT LEAST 2.00 CPU SECONDS?
         BL    MAINLINE                NO, DROP IT
         L     R12,TBLSTRT             POINT TO PROGRAM NAME TABLE
SEARCH   CLC   INPPGM,0(R12)           FOUND NAME IN TABLE?
         BE    WRITE                   YES
         LA    R12,8(R12)              NO, STEP TO NEXT ENTRY
         C     R12,TBLEND              STILL IN TABLE?
         BL    SEARCH                  YES
         B     MAINLINE                NO, PROGRAM NAME NOT FOUND
WRITE    PUT   OUTPUT1,INPREC          WRITE OUTPUT RECORD
         PUT   PRINT1,PRTREC           WRITE PRINT LINE
         AP    LINECT,P1               INCREMENT LINE COUNTER
         CP    LINECT,ENDPAGE          PAGE IS FULL?
         BL    MAINLINE                NO
         BAS   R9,HEADING              YES, PRINT A HEADING
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        PRINT A HEADING                                              *
***********************************************************************
*
HEADING  AP    PAGENO,P1               INCREMENT PAGE COUNTER
         MVC   HDGPAGE,PAGEDIT         MOVE EDIT MASK
         ED    HDGPAGE,PAGENO          MOVE PAGE NUMBER
         PUT   PRINT1,HDGLINE          WRITE HEADING LINE
         ZAP   LINECT,P1               RESET LINE COUNTER
         BR    R9                      EXIT
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (PRINT1,,OUTPUT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         GET CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE = 0
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
PAGENO   DC    PL2'0'                  PAGE COUNTER
PAGEDIT  DC    XL4'40202021'           PAGE EDIT MASK
ENDPAGE  DC    P'58'                   NUMBER LINES PER PAGE
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
TBLSTRT  DC    A(0)                    PROGRAM NAME TABLE START
TBLEND   DC    A(0)                    PROGRAM NAME TABLE END
TBLENGTH DC    A(160000)               PROGRAM NAME TABLE LENGTH
MAXPGMS  DC    P'20000'                MAX PROGRAMS TO FIT IN STRG
TOTPGMS  DC    PL3'0'                  TOTAL PROGRAMS IN TABLE
MINCPU   DC    CL25'                     2.00'
*
HDGLINE  DS    0CL80
         DC    CL1'1'
         DC    CL9' PROGRAM '
         DC    CL26'              CPU SECONDS '
         DC    CL6' RUNS '
         DC    CL20'            AVG CPU '
         DC    CL8'   PAGE '
HDGPAGE  DS    CL4
         DC    CL6' '
*
PRTREC   DS    0CL80
         DC    CL1' '
INPREC   DS    0CL80
INPPGM   DC    CL8' '          PROGRAM NAME
         DC    CL1' '
INPTCPU  DC    CL25' '         TOTAL CPU TIME
         DC    CL1' '
INPRUNS  DC    CL5' '          TOTAL RUNS
         DC    CL1' '
INPAVG   DC    CL19' '         AVERAGE CPU PER RUN
         DC    CL20' '
         LTORG
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=FB,LRECL=80
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
*
SMFSTEP3 CSECT
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

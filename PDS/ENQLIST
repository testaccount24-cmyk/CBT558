         PRINT GEN
         TITLE '*****ENQLIST: DISPLAY ENQUEUE INFORMATION *****'
ENQLIST  SVLNK R3,R4
*
***********************************************************************
* READS A COMMAND FROM THE SYSIN DATASET, USES THE GQSCAN MACRO TO    *
* OBTAIN THE REQUESTED INFORMATION CONCERNING ENQ CONTROL BLOCK CHAINS*
* AND PRINTS THE RESULTS TO THE SYSPRINT DATASET. ACCEPTABLE COMMANDS *
* ARE:                                                                *
*    (1) DISPLAY ENQ CONFLICT INFORMATION                             *
*        EXAMPLE:                                                     *
*        COL  1-72 = ALL BLANK                                        *
*                                                                     *
*    (2) TERMINATE PROCESSING (END OF JOB REQUEST)                    *
*        EXAMPLE: END                                                 *
*        COL  1-3  = "END", REST OF COMMAND IS BLANK                  *
*                                                                     *
*    (3) DISPLAY ENQ INFO FOR THE JOBNAME SPECIFIED                   *
*        EXAMPLE: JOB=GMMCPRW6                                        *
*        COL  1-4  = "JOB="                                           *
*        COL  5-12 = JOBNAME (1-8 CHARACTERS), REST OF COMMAND BLANK  *
*                                                                     *
*    (4) DISPLAY ENQ INFO FOR ALL QNAMES IN USE                       *
*        EXAMPLE: QNAMES                                              *
*        COL  1-6  = "QNAMES"                                         *
*                                                                     *
*    (5) DISPLAY ENQ INFO FOR THE Q-NAME SPECIFIED                    *
*        EXAMPLE: SYSZVOLS                                            *
*        COL  1-8  = Q-NAME (1-8 CHARACTERS), REST OF COMMAND BLANK   *
*                                                                     *
*    (6) DISPLAY ENQ INFO FOR THE RESOURCE SPECIFIED                  *
*        EXAMPLE: SYSDSN  .SYS1.LINKLIB                               *
*        COL  1-8  = Q-NAME (1-8 CHARACTERS)                          *
*        COL   9   = "."                                              *
*        COL 10-80 = R-NAME FOLLOWED BY BLANKS                        *
*                                                                     *
*    (7) DISPLAY HELP PANEL                                           *
*        EXAMPLE: HELP                                                *
*        COL  1-4  = "HELP", REST OF COMMAND BLANK                    *
*                                                                     *
* AUTHOR R THORNTON APR 1985                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES, GET GQSCAN STORAGE AREA, FORMAT SYSTEM  *
*                 DATE IN TEMPDS AREA.                                *
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN FILES
         GETMAIN R,LV=524288           GQSCAN AREA STORAGE
         ST    R1,AREAADDR             SAVE THE STORAGE ADDRESS
         L     R1,16                   POINT TO CVT
         UNPK  SYSDATE,57(3,R1)        UNPACK DATE TO SYSTEM DATE FLD
         OI    SYSDATE+4,C'0'          CLEAR DATE
*
***********************************************************************
* MAINLINE PROCESSING: READ THE SYSIN DATASET TO OBTAIN THE COMMAND.  *
* EVALUATE THE COMMAND AND PERFORM PROCESSING ROUTINES TO ACCOMPLISH  *
* THE REQUESTED FUNCTION.                                             *
***********************************************************************
*
MAINLINE GET   SYSIN,COMMAND           READ NEXT COMMAND
         MVI   MAJIND,0                CLEAR MAJOR NAME SWITCH
         MVI   PRT,C'*'                SEPARATOR LINE
         BAL   R9,PRINT                PRINT SEPARATOR
         MVI   PRT,C'*'                SEPARATOR LINE
         BAL   R9,PRINT                PRINT SEPARATOR
         MVI   PRT,C'*'                SEPARATOR LINE
         BAL   R9,PRINT                PRINT SEPARATOR
         MVC   PRT,COMMAND             MOVE IT TO PRINT AREA
         BAL   R9,PRINT                ECHO REQUEST BACK
         BAL   R9,PRINT                PRINT A BLANK LINE
CHKJBNM  CLC   CMDDLM,=CL4'JOB='       THIS A JOB REQUEST?
         BNE   CHKALLQ                 NO
         CLC   JOBREST,BLANX           REST OF COMMAND BLANK?
         BNE   BADCOMD                 NO, ERROR
         BAL   R9,JOBINFO              YES, PROCESS BY JOBNAME
         B     MAINLINE                GO GET ANOTHER COMMAND
CHKALLQ  CLC   CMDDLM,=CL6'QNAMES'     THIS AN ALL-QNAMES REQUEST?
         BNE   CHKHELP                 NO
         CLC   CMDDLM+6(66),BLANX      REST OF COMMAND BLANK?
         BNE   CHKQNAME                NO
         BAL   R9,QNAMES               YES, PROCESS ALL QNAMES
         B     MAINLINE                GO GET ANOTHER COMMAND
CHKHELP  CLC   CMDDLM,=CL4'HELP'       THIS A HELP REQUEST?
         BNE   CHKQNAME                NO
         CLC   JOBNAME,BLANX           REST OF COMMAND BLANK?
         BNE   CHKQNAME                NO
         CLC   JOBREST,BLANX           REST OF COMMAND BLANK?
         BE    HELPDSPY                YES
CHKQNAME CLC   QNAME,BLANX             QNAME SPECIFIED?
         BNE   CHKDOT                  YES
         CLC   COMMAND(72),BLANX       CONFLICT REQUEST?
         BNE   BADCOMD                 NO, ERROR
         BAL   R9,CONFLICT             YES, PERFORM PROCESSING
         B     MAINLINE                GO GET ANOTHER COMMAND
CHKDOT   CLI   CMDDOT,C'.'             MAJOR.MINOR REQUEST?
         BE    CHKRNAME                YES
         CLC   QREST,BLANX             REST OF CARD BLANK?
         BNE   BADCOMD                 NO, ERROR
         BAL   R9,MAJORNAM             YES, PERFORM Q-NAME PROCESSING
         B     MAINLINE                GO GET ANOTHER COMMAND
CHKRNAME CLC   RNAME,BLANX             IS THERE A MINOR NAME?
         BE    BADCOMD                 NO, ERROR
         BAL   R9,RESOURCE             YES, PERFORM RESOURCE PROCESSING
         B     MAINLINE                GO GET ANOTHER COMMANDE
BADCOMD  MVC   PRT,BADCOMM             ERROR MESSAGE TO PRINT
         BAL   R9,PRINT                PRINT THE MESSAGE
HELPDSPY LA    R12,H1                  POINT TO 1ST "HELP" LINE
         LA    R11,28                  NUMBER LINES TO DISPLAY
HELPLUP  MVC   PRT,0(R12)              MOVE A LINE TO PRINT
         BAL   R9,PRINT                PRINT IT
         LA    R12,80(R12)             ADVANCE TO NEXT LINE
         BCT   R11,HELPLUP             CONTINUE
         B     MAINLINE                GO READ NEXT COMMAND
*
***********************************************************************
*       DISPLAY ENQ CONFLICT DATA                                     *
***********************************************************************
*
CONFLICT ST    R9,CON9                  SAVE RETURN ADDRESS
         MVI   FOUND,0                  CLEAR FOUND SWITCH
SCNWAIT  L     R11,AREAADDR             GET STORAGE ADDRESS
         L     R12,AREALENG             GET STORAGE LENGTH
         XC    TOKEN,TOKEN              ZERO THE TOKEN ADDRESS
         GQSCAN AREA=((11),(12)),REQLIM=0,SCOPE=ALL,WAITCNT=1,         X
               TOKEN=TOKEN
         BAL   R9,GQCOMMON              GO SAVE DATA, CHECK ERRORS
CONFMT   MVI   FOUND,1                  SHOW CONFLICT DATA FOUND
         BAL   R9,ENQPRNT               GO PRINT ENQ DATA
CONEND   CLI   GSRETCOD,8               MORE DATA AVAILABLE?
         BE    SCNWAIT                  YES, GO SCAN AGAIN
CONXIT   L     R9,CON9                  GET RETURN ADDRESS
         BR    R9                       EXIT
*
***********************************************************************
*       DISPLAY ENQ INFORMATION FOR A MAJOR NAME (QNAME)              *
***********************************************************************
*
MAJORNAM ST    R9,MAJ9                  SAVE RETURN ADDRESS
         MVI   FOUND,0                  CLEAR FOUND SWITCH
         MVI   MAJIND,1                 SET MAJOR NAME SWITCH
SCNMAJR  L     R11,AREAADDR             GET STORAGE ADDRESS
         L     R12,AREALENG             GET STORAGE LENGTH
         XC    TOKEN,TOKEN              ZERO THE TOKEN ADDRESS
         GQSCAN AREA=((11),(12)),REQLIM=0,SCOPE=ALL,RESNAME=QNAME,     X
               TOKEN=TOKEN
         BAL   R9,GQCOMMON              GO SAVE DATA, CHECK ERRORS
MAJFMT   MVI   FOUND,1                  SHOW DATA FOUND
         BAL   R9,ENQPRNT               GO PRINT ENQ DATA
MAJEND   CLI   GSRETCOD,8               MORE DATA AVAILABLE?
         BE    SCNMAJR                  YES, GO SCAN AGAIN
MAJXIT   L     R9,MAJ9                  GET RETURN ADDRESS
         BR    R9                       EXIT
*
***********************************************************************
*       DISPLAY ENQ INFORMATION FOR ALL MAJOR NAMES (QNAMES) IN USE   *
***********************************************************************
*
QNAMES   ST    R9,ALQ9                  SAVE RETURN ADDRESS
         MVI   FOUND,0                  CLEAR FOUND SWITCH
SCNALLQ  L     R11,AREAADDR             GET STORAGE ADDRESS
         L     R12,AREALENG             GET STORAGE LENGTH
         XC    TOKEN,TOKEN              ZERO THE TOKEN ADDRESS
         GQSCAN AREA=((11),(12)),REQLIM=0,SCOPE=ALL,TOKEN=TOKEN
         BAL   R9,GQCOMMON              GO SAVE DATA, CHECK ERRORS
         MVI   FOUND,1                  SHOW DATA FOUND
         L     R12,AREAADDR             POINT TO GQSCAN AREA
         USING RIB,R12
         LR    R11,R12                  1ST AVAILABLE QNM TABLE ENTRY
QNMNEXT  L     R10,AREAADDR             POINT TO 1ST TABLE ENTRY
         USING QNMTBL,R10
QNMCKND  CR    R10,R11                  PAST TABLE END?
         BNL   QNMNEW                   YES, NEW ENTRY
         CLC   RIBQNAME,QNMQNAME        FOUND ENTRY FOR THIS QNAME?
         BE    QNMFND                   YES
         LA    R10,L'QNMNTRY(R10)       NO, STEP TO NEXT QNAME ENTRY
         B     QNMCKND                  CONTINUE SEARCH
QNMNEW   XC    QNMNTRY,QNMNTRY          CLEAR ENTRY TO ZEROS
         MVC   QNMQNAME,RIBQNAME        QNAME TO TABLE ENTRY
         LA    R11,L'QNMNTRY(R10)       SET NEW TABLE END
QNMFND   L     R1,QNMTOTR               GET TOTAL RESOURCES
         LA    R1,1(R1)                 ADD 1 MORE
         ST    R1,QNMTOTR               UPDATE RESOURCE COUNT
         L     R1,RIBNTO                GET NBR OWNERS THIS RIB
         A     R1,QNMTOTO               ADD TO TOTAL
         ST    R1,QNMTOTO               UPDATE TOTAL OF OWNERS
         L     R1,RIBNTWE               GET EXC WAITERS, THIS RIB
         A     R1,RIBNTWS               ADD SHR WAITERS, THIS RIB
         A     R1,QNMTOTW               ADD TOTAL OF WAITERS
         ST    R1,QNMTOTW               UPDATE TOTAL OF WAITERS
         L     R1,RIBNRIBE              NBR RIBE'S FOR THIS RIB
         MH    R1,RIBELENG              COMPUTE TOTAL OF RIBE LENGTHS
         AH    R1,RIBVLEN               ADD RIB VARIABLE LENGTH
         AH    R1,RIBFIXLN              ADD RIB FIXED LENGTH
         AR    R12,R1                   POINT TO NEXT RIB IN AREA
         L     R1,NUMRIBS               GET NUMBER RIB'S REMAINING
         BCTR  R1,0                     SUBTRACT 1
         ST    R1,NUMRIBS               SAVE REMAINDER
         LTR   R1,R1                    ARE THERE ANY MORE RIB'S?
         BP    QNMNEXT                  CONTINUE IF MORE RIB'S
QNMSORT  L     R10,AREAADDR             POINT TO TABLE START
QNMSTPAS LA    R12,L'QNMNTRY(R10)       POINT TO SECOND ENTRY
QNMPASS  CR    R12,R11                  PAST TABLE END, THIS PASS?
         BL    QNMSAMP                  NO
         LA    R10,L'QNMNTRY(R10)       YES, SET UP TO NEXT ENTRY
         CR    R10,R11                  PAST TABLE END?
         BL    QNMSTPAS                 NO
         B     QNMSRTD                  YES, SORT COMPLETE
QNMSAMP  CLC   QNMQNAME,QNMQNAME-QNMNTRY(R12) ENTRIES IN ORDER?
         BL    QNMSTP1                  YES
         MVC   ENTSAVE,QNMNTRY          NO, SAVE 1ST ENTRY
         MVC   QNMQNAME,QNMQNAME-QNMNTRY(R12) MOVE 2ND TO 1ST
         MVC   QNMQNAME-QNMNTRY(L'QNMNTRY,R12),ENTSAVE MOVE 1ST TO 2ND
QNMSTP1  LA    R12,L'QNMNTRY(R12)       ADVANCE 2ND POINTER 1 ENTRY
         B     QNMPASS                  CONTINUE THE PASS
QNMSRTD  L     R10,AREAADDR             POINT TO TABLE START
QNMPCKND CR    R10,R11                  PAST TABLE END?
         BNL   QNMEND                   YES
         MVC   PQNAME,QNMQNAME          QNAME TO PRINT
         L     R1,QNMTOTR               GET TOTAL OF RESOURCES
         CVD   R1,DBLWD                 CONVERT TO PACKED
         MVC   PQNTOTR,=X'40202120'     EDIT WORD
         ED    PQNTOTR,DBLWD+6          EDIT TO PRINT
         MVC   PQLRES,=CL10'RESOURCES,' LITERAL TO PRINT
         L     R1,QNMTOTO               GET TOTAL OF OWNERS
         CVD   R1,DBLWD                 CONVERT TO PACKED
         MVC   PQNTOTO,=X'40202120'     EDIT WORD
         ED    PQNTOTO,DBLWD+6          EDIT TO PRINT
         MVC   PQLOWN,=CL7'OWNERS,'     LITERAL TO PRINT
         L     R1,QNMTOTW               GET TOTAL OF WAITERS
         CVD   R1,DBLWD                 CONVERT TO PACKED
         MVC   PQNTOTW,=X'40202120'     EDIT WORD
         ED    PQNTOTW,DBLWD+6          EDIT TO PRINT
         MVC   PQLWTR,=CL7'WAITERS'     LITERAL TO PRINT
         BAL   R9,PRINT                 PRINT LAST QNAME LINE
         LA    R10,L'QNMNTRY(R10)       STEP TO NEXT ENTRY
         B     QNMPCKND                 CONTINUE THRU TABLE
QNMEND   CLI   GSRETCOD,8               MORE DATA AVAILABLE?
         BE    SCNALLQ                  YES, GO SCAN AGAIN
ALLXIT   L     R9,ALQ9                  GET RETURN ADDRESS
         BR    R9                       EXIT
         DROP  R12
*
***********************************************************************
*       DISPLAY ENQ INFORMATION RELATED TO A SPECIFIC RESOURCE        *
***********************************************************************
*
RESOURCE ST    R9,RES9                  SAVE RETURN ADDRESS
         MVI   FOUND,0                  CLEAR FOUND SWITCH
         LA    R10,CMDEND-1             POINT TO LAST BYTE OF COMMAND
SCNRESO  L     R11,AREAADDR             GET STORAGE ADDRESS
         L     R12,AREALENG             GET STORAGE LENGTH
RNRHE    CLI   0(R10),C' '              FOUND LAST BYTE OF RNAME?
         BNE   RNLENG                   YES
         BCT   R10,RNRHE                NO, CONTINUE SEARCH
RNLENG   S     R10,=A(RNAME-1)          R10 = RNAME LENGTH
         XC    TOKEN,TOKEN              ZERO THE TOKEN ADDRESS
         GQSCAN AREA=((11),(12)),REQLIM=MAX,SCOPE=ALL,                 X
               RESNAME=(QNAME,RNAME,(10)),TOKEN=TOKEN
         BAL   R9,GQCOMMON              GO SAVE DATA, CHECK ERRORS
RESFMT   MVI   FOUND,1                  SHOW RESOURCE DATA FOUND
         BAL   R9,ENQPRNT               GO PRINT ENQ DATA
RESEND   CLI   GSRETCOD,8               MORE DATA AVAILABLE?
         BE    SCNRESO                  YES, GO SCAN AGAIN
RESXIT   L     R9,RES9                  GET RETURN ADDRESS
         BR    R9                       EXIT
*
***********************************************************************
*       DISPLAY ENQ INFORMATION FOR A SPECIFIED JOB NAME              *
***********************************************************************
*
JOBINFO  ST    R9,JOB9                  SAVE RETURN ADDRESS
         MVI   FOUND,0                  CLEAR FOUND SWITCH
SCNJOBN  L     R11,AREAADDR             GET STORAGE ADDRESS
         L     R12,AREALENG             GET STORAGE LENGTH
         L     R1,16                    POINT TO CVT
         L     R1,556(R1)               POINT TO ASVT (CVTASVT)
         L     R2,516(R1)               NBR ASCB'S IN ASVT (ASVTMAXU)
         LA    R1,528(R1)               PT TO 1ST ASVT ENTRY (ASVTENTY)
JOBSLUP  TM    0(R1),X'80'              THIS ASCB POINTER IN USE?
         BZ    JOBCHEK                  YES
JOBNEXT  LA    R1,4(R1)                 NO, STEP TO NEXT ASVTENTY
         BCT   R2,JOBSLUP               CONTINUE SEARCH
         MVC   PRT,NOSUCHJB             MOVE ERROR MESSAGE
         BAL   R9,PRINT                 PRINT IT
         B     JOBXIT                   AND EXIT
JOBCHEK  L     R10,0(R1)                POINT TO ASCB FOR JOB
         L     R15,172(R10)             JOBNAME POINTER (ASCBJBNI)
         CLM   R15,7,=F'0'              IS THERE A POINTER?
         BNE   JOBCMPR                  YES
         L     R15,176(R10)             NO, PICK UP ASCBJBNS
JOBCMPR  CLC   JOBNAME,0(R15)           FOUND ASCB FOR SPECIFIED JOB?
         BNE   JOBNEXT                  NO, CONTINUE SEARCH
         LH    R10,36(R10)              YES, GET ASID FROM ASCB
         XC    TOKEN,TOKEN              ZERO THE TOKEN ADDRESS
         GQSCAN AREA=((11),(12)),REQLIM=0,SCOPE=ALL,                   X
               SYSNAME=(0,(10)),TOKEN=TOKEN
         BAL   R9,GQCOMMON              GO SAVE DATA, CHECK ERRORS
JOBFMT   MVI   FOUND,1                  SHOW DATA FOUND
         BAL   R9,ENQPRNT               GO PRINT ENQ DATA
JOBEND   CLI   GSRETCOD,8               MORE DATA AVAILABLE?
         BE    SCNJOBN                  YES, GO SCAN AGAIN
JOBXIT   L     R9,JOB9                  GET RETURN ADDRESS
         BR    R9                       EXIT
*
***********************************************************************
*       FORMAT AND PRINT INFORMATION RETURNED FROM GQSCAN             *
***********************************************************************
*
ENQPRNT  ST    R9,EP9                   SAVE RETURN ADDRESS
         L     R11,AREAADDR             POINT TO GQSCAN AREA
         USING RIB,R11
EPNEXRIB ST    R11,CURRIB               SAVE CURRENT RIB ADDRESS
         BAL   R9,RIBPRNT               GO PRINT A RIB
EPRIBSTP L     R11,CURRIB               GET CURRENT RIB ADDRESS
         L     R1,RIBNRIBE              NBR RIBE'S FOR THIS RIB
         MH    R1,RIBELENG              COMPUTE TOTAL OF RIBE LENGTHS
         AH    R1,RIBVLEN               ADD RIB VARIABLE LENGTH
         AH    R1,RIBFIXLN              ADD RIB FIXED LENGTH
         AR    R11,R1                   POINT TO NEXT RIB IN AREA
         L     R1,NUMRIBS               GET NUMBER RIB'S REMAINING
         BCTR  R1,0                     SUBTRACT 1
         ST    R1,NUMRIBS               SAVE REMAINDER
         LTR   R1,R1                    ARE THERE ANY MORE RIB'S?
         BP    EPNEXRIB                 CONTINUE IF MORE RIB'S
EPXIT    L     R9,EP9                   GET RETURN ADDRESS
         BR    R9                       RETURN TO CALLER
         DROP  R11
*
***********************************************************************
*       FORMAT AND PRINT INFORMATION FROM A RIB                       *
***********************************************************************
*
RIBPRNT  ST    R9,BP9                   SAVE RETURN ADDRESS
         L     R11,CURRIB               POINT TO CURRENT RIB
         USING RIB,R11
         MVC   NUMRIBES,RIBNRIBE        SAVE NBR RIBES REMAINING
         LH    R1,RIBVLEN               VARIABLE RIB LENGTH
         AH    R1,RIBFIXLN              ADD RIB FIXED LENGTH
         LA    R1,0(R11,R1)             POINT TO FIRST RIBE
         ST    R1,CURRIBE               SAVE RIBE POINTER
         MVC   EPQNAM,RIBQNAME          QNAME TO PRINT
         SR    R1,R1                    CLEAR FOR INSERT
         IC    R1,RIBRNMLN              GET RNAME LENGTH
         CH    R1,=H'44'                NAME TOO LONG?
         BNH   EPLENCD                  NO
         LH    R1,=H'44'                YES, SET TO MAX
         MVI   EPTRUNC,C'+'             SHOW TRUNCATION
EPLENCD  BCTR  R1,0                     -1 = LENGTH CODE
         LR    R2,R11                   COPY RIB ADDRESS
         AH    R2,RIBFIXLN              POINT TO RIB VARIABLE PART
         EX    R1,EPRNMVC               MOVE RNAME TO PRINT LINE
         L     R1,RIBNTO                GET NBR OWNERS
         MVC   EPOWN,=CL4' OWN'         LITERAL TO PRINT
EPEDOWN  CVD   R1,DBLWD                 CONVERT TO PACKED
         MVC   EPNBROWN,=X'40202120'    EDIT WORD FOR OWNERS
         ED    EPNBROWN,DBLWD+6         EDIT NBR OWNERS TO PRINT
EPWAITR  L     R1,RIBNTWE               GET NBR EXCLUSIVE WAITERS
         A     R1,RIBNTWS               ADD NBR SHARED WAITERS
         MVC   EPWTR,=CL4' WTG'         LITERAL TO PRINT
EPEDWTR  CVD   R1,DBLWD                 CONVERT TO PACKED
         MVC   EPNBRWTR,=X'40202120'    EDIT WORD FOR WAITERS
         ED    EPNBRWTR,DBLWD+6         EDIT NBR WAITERSTO PRINT
EPSYSTEM MVC   EPNSCOPE,=CL7'SYSTEM'    ASSUME SYSTEM SCOPE
         TM    RIBSCOPE,RIBSYS          IS IT SYSTEM?
         BO    EPRIBCHK                 YES, GO PRINT
         MVC   EPNSCOPE,=CL7'SYSTEMS'   ASSUME SYSTEMS
         TM    RIBSCOPE,RIBSYSS         IS IT SYSTEMS?
         BO    EPRIBCHK                 YES, GO PRINT
         MVC   EPNSCOPE,=CL7'STEP'      ASSUME STEP
         TM    RIBSCOPE,RIBSTEP         IS IT STEP?
         BO    EPRIBCHK                 YES
         MVC   EPNSCOPE,=CL7'GLOBAL'    ASSUME GLOBAL
         TM    RIBSCOPE,RIBGLBL         IS IT GLOBAL?
         BO    EPRIBCHK                 YES
         MVC   EPNSCOPE,=CL7'???????'   NO, UNKNOWN SCOPE
EPRIBCHK CLI   MAJIND,1                 MAJOR NAME LISTING?
         BNE   EPRIBPRT                 NO
         MVC   EPQNAM,BLANX             YES, DON'T PRINT QNAME
         CLC   QNAME,=CL8'SYSDSN'       MAJOR NAME IS SYSDSN?
         BNE   EPRIBPRT                 NO
         CLC   TEMPNAME,EPRNAME         YES, THIS A TEMPORARY DATASET?
         BE    EPNXRIBE                 YES, DON'T PRINT IT
EPRIBPRT BAL   R9,PRINT                 GO PRINT A LINE
EPNXRIBE CLC   NUMRIBES,=F'0'           ANY RIBE'S REMAIN TO PRINT?
         BNP   BPXIT                    NO
         BAL   R9,RIBEPRNT              YES, GO FORMAT/PRINT A RIBE
         L     R1,NUMRIBES              GET NBR RIBE'S REMAINING
         BCTR  R1,0                     YES, DEDUCT ONE
         ST    R1,NUMRIBES              AND SAVE REMAINDER
         L     R1,CURRIBE               POINT TO CURRENT RIBE
         AH    R1,RIBELENG              POINT TO NEXT RIBE
         ST    R1,CURRIBE               UPDATE CURRENT RIBE POINTER
         B     EPNXRIBE                 LOOP TO PRINT ALL RIBES
BPXIT    L     R9,BP9                   GET RETURN ADDRESS
         BR    R9                       RETURN TO CALLER
         DROP  R11
         USING RIBVAR,R2
EPRNMVC  MVC   EPRNAME(1),RIBRNAME      MOVE RNAME TO PRINT
         DROP  R2
*
***********************************************************************
*       FORMAT AND PRINT INFORMATION FROM A RIBE                      *
***********************************************************************
*
RIBEPRNT ST    R9,IP9                   SAVE RETURN ADDRESS
         L     R11,CURRIBE              POINT TO CURRENT RIBE
         USING RIBE,R11
         MVC   EPRJOB,RIBEJBNM          JOB NAME TO PRINT
         UNPK  EPRASID(5),RIBEASID(3)   UNPACK TO MAKE PRINTABLE
         TR    EPRASID(4),TRALFA-C'0'   MAKE PRINTABLE
         MVI   EPRASID+4,C' '           CLEAN UP GARBAGE
         MVC   EPRJOBL,=CL4'JOB='       LITERAL
         MVC   EPRASIDL,=CL5'ASID='     LITERAL
         MVC   EPROW,=CL7'OWNS'         ASSUME OWNER
         TM    RIBESFLG,RIBESTAT        IS IT AN OWNER?
         BO    EPCKTYPE                 YES
         MVC   EPROW,=CL7'WAITING'      NO, SHOW WAITING
EPCKTYPE MVC   EPEXCSHR,=CL3'EXC'       ASSUME EXCLUSIVE
         TM    RIBERFLG,RIBETYPE        IS IT EXCLUSIVE?
         BZ    EPCKMC                   YES
         MVC   EPEXCSHR,=CL3'SHR'       NO
EPCKMC   TM    RIBERFLG,RIBEMC          IS IT MUST COMPLETE?
         BZ    EPCKRSV                  NO
         MVC   EPRMC,=CL2'MC'           YES, SHOW MUST COMPLETE
EPCKRSV  TM    RIBERFLG,RIBERESV        IS IT A RESERVE?
         BZ    EPCKAUTH                 NO
         MVC   EPRESERV,=CL7'RESERVE'   YES, SHOW RESERVE
EPCKAUTH TM    RIBERFLG,RIBEAUTH        AUTHORIZED CALLER?
         BZ    EPRIBEPT                 NO
         MVC   EPAUTH,=CL4'AUTH'        YES
EPRIBEPT BAL   R9,PRINT                 GO PRINT RIBE DATA
         L     R9,IP9                   GET RETURN ADDRESS
         BR    R9                       RETURN TO CALLER
         DROP  R11
*
***********************************************************************
*       COMMON GQSCAN PROCESSING                                      *
***********************************************************************
*
GQCOMMON STCM  R0,12,RIBFIXLN           LENGTH OF FIXED PART OF RIB
         STH   R0,RIBELENG              LENGTH OF RIBE
         ST    R1,NUMRIBS               NUMBER OF RIB'S REMAINING
         ST    R1,RIBCNT                NUMBER OF RIB'S RETURNED
         ST    R15,GSRETCOD             SAVE RETURN CODE
         LTR   R15,R15                  DATA OBTAINED?
         BZ    GQCMGOOD                 YES, EXIT
         CH    R15,=H'4'                WERE THERE ANY MATCHES?
         BE    NOMATCH                  NO
         CH    R15,=H'8'                AREA FILLED?
         BE    GQCMGOOD                 YES, EXIT
         CH    R15,=H'12'               GQSCAN ERROR?
         BE    BADSCAN                  YES
         CH    R15,=H'16'               INVALID SYSNAME?
         BE    BADNAME                  YES
INVGQRET MVC   PRT,INVRETCD             INVALID RETURN CODE MESSAGE
         B     PRTERMG                  GO PRINT MESSAGE
BADNAME  MVC   PRT,INVSYSNM             INVALID SYSNAME ERROR MESSAGE
         B     PRTERMG                  GO PRINT MESSAGE
BADSCAN  MVC   PRT,GQSCNER              ERROR DURING GQSCAN MESSAGE
         B     PRTERMG                  GO PRINT MESSAGE
NOMATCH  CLI   FOUND,0                  WAS ANYTHING DISPLAYED?
         BNE   GQCMGOOD                 YES
         MVC   PRT,NOMCHMG              NO, SHOW NO CONFLICTS FOUND
         B     PRTERMG                  GO PRINT MESSAGE
GQCMGOOD BR    R9                       EXIT
PRTERMG  BAL   R9,PRINT                 PRINT THE ERROR
         B     MAINLINE                 GO GET NEXT COMMAND
*
***********************************************************************
*        PRINT A LINE                                                 *
***********************************************************************
*
PRINT    PUT   SYSPRINT,PRT            WRITE PRINT LINE
         MVC   PRT,BLANX               CLEAR PRINT LINE
         BR    R9                      EXIT
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (SYSPRINT,,SYSIN)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                        DOUBLEWORD WORK AREA
CURRIB   DC    A(0)                     CURRENT RIB ADDRESS
CURRIBE  DC    A(0)                     CURRENT RIBE ADDRESS
NUMRIBS  DC    F'0'                     NUMBER OF RIB'S REMAINING
NUMRIBES DC    F'0'                     NUMBER OF RIBE'S REMAINING
RIBCNT   DC    F'0'                     NUMBER OF RIB'S RETURNED
GSRETCOD DC    F'0'                     GQSCAN RETURN CODE
RIBFIXLN DC    H'0'                     LENGTH OF FIXED PART OF RIB
RIBELENG DC    H'0'                     LENGTH OF RIBE
CON9     DS    F                       CONFLICT RETURN SAVE
EP9      DS    F                       ENQPRNT RETURN SAVE
BP9      DS    F                       RIBPRNT RETURN SAVE
IP9      DS    F                       RIBEPRNT RETURN SAVE
RES9     DS    F                       RESOURCE RETURN SAVE
MAJ9     DS    F                       MAJORNAM RETURN SAVE
ALQ9     DS    F                       QNAMES RETURN SAVE
JOB9     DS    F                       JOBINFO RETURN SAVE
TOKEN    DC    A(0)                    RETURN ADDR FROM GQSCAN
AREAADDR DC    A(0)                    ADDRESS OF GOTTEN STORAGE
AREALENG DC    F'524288'               LENGTH OF GOTTEN STORAGE
BLANX    DC    CL80' '                 BLANKS
TRALFA   DC    C'0123456789ABCDEF'      HEX-PRINTABLE CONVERSION
TEMPNAME DS    0CL10                    BEGINNING OF TEMPORARY DSNAME
         DC    CL3'SYS'                 BEGINS WITH "SYS"
SYSDATE  DC    CL5'YYDDD'               FOLLOWED BY DATE
         DC    CL2'.T'                  THEN A DOT AND TIME
FOUND    DC    X'00'                    DATA FOUND SWITCH
MAJIND   DC    X'00'                    1=MAJOR NAME (QNAME) DISPLAY
ENTSAVE  DS    CL20                     MATCH SIZE WITH QNMNTRY
*
*
*
COMMAND  DS    0CL80                   COMMAND INPUT AREA
QNAME    DS    CL8                     MAJOR NAME OF RESOURCE
QREST    DS    0CL64                   BLANKS AFTER QNAME REQUEST
CMDDOT   DS    CL1                     PERIOD=MAJOR.MINOR ENTERED
RNAME    DS    CL63                    MINOR NAME OF RESOURCE
CMDEND   EQU   *                       END OF COMMAND RECORD
         DS    CL8                     CARD NUMBER FIELD
         ORG   COMMAND
CMDDLM   DS    CL4                     COMMAND DELIMITER: END, JOB=
JOBNAME  DS    CL8                     JOBNAME IF CMDDLM IS JOB=
JOBREST  DS    CL60                    BLANKS
         DS    CL8                     CARD NUMBER FIELD
         ORG
*
*
*
PRT      DC    CL80' '                 PRINT LINE
         ORG   PRT
EPRIB    DS    0CL80                    RIB PRINT AREA FOR RIBPRNT
EPQNAM   DS    CL8                      QNAME OF RESOURCE
         DS    CL1
EPRNAME  DS    CL44                     RNAME OF RESOURCE
EPTRUNC  DS    CL1                      + = NAME TRUNCATED
EPNBROWN DS    CL4                      NUMBER OWNERS
EPOWN    DS    CL4                      " OWN"
EPNBRWTR DS    CL4                      NUMBER WAITERS
EPWTR    DS    CL4                      " WTG"
         DS    CL1
EPNSCOPE DS    CL7                      SCOPE
         DS    CL2
         ORG
         ORG   PRT
EPRIBE   DS    0CL80                    RIBE PRINT AREA FOR RIBEPRNT
         DS    CL15
EPRJOBL  DS    CL4                      "JOB="
EPRJOB   DS    CL8                      JOB NAME
         DS    CL1
EPRASIDL DS    CL5                      "ASID="
EPRASID  DS    CL4                      ASID
         DS    CL1
EPROW    DS    CL7                      "OWNS" OR "WAITING"
         DS    CL1
EPEXCSHR DS    CL3                      "EXC" OR "SHR"
         DS    CL1
EPRMC    DS    CL2                      "MC" OR BLANK
         DS    CL1
EPRESERV DS    CL7                      "RESERVE" OR BLANK
         DS    CL1
EPAUTH   DS    CL4                      "AUTH" OR BLANK
         DS    CL15
*
*
*
         ORG   PRT
PQNM     DS    0CL80                 PRINT AREA FOR ALLQNAM
PQNAME   DS    CL8                   QNAME
         DS    CL1
PQNTOTR  DS    CL4                   TOTAL OF RESOURCES
         DS    CL1
PQLRES   DS    CL10                  "RESOURCES,"
         DS    CL1
PQNTOTO  DS    CL4                   TOTAL OF OWNERS
         DS    CL1
PQLOWN   DS    CL7                   "OWNERS,"
         DS    CL1
PQNTOTW  DS    CL4                   TOTAL OF WAITERS
         DS    CL1
PQLWTR   DS    CL7                   "WAITERS"
         DS    CL30
         ORG
*
*
*
BADCOMM  DC    CL80'ENQLIST: INVALID COMMAND, USE ONE OF THE BELOW'
GQSCNER  DC    CL48'ENQLIST: RETURN CODE 12 FROM GQSCAN, ERROR DURIN'
         DC    CL32'G GQSCAN PROCESSING'
INVSYSNM DC    CL48'ENQLIST: RETURN CODE 16 FROM GQSCAN, INVALID SYS'
         DC    CL32'NAME INPUT TO GQSCAN'
INVRETCD DC    CL80'ENQLIST: INVALID RETURN CODE FROM GQSCAN'
NOMCHMG  DC    CL80'ENQLIST: NO MATCHING ENQ DATA FOUND'
NOSUCHJB DC    CL80'ENQLIST: NO SUCH JOB FOUND IN EXECUTION'
H1       DC    CL48'************************************************'
         DC    CL32'******************************  '
H2       DC    CL48'*(1) DISPLAY ENQ CONFLICT INFORMATION           '
         DC    CL32'                             *  '
H3       DC    CL48'*    EXAMPLE:                                   '
         DC    CL32'                             *  '
H4       DC    CL48'*    COL  1-72 = ALL BLANK                      '
         DC    CL32'                             *  '
H5       DC    CL48'*                                               '
         DC    CL32'                             *  '
H6       DC    CL48'*(2) DISPLAY ENQ INFO FOR THE JOBNAME SPECIFIED '
         DC    CL32'                             *  '
H7       DC    CL48'*    EXAMPLE: JOB=GGMPCPW9                      '
         DC    CL32'                             *  '
H8       DC    CL48'*    COL  1-4  = "JOB="                         '
         DC    CL32'                             *  '
H9       DC    CL48'*    COL  5-12 = JOBNAME (1-8 CHARACTERS), REST '
         DC    CL32'OF COMMAND BLANK             *  '
H10      DC    CL48'*                                               '
         DC    CL32'                             *  '
H11      DC    CL48'*(3) DISPLAY ENQ INFO FOR ALL QNAMES IN USE     '
         DC    CL32'                             *  '
H12      DC    CL48'*    EXAMPLE: QNAMES                            '
         DC    CL32'                             *  '
H13      DC    CL48'*    COL  1-4  = "QNAMES", REST OF COMMAND BLANK'
         DC    CL32'                             *  '
H14      DC    CL48'*                                               '
         DC    CL32'                             *  '
H15      DC    CL48'*(4) DISPLAY ENQ INFO FOR THE Q-NAME SPECIFIED  '
         DC    CL32'                             *  '
H16      DC    CL48'*    EXAMPLE: SYSZVOLS                          '
         DC    CL32'                             *  '
H17      DC    CL48'*    COL  1-8  = Q-NAME (1-8 CHARACTERS), REST O'
         DC    CL32'F COMMAND BLANK              *  '
H18      DC    CL48'*                                               '
         DC    CL32'                             *  '
H19      DC    CL48'*(5) DISPLAY ENQ INFO FOR THE RESOURCE SPECIFIED'
         DC    CL32'                             *  '
H20      DC    CL48'*    EXAMPLE: SYSDSN  .SYS1.LINKLIB             '
         DC    CL32'                             *  '
H21      DC    CL48'*    COL  1-8  = Q-NAME (1-8 CHARACTERS)        '
         DC    CL32'                             *  '
H22      DC    CL48'*    COL   9   = "."                            '
         DC    CL32'                             *  '
H23      DC    CL48'*    COL 10-80 = R-NAME FOLLOWED BY BLANKS      '
         DC    CL32'                             *  '
H24      DC    CL48'*                                               '
         DC    CL32'                             *  '
H25      DC    CL48'*(6) DISPLAY HELP PANEL                         '
         DC    CL32'                             *  '
H26      DC    CL48'*    EXAMPLE: HELP                              '
         DC    CL32'                             *  '
H27      DC    CL48'*    COL  1-4  = "HELP", REST OF COMMAND BLANK  '
         DC    CL32'                             *  '
H28      DC    CL48'************************************************'
         DC    CL32'******************************  '
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
         LTORG
         PRINT GEN
         ISGRIB
*
* TABLE USED WITH ALL QNAMES PROCESSING. BE SURE THAT QNMNTRY LENGTH
* IS ACCURATE, AND THAT FIELD ENTSAVE HAS THE SAME LENGTH AS QNMNTRY
*
QNMTBL   DSECT ,                QNAME TABLE ENTRY DESCRIPTION
QNMNTRY  DS    0CL20            QNAME TABLE ENTRY
QNMQNAME DS    CL8              QNAME
QNMTOTR  DS    F                TOTAL NUMBER RESOURCES
QNMTOTO  DS    F                TOTAL OF OWNERS
QNMTOTW  DS    F                TOTAL OF WAITERS
ENQLIST  CSECT
         END

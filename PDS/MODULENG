         TITLE 'MODULENG - PRINT LOAD MODULE LENGTHS'
MODULENG SVLNK R3
* PROGRAM TO READ THE DIRECTORY IN AN INPUT PDS AND PRINT A LISTING
* SHOWING THE MODULE NAME AND LENGTH IN HEX.
*
* AUTHOR R. THORNTON   MAY 1980
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
*
*
*
READDIR  LH    R12,REMBYTS             GET NBR BYTES REMAINING IN BLOCK
         CH    R12,=H'35'              ANY MORE ENTRIES IN BLOCK
         BH    GETADR                  YES
NEXREC   GET   INPUT1                  READ DIRECTORY BLOCK
         LR    R12,R1                  GET BLOCK ADDRESS
         MVC   REMBYTS,0(R12)          MOVE NBR BYTES IN BLOCK
         CLC   REMBYTS,ZERO            ANY BYTES IN BLOCK
         BE    EOJ                     NO, EOF
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         USING DIRNTRY,R12
         CLI   DIRNAME,X'FF'           END OF BLOCK?
         BE    NEXREC                  YES
         MVC   PNAME,DIRNAME           MOVE MEMBER NAME TO PRINT
         UNPK  PLENG(7),DIRLGTH(4)     UNPACK MODULE LENGTH
         MVI   PLENG+6,C' '            CLEAR OUT TRASH
         TR    PLENG,TRTBL-C'0'        MAKE HEX PRINTABLE
         BAL   R9,PRINT                LINE FULL, PRINT IT
NEXDNT   LA    R11,33                  BASIC DIRECTORY ENTRY LENGTH
         TM    DIRATTR1,X'04'          IN SCATTER MODE?
         BZ    CKALIAS                 NO
         LA    R11,8(R11)              YES, ADD FOR SCATTER
CKALIAS  TM    DIRALIAS,X'80'          MODULE HAS ALIAS?
         BZ    CKSSI                   NO
         LA    R11,11(R11)             YES, ADD 11 FOR ALIAS
CKSSI    TM    DIRATTR2,X'10'          SSI BYTES PRESENT?
         BZ    TSTAUTH                 NO
         LA    R11,1(R11)              ADD FOR HALFWORD ALIGNMENT
         SRL   R11,1                   SHIFT OUT LOW BIT
         SLL   R11,1                   SHIFT IN A ZERO
         LA    R11,4(R11)              ADD 4 BYTES FOR SSI DATA
TSTAUTH  LR    R1,R11                  COPY LENGTH
         A     R1,RECADDR              POINT TO AUTH LENGTH BYTE
         SR    R2,R2                   ZERO FOR INSERT
         IC    R2,0(R1)                GET AUTH CODE LENGTH
         LA    R11,2(R2,R11)           STEP PAST AUTH CODE BYTES+1
         SRL   R11,1                   SHIFT OUT LOW BIT
         SLL   R11,1                   SHIFT BACK IN AS ZERO
CALCREM  LH    R12,REMBYTS             GET NBR BYTES REMAINING
         SR    R12,R11                 SUBTRACT THIS ENTRY LENGTH
         BP    STREM                   ANY LEFT
         SR    R12,R12                 NO, SET TO ZERO
STREM    STH   R12,REMBYTS             STORE REMAINING BYTES
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         B     READDIR                 GO READ AGAIN
*
*
*
PRINT    PUT   PRINT1,PRTLINE          PRINT A LINE
         MVI   PRT,C' '                BEGIN CLEAR
         MVC   PRT+1(79),PRT           FINISH CLEAR
         BR    R9                      RETURN
*
*
*
EOJ      BAL   R9,PRINT                PRINT LAST LINE
         CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
PRTBLOK  DC    A(PRT)
PRTBLOX  DC    P'4'
TRTBL    DC    C'0123456789ABCDEF'
REMBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ZERO     DC    H'0'
RECADDR  DS    F
PRTLINE  DS    0CL80
PRT      DS    0CL80
PNAME    DS    CL8                     MODULE NAME
         DS    CL1
PLENG    DS    CL6                     MODULE LENGTH
         DS    CL65
*
*
*
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,RECFM=F,                X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,RECFM=FB,               X
               LRECL=80
*
*
*
DIRNTRY  DSECT
DIRNAME  DS    CL8
DIRTTR   DS    XL3
DIRALIAS DS    XL1
DIRFRST  DS    XL3
         DS    XL1
DIRNOTE  DS    XL3
DIRNBRN  DS    XL1
DIRATTR1 DS    XL2
DIRLGTH  DS    XL3
DIR1TXT  DS    XL2
DIREPA   DS    XL3
DIRATTR2 DS    XL1
         DS    XL2
DIRACDL  DS    XL1
DIRAUTH  DS    XL1
         END

         TITLE '*** CALLGND2: TEST GENLDAT2  ***'
CALLGND2 CSECT
*********************************************************************
* EXERCISES GENLDAT2 BY CALLING SUCCESSIVELY WITH A RANGE OF JULIAN *
* DATES FOLLOWED BY A RANGE OF GREGORIAN DATES. THE RANGE FOR JULIAN*
* DATES IS 1899/360 THROUGH 2099/005 AND THE JULIAN DAY RANGES FROM *
* 000-367 FOR EACH YEAR. GREGORIAN DATES RANGE FROM 12/25/1899 THRU *
* 13/32/2099, MONTHS RANGE FROM 00-13 EACH YEAR, AND DAYS RANGE FROM*
* 00-32 FOR EACH MONTH.                                             *
*                                                                   *
* INVALID DATES ARE DELIBERATELY GENERATED TO INSURE GENLDAT2 WILL  *
* RECOGNIZE INVALID DATES.                                          *
*                                                                   *
* R THORNTON JAN 2002                                               *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CALLGND2,R3,R4
         STM   R14,R12,12(R13)   SAVE CALLER'S REGS
         LR    R3,R15            SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)       SET UP SECOND
         LA    R4,1(R4)              BASE REGISTER
         LA    R15,MYSAVE        POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)        SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)        SET HSA IN MY SAVE AREA
         LR    R13,R15           SET UP SAVE AREA POINTER
         B     OPENS             BRANCH AROUND CONSTANTS
         DC    CL8'CALLGND2'     PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'     ASSEMBLY DATE
         DC    CL8'&SYSTIME'     ASSEMBLY TIME
MYSAVE   DS    18F               MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT) OPEN FILES
*********************************************************************
* MAINLINE EXECUTIVE PROGRAM                                        *
*********************************************************************
MAINLINE BAS   R9,GETDATE        GO GET NEXT DATE
         CALL  GENLDAT2,PARMFLD  CALL GENLDAT2
         MVC   PRT(L'PARMFLD+1),PARMFLD MOVE RETURNED DATA
         BAS   R9,PRINT          PRINT RETURNED DATA
         B     MAINLINE          LOOP UNTIL FINISHED
*********************************************************************
* SETS A NEW DATE REQUEST IN THE PARMFLD AREA WHICH WILL BE PASSED  *
* TO GENLDAT2.                                                      *
*********************************************************************
GETDATE  ST    R9,GD9            SAVE RETURN ADDRESS
GDGSW    NOP   GDGMDY            BRANCH SWITCH
         CP    JULDATE,=PL4'2099005' NOW AT JAN 5, 2099?
         BE    GDJEND            YES, START WITH GREGORIAN MDY
         CP    JULDATE+2(2),=PL2'367' NOW AT DAY 367?
         BNL   GDJNEWY           YES
         AP    JULDATE+2(2),=P'1' ADD 1 TO JULIAN DAY
         OI    JULDATE+3,X'0F'   CLEAR SIGN TO F
         B     GDXIT             AND RETURN
GDJNEWY  ZAP   JULDATE+2(2),=P'0' ZERO OUT THE DAY
         AP    JULDATE,=P'1000'  ADD 1 TO JULIAN YEAR
         OI    JULDATE+3,X'0F'   CLEAR SIGN TO F
         B     GDXIT             CONTINUE
GDJEND   MVI   DATYP,C'G'        CHANGE DATE TYPES
         OI    GDGSW+1,X'F0'     TURN ON GREGORIAN MDY SWITCH
         ZAP   JULDATE,=P'0'     CLEAR JULIAN DATE
         OI    JULDATE+3,X'0F'   CLEAR SIGN TO F
         MVC   GREGMDY,=CL8'12251899' SET INITIAL DATE TO 12/25/1899
         B     GDXIT             RETURN
GDGMDY   CLC   GREGMDY+2(2),=CL2'32' NOW AT DAY 32?
         BNL   GDGNEWMO          YES
         PACK  DBLWORD(2),GREGMDY+2(2) ADD 1
         AP    DBLWORD(2),=P'1'          TO
         UNPK  GREGMDY+2(2),DBLWORD(2)     DAY OF MONTH
         OI    GREGMDY+3,X'F0'   CLEAR SIGN
         B     GDXIT             AND EXIT
GDGNEWMO MVC   GREGMDY+2(2),=CL2'00' SET DAY OF MONTH TO ZEROS
         CLC   GREGMDY(2),=CL2'13' END OF YEAR?
         BNL   GDGNEWYR          YES
         PACK  DBLWORD(2),GREGMDY(2) ADD 1
         AP    DBLWORD(2),=P'1'          TO
         UNPK  GREGMDY(2),DBLWORD(2)       MONTH
         OI    GREGMDY+1,X'F0'   CLEAR SIGN
         B     GDXIT             RETURN
GDGNEWYR MVC   GREGMDY(4),=CL4'0000' SET MONTH AND DAY TO ZEROS
         CLC   GREGMDY+4(4),=CL4'2100' NOW AT YEAR 2100?
         BE    EOJ               YES, ALL THROUGH
         PACK  DBLWORD(3),GREGMDY+4(4) ADD 1
         AP    DBLWORD(3),=P'1'        TO
         UNPK  GREGMDY+4(4),DBLWORD(3)     YEAR
         OI    GREGMDY+7,X'F0'   CLEAR SIGN
GDXIT    L     R9,GD9            GET RETURN ADDRESS
         BR    R9                RETURN
GD9      DC    A(0)              RETURN ADDRESS
*********************************************************************
* PRINT A LINE TO SYSPRINT                                          *
*********************************************************************
PRINT    ST    R9,P9             SAVE RETURN ADDRESS
         PUT   SYSPRINT,PRTLINE  PRINT THE LINE
         MVC   PRTLINE,BLANKS    CLEAR PRINT LINE
         L     R9,P9             GET RETURN ADDRESS
         BR    R9                RETURN
P9       DC    A(0)              RETURN ADDRESS
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE SYSPRINT          CLOSE PRINT FILE
         L     R13,4(R13)        POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)   RESTORE CALLER'S REGS
         SR    R15,R15           SET RC=0
         BR    R9                RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
DBLWORD  DS    D                 DOUBLEWORD WORK AREA
BLANKS   DC    CL133' '                BLANKS
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DC    CL1' '                  CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DC    CL132' '
*
PARMFLD  DS    0CL79             PARAMETER AREA DESCRIPTION
DATYP    DC    CL1'J'             .DATE TYPE SUPPLIED: J=JULIAN,
*                                    G=GREGORIAN MDY, Y=GREGORIAN YMD
DATERR   DS    CL1                .ERROR INDICATOR:  BLANK=SUCCESSFUL
*                                    E=ERROR IN DATA SUPPLIED
DAYWK    DS    CL1                .DAY OF WEEK INDICATOR: 1=MON,
*                                    2=TUE, 3=WED, 4=THU,
*                                    5=FRI, 6=SAT, 7=SUN
HOLIDAY  DS    CL1                .HOLIDAY INDICATOR: BLANK=NOT A
*                                    HOLIDAY, 1=NEW YEAR'S DAY,
*                                    2=(NOT USED), 3=GOOD FRIDAY
*                                    4=MEMORIAL DAY, 5=INDPNDNC DAY,
*                                    6=LABOR DAY, 7=THANKSGIVING DAY,
*                                    8=FRIDAY  AFTER THANKSGIVING,
*                                    9=CHRISTMAS DAY
YEARTYP  DS    CL1                .0=NOT LEAP YEAR, 1=LEAP YEAR
JULDATE  DC    XL4'1899360F'      .JULIAN DATE. CONTAINS AN UNSIGNED,
*                                    PACKED DECIMAL DATE OF THE FORM
*                                    FORM YYYYDDDF, IF DATYP=J. IT WILL
*                                    CONTAIN THE CORRECT JULIAN DATE ON
*                                    SUCCESSFUL RETURN.
GREGMDY  DS    CL8                .GREGORIAN DATE, AN UNSIGNED,
*                                    ZONED DECIMAL DATE OF THE FORM
*                                    MMDDYYYY, IF DATYP=G. ON GOOD
*                                    RETURN THIS FIELD WILL CONTAIN THE
*                                    GREGORIAN DATE IN THIS FORMAT.
GREGYMD  DS    CL8                .GREGORIAN DATE, AN UNSIGNED,
*                                    ZONED DECIMAL DATE OF THE FORM
*                                    YYYYMMDD, IF DATYP=Y. ON GOOD
*                                    RETURN THIS FIELD WILL CONTAIN
*                                    GREGORIAN DATE IN THIS FORMAT.
LITDATE  DS    CL18               .A LITERAL DATE, LEFT JUSTIFIED
*                                    IN AN 18-BYTE FIELD. EXAMPLES ARE:
*                                    JANUARY 16, 1979
*                                    MAY 9, 2002
JULSLASH DS    CL8                .JULIAN DATE WITH SLASH: YYYY/DDD
GRGSLASH DS    CL10               .GREGORIAN DATE WITH SLASHES:
*                                    MM/DD/YYYY
DAYLIT   DS    CL9                .LITERAL DAY OF WEEK
MONTHLIT DS    CL9                .LITERAL MONTH
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
         LTORG
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CALLGND2 CSECT
         END

         TITLE '*****COMPARE: COMPARE ONE FILE TO ANOTHER *****'
COMPARE SVLNK R3
         PRINT NOGEN
*
***********************************************************************
* COMPARE TWO FILES USING PROGRAM LOGIC TO ACCOUNT FOR ANTICIPATED    *
* SIMILARITIES AND DIFFERENCES. SHOULD BE RE-SPECIALIZED FOR EACH USE.*
* AUTHOR R THORNTON APR 2000                                          *
***********************************************************************
*
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
*
         OPEN  (INPUT1,,INPUT2)       OPEN FILES
*
***********************************************************************
* MAINLINE PROCESSING: READ INPUT RECORD, MODIFY AS NEEDED, AND       *
* WRITE RESULT TO OUTPUT FILE                                         *
***********************************************************************
*
MAINLINE GET   INPUT1                  READ NEXT INPUT1 BLOCK
         LR    R8,R1                   GET INPUT1 BLOCK ADDRESS
         LH    R9,INPUT1+82            GET INPUT1 BLOCK LENGTH
READ2    GET   INPUT2                  READ NEXT INPUT2 BLOCK
         CLI   END1,0                  EOF ON INPUT1?
         BE    SETREGS2                NO
         ABEND 555,DUMP                YES, MISMATCH NBR BLOCKS
SETREGS2 LR    R10,R1                  GET INPUT2 BLOCK ADDRESS
         LH    R11,INPUT2+82           GET INPUT2 BLOCK LENGTH
         CR    R9,R11                  BLOCK LENGTHS SAME?
         BE    COMPAREM                YES
         BH    ONELONG                 NO, INPUT1 LONGER
TWOLONG  LR    R7,R11                  COPY INPUT2 BLOCK LENGTH
         SR    R7,R9                   SUBTRACT INPUT1 BLOCK LENGTH
         C     R7,F1                   DIFFERENCE = 1?
         BE    SETUP                   YES, CONTINUE
         ABEND 111,DUMP                BAD DIFFERENCE, ABEND
ONELONG  LR    R7,R9                   COPY INPUT1 BLOCK LENGTH
         SR    R7,R11                  SUBTRACT BLOCK 2 LENGTH
         LR    R9,R11                  MOVE SHORTER LENGTH TO R9
         C     R7,F1                   DIFFERENCE = 1?
         BE    SETUP                   YES
         ABEND 222,DUMP                BAD DIFFERENCE, ABEND
SETUP    LR    R11,R9                  MAKE COMPARE LENGTHS SAME
COMPAREM AP    NBRBLKS,P1              COUNT BLOCKS
         CLCL  R8,R10                  COMPARE BLOCKS
         BE    MAINLINE                LOOP TO COMPARE ALL BLOCKS
         ABEND 333,DUMP                MISMATCH, ABEND
EOF1     MVI   END1,1                  SET EOF1 SWITCH
         B     READ2                   CONTINUE
EOF2     CLI   END1,1                  ALREADY AT EOF INPUT1?
         BE    EOJ                     YES, GOOD
         ABEND 444,DUMP                MISMATCH NBR BLOCKS, ERROR
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (INPUT1,,INPUT2)        CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
DBLWD    DS    D                       WORK AREA
F1       DC    F'1'                    CONSTANT 1
END1     DC    X'00'                   1=EOF INPUT1
NBRBLKS  DC    PL4'0'                  NBR BLOCKS READ
P1       DC    P'1'                    CONSTANT 1
*
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,EODAD=EOF1,             X
               RECFM=U,BLKSIZE=32720
INPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=U,BLKSIZE=32720
         LTORG
         END

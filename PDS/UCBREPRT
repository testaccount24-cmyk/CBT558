         TITLE '***** DASD DEVICE REPORT *****'
         PRINT NOGEN
UCBREPRT CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING UCBREPRT,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'UCBREPRT'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
* THE UCBLUKUP PROGRAM IS FIRST RUN ON BOTH CPU'S, CREATING A FILE   *
* OF DASD DEVICE INFORMATION. THE DASD INFO FILE IS THEN SORTED IN   *
* VOLUME-SERIAL, SYSTEM, PRIMARY CUU, ALTERNATE CUU ORDER BEFORE IT  *
* IS INPUT TO THIS PROGRAM. THE REPORT OUTPUT FROM THIS PROGRAM      *
* CONTAINS ONE LINE PER VOLUME SERIAL NUMBER, SHOWING THE PRIMARY AND*
* ALTERNATE CUU ADDRESSES FOR EACH CPU.                              *
*  AUTHOR R. THORNTON DEC 1985                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
INIT     OPEN  (SYSPRINT,OUTPUT,INPUT1) OPEN FILES
         L     R1,4(R13)               POINT TO CALLER'S SAVE
         L     R1,24(R1)               GET R1 AS AT ENTRY
         L     R1,0(R1)                POINT TO EXEC PARM DATA
         LH    R15,0(R1)               GET PARM LENGTH
         CH    R15,H9                  9 BYTES OF PARM DATA?
         BE    GOODPARM                YES
BADPARM  MVC   PRT(48),ERR0            SET ERROR MESSAGE
         BAL   R9,PRINT                PRINT IT
         ABEND 777,DUMP                TERMINATE ABNORMALLY
GOODPARM CLI   6(R1),C','              PARM FORMAT CORRECT?
         BNE   BADPARM                 NO
         CLC   2(4,R1),7(R1)           YES, FIRST IS LOW?
         BL    LOW1                    YES
         MVC   CPULOW,7(R1)            NO, SECOND IS LOW
         MVC   CPUHI,2(R1)             FIRST IS HIGH
         B     MAINLINE                CONTINUE
LOW1     MVC   CPULOW,2(R1)            NO, SECOND IS LOW
         MVC   CPUHI,7(R1)             FIRST IS HIGH
         MVC   PRTLINE,BLANX           CLEAR PRINT LINE
         MVI   PVOL,0                  SET FIRST TIME SWITCH
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
MAINLINE GET   INPUT1,DAREC            READ A DEVICE RECORD
         CLI   PVOL,0                  FIRST TIME HERE?
         BE    NEWVOL                  YES
         CLC   PVOL,OVOLSER            SAME VOLUME SERIAL?
         BE    CHKIDS                  YES
OUTCURR  BAL   R9,PRINT                PRINT CURRENT LINE
NEWVOL   MVC   PVOL,OVOLSER            VOLUME SERIAL TO PRINT
         MVC   PDEV,ODEVTYPE           DEVICE TYPE TO PRINT
         CLI   ODEVTYPE+4,C' '         SUFFIX ON TYPE?
         BE    CHKIDS                  NO
         MVI   PDEVD,C'-'              YES, DELIMIT FOR MODEL
         MVC   PDEVM,ODEVTYPE+4        MODEL NUMBER TO PRINT
CHKIDS   CLC   OUTID,CPULOW            IS IT THE LOWER CPU ID?
         BE    GOTLOW                  YES
         CLC   OUTID,CPUHI             NO, IS IT THE HIGHER CPU ID?
         BE    GOTHI                   YES
BADCPU   MVC   PRT(21),DAREC           RECORD TO PRINT
         MVC   PRT+21(39),ERR1         ERROR MESSAGE TO PRINT LINE
         BAL   R9,PRINT                PRINT ERROR
         B     MAINLINE                GO READ NEXT RECORD
GOTLOW   CLC   OCUU,OPCUU              THIS RECORD FOR PRIMARY CUU?
         BNE   ALTLOW                  NO
         CLC   PCUULOW,BLANX           ALREADY GOT PRIMARY?
         BNE   OUTCURR                 YES
         MVC   PLOW,CPULOW             LOW CPU ID TO PRINT
         MVC   PPRI1,PRILIT            PRIMARY CUU IDENTIFIER
         MVC   PCUULOW,OPCUU           MOVE PRIMARY CUU ADDR
         B     MAINLINE                CONTINUE
ALTLOW   CLC   ACUULOW,BLANX           ALREADY GOT ALT CUU?
         BNE   OUTCURR                 YES
         MVC   PALT1,ALTLIT            ALTERNATE CUU IDENTIFIER
         MVC   ACUULOW,OCUU            SET ALTERNATE CUU
         B     MAINLINE                CONTINUE
GOTHI    CLC   OCUU,OPCUU              THIS RECORD FOR PRIMARY CUU?
         BNE   ALTHI                   NO
         CLC   PCUUHI,BLANX            ALREADY GOT PRIMARY?
         BNE   OUTCURR                 YES
         MVC   PHI,CPUHI               HIGH CPU ID TO PRINT
         MVC   PPRI2,PRILIT            PRIMARY CUU IDENTIFIER
         MVC   PCUUHI,OPCUU            MOVE PRIMARY CUU ADDR
         B     MAINLINE                CONTINUE
ALTHI    CLC   ACUUHI,BLANX            ALREADY GOT ALT CUU?
         BNE   OUTCURR                 YES
         MVC   PALT2,ALTLIT            ALTERNATE CUU IDENTIFIER
         MVC   ACUUHI,OCUU             SET ALTERNATE CUU
         B     MAINLINE                CONTINUE
*
***********************************************************************
*        PRINT SUBROUTINE                                             *
***********************************************************************
*
PRINT    PUT   SYSPRINT,PRTLINE        WRITE THE OUTPUT RECORD
         MVI   PCC,C' '                DEFAULT SPACING
         MVC   PRT,PRTLINE             CLEAR PRINT LINE
         BR    R9                      RETURN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (SYSPRINT,,INPUT1)      CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
DAREC    DS    0CL21                   DEVICE RECORD
OVOLSER  DS    CL6                     VOLUME SERIAL NUMBER
OUTID    DS    CL4                     SYSTEM ID
OPCUU    DS    CL3                     PRIMARY CUU ADDRESS
OCUU     DS    CL3                     CHANNEL-UNIT ADDRESS (EBCDIC)
ODEVTYPE DS    CL5                     DEVICE TYPE (EBCDIC)
*
PRTLINE  DS    0CL80                   PRINT LINE
PCC      DC    C' '                    CARR. CTL
PRT      DS    0CL79
PVOL     DC    CL6' '                  VOLUME SERIAL
         DC    CL2' '
PDEV     DC    CL4' '                  DEVICE TYPE
PDEVD    DC    CL1' '                  DELIMITER FOR MODEL
PDEVM    DC    CL1' '                  MODEL NUMBER
         DC    CL4'    '               LOW CPU ID
PLOW     DC    CL4'    '               LOW CPU ID
PPRI1    DC    CL5'     '              IDENTIFIER
PCUULOW  DC    CL3' '                  PRIMARY CUU, LOW CPU
PALT1    DC    CL6'      '             IDENTIFIER
ACUULOW  DC    CL3' '                  ALT CUU, LOW CPU
         DC    CL4' '
PHI      DC    CL4'    '               HI CPU ID
PPRI2    DC    CL5'     '              IDENTIFIER
PCUUHI   DC    CL3' '                  PRIMARY CUU, HI CPU
PALT2    DC    CL6'      '             IDENTIFIER
ACUUHI   DC    CL3' '                  ALT CUU, HI CPU
         DC    CL80' '
*
PRILIT   DC    CL5' PRI='              IDENTIFIER
ALTLIT   DC    CL6', ALT='             IDENTIFIER
CPULOW   DC    CL4'    '               LOW CPU ID FROM EXEC PARM
CPUHI    DC    CL4'    '               HIGH CPU ID FROM EXEC PARM
ERR0     DC    CL48'UCBREPRT: EXEC PARM MUST HAVE CPU IDS, XXXX,YYYY'
ERR1     DC    CL39'UCBREPRT: CPU ID NOT FOUND IF EXEC PARM'
RETCODE  DC    F'0'                    RETURN CODE
H9       DC    H'9'                    CONSTANT 9
BLANX    DC    CL80' '                 BLANKS
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=21
         LTORG
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

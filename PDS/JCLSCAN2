         TITLE '*** JCLSCAN2: FIND EXECUTED PROGRAMS   ***'
***********************************************************************
* SCAN A JCL LIBRARY FOR PROGRAMS BEING EXECUTED. FOR EACH PROGRAM    *
* WRITE AN OUTPUT RECORD CONTAINING THE NAME OF THE JCL DATASET, THE  *
* MEMBER NAME, STEP NAME, AND PROGRAM NAME. IF THIS IS AN IMS RUN,    *
* THE IMS TYPE (BMP, DLI, ETC) IS INCLUDED.                           *
* AUTHOR R THORNTON OCTOBER 1994                                      *
***********************************************************************
*
JCLSCAN2 SVLNK R3,R4
***********************************************************************
*        PROCESS PARM FIELD                                           *
***********************************************************************
         L     R2,0(R1)                GET PARM FIELD ADDR
         LH    R15,0(R2)               PIC UP PARM LENGTH
         CLC   0(2,R2),H0              ANY PARM DATA?
         BE    OPENS                   NO, CONTINUE
         BCTR  R15,0                   COMPUTE LENGTH CODE
         CH    R15,=H'43'              OVER 44 BYTES?
         BNH   MOVEPARM                NO
         LA    R15,43                  YES, FORCE 44 BYTES MAX
MOVEPARM EX    R15,PARMOK              MOVE PARM FIELD
         B     OPENS                   GO OPEN FILES
PARMOK   MVC   JCLDSNAM(1),2(R2)       MOVE PARM DATA
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT)        OPEN FILES
         CALL  READDIR,OPEN            OPEN DIRECTORY FOR INPUT
         CALL  READMEM,OPEN            OPEN MEMBERS FOR INPUT
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
NEXDIR   BAS   R9,PUTOUT               WRITE ANY RECORD IN PROGRESS
         CALL  READDIR,(READ,DIRCTY)   READ DIRECTORY RECORD
         CLI   DIRCTY,X'FE'            END OF DIRECTORY
         BNL   EOJ                     YES, ALL DONE
         MVC   FINDTTR,DIRTTR          TTR TO FIND PARAMETER
         CALL  READMEM,FIND            GO FIND MEMBER
NEXREC   CALL  READMEM,(READ,INREC)    READ NEXT RECORD
         CLI   INREC,X'FF'             END OF MEMBER
         BNE   CKSLSL                  NO
         BAS   R9,PUTOUT               WRITE ANY STATEMENT IN PROGRESS
         B     NEXDIR                  AND GO READ DIRECTORY ENTRY
CKSLSL   CLC   INREC(2),=CL2'//'       COL 1-2 = //
         BNE   NEXREC                  NO, IGNORE IT
         CLC   INREC+2(8),=CL8'STEPLIB ' IS IT A STEPLIB DD?
         BNE   TRY4EXEC                NO
         XC    TRTBL(256),TRTBL        RESET SCAN
         MVI   TRTBL+C'=',C'='         SET SCAN FOR = SIGN
         LA    R1,INREC+9              POINT PAST //STEPLIB
SCAN4DSN TRT   1(80,R1),TRTBL          LOOK FOR THE = OF DSN=
         BZ    NOSTPLB                 BRANCH NOT FOUND
         C     R1,INEND                FOUND WITHIN RECORD?
         BNL   NOSTPLB                 NO
         SH    R1,=H'3'                BACK UP 3 BYTES
         CLC   0(3,R1),=CL3'DSN'       FOUND DSN=?
         BE    GOTSTLB                 YES
         CLC   0(3,R1),=CL3'AME'       FOUND AME=?
         BE    GOTSTLB                 YES
         LA    R1,3(R1)                NO, RETURN TO =
         B     SCAN4DSN                AND CONTINUE SCAN
NOSTPLB  XC    TRTBL(256),TRTBL        CLEAR TRT TABLE
         B     NEXREC                  GO READ NEXT RECORD
GOTSTLB  LA    R1,4(R1)                POINT TO 1ST BYTE OF DSN
         LA    R2,STEPLIB              POINT TO 1ST BYTE OF FIELD
         LA    R15,44                  SET MAX LENGTH
CHKSTLB  CLI   0(R1),C' '              END OF DSNAME?
         BE    NEXREC                  YES
         CLI   0(R1),C','              END OF DSNAME?
         BE    NEXREC                  YES
         MVC   0(1,R2),0(R1)           NO, MOVE A BYTE
         BCT   R15,UNDR44              CONTINUE IF UNDER 44 BYTES
         B     NEXREC                  STOP AFTER 44 BYTES
UNDR44   LA    R1,1(R1)                ADVANCE A BYTE
         LA    R2,1(R2)                ADVANCE A BYTE
         C     R1,INEND                STILL WITHIN RECORD?
         BL    CHKSTLB                 YES, CONTINUE
         B     NEXREC                  NO, ALL THROUGH
TRY4EXEC LA    R1,INREC+1              POINT PAST LEADING SLASH
         MVI   TRTBL+C'P',0            RESET SCAN FOR 'P'
         MVI   TRTBL+C'E',C'E'         SET SCAN FOR 'E'
SCANEXEC TRT   1(80,R1),TRTBL          SCAN FOR AN 'E'
         BZ    CHKPARM                 NO 'E' FOUND
         C     R1,INEND                YES, FOUND WITHIN RECORD?
         BNL   CHKPARM                 NOT AN EXEC STATEMENT
         CLC   0(5,R1),=CL5'EXEC '     IS IT AN EXEC STATEMENT?
         BNE   SCANEXEC                NO
         BAS   R9,PUTOUT               WRITE ANY STATEMENT IN PROGRESS
         MVC   STEPNAME,BLANKS         PRE-CLEAR STEPNAME
         MVC   STEPLIB,BLANKS          CLEAR STEPLIB TO BLANKS
         LA    R14,INREC+2             POINT TO JCL STEPNAME
         LA    R2,STEPNAME             POINT TO BYTE 1
         LA    R15,8                   MAX BYTES TO MOVE
MOVSTEP  CLI   0(R14),C' '             END OF NAME?
         BE    SCANPGM                 YES
         MVC   0(1,R2),0(R14)          MOVE A BYTE OF STEPNAME
         LA    R14,1(R14)              ADVANCE 1
         LA    R2,1(R2)                ADVANCE 1
         BCT   R15,MOVSTEP             NO, CONTINUE MOVING
SCANPGM  MVI   TRTBL+C'E',0            RESET SCAN FOR 'E'
         MVI   TRTBL+C'P',C'P'         SET SCAN FOR 'P'
SCANPROG TRT   1(80,R1),TRTBL          SCAN FOR A 'P'
         BZ    CHKPARM                 NOT FOUND
         C     R1,INEND                FOUND WITHIN RECORD?
         BNL   CHKPARM                 NO
         CLC   0(4,R1),=CL4'PGM='      FOUND PGM=?
         BNE   SCANPROG                NO
         MVC   PGMNAME,BLANKS          PRE-CLEAR PROGRAM NAME
         LA    R14,4(R1)               POINT TO JCL PROGRAM NAME
         LA    R2,PGMNAME              POINT TO BYTE 1
         LA    R15,8                   MAX BYTES TO MOVE
MOVEPGM  CLI   0(R14),C' '             END OF NAME?
         BE    CHKPRM2                 YES
         MVC   0(1,R2),0(R14)          MOVE A BYTE OF PROGRAM NAME
         LA    R14,1(R14)              ADVANCE 1
         LA    R2,1(R2)                ADVANCE 1
         BCT   R15,MOVEPGM             LOOP IF MORE TO GO
         B     CHKPRM2                 ALL DONE, BRANCH
CHKPARM  CLI   INREC+2,C' '            ANY STEPNAME?
         BNE   NEXREC                  NO, NOT A PARM
CHKPRM2  CLC   PGMNAME,=CL8'DFSRRC00'  EXECUTING IMS?
         BNE   NEXREC                  NO
         MVI   TRTBL+C'E',0            RESET SCAN FOR 'E'
         MVI   TRTBL+C'P',C'P'         SET SCAN FOR 'P'
         LA    R1,INREC+2              SET SCAN BEGIN ADDRESS
SCANPARM TRT   1(80,R1),TRTBL          SCAN FOR A 'P'
         BZ    NEXREC                  NOT FOUND
         C     R1,INEND                FOUND WITHIN RECORD?
         BNL   NEXREC                  NO
         CLC   0(5,R1),=CL5'PARM='     FOUND PARM=?
         BNE   SCANPARM                NO
         MVC   IMSTYPE,BLANKS          PRE-CLEAR IMSTYPE
         LA    R14,5(R1)               POINT TO JCL PARM DATA
CHKDLM   CLI   0(R14),C''''            LEADING QUOTE?
         BE    SKIPDLM                 YES
         CLI   0(R14),C'('             OPEN PAREN?
         BNE   TESTCMA                 NO
SKIPDLM  LA    R14,1(R14)              STEP PAST DELIMITER
         B     CHKDLM                  CONTINUE
TESTCMA  CLI   3(R14),C','             3-CHARACTER TYPE?
         BNE   NEXREC                  NO, SKIP IT
         MVC   IMSTYPE,0(R14)          MOVE IMS TYPE
         LA    R14,4(R14)              POINT TO JCL PROGRAM NAME
         LA    R2,PGMNAME              POINT TO BYTE 1
         LA    R15,8                   MAX BYTES TO MOVE
MOVEPGM2 CLI   0(R14),C' '             END OF NAME?
         BE    CHKPRM2                 YES
         MVC   0(1,R2),0(R14)          MOVE A BYTE OF PROGRAM NAME
         LA    R14,1(R14)              ADVANCE 1
         LA    R2,1(R2)                ADVANCE 1
         BCT   R15,MOVEPGM2            NO, CONTINUE MOVING
         B     NEXREC                  GO READ NEXT RECORD
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PUTOUT   MVC   OUTREC,BLANKS           CLEAR OUTPUT RECORD
         CLC   PGMNAME,BLANKS          ANY PROGRAM NAME?
         BER   R9                      NO, EXIT
         MVC   OUTDSN,JCLDSNAM         DATASET NAME TO OUTPUT
         MVC   OUTPGM,PGMNAME          PROGRAM NAME TO OUTPUT
         MVC   OUTMEMB,DIRNAME         MEMBER NAME TO OUTPUT
         MVC   OUTSTEP,STEPNAME        STEPNAME TO OUTPUT
         MVC   OUTIMS,IMSTYPE          IMS TYPE TO OUTPUT
         MVC   OUTSTLB,STEPLIB         STEPLIB TO OUTPUT
         CLI   OUTSTEP,C'*'            COMMENTED OUT?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'DFS'     DFS PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL6'IDCAMS'  IDCAMS?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL6'ABDUMP'  ABDUMP?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL6'LOADAT'  LOADATE?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL4'SORT'    SORT?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL8'SYNCSORT' SYNCSORT?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL8'DPKDBD01' DPKDBD01?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL8'DSPURX00' DSPURX00?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'IEB'     IEB PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'DSN'     DSN PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'IEF'     IEF PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'IGH'     IGH PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'IKJ'     IKJ PROGRAM?
         BE    PUTXIT                  YES
         CLC   OUTPGM(3),=CL3'SAS'     SAS PROGRAM?
         BE    PUTXIT                  YES
FINALCK  TRT   OUTPGM,VALCHAR          IS PROGRAM NAME VALID?
         BNZ   PUTXIT                  NO
         CLC   STEPLIB,BLANKS          IS THERE A STEPLIB NAME?
         BNE   PUTOUTR                 YES
         AP    NOSTPLBR,=P'1'          NO, COUNT ONE
PUTOUTR  PUT   OUTPUT1,OUTREC          READ NEXT RECORD
         MVC   IMSTYPE,BLANKS          CLEAR IMS TYPE
         MVC   STEPNAME,BLANKS         CLEAR STEPNAME
         MVC   PGMNAME,BLANKS          CLEAR PROGRAM NAME
PUTXIT   BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE OUTPUT1                 CLOSE FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
PGMNAME  DC    CL8' '                  CURRENT PROGRAM NAME
STEPNAME DC    CL8' '                  CURRENT STEP NAME
IMSTYPE  DC    CL3' '                  CURRENT IMS TYPE
NOSTPLBR DC    PL3'0'                  MISSING STEPLIB COUNTER
JCLDSNAM DC    CL44' '                 PARM DATA FROM EXEC
STEPLIB  DC    CL44' '                 CURRENT STEPLIB NAME
BLANKS   DC    CL133' '                BLANKS
H0       DC    H'0'                    CONSTANT ZERO
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER
READ     DC    C'R'                    READ PARAMETER
CLOSE    DC    C'C'                    CLOSE PARAMETER
DIRCTY   DS    0CL11                   READ DIRECTORY PARAMETER
DIRNAME  DS    CL8                     DIRECTORY NAME
DIRTTR   DS    XL3                     DIRECTORY TTR OF MEMBER
         DC    X'00'                   Z FOR TTRZ IN OUTPUT DIRECTORY E
FIND     DS    0CL5                    FIND PARAMETER
         DC    C'FT'                   FIND BY TTR
FINDTTR  DS    XL3                     TTR TO FIND
*
*
INREC    DS    0CL80                    INPUT RECORD
         DS    CL80
INEND    DC    A(*)                     INPUT RECORD END ADDRESS
*
OUTREC   DS    0CL120                   OUTPUT RECORD
OUTSTLB  DS    CL44                     STEPLIB NAME
         DS    CL1
OUTPGM   DS    CL8                      PROGRAM NAME
         DS    CL1
OUTDSN   DS    CL44                     PROCLIB DSNAME
         DS    CL1
OUTMEMB  DS    CL8                      PROCLIB MEMBER NAME
         DS    CL1
OUTSTEP  DS    CL8                      STEP NAME IN PROC
         DS    CL1
OUTIMS   DS    CL3                      IMS TYPE
TRTBL    DC    XL256'00'                SCAN TABLE
         ORG   TRTBL+C'P'
         DC    C'P'
         ORG
VALCHAR  DC    256X'FF'                 SCAN FOR VALID PGMNAME CHARS
         ORG   VALCHAR+C' '
         DC    X'00'
         ORG   VALCHAR+C'$'
         DC    X'00'
         ORG   VALCHAR+C'A'
         DC    XL9'00'
         ORG   VALCHAR+C'J'
         DC    XL9'00'
         ORG   VALCHAR+C'S'
         DC    XL8'00'
         ORG   VALCHAR+C'0'
         DC    XL10'00'
         ORG
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=120
         END

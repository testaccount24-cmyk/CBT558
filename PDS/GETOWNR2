         TITLE '*** GETOWNR2: FIND SYSTEM OWNER FOR PROGRAMS ***'
         PRINT NOGEN
***********************************************************************
* THE OWNER FILE CREATED BY PROGRAM GETOWNER IS SORTED IN PROGRAM NAME*
* ORDER BEFORE USE BY THIS PROGRAM. A LISTING IS PRODUCED SHOWING     *
* ALL PROGRAMS HAVING AN ENDEVOR SYSTEM (PTINT1). A SECOND LISTING    *
* CONTAINS ALL PROGRAMS HAVING SINGLE OWNERS (PRINT2), AND A THIRD    *
* CONTAINS PROGRAMS HAVING MULTIPLE OWNERS (PRINT3).                  *
*                                                                     *
* AUTHOR: R. THORNTON DEC 1993                                        *
***********************************************************************
*
GETOWNR2 SVLNK R3
OPENS    OPEN  (PRINT1,OUTPUT,PRINT2,OUTPUT,PRINT3,OUTPUT,INPUT1)
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE BAS   R9,GETIN                READ INPUT RECORD
         CLC   PGMNAME,CURRPGM         SAME PROGRAM NAME?
         BE    SAMEONE                 YES
         BAS   R9,ENDPGM               NO, TERMINATE CURRENT
         BAS   R9,BEGINPGM             AND INITIALIZE FOR NEW PGM
SAMEONE  BAS   R9,SAMEPGM              PROCESS CURRENT PROGRAM
         B     MAINLINE                LOOP THROUGH INPUT FILE
***********************************************************************
*        END THE CURRENT PROGRAM: WRITE TO APPROPRIATE LISTING        *
***********************************************************************
ENDPGM   ST    R9,EP9                  SAVE RETURN REG
         CLI   CURRPGM,C' '            ANY PROGRAM IN PROGRESS?
         BE    EPXIT                   NO, JUST EXIT
         L     R12,OWNTBLA             POINT TO TABLE
         CLI   8(R12),X'FF'            SINGLE OWNER?
         BE    SNGL                    YES
         BAS   R9,MULTOWNR             NO, MULTIPLE OWNERS
         B     EPXIT                   EXIT
SNGL     BAS   R9,SNGLOWNR             SINGLE OWNER
EPXIT    L     R9,EP9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        PROCESS LISTING FOR A MULTIPLY OWNED PROGRAM                 *
***********************************************************************
MULTOWNR ST    R9,MO9                  SAVE RETURN REG
         MVC   PRTPGM,CURRPGM          PROGRAM NAME TO PRINT
NEXMLT   MVC   PRTENDV,CURRENDV        ENDEVOR SYSTEM TO OUTPUT
         MVC   PRTOWN,0(R12)           OWNER TO OUTPUT
         BAS   R9,LIST3                PRINT A LINE
         LA    R12,8(R12)              ADVANCE TO NEXT LINE
         CLI   0(R12),X'FF'            END OF OWNER TABLE?
         BNE   NEXMLT                  NO, CONTINUE
MOXIT    L     R9,MO9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        PROCESS LISTING FOR A SINGLY OWNED PROGRAM                   *
***********************************************************************
SNGLOWNR ST    R9,SO9                  SAVE RETURN REG
         MVC   PRTPGM,CURRPGM          PROGRAM NAME TO PRINT
         MVC   PRTENDV,CURRENDV        ENDEVOR SYSTEM TO OUTPUT
         MVC   PRTOWN,0(R12)           OWNER TO OUTPUT
         BAS   R9,LIST2                PRINT A LINE
SOXIT    L     R9,SO9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        INITIALIZE FOR A NEW PROGRAM                                 *
***********************************************************************
BEGINPGM ST    R9,BP9                  SAVE RETURN REG
         L     R12,OWNTBLA             POINT TO OWNER TABLE
         MVC   0(8,R12),OWNER          OWNER TO TABLE
         MVI   8(R12),X'FF'            SET TABLE END
         MVC   CURRPGM,PGMNAME         SET UP CURRENT PROGRAM NAME
         MVC   CURRENDV,ENDVSYS        SET UP CURRENT ENDEVOR SYSTEM
         CLI   ENDVSYS,C' '            IS THERE AN ENDEVOR SYSTEM?
         BE    BPXIT                   NO
         MVC   PRTPGM,CURRPGM          PROGRAM NAME TO OUTPUT
         MVC   PRTENDV,CURRENDV        ENDEVOR SYSTEM TO OUTPUT
         MVC   PRTOWN,BLANKS           CLEAR OWNER NAME
         BAS   R9,LIST1                PRINT A LINE
BPXIT    L     R9,BP9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        PROCESS RECORD FOR SAME PROGRAM IN PROGRESS                  *
***********************************************************************
SAMEPGM  ST    R9,SP9                  SAVE RETURN REG
         CLC   ENDVSYS,CURRENDV        SAME ENDEVOR SYSTEM?
         BE    CHKOWN                  YES
         MVC   PRTPGM,CURRPGM          PROGRAM NAME TO OUTPUT
         MVC   PRTENDV,ENDVSYS         ENDEVOR SYSTEM TO OUTPUT
         MVC   PRTOWN,=C'????????'     SHOW PROBLEM
         BAS   R9,LIST1                PRINT A LINE
CHKOWN   L     R12,OWNTBLA             POINT TO TABLE
CHEKFND  CLC   OWNER,0(R12)            SAME OWNER AS ORIGINAL?
         BE    SPXIT                   YES, EXIT
         CLI   0(R12),X'FF'            NO, END OF TABLE?
         BE    ADDPGM                  YES, MAKE NEW ENTRY
         LA    R12,8(R12)              STEP TO NEXT ENTRY
         B     CHEKFND                 CONTINUE SEARCH
ADDPGM   MVC   0(8,R12),OWNER          SAVE NEW OWNER IN TABLE
         MVI   8(R12),X'FF'            NEW TABLE END
SPXIT    L     R9,SP9                  GET RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE TO ENDEVOR SYSTEM LISTING                         *
***********************************************************************
LIST1    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE TO SINGLE OWNER LISTING                           *
***********************************************************************
LIST2    PUT   PRINT2,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE TO MULTIPLE OWNER LISTING                         *
***********************************************************************
LIST3    PUT   PRINT3,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (PRINT1,,PRINT2,,PRINT3,,INPUT1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
CURRPGM  DC    CL8' '                  CURRENT PROGRAM NAME
CURRENDV DC    CL8' '                  CURRENT ENDEVOR SYSTEM
BLANKS   DS    CL133' '                CURRENT OWNER
OWNTBLA  DC    A(OWNTBL)               START OF OWNER TABLE
SP9      DS    A                       RETURN ADDRESS FOR SAMEPGM
EP9      DS    A                       RETURN ADDRESS FOR ENDPGM
BP9      DS    A                       RETURN ADDRESS FOR BEGINPGM
MO9      DS    A                       RETURN ADDRESS FOR MULTOWNR
SO9      DS    A                       RETURN ADDRESS FOR SNGLOWNR
*
INREC    DS    0CL133                  INPUT RECORD
         DC    CL1' '
PGMNAME  DC    CL8' '                     PROGRAM NAME
         DC    CL4' '
ENDVSYS  DC    CL8' '                     ENDEVOR SYSTEM NAME
         DC    CL2' '
OWNER    DC    CL8' '                     PROGRAM OWNER
         DC    CL1' '
PARMLTTR DC    CL6' PROC='
PROC     DC    CL8' '
         DC    CL10'  OWNERS: '
OWNERLST DC    CL47' '
         DC    CL30' '
*
PRTLINE  DS    0CL133                  PRINT LINE
         DC    CL1' '
PRTPGM   DC    CL8' '                    PROGRAM NAME
         DC    CL14' '
PRTENDV  DC    CL8' '                    ENDEVOR SYSTEM
         DC    CL4' '
PRTOWN   DC    CL8' '                    OWNER
         DC    CL90' '
*
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
PRINT2   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT2,                        X
               RECFM=FBA,LRECL=133
PRINT3   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT3,                        X
               RECFM=FBA,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=133
GETOWNR2 CSECT
         LTORG
OWNTBL   DS    500CL8       TABLE OF OWNERS
         END

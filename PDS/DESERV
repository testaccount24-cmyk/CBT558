         TITLE '*** DESERV: SAMPLE OF DESERV MACRO USE ***'
DESERV CSECT
*********************************************************************
* EXPERIMENT WITH USE OF THE DESERV MACRO AND DATA RETURNED FOR     *
* A PROGRAM OBJECT IN A PDSE.                                       *
*                                                                   *
* DESERV RETURNS DIRECTORY INFORMATION IN SYSTEM MANAGED DIRECTORY  *
* ENTRY (SMDE) FORMAT.                                              *
*                                                                   *
* AUTHOR R THORNTON OCT 2000                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING DESERV,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'DESERV'             PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES, PRINT HEADING                                  *
*********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT,INPDSE) OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE DESERV FUNC=GET,                                              X
               AREA=(SMDEBUFR,BUFSIZE),                                X
               BYPASS_LLA=YES,                                         X
               CONN_INTENT=HOLD,                                       X
               DCB=INPDSE,                                             X
               ENTRY_GAP=0,                                            X
               NAME_LIST=(NAMENTRY,1),                                 X
               MF=S
         LTR   R15,R15           WAS DESERV SUCCESSFUL?
         BZ    MLLOAD            YES
         ST    R15,DE15          NO, SAVE R15
         WTO   'DESERV FAILED',ROUTCDE=11
         ABEND 111,DUMP
MLLOAD   LM    R8,R11,MVCLREGS         LOAD MOVE LONG REGS
MLCKEND  CLM   R11,7,F0                ALL DATA PRINTED?
         BE    EOJ                     YES
         MVCL  R8,R10                  NO, MOVE NEXT LINE
         MVC   SAVEDATA,PRTDATA        SAVE PRINT DATA FOR HEX
         BAL   R15,PRINT               GO PRINT THE CHARACTER LINE
         MVZ   PRTDATA,SAVEDATA        MOVE ZONES
         TR    PRTDATA,TRZONES         TRANSLATE TO PRINTABLE
         BAL   R15,PRINT               PRINT ZONE LINE
         MVN   PRTDATA,SAVEDATA        MOVE NUMERICS
         TR    PRTDATA,TRNUM           TRANSLATE TO PRINTABLE
         BAL   R15,PRINT               PRINT NUMERIC LINE
         BAL   R15,PRINT               PRINT A BLANK LINE
         LM    R8,R9,MVCLREGS          REFRESH RECEIVE REGS
         B     MLCKEND                 CONTINUE
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    ST    R15,PR15                SAVE RETURN ADDRESS
         PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         L     R15,PR15                GET RETURN ADDRESS
         BR    R15                     NO, RETURN
PR15     DC    A(0)                    RETURN ADDRESS
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (PRINT1,,INPDSE)        CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
DE15     DC    F'0'                    RETURN CODE FROM DESERV
MVCLREGS DS    0A                      MOVE LONG REGS
         DC    A(PRTDATA)                 R8 = RECEIVE ADDRESS
         DC    A(L'PRTDATA)               R9 = RECEIVE LENGTH
         DC    A(SMDEBUFR)                R10 = SEND ADDRESS
         DC    C' ',AL3(BUFSIZE)          R11 = PAD, SEND LENGTH
F0       DC    F'0'                    CONSTANT 0
SMDEBUFR DS    0C                      SMDE BUFFER AREA
         DS    CL(L'DESB_FIXED)          FIXED PORTION
         DS    CL(SMDE_MAXLEN)           VARIABLE SIZE PORTION
SMDEBFND EQU   *
BUFSIZE  EQU   SMDEBFND-SMDEBUFR       LENGTH OF SMDE BUFFER AREA
*
NAMENTRY DS    0D                      NAME_LIST ENTRY FOR DSERV MACRO
NEFLAGS  DC    B'00000000'                FLAGS
NECODE   DC    AL1(0)                     RESULT CODE
NERRCODE DC    AL2(0)                     LOW HALFWORD OF REASON CODE
         DC    F'0'
NESMDEPT DC    A(SMDEBUFR)                POINTER TO SMDE
NENAMEPT DC    A(NAMEVALU)                POINTER TO NAME VALUE
*
NAMEVALU DS    0H                      NAME VALUE DECRIPTOR
         DC    H'8'                       LENGTH OF NAME THAT FOLLOWS
NVNAME   DC    CL8'MAINPROG'              NAME OF PROGRAM
*
BLANKS   DC    CL133' '                BLANKS
*
PRTLINE  DS    0CL80                   PRINT LINE
         DC    CL15' '
PRTDATA  DC    CL32' '                 PRINT DATA
         DC    CL32' '                 PRINT DATA
*
SAVEDATA DS    CL64                    HEX WORK AREA
TRZONES  DC    CL16'0000000000000000'  MAKE ZONES PRINTABLE
         DC    CL16'1111111111111111'    10-1F
         DC    CL16'2222222222222222'    20-2F
         DC    CL16'3333333333333333'    30-3F
         DC    CL16'4444444444444444'    40-4F
         DC    CL16'5555555555555555'    50-5F
         DC    CL16'6666666666666666'    60-6F
         DC    CL16'7777777777777777'    70-7F
         DC    CL16'8888888888888888'    80-8F
         DC    CL16'9999999999999999'    90-9F
         DC    CL16'AAAAAAAAAAAAAAAA'    A0-AF
         DC    CL16'BBBBBBBBBBBBBBBB'    B0-BF
         DC    CL16'CCCCCCCCCCCCCCCC'    C0-CF
         DC    CL16'DDDDDDDDDDDDDDDD'    D0-DF
         DC    CL16'EEEEEEEEEEEEEEEE'    E0-EF
         DC    CL16'FFFFFFFFFFFFFFFF'    F0-FF
TRNUM    DC    CL16'0123456789ABCDEF'  MAKE NUMERICS PRINTABLE
         DC    CL16'0123456789ABCDEF'    10-1F
         DC    CL16'0123456789ABCDEF'    20-2F
         DC    CL16'0123456789ABCDEF'    30-3F
         DC    CL16'0123456789ABCDEF'    40-4F
         DC    CL16'0123456789ABCDEF'    50-5F
         DC    CL16'0123456789ABCDEF'    60-6F
         DC    CL16'0123456789ABCDEF'    70-7F
         DC    CL16'0123456789ABCDEF'    80-8F
         DC    CL16'0123456789ABCDEF'    90-9F
         DC    CL16'0123456789ABCDEF'    A0-AF
         DC    CL16'0123456789ABCDEF'    B0-BF
         DC    CL16'0123456789ABCDEF'    C0-CF
         DC    CL16'0123456789ABCDEF'    D0-DF
         DC    CL16'0123456789ABCDEF'    E0-EF
         DC    CL16'0123456789ABCDEF'    F0-FF
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPDSE   DCB   DSORG=PO,MACRF=R,DDNAME=INPDSE
         LTORG
*********************************************************************
*        DESERV PARAMETER DSECT                                     *
*********************************************************************
         IGWDES
*********************************************************************
*        SMDE DESCRIPTION DSECT                                     *
*********************************************************************
         IGWSMDE
DESERV CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

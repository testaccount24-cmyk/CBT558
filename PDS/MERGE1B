         TITLE '*** MERGE1B: MERGE ENDVMTCH FILE INTO MERGE1A FILE ***'
***********************************************************************
* MERGE ENDEVOR SYSTEM AND SUBSYSTEM INFORMATION AS WELL AS SYSTEM    *
* OWNER INTO THE PROGRAM INFORMATION FILE FROM PROGRAM MERGE1A. THE   *
* THE MERGE1A OUTPUT FILE IS SORTED IN CSECT NAME ORDER AND THE       *
* ENDVMTCH FILE ON ENDEVOR ELEMENT NAME BEFORE PROCESSING HERE.       *
* A MATCH OCCURS WHEN THE MERGE1A RECORD HAS NO ENDEVOR SYSTEM INFO   *
* CSECT NAME IS THE SAME AS THE ENDEVOR ELEMENT NAME.                 *
*   THE MERGE1A INPUT FILE IS THE SORTED MERGE1A FILE                 *
*   THE ENDVMTCH INPUT FILE IS THE SORTED ENDVMTCH FILE               *
* AUTHOR R THORNTON DEC 1994                                          *
***********************************************************************
*
MERGE1B SVLNK R3,R4
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,ENDVMTCH,,MERGE1A) OPEN FILES
         BAS   R9,GETEND               READ FIRST ENDVMTCH RECORD
         BAS   R9,GETMG1A              READ FIRST MERGE1A RECORD
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLC   ENDELEM,MG1CSECT        ELEMENT = CSECT NAME?
         BL    ENDLOW                  NO, ENDVMTCH RECORD LOW
         BH    MG1LOW                  NO, MERGE1A RECORD LOW
         CLC   MG1SYSTM,BLANKS         ALREADY HAVE ENDEVOR SYSTEM?
         BNE   MG1LOW                  YES
         MVC   MG1SYSTM,ENDSYSTM       ENDEVOR SYSTEM TO OUTPUT
         MVC   MG1SUBSY,ENDSUBSY       ENDEVOR SUBSYSTEM TO OUTPUT
         MVC   MG1OWNR,ENDOWNR         SYSTEM OWNER TO OUTPUT
MG1LOW   BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         BAS   R9,GETMG1A              READ NEXT MERGE1A RECORD
         B     MAINLINE                CONTINUE
ENDLOW   BAS   R9,GETEND               READ NEXT ENDVMTCH RECORD
         B     MAINLINE                CONTINUE
***********************************************************************
*        READ MERGE1A RECORD                                          *
***********************************************************************
GETMG1A  GET   MERGE1A,MG1REC          READ MERGE1A RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        READ ENDVMTCH RECORD                                         *
***********************************************************************
GETEND   GET   ENDVMTCH,ENDREC         READ ENDVMTCH RECORD
         BR    R9                      RETURN TO CALLER
EOFEND   MVC   ENDREC,HIVAL            SET RECORD TO HEX FF'S
         BR    R9
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PUTOUT   PUT   OUTPUT1,MG1REC          READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,ENDVMTCH,,MERGE1A) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
MATCH    DC    XL1'0'                  MATCH FOUND
BLANKS   DC    CL107' '                BLANKS
HIVAL    DC    80X'FF'                 FIELD OF HEX FF'S
*
PRTLINE  DS    0CL133                  PRINT LINE
         DC    CL11' UNMATCHED '
PRTPGM   DC    CL8' '
         DC    CL15' RECORD: CSECT='
PRTCSECT DC    CL8' '
         DC    CL9', MEMBER='
PRTMEMB  DC    CL8' '
         DC    CL9', DSNAME='
PRTDSNAM DC    CL44' '
         DC    CL21' '
*
MG1REC   DS    0CL107                  MERGE1 OUTPUT RECORD
MG1DSNAM DC    CL44' '                   LOAD LIBRARY DSNAME
MG1MEMB  DC    CL8' '                    LOAD LIBRARY MEMBER NAME
MG1CSECT DC    CL8' '                    CSECT NAME WITHIN MEMBER
MG1LANG  DC    CL10' '                   LANGUAGE TRANSLATOR NAME
MG1VVMM  DC    CL4' '                    VERSION AND MOD LEVEL
MG1EPIND DC    CL1' '                    * IF ENTRY POINT FOR MODULE
MG1SYSTM DC    CL8' '                  SYSTEM NAME
MG1SUBSY DC    CL8' '                  SUBSYSTEM NAME
MG1OWNR  DS    CL16                    SYSTEM OWNER NAME
*
ENDREC   DS    0CL80                   OUTPUT RECORD
ENDELEM  DS    CL8                     ELEMENT NAME
         DC    CL1' '
ENDTYPE  DC    CL8' '                  ELEMENT TYPE
         DC    CL1' '
ENDENV   DC    CL8' '                  ENVIRONMENT NAME
         DC    CL1' '
ENDSYSTM DC    CL8' '                  SYSTEM NAME
         DC    CL1' '
ENDSUBSY DC    CL8' '                  SUBSYSTEM NAME
         DC    CL1' '
ENDOWNR  DS    CL16                    SYSTEM OWNER NAME
         DC    CL19' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=107
ENDVMTCH DCB   DSORG=PS,MACRF=GM,DDNAME=ENDVMTCH,EODAD=EOFEND,         X
               RECFM=FB,LRECL=80
MERGE1A  DCB   DSORG=PS,MACRF=GM,DDNAME=MERGE1A,EODAD=EOJ,             X
               RECFM=FB,LRECL=107
         LTORG
MERGE1B  CSECT
         END

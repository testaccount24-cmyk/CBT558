         TITLE 'ILBCHECK - VALIDATE COBOL ILBO MODULES '
ILBCHECK SVLNK R3,R4
**********************************************************************
*       A CONTROL CARD IS READ FROM THE //SYSIN DD FILE. THE CONTROL *
* CARD CONTAINS THE NAME OF A MEMBER IN THE //SYSLIB DD FILE THAT    *
* IS TO BE CHECKED.                                                  *
*       NEXT, THE MEMBER IS READ FROM THE SYSLIB LIBRARY, AND EACH   *
* CSECT IS CHECKED. IF THE CSECT NAME BEGINS WITH ILB IT IS USED     *
* AS AN ARGUMENT TO SEARCH THE INTERNAL TABLE OF ILB..... SUBROUTINES*
* AND THE LENGTHS ARE COMPARED. IF THE CSECT CONTAINED IN THE MEMBER *
* IS THE SAME AS THAT IN THE TABLE, IT IS ASSUMED TO BE A VALID      *
* CURRENT MODULE. IF THE LENGTHS DIFFER, THE ILB..... SUBROUTINE IN  *
* THE MEMBER IS ASSUMED TO BE AN OLD VERSION, AND A MESSAGE IS       *
* WRITTEN TO SHOW THE MEMBER NAME, ILB..... NAME, AND SIZES FROM THE *
* MODULE AND THE TABLE.                                              *
*                                                                    *
* PURPOSE: TO AID PROGRAMMERS IN IDENTIFYING POTENTIAL MIXTURES OF   *
* OLD AND NEW COBOL ILB..... MODULES, WHICH WILL RESULT IN VARIOUS   *
* EXECUTION TIME ABENDS INCLUDING S0C1, S0C2, AND S0C4.              *
*                                                                    *
* AUTHOR R. THORNTON   NOV 1993                                      *
**********************************************************************
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),=H'0'           ANY PARM DATA?
         BE    OPENS                   NO, CONTINUE
         MVI   SUMMARY,1               YES, SHOW SUMMARY REPORT
OPENS    OPEN  (SYSPRINT,OUTPUT,SYSLIB,,SYSIN) OPEN FILES
         RDJFCB SYSLIB                 READ THE JFCB FOR SYSLIB
         STORAGE OBTAIN,LENGTH=65535   GET BUFFER STORAGE
         ST    R1,BUFAD                SAVE BUFFER ADDRESS
         PUT   SYSPRINT,HDRREC         WRITE HEADER LINE
***********************************************************************
* READ NEXT CONTROL CARD AND ISSUE BLDL FOR THE MODULE                *
***********************************************************************
*
NEXTCARD CLC   CTLCARD,BLANKS          FIRST TIME?
         BE    WRTBLNK                 YES
         MVC   PRTMEM,MEMBER           NO, MEMBER NAME TO PRINT
         MVC   PRTSUB,=CL8'--------'   CLEAR
         MVC   PRTMSIZ,=CL8'--------'   CLEAR
         MVC   PRT2SIZ,=CL8'--------'   CLEAR
         MVC   PRTVSIZ,=CL8'--------'   CLEAR
         CLI   FOUNDSW,0               ANY ILB'S FOR LAST MEMBER?
         BNE   TESTMIX                 YES
         MVC   PRTMSG,NOIDENT          NO IDENTIFIABLE ILB MSG
         BAS   R9,PRINT                PRINT MESSAGE
         B     WRTBLNK                 CONTINUE
TESTMIX  CLI   FOUNDSW,X'11'           MIXTURE OF OLD AND NEW ILB'S?
         BNE   NOMIX                   NO
         MVC   PRTMEM,MEMBER           YES, MEMBER NAME TO PRINT
         MVC   PRTMSG,DANGER           DANGER: BOTH OLD AND NEW ILBS
         BAS   R9,PRINT                PRINT MESSAGE
         B     WRTBLNK                 CONTINUE
NOMIX    CLI   FOUNDSW,X'01'           ONLY FOUND OLD ILB'S?
         BE    ONLYOLD                 YES
         MVC   PRTMEM,MEMBER           NO, MEMBER NAME TO PRINT
         MVC   PRTMSG,ONLYNEW          ONLY NEW ILB SUBROUTINES FOUND
         BAS   R9,PRINT                PRINT MESSAGE
         B     WRTBLNK                 CONTINUE
ONLYOLD  MVC   PRTMEM,MEMBER           MEMBER NAME TO PRINT
         MVC   PRTMSG,WARNING          WARNING, OLD ILB'S
         BAS   R9,PRINT                PRINT MESSAGE
WRTBLNK  MVI   FOUNDSW,0               RESET SWITCH
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    GETCTLC                 YES
         BAS   R9,PRINT                BLANK LINE BETWEEN MEMBERS
GETCTLC  GET   SYSIN,CTLCARD           READ CONTROL CARD
         MVC   MEMBER,CCMEMB           MOVE MEMBER NAME TO BLDL LIST
         BLDL  SYSLIB,BLDLIST          ISSUE BLDL
         LTR   R15,R15                 ANY ERRORS
         BZ    GOTMEM                  NO
         MVC   PRTMEM,MEMBER           MEMBER NAME TO MESSAGE
         MVC   MISSMBR,MEMBER          MEMBER NAME TO MESSAGE
         MVC   PRTMSG(L'INVMEM),INVMEM MEMBER NOT IN PDS MESSAGE
         BAS   R9,PRINT                PRINT MESSAGE
         B     NEXTCARD                GO READ NEXT CONTROL CARD
GOTMEM   MVI   CCAT,0                  INSURE TTR0
         POINT SYSLIB,TTRMOD           POINT TO 1ST BLOCK OF MODULE
*
***********************************************************************
* MAINLINE PROCESSING FOR THE LOAD MODULE.                            *
***********************************************************************
*
MAINLINE L     R6,BUFAD                GET BUFFER ADDRESS
         READ  DECB,SF,SYSLIB,(6),'S'  READ BLOCK FROM MEMBER
         CHECK DECB                    AWAIT COMPLETION
         CLI   0(R6),X'20'             CESD RECORD
         BNE   TESTOTHR                NO
         BAS   R9,CESDREC              PROCESS CESD RECORDS
         B     MAINLINE                GO READ AGAIN
TESTOTHR CLI   0(R6),1                 IT IS CONTROL
         BE    PERFORM                 YES
         CLI   0(R6),5                 IS IT CONTROL
         BE    PERFORM                 YES
         CLI   0(R6),13                IS IT CONTROL
         BE    PERFORM                 YES
         CLI   0(R6),3                 IS IT CTL AND RLD
         BE    PERFORM                 YES
         CLI   0(R6),7                 IS IT CTL AND RLD
         BE    PERFORM                 YES
         CLI   0(R6),15                IS IT CTL AND RLD
         BE    PERFORM                 YES
         TM    0(R6),X'08'             LAST RECORD IN MEMBER?
         BNO   MAINLINE                NO, SKIP IT
         B     NEXTCARD                AND READ NEXT CONTROL CARD
PERFORM  TM    0(R6),X'08'             RECD PRECEDES LAST RECD OF MBR?
         BO    NEXTCARD                YES, READ NEXT CONTROL CARD
         READ  DECB,SF,,(6),MF=E       READ FOLLOWING TEXT RECORD
         CHECK DECB                    AWAIT COMPLETION
         B     MAINLINE                NO, GO READ AGAIN
****************************************************************
* PROCESS COMPOSITE ESD RECORDS. IF AN ILB..... SUBROUTINE IS  *
* FOUND THE ILBVRFY SUBROUTINE IS CALLED TO VALIDATE IT.       *
****************************************************************
CESDREC  ST    R9,CES9                 SAVE RETURN
         LH    R10,4(R6)               GET ESD ID OF 1ST ITEM
         LH    R8,6(R6)                NBR BYTES OF ESD DATA
         SRL   R8,4                    COMPUTE NBR ENTRIES
         LA    R6,8(R6)                STEP TO 1ST RECORD ESD ITEM
CESDNXT  NI    8(R6),X'0F'             CLEAR INDICATOR BITS
         CLI   8(R6),0                 THIS A CSECT (SD) ENTRY?
         BNE   ADVNC2                  NO, SKIP IT
         CLC   0(3,R6),=CL3'ILB'       IS IT AN ILB..... SUBROUTINE?
         BNE   ADVNC2                  NO
         MVC   SUBNAME,0(R6)           SAVE SUBROUTINE NAME
         ICM   R1,7,13(R6)             PICK UP MODULE SIZE
         LA    R1,7(R1)                ADD FOR ROUNDING
         STCM  R1,7,SUBLENG            SAVE SIZE
         NI    SUBLENG+2,B'11111000'   FINISH ROUNDING
         BAS   R9,ILBVRFY              GO VERIFY ILB..... MODULE
ADVNC2   LA    R10,1(R10)              ADD 1 TO ESD COUNTER
         LA    R6,16(R6)               TO NEXT ESD ITEM IN INPUT
         BCT   R8,CESDNXT              LOOP THRU INPUT RECORD
CESDEXIT L     R9,CES9                 GET RETURN ADDR
         BR    R9                      EXIT
****************************************************************
* LOOK THE ILB..... SUBROUTINE UP IN THE TABLE. COMPARE TABLE  *
* LENGTH AND MODULE LENGTH. IF SAME, RETURN, ELSE PRINT A LINE.*
****************************************************************
ILBVRFY  ST    R9,ILB9                 SAVE RETURN ADDRESS
         MVC   PRTMEM,MEMBER           MEMBER NAME TO PRINT LINE
         MVC   PRTSUB,SUBNAME          ILB..... NAME TO PRINT LINE
         LA    R12,SUBLENG             CONVERT
         BAS   R9,HEXPRT3                MODULE SIZE
         MVC   PRTMSIZ,PRTABL               TO PRINTABLE
         LA    R10,ILBTABLE            POINT TO ILB SUBROUTINE TABLE
         USING ILBTBL,R10
ILBCKND  CLI   0(R10),X'FF'            END OF TABLE?
         BE    ILBNOTFD                YES
         CLC   ILBNAME,SUBNAME         FOUND THE ENTRY?
         BE    ILBFND                  YES
         LA    R10,L'ILBNTRY(R10)      STEP TO NEXT TABLE ENTRY
         B     ILBCKND                 CONTINUE SEARCH
ILBFND   LA    R12,ILBSIZE2            CONVERT
         BAS   R9,HEXPRT3                COB2  SIZE
         MVC   PRT2SIZ,PRTABL               TO PRINTABLE
         LA    R12,ILBSIZEV            CONVERT
         BAS   R9,HEXPRT3                VS COB SIZE
         MVC   PRTVSIZ,PRTABL               TO PRINTABLE
         CLC   ILBSIZE2,SUBLENG        MODULE SIZE SAME AS COBOL2?
         BE    ILBVNEW                 YES
         CLC   ILBSIZEV,SUBLENG        MODULE SIZE SAME AS VS COBOL?
         BNE   UNIDENT                 NO
         OI    FOUNDSW,X'01'           SHOW OLD ILB SUBROUTINE FOUND
         MVC   PRTMSG(20),=CL20'VSCOB ILB SUBROUTINE'
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    ILBVXIT                 YES
         BAS   R9,PRINT                PRINT MESSAGE
         B     ILBVXIT                 AND EXIT
UNIDENT  MVC   PRTMSG(29),=CL29'UNIDENTIFIABLE ILB SUBROUTINE'
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    ILBVXIT                 YES
         BAS   R9,PRINT                PRINT MESSAGE
         B     ILBVXIT                 AND EXIT
ILBVNEW  CLC   ILBSIZEV,ILBSIZE2       VS AND COBOL2 MODULES SAME?
         BE    CANTTELL                YES
         OI    FOUNDSW,X'10'           SHOW OLD ILB SUBROUTINE FOUND
         MVC   PRTMSG(19),=CL19'COB2 ILB SUBROUTINE'
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    ILBVXIT                 YES
         BAS   R9,PRINT                PRINT MESSAGE
         B     ILBVXIT                 AND EXIT
CANTTELL MVC   PRTMSG(24),=CL24'COB2 AND VSCOB SAME SIZE'
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    ILBVXIT                 YES
         BAS   R9,PRINT                PRINT MESSAGE
         B     ILBVXIT                 AND EXIT
ILBNOTFD MVC   PRTMSG(27),=CL27'SUBROUTINE NOT IN ILB TABLE'
         CLI   SUMMARY,1               SUMMARY REPORT?
         BE    ILBVXIT                 YES
         BAS   R9,PRINT                PRINT MESSAGE
ILBVXIT  L     R9,ILB9                 GET RETURN ADDR
         BR    R9                      EXIT
****************************************************************
* CREATE PRINTABLE HEX FROM HEX. ON ENTRY, REG 12 CONTAINS THE *
* ADDRESS OF THE DATA TO BE REFORMATTED. ENTRY POINT USED      *
* DETERMINES THE SIZE OF THE FIELD. OUTPUT DATA IS PLACED IN   *
* THE PRTABL FIELD, 2 CHARACTERS PER BYTE.                     *
****************************************************************
HEXPRT1  UNPK  PRTABL(3),0(2,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT2  UNPK  PRTABL(5),0(3,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT3  UNPK  PRTABL(7),0(4,R12)      UNPACK HEX
         B     HEXCLTR                 CONTINUE
HEXPRT4  UNPK  PRTABL(9),0(5,R12)      UNPACK HEX
HEXCLTR  MVZ   PRTABL(8),XZROS         CLEAR FOR TRANSLATE
         TR    PRTABL(8),TRTBL         MAKE PRINTABLE
         BR    R9                      EXIT
****************************************************************
* PRINT TO MESSAGE FILE                                        *
****************************************************************
PRINT    PUT   SYSPRINT,PRT            WRITE PRINT RECORD
         MVC   PRT,BLANKS              CLEAR PRINT  RECORD
         BR    R9                      EXIT
****************************************************************
* END OF JOB.                                                  *
****************************************************************
EOJ      CLOSE (SYSLIB,,SYSPRINT,,SYSIN) CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
SYSLIB   DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,EXLST=JFCBEXIT,          X
               RECFM=U,NCP=1,EODAD=NEXTCARD
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
***********************************************************************
*        WORKING STORAGE AND CONSTANTS                                *
***********************************************************************
BUFAD    DS    F                       SYSLIB BUFFER ADDR
SUMMARY  DC    X'00'                   1 = SUMMARY REPORT REQUESTED
SUBNAME  DC    CL8' '                  ILB..... SUBROUTINE NAME
SUBLENG  DC    XL3'00'                 ILB..... SUBROUTINE LENGTH
FOUNDSW  DC    X'00'                   1 = OLD ILB SUBROUTINES FOUND
REMBYTS  DC    H'0'                    BYTES REMAINING IN BLOCK
ELEVEN   DC    H'11'                   MIN BYTES PER DIRECTORY ENTRY
ZERO     DC    H'0'                    CONSTANT ZERO
RECADDR  DS    A                       CURRENT ADDR IN DIRECTORY BLK
CLRBITS  EQU   X'1F'                   AND MASK TO CLEAR BITS
DIRBLK   DS    CL256                   DIRECTORY BLOCK
         DS    0D
DBLWD    DS    D                       DOUBLEWORD WORK AREA
ILB9     DS    A                       RETURN ADDRESS FROM ILBVRFY
BLANKS   DC    CL133' '                CONSTANT BLANKS
*
PRT      DS    0CL80                   PRINT LINE
PRTMEM   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
PRTSUB   DC    CL8' '                    ILB..... SUBROUTINE NAME
         DC    CL1' '
PRTMSIZ  DC    CL6' '                    MODULE SIZE
         DC    CL1' '
PRT2SIZ  DC    CL6' '                    TABLE SIZE
         DC    CL1' '
PRTVSIZ  DC    CL6' '                    TABLE SIZE
         DC    CL2' '
PRTMSG   DC    CL40' '                   MESSAGE/COMMENT
*
NOIDENT  DC    CL42'NO IDENTIFIABLE ILB SUBROUTINES           '
DANGER   DC    CL42'!!!!! DANGER: BOTH VSCOB AND COB2 !!!!!!! '
ONLYNEW  DC    CL42'ONLY COB2 ILB SUBROUTINES                 '
WARNING  DC    CL42'***** WARNING: VSCOB SUBROUTINES          '
*
HDRREC   DS    0CL80                   HEADER LINE
         DC    CL8'MEMBER'               MEMBER NAME
         DC    CL1' '
         DC    CL8'SUBRTN'               ILB..... SUBROUTINE NAME
         DC    CL1' '
         DC    CL6'MODSIZ'               MODULE SIZE
         DC    CL1' '
         DC    CL6'SIZ-II'               TABLE SIZE
         DC    CL1' '
         DC    CL6'SIZ-VS'               TABLE SIZE
         DC    CL2' '
         DC    CL40'MESSAGE/COMMENT'
*
CTLCARD  DS    0CL80                   CONTROL CARD
CCMEMB   DC    CL8' '                  MEMBER NAME
         DC    CL72' '
*
*
*
         DS    0F
BLDLIST  DS    0CL62                   BLDL LIST
         DC    H'1'                    ONE ENTRY
         DC    H'58'                   LENGTH OF ENTRY
MEMBER   DC    CL8' '                  MEMBER NAME
TTRMOD   DC    XL3'000000'             TTR OF MODULE
CCAT     DC    XL1'00'                 CONCATENATION NUMBER
         DC    XL1'00'
ALIASIND DC    XL1'00'                 ALIAS AND MISC INDICATOR
*                           80=ALIAS
TTR1TXT  DC    XL3'000000'             TTR OF 1ST TXT RECORD
         DC    XL1'00'
TTRNS    DC    XL3'000000'             TTR OF NOTE OR SCATTER LIST
NNOTE    DC    XL1'00'                 NBR ENTRIES IN NOTE LIST
ATTR1A   DC    XL1'00'                 MODULE ATTRIBUTES 1, BYTE 1
*                           80=RENT
*                           40=REUS
*                           20=OVERLAY
*                           10=UNDER TEST
*                           08=ONLY LOADABLE
*                           04=SCATTER FORMAT
*                           02=EXECUTABLE
*                           01=ONE TXT, NO RLD RECORDS
ATTR1B   DC    XL1'00'                 ATTRIBUTES 1, BYTE 2
*                           80=CANNOT BE REPROCESSED BY LKED E
*                           40=ORIGIN OF 1ST TXT RECORD IS ZERO
*                           20=ASSIGNED ENTRY POINT ADDR IS ZERO
*                           10=CONTAINS NO RLD RECORD
*                           08=CANNOT BE REPROCESSED BY LKED
*                           04=CONTAINS TESTRAN SYMBOLS
*                           02=CREATED BY LKED
*                           01=REFR
TOTVIRT  DC    XL3'000000'             TOTAL VIRTUAL STRG REQRD FOR MOD
LENG1    DC    XL2'0000'               LENGTH OF 1ST TEXT RECORD
LKEPA    DC    XL3'000000'             ASSIGNED ENTRY POINT ADDR
ATTR2    DC    XL1'00'                 ATTRIBUTES 2
*                           80=PROCESSED BY OS/VS LKED
*                           20=PAGE ALIGNMENT REQUIRED FOR MODULE
*                           10=SSI PRESENT
         DC    XL2'0000'
SCTRLEN  DC    XL2'0000'               SCATTER LIST LENGTH
TTLEN    DC    XL2'0000'               TRANSLATION TABLE LENGTH
SCESDID  DC    XL2'0000'               CESD NBR FOR 1ST TXT RECD
SCEPESD  DC    XL2'0000'               CESD NBR FOR ENTRY POINT
ALEPA    DC    XL3'000000'             ENTRY POINT OF THE MEMBER NAME
ALMEM    DC    CL8' '                  REAL MEMBER NAME FOR ALIAS
SSI      DS    XL4'00000000'           SSI BYTES
AUTHLEN  DC    XL1'00'                 AUTH CODE LENGTH
AUTHCOD  DC    XL1'00'                 AUTH CODE
*
*
*
PROCESS  DC    XL1'00'                 PROCESS INDIC 40=LAST RECD
CES9     DS    F                       CESDREC RETURN ADDR
BL9      DS    F                       BLDL RTN RETURN ADDR
CR9      DS    F                       CTRLRECS RETURN ADDR
CT9      DS    F                       CNTLRECS RETURN ADDR
RL9      DS    F                       RLDRECS RETURN ADDR
TX9      DS    F                       TXTFMT RETURN ADDR
HIVAL    DC    4X'FF'                  CONSTANT F'S
H1000    DC    H'1000'                 CONSTANT 1000
H100     DC    H'100'                  CONSTANT 100
H10      DC    H'10'                   CONSTANT 10
PRTABL   DS    CL9                     HEX-PRINTABLE CONVERSION AREA
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
P58      DC    P'58'                   CONSTANT 58
*
*
H1       DC    H'1'                    CONSTANT 1
H5       DC    H'5'                    CONSTANT 5
H11      DC    H'11'                   CONSTANT 11
H16      DC    H'16'                   CONSTANT 16
H256     DC    H'256'                  CONSTANT 256
XZROS    DC    8X'00'                  CONSTANT ZEROS
TRTBL    DC    C'0123456789ABCDEF'     TRANSLATE TBL
*
INVMEM   DS    0CL80                   MISSING MEMBER MESSAGE
         DC    CL7'MEMBER '
MISSMBR  DC    CL8' '                  MEMBER NAME
         DC    CL65' NOT FOUND IN PDS DIRECTORY'
*
POFSET   DC    F'0'                    OFFSET FOR TEXT PRINT
PRTCHAR  DC    256C'.'                 CHARACTER TRANSLATE TABLE
         ORG   PRTCHAR+C' '
         DC    C' '
         ORG   PRTCHAR+C'A'
         DC    C'ABCDEFGHI'
         ORG   PRTCHAR+C'J'
         DC    C'JKLMNOPQR'
         ORG   PRTCHAR+C'S'
         DC    C'STUVWXYZ'
         ORG   PRTCHAR+C'0'
         DC    C'0123456789'
         ORG
JFCBEXIT DS    0F                      EDITEXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
*
         JFCBDEF DSECT=NO
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
ILBTBL   DSECT ,                       ILB SUBROUTINE TABLE ENTRY
ILBNTRY  DS    0CL14
ILBNAME  DS    CL8                       ILB..... SUBROUTINE NAME
ILBSIZE2 DS    XL3                       ILB..... SUBROUTINE LENGTH
ILBSIZEV DS    XL3                       ILB..... SUBROUTINE LENGTH
ILBCHECK CSECT
***********************************************************************
*        TABLE AND LARGE BUFFER AREAS                                 *
***********************************************************************
         LTORG
ILBTABLE DS    0C                      ILB SUBROUTINE TABLE
         DC    CL8'ILBOABN ',XL3'0000B8',XL3'000098'
         DC    CL8'ILBOACP ',XL3'000318',XL3'000260'
         DC    CL8'ILBOACS ',XL3'000120',XL3'000110'
         DC    CL8'ILBOANE ',XL3'000168',XL3'000158'
         DC    CL8'ILBOANF ',XL3'000098',XL3'000080'
         DC    CL8'ILBOATB ',XL3'000130',XL3'000120'
         DC    CL8'ILBOBEG ',XL3'000310',XL3'000188'
         DC    CL8'ILBOBID ',XL3'0000A8',XL3'000090'
         DC    CL8'ILBOBIE ',XL3'0000A8',XL3'000098'
         DC    CL8'ILBOBII ',XL3'000208',XL3'0001F0'
         DC    CL8'ILBOBUG ',XL3'000830',XL3'000808'
         DC    CL8'ILBOCHN ',XL3'0001D0',XL3'0001C8'
         DC    CL8'ILBOCJS ',XL3'002090',XL3'002088'
         DC    CL8'ILBOCKP ',XL3'000070',XL3'000058'
         DC    CL8'ILBOCLS ',XL3'0000C8',XL3'0000B0'
         DC    CL8'ILBOCMM ',XL3'000548',XL3'0003A0'
         DC    CL8'ILBOCOM0',XL3'000170',XL3'000170'
         DC    CL8'ILBOCT1 ',XL3'000108',XL3'0000F8'
         DC    CL8'ILBOCVB ',XL3'000440',XL3'000428'
         DC    CL8'ILBODBE ',XL3'000618',XL3'000600'
         DC    CL8'ILBODBG ',XL3'0010A8',XL3'000E60'
         DC    CL8'ILBODCI ',XL3'000110',XL3'000100'
         DC    CL8'ILBODSP ',XL3'000C88',XL3'000A08'
         DC    CL8'ILBODSS ',XL3'000000',XL3'000418'
         DC    CL8'ILBODTE ',XL3'000218',XL3'000208'
         DC    CL8'ILBOD01 ',XL3'000330',XL3'000318'
         DC    CL8'ILBOD10 ',XL3'000A28',XL3'000A10'
         DC    CL8'ILBOD11 ',XL3'0002E0',XL3'0002D0'
         DC    CL8'ILBOD12 ',XL3'0006D8',XL3'0006C0'
         DC    CL8'ILBOD13 ',XL3'000620',XL3'000610'
         DC    CL8'ILBOD14 ',XL3'0005A8',XL3'000560'
         DC    CL8'ILBOD20 ',XL3'0003E0',XL3'0003C8'
         DC    CL8'ILBOD21 ',XL3'000600',XL3'0005F0'
         DC    CL8'ILBOD22 ',XL3'000930',XL3'000910'
         DC    CL8'ILBOD23 ',XL3'000F28',XL3'000F10'
         DC    CL8'ILBOD24 ',XL3'000C00',XL3'000C10'
         DC    CL8'ILBOD25 ',XL3'000450',XL3'000440'
         DC    CL8'ILBOD26 ',XL3'0009B0',XL3'0009A0'
         DC    CL8'ILBOEFL ',XL3'000278',XL3'000268'
         DC    CL8'ILBOERR ',XL3'000310',XL3'0002F8'
         DC    CL8'ILBOETB ',XL3'000130',XL3'000120'
         DC    CL8'ILBOEXT ',XL3'000080',XL3'000068'
         DC    CL8'ILBOFLW ',XL3'000568',XL3'000550'
         DC    CL8'ILBOFPW ',XL3'0003D0',XL3'000338'
         DC    CL8'ILBOGDO ',XL3'000138',XL3'000120'
         DC    CL8'ILBOGPW ',XL3'000080',XL3'000068'
         DC    CL8'ILBOIDB ',XL3'0000A0',XL3'000090'
         DC    CL8'ILBOIDR ',XL3'0006A8',XL3'000698'
         DC    CL8'ILBOIDT ',XL3'0002D8',XL3'0002C8'
         DC    CL8'ILBOIFB ',XL3'000228',XL3'000168'
         DC    CL8'ILBOIFD ',XL3'000100',XL3'0000F0'
         DC    CL8'ILBOINS ',XL3'0006F0',XL3'0006E0'
         DC    CL8'ILBOINT ',XL3'000118',XL3'000100'
         DC    CL8'ILBOITB ',XL3'000130',XL3'000120'
         DC    CL8'ILBOIVL ',XL3'000070',XL3'000060'
         DC    CL8'ILBOLBL ',XL3'000200',XL3'0001E8'
         DC    CL8'ILBOMCPN',XL3'000008',XL3'000000'
         DC    CL8'ILBOMRG ',XL3'0003C8',XL3'0003B0'
         DC    CL8'ILBOMSC ',XL3'000128',XL3'0000F8'
         DC    CL8'ILBOMSG ',XL3'000118',XL3'000100'
         DC    CL8'ILBONBL ',XL3'000AB8',XL3'000AB0'
         DC    CL8'ILBONED ',XL3'000590',XL3'000580'
         DC    CL8'ILBONTR ',XL3'001160',XL3'000BF8'
         DC    CL8'ILBOOCR ',XL3'0005A0',XL3'000588'
         DC    CL8'ILBOPRM ',XL3'000680',XL3'000538'
         DC    CL8'ILBOPTV ',XL3'0000B8',XL3'0000A0'
         DC    CL8'ILBOQIO ',XL3'0008F8',XL3'000800'
         DC    CL8'ILBOQSS ',XL3'000368',XL3'000350'
         DC    CL8'ILBOQSU ',XL3'001898',XL3'001890'
         DC    CL8'ILBOREC ',XL3'000D48',XL3'000D28'
         DC    CL8'ILBORNT ',XL3'000410',XL3'000400'
         DC    CL8'ILBOSAM ',XL3'000560',XL3'0004F8'
         DC    CL8'ILBOSCD ',XL3'0000E0',XL3'0000C8'
         DC    CL8'ILBOSCH ',XL3'000420',XL3'000410'
         DC    CL8'ILBOSDB ',XL3'000448',XL3'000430'
         DC    CL8'ILBOSGM ',XL3'000220',XL3'000210'
         DC    CL8'ILBOSMG ',XL3'000F90',XL3'000D78'
         DC    CL8'ILBOSMV ',XL3'000060',XL3'000050'
         DC    CL8'ILBOSND ',XL3'000D50',XL3'000CF8'
         DC    CL8'ILBOSNT ',XL3'0002D0',XL3'0002C0'
         DC    CL8'ILBOSPA ',XL3'000878',XL3'0006C8'
         DC    CL8'ILBOSPI ',XL3'000148',XL3'000138'
         DC    CL8'ILBOSPN ',XL3'000178',XL3'000160'
         DC    CL8'ILBOSQA ',XL3'000720',XL3'000720'
         DC    CL8'ILBOSRT ',XL3'000650',XL3'000640'
         DC    CL8'ILBOSRV ',XL3'000D90',XL3'0004A8'
         DC    CL8'ILBOSSN ',XL3'0001B0',XL3'0000F8'
         DC    CL8'ILBOSTG ',XL3'0002D0',XL3'0002B8'
         DC    CL8'ILBOSTI ',XL3'000278',XL3'000268'
         DC    CL8'ILBOSTN ',XL3'000318',XL3'000308'
         DC    CL8'ILBOSTR ',XL3'000080',XL3'000070'
         DC    CL8'ILBOSTT ',XL3'000230',XL3'000178'
         DC    CL8'ILBOSYN ',XL3'000478',XL3'000480'
         DC    CL8'ILBOTC0 ',XL3'000350',XL3'000330'
         DC    CL8'ILBOTC2 ',XL3'0003E0',XL3'0003C0'
         DC    CL8'ILBOTC3 ',XL3'001208',XL3'0011F8'
         DC    CL8'ILBOTEF ',XL3'0002D0',XL3'0002C0'
         DC    CL8'ILBOTRN ',XL3'000130',XL3'000120'
         DC    CL8'ILBOUST ',XL3'000870',XL3'000858'
         DC    CL8'ILBOUTB ',XL3'000130',XL3'000120'
         DC    CL8'ILBOVCO ',XL3'000228',XL3'000218'
         DC    CL8'ILBOVIO ',XL3'001F30',XL3'001D28'
         DC    CL8'ILBOVMO ',XL3'000298',XL3'000240'
         DC    CL8'ILBOVOC ',XL3'002378',XL3'002138'
         DC    CL8'ILBOVTR ',XL3'0000B0',XL3'0000A0'
         DC    CL8'ILBOWAT ',XL3'0000B0',XL3'000098'
         DC    CL8'ILBOWTB ',XL3'000130',XL3'000120'
         DC    CL8'ILBOXDI ',XL3'000148',XL3'000128'
         DC    CL8'ILBOXMU ',XL3'0000E0',XL3'0000D0'
         DC    CL8'ILBOXPR ',XL3'0002D0',XL3'0002B0'
         DC    X'FF'                   END OF TABLE
         END

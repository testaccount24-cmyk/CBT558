         TITLE '*** SVCDMERG: MERGE IN NEW SVCDUMP INFORMATION ***'
         PRINT NOGEN
SVCDMERG SVLNK R3
***********************************************************************
* MERGES THE DEL.SVCDUMP.SORT AND DEL.NEWDUMP.SORT FILES TO PRODUCE   *
* THE NEW DUMP INFORMATION FILE DEL.SVCDUMP.MERG WHICH WILL BE        *
* REFORMATTED BY SVCDRFM2.                                            *
* AUTHOR R THORNTON SEP 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION                                                      *
***********************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
         GET   INPUT1,OLDRECD          READ FIRST OLD DUMP RECORD
         PACK  NEXTNBR,OLDMPNBR        SAVE NEXT DUMP NUMBER TO ASSIGN
         MVC   MRGRECD,BLANKS          CLEAR MERGE RECORD
         MVI   MGTITLE,0               INDICATE EMPTY RECORD
***********************************************************************
* MAINLINE PROCESSING: MERGE THE OLD AND NEW SVCDUMP DATASETS         *
***********************************************************************
MAINLINE CLI   READ1,0                 SHOULD I READ INPUT1?
         BE    TRYIN2                  NO
         GET   INPUT1,OLDRECD          READ OLD DUMP RECORD
         MVI   READ1,0                 RESET READ SWITCH
TRYIN2   CLI   READ2,0                 SHOULD I READ INPUT2?
         BE    COMPARE                 NO
         GET   INPUT2,NEWRECD          READ NEW DUMP RECORD
         MVI   READ2,0                 RESET READ SWITCH
COMPARE  CLC   OLTITLE,NEWTITLE        OLD TITLE: NEW TITLE
         BNH   OLDLOW                  OLD TITLE IS LOW OR EQUAL
NEWLOW   MVI   READ2,1                 INDICATE READ INPUT2
         CLI   MGTITLE,0               MERGE RECORD IN PROGRESS?
         BE    NEWADD                  NO, THIS IS A NEW DUMP
         CLC   NEWTITLE,MGTITLE        THIS SAME AS RECORD IN PROGRESS?
         BE    NEWSAME                 YES
         BAS   R9,WRITMERG             NOT SAME, WRITE CURRENT
NEWADD   CLI   NEWSIDE,C'A'            VALID DSNAME?
         BL    MAINLINE                NO
         CLI   NEWSIDE,C'B'            VALID DSNAME?
         BH    MAINLINE                NO
         MVC   MRGRECD,BLANKS          CLEAR FOR NEW RECORD
         UNPK  MGDMPNBR,NEXTNBR        NEW DUMP NUMBER
         OI    MGDMPNBR+3,C'0'         CLEAR SIGN
         AP    NEXTNBR,P1              INCREMENT NEXT DUMP NUMBER
         MVC   MGDSNAME(1),NEWSIDE     SIDE (A OR B)
         MVC   MGDSNAME+1(1),NEWDMPNO  DUMP NUMBER (0-9)
         MVC   MGDSNAME+2(4),NEWGENNO  GENERATION NUMBER
         MVC   MGLSTDAT,NEWDATE        DATE OF DUMP
         MVC   MGLSTTIM,NEWTIME        TIME OF DUMP
         MVC   MGFSTDAT,NEWDATE+2      DATE OF FIRST OCCURRENCE
         MVI   MGTOTAL+3,C'1'          TOTAL OF 1 DUMP SO FAR
         CLI   NEWSIDE,C'A'            DUMP ON SYSA?
         BE    SETA1                   YES
         MVI   MGADUMPS+3,C'0'         SET SYSA DUMPS = 0
         MVI   MGBDUMPS+3,C'1'         SET SYSB DUMPS = 1
         B     NATITLE                 CONTINUE
SETA1    MVI   MGADUMPS+3,C'1'         SET SYSA DUMPS = 1
         MVI   MGBDUMPS+3,C'0'         SET SYSB DUMPS = 0
NATITLE  MVC   MGTITLE,NEWTITLE        DUMP TITLE
         B     MAINLINE                CONTINUE
NEWSAME  CLC   NEWDATE,MGLSTDAT        ALREADY PROCESSED THIS DUMP?
         BH    NEWRECUR                NO, RECURRENCE
         BL    MAINLINE                YES, DROP THE RECORD
         CLC   NEWTIME,MGLSTTIM        ALREADY PROCESSED THIS DUMP?
         BNH   MAINLINE                YES, DROP THE RECORD
NEWRECUR MVC   MGDSNAME(1),NEWSIDE     SIDE (A OR B)
         MVC   MGDSNAME+1(1),NEWDMPNO  DUMP NUMBER (0-9)
         MVC   MGDSNAME+2(4),NEWGENNO  GENERATION NUMBER
         MVC   MGLSTDAT,NEWDATE        DATE OF DUMP
         MVC   MGLSTTIM,NEWTIME        TIME OF DUMP
         PACK  WORKNBR,MGTOTAL         INCREMENT
         AP    WORKNBR,P1                 TOTAL
         MVC   WORKEDIT,ED5                  NUMBER
         ED    WORKEDIT,WORKNBR                 OF
         MVC   MGTOTAL,WORKEDIT+2                  DUMPS
         CLI   NEWSIDE,C'A'            DUMP ON SYSA?
         BE    ADD1A                   YES
         PACK  WORKNBR,MGBDUMPS        INCREMENT
         AP    WORKNBR,P1                 TOTAL
         MVC   WORKEDIT,ED5                  NUMBER
         ED    WORKEDIT,WORKNBR                 OF SYSB
         MVC   MGBDUMPS,WORKEDIT+2                 DUMPS
         B     MAINLINE                GO READ NEXT RECORD
ADD1A    PACK  WORKNBR,MGADUMPS        INCREMENT
         AP    WORKNBR,P1                 TOTAL
         MVC   WORKEDIT,ED5                  NUMBER
         ED    WORKEDIT,WORKNBR                 OF SYSA
         MVC   MGADUMPS,WORKEDIT+2                 DUMPS
         B     MAINLINE                GO READ NEXT RECORD
OLDLOW   MVI   READ1,1                 INDICATE INPUT1 TO BE READ
         CLI   MGTITLE,0               ANY MERGE RECORD IN PROGRESS?
         BE    MOVOLD                  NO
         BAS   R9,WRITMERG             YES, GO WRITE THE RECORD
MOVOLD   MVC   MRGRECD,OLDRECD         COPY OLD RECORD FOR OUTPUT
         B     MAINLINE                GO READ NEXT RECORD
***********************************************************************
*        END OF INPUT1 FILE                                           *
***********************************************************************
WRITMERG PUT   OUTPUT1,MRGRECD         WRITE MERGE OUTPUT RECORD
         MVC   MRGRECD,BLANKS          CLEAR MERGE RECORD
         MVI   MGTITLE,0               INDICATE EMPTY RECORD
         BR    R9                      RETURN
***********************************************************************
*        END OF INPUT1 FILE                                           *
***********************************************************************
EOF1     CLI   NEWTITLE,X'FF'          ALREADY AT EOF ON INPUT2?
         BE    EOJ                     YES, ALL THRU
         MVI   READ1,0                 CLEAR READ SWITCH
         MVI   OLTITLE,X'FF'           MAKE RECORD HIGH
         B     MAINLINE                CONTINUE
***********************************************************************
*        END OF INPUT2 FILE                                           *
***********************************************************************
EOF2     CLI   OLTITLE,X'FF'           ALREADY AT EOF ON INPUT1?
         BE    EOJ                     YES, ALL THRU
         MVI   READ2,0                 CLEAR READ SWITCH
         MVI   NEWTITLE,X'FF'          MAKE RECORD HIGH
         B     MAINLINE                CONTINUE
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      CLI   MGTITLE,0               ANY MERGE RECORD IN PROGRESS?
         BE    EOJ1                    NO
         BAS   R9,WRITMERG             YES, GO WRITE IT
EOJ1     CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
NEXTNBR  DC    PL3'0'                  NEXT DUMP NUMBER TO ASSIGN
WORKNBR  DC    PL3'0'                  WORK FIELD
WORKEDIT DS    CL6                     EDIT FIELD
ED5      DC    XL6'402020202020'       EDIT MASK
P1       DC    PL1'1'                  CONSTANT 1
BLANKS   DC    CL236' '                BLANKS
READ1    DC    X'01'                   1=READ INPUT1
READ2    DC    X'01'                   1=READ INPUT2
*
OLDRECD  DS    0CL236                  DUMP INFORMATION RECORD
OLDMPNBR DS    CL4                      DUMP NBR
OLDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
OLLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
OLLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
OLFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
OLTOTAL  DS    CL4                      TOTAL DUMPS
OLADUMPS DS    CL4                      TOTAL SYSA DUMPS
OLBDUMPS DS    CL4                      TOTAL SYSB DUMPS
OLREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
OLIBMNBR DS    CL5                      IBM PROBLEM NUMBER
OLPTFNBR DS    CL7                      FIXING PTF NUMBER
OLAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
OLAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
OLTITLE  DS    CL116                    DUMP TITLE
OLNOTE   DS    CL60                     NOTE
*
NEWRECD  DS    0CL152                  OUTPUT RECORD
NEWDATE  DS    CL6                        YYMMDD DATE
NEWTIME  DS    CL4                        HHMM TIME
         DS    CL10'SYSTEM.SYS'           DSNAME
NEWSIDE  DS    CL1                         A OR B
         DS    CL5'.DUMP'
NEWDMPNO DS    CL1                         0-9
         DS    CL2'.G'
NEWGENNO DS    CL4                         GENERATION NBR
         DS    CL3'V00'
NEWTITLE DS    CL116                      TITLE
*
MRGRECD  DS    0CL236                  DUMP INFORMATION RECORD
MGDMPNBR DS    CL4                      DUMP NBR
MGDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
MGLSTDAT DS    CL6                      YYMMDD DATE OF LAST DUMP
MGLSTTIM DS    CL4                      HHMM TIME OF LAST DUMP
MGFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
MGTOTAL  DS    CL4                      TOTAL DUMPS
MGADUMPS DS    CL4                      TOTAL SYSA DUMPS
MGBDUMPS DS    CL4                      TOTAL SYSB DUMPS
MGREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
MGIBMNBR DS    CL5                      IBM PROBLEM NUMBER
MGPTFNBR DS    CL7                      FIXING PTF NUMBER
MGAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
MGAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
MGTITLE  DS    CL116                    DUMP TITLE
MGNOTE   DS    CL60                     NOTE
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOF1,             X
               RECFM=FB,LRECL=236
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=FB,LRECL=152
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=236
         END

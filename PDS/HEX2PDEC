         TITLE 'CONVERT HEX BYTE TO PRINTABLE DECIMAL'
HEX2PDEC CSECT
******************************************************************
*ON ENTRY, TWO PARAMETER ADDRESSES ARE PASSED USING STANDARD     *
*SUBROUTINE LINKAGE CONVENTIONS. THE FIRST PARAMETER IS A ONE-   *
*BYTE FIELD CONTAINING A HEX NUMBER. THE SECOND PARAMETER IS A   *
*THREE-BYTE CHARACTER FIELD WHICH WILL CONTAIN THE PRINTABLE     *
*DECIMAL EQUIVALENT OF THE FIRST PARAMETER ON RETURN TO THE      *
*CALLER. EXAMPLE OF USE FROM A COBOL PROGRAM:                    *
*       .                                                        *
*   WORKING-STORAGE SECTION.                                     *
*       .                                                        *
*       .                                                        *
*       05 BINARY-HALFWORD-FIELD         PIC S99 COMP.           *
*       05 FILLER REDEFINES BINARY-HALFWORD-FIELD.               *
*          10 FIRST-BINARY-BYTE          PIC X.                  *
*          10 SECOND-BINARY-BYTE         PIC X.                  *
*       .                                                        *
*       .                                                        *
*       05 UNPK-UNSIGN-DECIMAL           PIC XXX.                *
*       .                                                        *
*       .                                                        *
*   PROCEDURE DIVISION.                                          *
*       .                                                        *
*       .                                                        *
*       CALL 'HEX2PDEC' USING SECOND-BINARY-BYTE,                *
*                             UNPK-UNSIGN-DECIMAL.               *
*       .                                                        *
*       .                                                        *
*                                                                *
*AUTHOR R. THORNTON  JAN, 1986                                   *
******************************************************************
*
*
         USING *,R15
         STM   R2,R3,28(R13)               SAVE WORK REGS
         L     R2,0(R1)                    POINT TO 1ST PARAMETER
         SR    R3,R3                       CLEAR FOR INSERT
         IC    R3,0(R2)                    PICK UP 1 HEX BYTE
         CVD   R3,DBLWD                    CONVERT IT TO PACKED DECIMAL
         L     R2,4(R1)                    POINT TO 2ND PARAMETER
         UNPK  0(3,R2),DBLWD+6(2)          UNPACK NUMBER TO 2ND PARM
         OI    2(R2),C'0'                  MAKE 2ND PARM UNSIGNED
         LM    R2,R3,28(R13)               RESTORE WORK REGS
         SR    R15,R15                     SET RETURN CODE = 0
         BR    R14                         RETURN TO CALLER
R1       EQU   1
R2       EQU   2
R3       EQU   3
R13      EQU   13
R14      EQU   14
R15      EQU   15
DBLWD    DS    D                           DOUBLEWORD WORK AREA
         END

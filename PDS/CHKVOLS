         TITLE '***** CHKVOLS: FIND ALL DSORG=PO,RECFM=U *****'
*        PRINT NOGEN
CHKVOLS  SVLNK R3,R4
*
**********************************************************************
*  READS THE VTOCS OF ALL ONLINE VOLUMES LOOKING FOR DATASETS HAVING *
*  DSORG=PO AND RECFM=U (LOAD MODULE LIBRARIES). WHEN FOUND, A LINE  *
*  IS PRINTED GIVING THE DSNAME AND VOLSER.                          *
*                                                                    *
*  AUTHOR R. THORNTON JUL 1992                                       *
**********************************************************************
*
***********************************************************************
* OPEN SYSPRINT FOR PRINTING.                                         *
* ALLOCATE A DD STATEMENT FOR USE BY EXCP AND OPEN THE DCB USED FOR   *
* EXCP. IF EITHER THE ALLOCATE OR OPEN FAILS, A U0333 ABEND WILL BE   *
* PRODUCED WITH AN EXPLANATORY MESSAGE.                               *
***********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         MVC   PRT,BLANKS              CLEAR PRINT LINE
RETDYN   CALL  DYNALLOC,(DAPARM1,DAPARM2) ALLOCATE A TEMPORARY DATASET
         CLC   RETRNCD,=C'00'          ANY ERROR?
         BE    OPENEXCP                NO
         CLC   ERRRET,=C'0410'         DSNAME IN USE?
         BNE   PRTBADA                 NO
         CLI   DDN+5,C'9'              ALREADY TRIED 9 TIMES?
         BE    PRTBADA                 YES
         IC    R9,DDN+5                NO, GET LAST BYTE OF DDNAME
         LA    R9,1(R9)                ADD 1
         STC   R9,DDN+5                UPDATE DDNAME
         B     RETDYN                  GO TRY AGAIN
PRTBADA  MVC   PRT,UNALOC              MESSAGE TO PRINT
         BAL   R9,PRINT                PRINT MESSAGE
         MVC   DYNRTCD,ERRRET          ERROR RETURN CODE TO MESSAGE
         MVC   DYNINFO,INFORET         INFO RETURN CODE TO MESSAGE
         MVC   PRT,DYNERR              MESSAGE TO PRINT
         BAL   R9,PRINT                PRINT ERROR MESSAGE
         ABEND 333,DUMP                TERMINATE WITH ERROR
OPENEXCP OPEN  (DADCB,OUTPUT)          OPEN FILE FOR EXCP
         TM    DADCB+48,X'10'          WAS OPEN SUCCESSFUL?
         BO    MAINLINE                YES
         MVC   PRT,UNOPEN              MESSAGE TO PRINT
         BAL   R9,PRINT                PRINT ERROR MESSAGE
         ABEND 333,DUMP                TERMINATE WITH ERROR
***********************************************************************
*   MAINLINE ROUTINE. CALL ROUTINES TO LOCATE A VOLUME AND READ ALL   *
*   THE FORMAT 1 DSCB'S, THEN PRINT IF DATASET IS DSORG=PO,RECFM=U    *
***********************************************************************
MAINLINE MVI   VOLSW,0                 RESET VOLUME SWITCH
         BAL   R9,VOLUME               GO LOCATE NEXT DASD VOLUME
         CLI   VOLSW,1                 VOLUME ALLOCATED O.K.?
         BNE   EOJ                     NO, ASSUME NO MORE VOLUMES
         BAL   R9,DSNAMEA              YES, GO READ THE VTOC
         B     MAINLINE                GO TO NEXT VOLUME
***********************************************************************
*        LOOK THROUGH THE UCB TABLE IN O/S FOR THE NEXT ONLINE DASD   *
*        VOLUME. WHEN FOUND, SET THE EXCP FILE DEB UCB ADDRESS TO THIS*
*        UCB, AND CONVERT THE UCB VTOC ADDRESS FROM TTR TO CCHHR FORM.*
*        READ THE  FORMAT 4 DSCB AND SET THE DEB EXTENTS FOR THE VTOC *
*        DATASET.                                                     *
***********************************************************************
VOLUME   ST    R9,VOL9                 SAVE RETURN ADDRESS
VLSLOOP  SMODE KEY=0,STATE=S
         UCBSCAN ADDRESS,WORKAREA=WORK100,UCBPTR=ADDRUCB,              X
               NOPIN,DYNAMIC=NO,RANGE=ALL,DEVCLASS=DASD
         ST    R15,SAV15               SAVE RETURN CODE
         SMODE KEY=NZERO,STATE=P
         CLC   SAV15,FZERO             FOUND UCB?
         BNE   VOLXIT                  NO
         L     R2,ADDRUCB              YES, GET THE UCB ADDRESS
         USING UCBOB,R2
         TM    UCBSTAT,UCBONLI         IS IT ONLINE?
         BZ    VLSLOOP                 NO
         MVC   VOLSER,UCBVOLI          SAVE THE VOLSER
         CLC   VOLSER(2),=C'SM'        ????????TESTING?????????
         BNH   VLSLOOP                 ????????TESTING?????????
         MVC   PRT(10),=CL10'***VOLUME '
         MVC   PRT+11(6),VOLSER        VOLSER TO PRINT
         BAL   R9,PRINT                PRINT VOLUME LINE
         L     R12,DADCB+44            @ DEB FOR EXCP FILE
         SMODE KEY=0,STATE=P
         STCM  R2,7,33(R12)            CHANGE DEB UCB ADDRESS
         SMODE KEY=NZERO,STATE=P
         SR    R10,R10                 CLEAR FOR DIVIDE
         SR    R11,R11                 CLEAR FOR INSERT
         ICM   R11,3,UCBVTOC           PICK UP VTOC START TRACK
         CLI   UCBUNTYP,X'0E'          IS IT A 3380?
         BE    GOT3380                 YES
         CLI   UCBUNTYP,X'0F'          IS IT A 3390?
         BE    GOT3390                 YES
         B     VLSLOOP                 INVALID DEVICE, GO TO NEXT
GOT3380  D     R10,=F'15'              COMPUTE CYL-HD
         B     SETVTOC                 CONTINUE
GOT3390  D     R10,=F'15'              COMPUTE CYL-HD
SETVTOC  STH   R11,VTOCCYL             SAVE VTOC CYLINDER NBR
         STH   R10,VTOCHD              SAVE VTOC HEAD NUMBER
         MVC   VTOCREC,UCBVTOC+2       VTOC BEGIN RECORD NBR
         ICM   R15,15,UCBVTOC          GET VTOC TTR0 TO R15
         DROP  R2
         MVC   BGNXTNT,VTOCADR         SET EXTENT BEGIN TO VTOC START
         MVC   ENDXTNT,VTOCADR         SET EXTENT END TO VTOC START
         MVI   XTNTS,1                 INDICATE SINGLE EXTENT
         BAL   R9,SETXTNT              GO SET DEB EXTENT
         BAL   R9,EXCPREAD             GO READ FORMAT 4 DSCB
         CLI   EXCPIND,C' '            ANY ERROR?
         BNE   VLSLOOP                 YES, GO TRY NEXT UCB
VOLCKID  LA    R1,RKEYDATA             POINT TO INPUT AREA
         CLI   DS4IDFMT-DSCBFMT4(R1),C'4' POSSIBLE FORMAT 4 DSCB?
         BNE   VLSLOOP                 NO, SKIP THIS VOLUME
         CLI   RKEYDATA,X'04'          POSSIBLE FORMAT 4 DSCB?
         BNE   VLSLOOP                 NO,SKIP THIS VOLUME
         CLC   RKEYDATA+1(43),RKEYDATA IS IT THE FORMAT 4 DSCB?
         BNE   VLSLOOP                 NO,SKIP THIS VOLUME
         MVC   DSCBFMT4,RKEYDATA       YES, SAVE FORMAT 4 DSCB
         MVC   EXTENTS,DS4VTOCE        SET EXTENTS TO VTOC EXTENTS
         MVI   XTNTS,1                 INDICATE SINGLE EXTENT
         BAL   R9,SETXTNT              SET DEB EXTENTS FOR VTOC
         MVI   VOLSW,1                 SHOW VOLUME ALLOCATED O.K.
VOLXIT   L     R9,VOL9                 GET RETURN ADDRESS
         BR    R9                      RETURN
***********************************************************************
*        READ DSCB'S FROM THE VTOC LOOKING FOR THE NEXT FORMAT 1      *
*        DSCB FOR A DSORG=PO,RECFM=U DATASET.                         *
***********************************************************************
DSNAMEA  ST    R9,DSN9                 SAVE RETURN ADDRESS
         MVC   EXTENTS(6),DS4VTOCE     BEGIN EXTENT FOR VTOC
         MVC   EXTENTS+6(4),DS4HPCHR   HIGHEST CCHHR USED FOR FMT1
         MVI   XTNTS,1                 INDICATE SINGLE EXTENT
         BAL   R9,SETXTNT              SET UP DEB EXTENTS FOR VTOC
READVTOC BAL   R9,EXCPREAD             GO READ A DSCB
         CLI   EXCPIND,C' '            ANY EXCEPTIONAL CONDITION?
         BNE   DSNXIT                  YES, ASSUME END OF VTOC
         LA    R1,RKEYDATA             POINT TO INPUT DATA
         CLI   DS1FMTID-DSCBFMT1(R1),C'1' IS IT A FORMAT 1 DSCB?
         BNE   READVTOC                NO
         MVC   DSCBFMT1,RKEYDATA       YES, SAVE IN FORMAT 1 DSCB
         TM    DS1DSORG,X'02'          IS IT PO?
         BZ    READVTOC                NO
         TM    DS1RECFM,X'C0'          IS IT UNDEFINED?
         BNO   READVTOC                NO
         MVC   PDSNAME,DS1DSNAM        DSNAME TO PRINT
         MVC   PVOLSER,VOLSER          VOLUME SERIAL TO PRINT
         BAL   R9,PRINT                PRINT DATA LINE
         B     READVTOC                AND CONTINUE VTOC SEARCH
DSNXIT   L     R9,DSN9                 GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
*   SET DEB EXTENTS FOR THE RANGE TO BE ACCESSED                      *
***********************************************************************
SETXTNT  L     R12,DADCB+44            @ DEB FOR INPUT1 FILE
         SMODE KEY=0,STATE=P
         MVC   38(5,R12),BGNXTNT       SET DEB START EXTENT
         MVC   42(5,R12),ENDXTNT       SET DEB END EXTENT
         SMODE KEY=NZERO,STATE=P
         MVC   RCOUNT(4),BGNXTNT       SET BEGIN SEARCH ADDRESS
         MVI   RCOUNT+4,0              BEGIN AT RECORD 0
         BR    R9                      EXIT
***********************************************************************
*        READ NEXT BLOCK USING EXCP. SEARCH ADDRESS IS IN THE RCOUNT  *
*        FIELD: SET UP BY THE CALLER.                                 *
***********************************************************************
EXCPREAD ST    R9,XRD9                 SAVE RETURN ADDRESS
EXCPSRCH LA    R1,CMDCHNR              @ CCWS TO READ RECORD 0
         CLI   RCOUNT+4,0              WANT FIRST RECORD ON TRACK?
         BNE   XRDLST                  NO
         LA    R1,CMDCHNR0             YES, USE ALTERNATE CCW'S
XRDLST   BAL   R9,EXCP                 CALL EXCP ROUTINE
         CLI   EXCPIND,C'X'            GOOD I/O?
         BE    EXCPRXIT                NO
         CLI   EXCPIND,C'E'            END OF EXTENT?
         BE    EXCPRXIT                YES
EXCPRXIT L     R9,XRD9                 GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
*           END OF JOB ROUTINE                                        *
***********************************************************************
EOJ      CLOSE (SYSIN,,SYSPRINT,,DADCB) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        ISSUE EXCP TO PERFORM THE OPERATION SET UP BY THE CALLER.    *
*        THE CALLER MUST PLACE THE ADDRESS OF THE COMMAND CHAIN TO BE *
*        EXECUTED IN R1 BEFORE CALLING EXCP.                          *
***********************************************************************
EXCP     ST    R9,EXCP9                SAVE RETURN ADDRESS
         ST    R1,DACCWAD              STORE COMMAND ADDRESS
REISSUE  MVI   EXCPIND,C' '            CLEAR ERROR INDICATOR
         MVC   DASRCH,RCOUNT           SEARCH ON LAST RECD READ
         EXCP  DAIOB                   READ COUNT, KEY, AND DATA
         WAIT  ECB=DAECB               AWAIT COMPLETION
         CLI   DAECB,X'7F'             GOOD READ
         BE    EXCPCHEK                YES
         MVI   EXCPIND,C'E'            INDICATE END OF EXTENT RANGE
         CLI   DAECB,X'42'             IS IT EOE?
         BNE   SETXHE                  NO
         SR    R15,R15                 CLEAR FOR INSERT
         IC    R15,XTNTS               GET NBR REMAINING EXTENTS
         BCT   R15,NEWXTNT             DECREMENT NUMBER EXTENTS
         B     EXCPEXIT                BRANCH IF NO MORE EXTENTS
NEWXTNT  MVC   EXTENTS,EXTADNL         SHIFT IN NEXT EXTENT
         STC   R15,XTNTS               UPDATE EXTENT COUNT
         BAL   R9,SETXTNT              GO SET NEW EXTENT IN DEB
         L     R1,DACCWAD              GET CCW ADDRESS
         B     REISSUE                 AND GO REISSUE COMMAND
SETXHE   MVI   EXCPIND,C'X'            INDICATE HARD I/O ERROR
         CLI   DAECB,X'41'             I/O ERROR INDICATED
         BNE   EXHDERR                 NO
         TM    DAUNST,1                IS IT EOF RECORD
         BO    ENDF                    YES
EXHDERR  BAL   R9,HARDERR              NO, HARD I/O ERROR
         B     EXCPEXIT                EXIT
EXCPCHEK MVI   NEWRECD,0               INDICATE NEW RECORD
         CLC   DACCWAD+1(3),=AL3(CMDCHNR0) THIS WAS THE R0 READ CCW?
         BNE   PNTRKD                  NO
         MVC   DACCWAD+1(3),=AL3(CMDCHNR) YES, SET TO NORMAL READ
PNTRKD   LA    R1,RKEYDATA             POINT TO I/O AREA
         SR    R15,R15                 CLEAR FOR INSERT
         IC    R15,RCOUNT+5            GET KEY LENGTH
         AR    R1,R15                  ADD TO START
         ST    R1,CURRECD              SAVE DATA RECORD ADDRESS
         AH    R1,RCOUNT+6             ADD DATA LENGTH
         ST    R1,AREAEND              SAVE AREA END ADDRESS
         ZAP   RECNO,P1                INITIALIZE RECORD NBR
         B     EXCPEXIT                AND EXIT
ENDF     MVI   EXCPIND,C'F'            INDICATE EOF RECORD
EXCPEXIT L     R9,EXCP9                GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
*       PRINT HARD I/O ERROR DATA                                     *
***********************************************************************
HARDERR  ST    R9,HE9                  SAVE RETURN ADDRESS
         ICM   R15,8,DAECBCC           PICK UP ECB COMPLETION CODE
         BAL   R9,HEXPRNT              GO MAKE PRINTABLE
         MVC   IOECC,WORK              MOVE TO PRINT
         ICM   R15,15,DACSW            1ST 4 BYTES OF CSW
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECSW1,WORK            MOVE TO PRINT
         ICM   R15,15,DACSW+4          2ND 4 BYTES OF CSW
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECSW2,WORK            MOVE TO PRINT
         L     R14,DANXCCW             POINT TO NEXT CCW
         LA    R14,0(R14)              CLEAR HIGH ORDER BYTE
         SH    R14,=H'8'               BACK UP TO FAILING CCW
         C     R14,=A(CMDCHNR0)        THIS ONE OF MY CCW'S?
         BL    UNKNCCW                 NO
         C     R14,=A(WRITCCW)         THIS ONE OF MY CCW'S?
         BNH   FORMCCWD                YES
UNKNCCW  MVC   IOECCW1,=CL8'????????'  UNKNOWN CCW
         MVC   IOECCW2,=CL8'????????'  UNKNOWN CCW
         MVC   IOEBYTS,BLANKS          IGNORE BYTES READ
         MVC   IOECD1,BLANKS           IGNORE DATA READ
         MVC   IOECD2,BLANKS           IGNORE DATA READ
         MVC   IOECD3,BLANKS           IGNORE DATA READ
         B     HECHNAM                 CONTINUE
FORMCCWD ICM   R15,15,0(R14)           1ST 4 BYTES OF FAILING CCW
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECCW1,WORK            MOVE TO PRINT
         ICM   R15,15,4(R14)           2ND 4 BYTES OF FAILING CCW
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECCW2,WORK            MOVE TO PRINT
         LH    R15,6(R14)              PICK UP CCW LENGTH
         SH    R15,DARESID             COMPUTE BYTES READ
         CVD   R15,DBLWD               CONVERT TO PACKED
         UNPK  IOEBYTS,DBLWD           UNPACK TO PRINT
         L     R14,0(R14)              GET CCW DATA ADDRESS
         ICM   R15,15,0(R14)           POINT TO CCW DATA
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECD1,WORK             MOVE TO PRINT
         ICM   R15,15,4(R14)           PICK UP NEXT 4 BYTES
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECD2,WORK             MOVE TO PRINT
         ICM   R15,15,8(R14)           PICK UP NEXT 4 BYTES
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOECD3,WORK             MOVE TO PRINT
         OI    IOEBYTS+4,C'0'          CLEAR SIGN
HECHNAM  MVC   IOECHNM,=CL8'CMDCHNR0'  ASSUME CMDCHNR0
         CLC   DACCWAD+1(3),=AL3(CMDCHNR0) IS IT CMDCHNR0?
         BE    ERRSCH                  YES
         MVC   IOECHNM,=CL8'CMDCHNR'   NO, ASSUME CMDCHNR
         CLC   DACCWAD+1(3),=AL3(CMDCHNR) IS IT CMDCHNR?
         BE    ERRSCH                  YES
         MVC   IOECHNM,=CL8'CMDCHNK'   ASSUME CMDCHNK
         CLC   DACCWAD+1(3),=AL3(CMDCHNK) IS IT CMDCHNK?
         BE    ERRSCH                  YES
         MVC   IOECHNM,=CL8'????????'  UNKNOWN CCW CHAIN
ERRSCH   ICM   R15,15,DASRCH           PICK UP SEARCH CCHH
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOESRCH(8),WORK         MOVE TO PRINT
         ICM   R15,8,DASRCH+4          PICK UP SEARCH RECORD NBR
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOESRCH+8(2),WORK       MOVE TO PRINT
         ICM   R15,12,DAUCSNS          PICK UP SENSE BYTES
         BAL   R9,HEXPRNT              MAKE PRINTABLE
         MVC   IOESNS,WORK             MOVE TO PRINT
         MVC   PRT,IOERR1              LINE 1 TO PRINT
         BAL   R9,PRINT                PRINT IT
         MVC   PRT,IOERR2              LINE 2 TO PRINT
         BAL   R9,PRINT                PRINT IT
         MVC   PRT,IOERR3              LINE 3 TO PRINT
         BAL   R9,PRINT                PRINT IT
         MVC   PRT+15(16),=CL16'CONDITIONS ARE: '
         LA    R14,PRT+31              POINT TO CONDITION DESCRIPTION
         CLI   DAECBCC,X'41'           IS IT PERMANENT I/O ERROR?
         BNE   IOE1                    NO
         LA    R15,0                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE1     CLI   DAECBCC,X'42'           IS IT EXTENT VIOLATION?
         BNE   IOE2                    NO
         LA    R15,1                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE2     CLI   DAECBCC,X'44'           IS IT INTERCEPTED?
         BNE   IOE3                    NO
         LA    R15,2                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE3     TM    DAUNST,X'20'            IS IT UNIT CHECK?
         BZ    IOE4                    NO
         LA    R15,3                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE4     TM    DAUNST,X'10'            IS IT UNIT EXCEPTION?
         BZ    IOE5                    NO
         LA    R15,4                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE5     TM    DACHST,X'40'            IS IT WRONG LENGTH RECORD?
         BZ    IOE6                    NO
         LA    R15,5                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE6     TM    DACHST,X'20'            IS IT CHANNEL PROGRAM CHECK?
         BZ    IOE7                    NO
         LA    R15,6                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE7     TM    DACHST,X'10'            IS IT PROTECTION CHECK?
         BZ    IOE8                    NO
         LA    R15,7                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE8     TM    DACHST,X'08'            IS IT CHANNEL DATA CHECK?
         BZ    IOE9                    NO
         LA    R15,8                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE9     TM    DACHST,X'04'            IS IT CHANNEL CONTROL CHECK?
         BZ    IOE10                   NO
         LA    R15,9                   LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE10    TM    DACHST,X'02'            IS IT INTERFACE CONTROL CHECK?
         BZ    IOE11                   NO
         LA    R15,10                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE11    TM    DAUCSNS,X'80'           IS IT COMMAND REJECT?
         BZ    IOE12                   NO
         LA    R15,11                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE12    TM    DAUCSNS,X'10'           IS IT EQUIPMENT CHECK?
         BZ    IOE13                   NO
         LA    R15,12                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE13    TM    DAUCSNS,X'08'           IS IT DATA CHECK?
         BZ    IOE14                   NO
         LA    R15,13                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE14    TM    DAUCSNS,X'04'           IS IT OVERRUN?
         BZ    IOE15                   NO
         LA    R15,14                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE15    TM    DAUCSNS,X'02'           IS IT TRK CONDITION CHECK?
         BZ    IOE16                   NO
         LA    R15,15                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE16    TM    DAUCSNS,X'01'           IS IT SEEK CHECK?
         BZ    IOE17                   NO
         LA    R15,16                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE17    TM    DAUCSNS+1,X'80'         IS IT DATA CHECK IN COUNT?
         BZ    IOE18                   NO
         LA    R15,17                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE18    TM    DAUCSNS+1,X'40'         IS IT WRITE TRACK OVERRUN?
         BZ    IOE19                   NO
         LA    R15,18                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE19    TM    DAUCSNS+1,X'20'         IS IT END OF CYLINDER ON M-T?
         BZ    IOE20                   NO
         LA    R15,19                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE20    TM    DAUCSNS+1,X'10'         IS IT INVALID COMMAND SEQUENCE?
         BZ    IOE21                   NO
         LA    R15,20                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE21    TM    DAUCSNS+1,X'08'         IS IT NO RECORD FOUND?
         BZ    IOE22                   NO
         LA    R15,21                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE22    TM    DAUCSNS+1,X'04'         IS IT FILE PROTECTED?
         BZ    IOE23                   NO
         LA    R15,22                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE23    TM    DAUCSNS+1,X'02'         IS IT MISSING ADDRESS MARKER?
         BZ    IOE24                   NO
         LA    R15,23                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOE24    TM    DAUCSNS+1,X'01'         IS IT OVERFLOW INCOMPLETE?
         BZ    IOECKPTD                NO
         LA    R15,24                  LITERAL SUBSCRIPT
         BAL   R9,CONDPRT              GO PRINT
IOECKPTD CLC   PRT,BLANKS              ANYTHING LEFT IN PRT?
         BE    HEXIT                   NO
         BAL   R9,PRINT                YES, PRINT IT
HEXIT    L     R9,HE9                  GET RETURN ADDRESS
         BR    R9                      EXIT
***********************************************************************
*        ROUTINE TO CONVERT HEX TO PRINTABLE CHARACTERS (2 CHARACTERS *
*        PER HEX BYTE. ON ENTRY, HEX TO BE CONVERTED IS IN R15. THE   *
*        CHARACTER EQUIVALENT IS PLACED IN THE FIRST 8 BYTES OF WORK. *
***********************************************************************
HEXPRNT  STCM  R15,15,WORK              SAVE HEX VALUE
         UNPK  WORK(9),WORK(5)          CONVERT TO 1 NIBBLE PER BYTE
         MVZ   WORK(8),XZROS            CLEAR ZONES FOR TRANSLATE
         TR    WORK(8),TRTBLA           TRANSLATE TO PRINTABLE
         BR    R9                       EXIT
***********************************************************************
*        PRINT I/O ERROR CONDITION LITERALS                           *
***********************************************************************
CONDPRT  ST    R9,CND9                 SAVE RETURN ADDRESS
         MH    R15,=H'30'              GET OFFSET TO LITERAL
         LA    R15,CONDTBL(R15)        POINT TO LITERAL
         ST    R15,LITADR              SAVE LITERAL ADDRESS
CHKLENG  L     R15,LITADR              POINT TO LITERAL
         LR    R1,R14                  POINT TO CURRENT PRINT ADDR
         AH    R1,0(R15)               ADD THIS LITERAL LENGTH
         C     R1,=A(PRT+78)           PAST END OF PRINT LINE?
         BH    LINEFULL                YES
         LH    R1,0(R15)               NO, GET LITERAL LENGTH
         EX    R1,MOVLTRL              MOVE TO PRINT
         AH    R14,0(R15)              STEP PAST LITERAL
         LA    R14,1(R14)              ADD A SPACE
         B     CNDXIT                  AND EXIT
LINEFULL BAL   R9,PRINT                GO PRINT THE LINE
         LA    R14,PRT+15              POINT TO NEXT PRINT LOC
         B     CHKLENG                 CONTINUE
CNDXIT   L     R9,CND9                 GET RETURN ADDRESS
         BR    R9                      EXIT
MOVLTRL  MVC   0(1,R14),2(R15)         EXECUTED MOVE
***********************************************************************
*        PRINT A LINE                                                 *
***********************************************************************
PRINT    PUT   SYSPRINT,PRT            WRITE PRINT LINE
CLRPRT   MVC   PRT,BLANKS              CLEAR PRINT LINE
         BR    R9                      EXIT
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
DAPARM1  DS    0F                      * 1ST PARAMETER FOR DYNALLOC *
DDN      DC    CL8'INPUT1'             DDNAME
DSNAME   DC    CL44'&&DADCB'           DATASET NAME
MEMBER   DC    XL8'00'                 MEMBER NAME
         DC    XL3'00'                 DISPOSITION:
*                                    BYTE1: 0=OLD,M=MOD,N=NEW,S=SHR
*                                    BYTE2: U=UNCATLG,C=CATLG,D=DELETE
*                                    K=KEEP; BYTE3: SAME AS BYTE2
         DC    CL8'DISK'               UNIT NAME
         DC    XL2'00'                 UNIT COUNT
         DC    XL6'00'                 VOLUME SERIAL NUMBER
         DC    XL1'00'                 LABEL TYPE:
*                                      N=NL, S=SL, B=BLP
         DC    XL2'00'                 DATASET SEQUENCE NBR (TAPE)
         DC    XL1'00'                 F: FREE=CLOSE
         DC    XL2'00'                 RETENTION PERIOD IN DAYS
         DC    XL3'00'                 RECORDING FORMAT:
*                                      BYTE1: F=FIXED, V=VARIABLE,
*                                      U=UNDEFINED; BYTE2: B=BLOCKED,
*                                      BYTE3: A=ASA CONTROL CHARACTERS
         DC    XL2'00'                 LOGICAL RECORD LENGTH
         DC    XL2'00'                 BLOCK SIZE
         DC    XL1'00'                 TAPE DENSITY: 1=1600,6=6250
         DC    XL2'00'                 BUFNO
         DC    XL2'00'                 KEY LENGTH
         DC    XL1'00'                 DATASET ORGANIZATION:
*                                      V=VSAM, P=PDS, D=DIRECT, S=SEQ
         DC    CL1'C'                  SPACE TYPE:
*                                      C=CYL, T=TRK
         DC    XL2'01'                 PRIMARY SPACE AMOUNT
         DC    XL2'00'                 SECONDARY SPACE AMOUNT
         DC    CL1'00'                 R=RLSE
         DC    XL2'00'                 NBR DIRECTORY BLOCKS
         DC    XL5'00'                 EXPIRATION DATE YYDDD
*
*
*
DAPARM2  DS    0F                      2ND PARAMETER FOR DYNALLOC
         DC    CL1'A'                  REQUEST CODE:
*                                      A=ALLOCATE A DATASET
*                                      V=VOLSER FEEDBACK
RETRNCD  DC    CL2' '                  DYNALLOC RETURN CODE
ERRRET   DC    CL4' '                  ERROR RETURN CODE
INFORET  DC    CL4' '                  INFO RETURN CODE
*
SAV15    DC    F'0'                    SAVE AREA FOR R15 AFTER UCBSCAN
FZERO    DC    F'0'                    CONSTANT ZERO
H0       DC    H'0'                    CONSTANT ZERO
H3       DC    H'3'                    CONSTANT 3
H4       DC    H'4'                    CONSTANT 4
H7       DC    H'7'                    CONSTANT 7
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
LOWVAL   DC    XL8'00'                 CONSTANT BINARY ZEROS
*
SWITCHES DS    0XL5                    SWITCHES
CATSW    DS    XL1                      1=CATALOG ENTRY FOUND
VOLSW    DS    XL1                      1=VOLUME ALLOCATED O.K.
DSNSW    DS    XL1                      1=FORMAT 1 DSCB READ O.K.
DIRSW    DS    XL1                      1=DIRECTORY DATA WAS FOUND
MEMSW    DS    XL1                      1=DIRECTORY DATA WAS FOUND
*
FO9      DC    A(0)                    FORMOUT RETURN ADDRESS
MEMTBLA  DC    A(MEMTABLE)             MEMBER TABLE START ADDRESS
MEMTBNDA DC    A(MEMTBND)              MEMBER TABLE END ADDRESS
CURRMEM  DC    A(0)                    CURRENT MEMBER TABLE ADDRESS
MEMTABLE DC    10CL64' '               TABLE OF POSSIBLE MEMBER NAMES
MEMTBND  EQU   *                       END OF MEMBER TABLE
BLANKS   DC    CL80' '                 CONSTANT BLANKS
*
         DS    0D                      DOUBLEWORD ALIGN WORK100
WORK100  DC    XL100'00'               WORK AREA FOR CVTUCBSC
ADDRUCB  DC    A(0)                    UCB ADDR FROM IOSVSUCB
*
DIR9     DS    F                       RETURN ADDRESS FOR MEMBRNM
TTR9     DS    F                       RETURN ADDRESS FOR TTR
VOL9     DS    F                       R9 SAVE FOR VOLUME ROUTINE
DSN9     DS    F                       R9 SAVE FOR DSNAMEA ROUTINE
EFI9     DS    F                       R9 SAVE
XRD9     DS    F                       R9 SAVE FOR EXCPREAD ROUTINE
HE9      DS    F                       R9 SAVE
CND9     DS    F                       R9 SAVE FOR CONDPRNT ROUTINE
LITADR   DS    F                       R15 SAVE FOR CONDPRNT ROUTINE
FKEY9    DS    F                       R9 SAVE FOR FORMKEY ROUTINE
EXCP9    DS    F                       R9 SAVE FOR EXCP ROUTINE
CAT9     DS    F                       R9 SAVE FOR CATALOG ROUTINE
FORM9    DS    F                       SAVE AREA FOR REG 9
CURRECD  DS    A                       CURRENT DATA RECORD ADDRESS
AREAST   DC    A(RKEYDATA)             I/O AREA ADDRESS
AREAEND  DS    A                       END OF I/O AREA ADDRESS
SAVECUNT DS    XL8                     SAVE AREA FOR COUNT RECORD
         DS    0H
DIRENTY  DS    0XL80                   DIRECTORY ENTRY SAVE AREA
DIRENAME DS    CL8                     MEMBER NAME
DIRETTR  DS    XL3                     MEMBER TTR
DIRESIZE DS    XL1                     ALIAS, SIZES
         DS    XL68                    USER HALFWORDS
*
CONDTBL  DC    H'19',CL28'PERMANENT I/O ERROR,        '
         DC    H'16',CL28'EXTENT VIOLATION,           '
         DC    H'11',CL28'INTERCEPTED,                '
         DC    H'10',CL28'UNIT CHECK,                 '
         DC    H'14',CL28'UNIT EXCEPTION,             '
         DC    H'19',CL28'WRONG LENGTH RECORD,        '
         DC    H'21',CL28'CHANNEL PROGRAM CHECK,      '
         DC    H'16',CL28'PROTECTION CHECK,           '
         DC    H'18',CL28'CHANNEL DATA CHECK,         '
         DC    H'21',CL28'CHANNEL CONTROL CHECK,      '
         DC    H'23',CL28'INTERFACE CONTROL CHECK,    '
         DC    H'14',CL28'COMMAND REJECT,             '
         DC    H'15',CL28'EQUIPMENT CHECK,            '
         DC    H'10',CL28'DATA CHECK,                 '
         DC    H'07',CL28'OVERRUN,                    '
         DC    H'21',CL28'TRACK CONDITION CHECK,      '
         DC    H'10',CL28'SEEK CHECK,                 '
         DC    H'19',CL28'DATA CHECK IN COUNT,        '
         DC    H'19',CL28'WRITE TRACK OVERRUN,        '
         DC    H'22',CL28'END OF CYLINDER ON M-T,     '
         DC    H'25',CL28'INVALID COMMAND SEQUENCE,   '
         DC    H'15',CL28'NO RECORD FOUND,            '
         DC    H'14',CL28'FILE PROTECTED,             '
         DC    H'22',CL28'MISSING ADDRESS MARKER,     '
         DC    H'19',CL28'OVERFLOW INCOMPLETE,        '
         DS    0F
CLRHI    DC    X'0000FFFF'             TO CLEAR HI-ORDER BYTES OF REG
NUMTST   DC    CL8'00000000'           NUMERIC TEST AREA
ZEROS    DC    CL8'00000000'           CONSTANT ZEROS
XZROS    DC    XL8'00'                 HEX ZEROS
HEX      DS    XL80                    HEX RESULT FROM CHARHEX
CHERR    DC    X'00'                   ERROR INDICATOR FROM CHARHEX
TRCHAR   DC    256X'FF'                TRANSLATE TABLE FOR CHARHEX
         ORG   TRCHAR+X'81'            ORG TO LOWER CASE A
         DC    X'0A0B0C0D0E0F'         A-F = 0A-0F
         ORG   TRCHAR+X'C1'            ORG TO UPPER CASE A
         DC    X'0A0B0C0D0E0F'         A-F = 0A-0F
         ORG   TRCHAR+C'0'             ORG TO ZERO
         DC    X'00010203040506070809' 0-9 = 00-09
         ORG
WORK     DS    CL11                    WORK AREA
POS      DC    PL3'0'                  RECORD POSITION COUNTER
RECNO    DC    PL2'0'                  RECORD NUMBER IN BLOCK
NEWRECD  DC    X'00'                   00=NEW RECORD READ
FOUNDFMT DC    X'00'                   01=FORMAT FOUND
XTNTS    DC    X'00'                   NUMBER OF EXTENTS
EXTENTS  DS    0XL10                   CURRENT EXTENT
         DS    XL2
BGNXTNT  DS    XL4                     CCHHR EXTENT BEGIN
ENDXTNT  DS    XL4                     CCHHR EXTENT END
EXTADNL  DS    15XL10                  15 ADDITIONAL EXTENTS
SAVEXT   DS    CL150                   SAVE AREA FOR DATASET EXTENTS
SAVNBRX  DS    XL1                     NUMBER EXTENTS
*
VALIDPO  DC    X'00'                   VALID PDS OPEN
         DS    0H
POXTNTS  DS    0XL4                    EXTENT TABLE FOR PDS TTR USE
PORELTT  DS    XL2                       RELATIVE TTR AT EXTENT START
POACTTT  DS    XL2                       ACTUAL TTR AT EXTENT START
         DS    16XL4                     REMAINING 15 EXTENTS
GOTFF    DC    X'00'                   SET TO 1 AT DIRECTORY END
EXCPIND  DS    XL1                     EXCP INDICATOR: BLANK=O.K.,
*                                      E=END EXTENT, X=HARD ERROR,
*                                      F=EOF RECORD
         DS    0H
VTOCADR  DS    0CL5                    VTOC CCHHR START ADDRESS
VTOCCYL  DS    H                       VTOC CYLINDER
VTOCHD   DS    H                       VTOC HEAD
VTOCREC  DS    XL1                     VTOC RECORD NUMBER
ALLCYL   DC    H'0'                    BEGIN CYLINDER
         DC    H'0'                    BEGIN HEAD
         DC    H'32767'                END CYLINDER
         DC    H'32767'                END HEAD
         DC    H'32767'                TRACKS IN EXTENT
TRTBLA   DC    C'0123456789ABCDEF'     TRANSLATE TABLE
DBLWD    DS    D                       DOUBLEWORD WORK AREA
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
P50      DC    P'50'                   CONSTANT 50
RECLEN   DS    H                       RECORD LENGTH
CHTRTBL  DC    CL75' '                 CHARACTER TRANSLATE TABLE
         DC    CL1'.'
         DC    CL21' '
         DC    CL1'/'
         DC    CL9' '
         DC    CL1','
         DC    CL85' '
         DC    CL9'ABCDEFGHI'
         DC    CL7' '
         DC    CL9'JKLMNOPQR'
         DC    CL8' '
         DC    CL8'STUVWXYZ'
         DC    CL6' '
         DC    CL10'0123456789'
         DC    CL6' '
TRTBLZ   DC    C'0'                    TRANSLATE TABLE FOR ZONE LINE
         DC    XL15'0'
         DC    C'1'
         DC    XL15'0'
         DC    C'2'
         DC    XL15'0'
         DC    C'3'
         DC    XL15'0'
         DC    C'4'
         DC    XL15'0'
         DC    C'5'
         DC    XL15'0'
         DC    C'6'
         DC    XL15'0'
         DC    C'7'
         DC    XL15'0'
         DC    C'8'
         DC    XL15'0'
         DC    C'9'
         DC    XL15'0'
         DC    C'A'
         DC    XL15'0'
         DC    C'B'
         DC    XL15'0'
         DC    C'C'
         DC    XL15'0'
         DC    C'D'
         DC    XL15'0'
         DC    C'E'
         DC    XL15'0'
         DC    C'F'
         DC    XL15'0'
*
IOERR1   DS    0CL80                   I/O ERROR LINE 1
         DC    CL21'*** I/O ERROR: ECBCC='
IOECC    DS    CL2
         DC    CL6', CSW='
IOECSW1  DS    CL8
         DC    CL1' '
IOECSW2  DS    CL8
         DC    CL14', FAILING CCW='
IOECCW1  DS    CL8
         DC    CL1' '
IOECCW2  DS    CL8
         DC    CL3' '
*
IOERR2   DS    0CL80                  I/O ERROR LINE 2
         DC    CL21'               SENSE='
IOESNS   DS    CL4
         DC    CL19', COMMAND CHAIN IS '
IOECHNM  DS    CL8
         DC    CL17', SEARCH ADDRESS='
IOESRCH  DS    CL10
         DC    CL1' '
*
IOERR3   DS    0CL80                 I/O ERROR LINE 3
         DC    CL26'               BYTES READ='
IOEBYTS  DS    CL5
         DC    CL22', DATA AT CCW ADDRESS='
IOECD1   DS    CL8
         DC    CL1' '
IOECD2   DS    CL8
         DC    CL1' '
IOECD3   DS    CL8
         DC    CL1' '
DYNERR   DC    CL31'DYNALLOC ERROR, RETURN CODE IS '
DYNRTCD  DS    CL4
         DC    CL22', INFO RETURN CODE IS '
DYNINFO  DS    CL4
         DC    CL19' '
*
UNALOC   DC    CL80'UNABLE TO ALLOCATE WORK DATASET'
UNOPEN   DC    CL80'UNABLE TO OPEN WORK DATASET'
*
BYDSN    CAMLST NAME,DSNAME,,LOCAREA READ CATALOG LIST
LOCAREA  DS    CL265                   CATALOG INFORMATION AREA
*
PRT      DS    0CL80                   PRINT AREA
PDSNAME  DS    CL44
         DS    CL1
PVOLSER  DS    CL6
         DS    CL29
*
BEGINAD  DS    F                       CHANGE BEGIN ADDRESS
ENDAD    DS    F                       CHANGE END ADDRESS
LENGCODE DS    XL1                     CHANGE LENGTH CODE
***********************************************************************
*       COMMAND CHAINS FOR EXCP                                       *
***********************************************************************
CMDCHNR0 CCW   X'39',DASRCH,X'40',4     SEARCH HOME ADDRESS EQUAL
         CCW   X'08',*-8,0,0            TIC
         CCW   X'1E',RCOUNT,X'20',32767 READ CKD
*
CMDCHNR  CCW   X'31',DASRCH,X'40',5    SEARCH FOR PRECEDING RECORD
         CCW   8,*-8,0,0               TIC
         CCW   X'9E',RCOUNT,X'20',32767 READ CKD MULTI TRK
*
CMDCHNK  CCW   X'39',DASRCH,X'40',4     SEARCH HOME ADDRESS EQUAL
         CCW   X'08',CMDCHNK,0,0        TIC
KRDCT    CCW   X'92',RCOUNT,X'40',8     READ COUNT RECORD
KSCH     CCW   X'29',DSNAME,X'40',44    SEARCH KEY EQUAL
         CCW   8,KRDCT,0,0              TIC
         CCW   X'06',RKEYDATA,X'20',32767  READ DATA
*
CMDCHNW  CCW   X'31',DASRCH,X'40',5    SEARCH FOR CURRENT RECORD
         CCW   8,*-8,0,0               TIC
WRITCCW  CCW   X'05',RKEYDATA,0,32767  WRITE DATA (OR KEY+DATA)
***********************************************************************
*        PRINT AND CONTROL FILES                                      *
***********************************************************************
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         LTORG
***********************************************************************
*        EXCP FILE, VTOC RECORDS, INPUT DATA AREA                     *
***********************************************************************
         PRINT GEN
DAIOB    EXCPIOB CCW=CMDCHNR,DDNAME=INPUT1
         DSCBDEF FMT2=NO,FMT5=NO,FMT6=NO,DSECT=NO
         DS    0D
RCOUNT   DS    XL8                     THIS RECORD'S COUNT
RKEYDATA DS    CL32768                 THIS RECORD'S KEY AND DATA
         PRINT NOGEN
         CVT   DSECT=YES               GENERATE CVT DSECT
         IEFUCBOB ,                    GENERATE UCB DSECT
CHKVOLS  CSECT
         END

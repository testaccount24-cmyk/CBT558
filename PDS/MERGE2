         TITLE '*** MERGE2: MERGE JCLSCAN2 FILE INTO MERGE1 FILE ***'
***********************************************************************
* MERGE EXECUTION JCL INFORMATION FROM THE JCLSCAN2 PROGRAM OUTPUT    *
* FILE INTO THE PROGRAM INFORMATION FILE OUTPUT FROM PROGRAM MERGE1.  *
* BOTH FILES ARE SORTED ON LIBRARY DSNAME AND MEMBER NAME. A MATCH    *
* OCCURS WHEN BOTH RECORDS HAVE THE SAME DSNAME AND MEMBER NAME AND   *
* THE MERGE1 RECORD IS FOR THE ENTRY POINT TO THE MEMBER.             *
* THE OUTPUT FILE FROM MERGE2 IS RECFM=VB AS THE JCL INFORMATION IS   *
* ADDED AS AN "OCCURS DEPENDING ON AREA" AND MAY OCCUR 0-100 TIMES.   *
* SINCE THERE MAY BE MULTIPLE JCL RECORDS THAT APPLY TO A GIVEN       *
* PROGRAM AND POSSIBLY MULTIPLE PROGRAM RECORDS FOR THE SAME PROGRAM, *
* EACH JCLSCAN READ LOADS ALL THE RECORDS FOR THE NEXT DSNAME/MEMBER. *
* AUTHOR R THORNTON DEC 1994                                          *
***********************************************************************
*
MERGE2 SVLNK R3,R4
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT)        OPEN OUTPUT FILE
         OPEN  (JCLSCAN2,,MERGE1)      OPEN INPUT FILES
         BAS   R9,GETMG1               READ FIRST MERGE1 RECORD
JCLLUP   GET   JCLSCAN2,JCLREC         READ A JCLSCAN2 RECORD
         CLC   JCLSTLB,MG1DSNAM        SAME DSNAME?
         BL    JCLLUP                  NO, JCLSCAN2 RECORD LOW
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLI   MG1EPIND,C'*'           THIS AN ENTRY POINT CSECT?
         BNE   MG1LOW                  NO, JUST COPY MERGE1 TO OUTPUT
         CLC   STEPLIB,MG1DSNAM        SAME DSNAME?
         BL    JCLLOW                  NO, JCLSCAN2 RECORD LOW
         BH    MG1LOW                  NO, MERGE1 RECORD LOW
         CLC   PROGRAM,MG1MEMB         SAME MEMBER/PROGRAM?
         BL    JCLLOW                  NO, JCLSCAN2 RECORD LOW
         BH    MG1LOW                  NO, MERGE1 RECORD LOW
         MVC   MG2JCLCT,JCLENTS        SET NBR JCL ENTRIES IN RECORD
         CLC   MG2JCLCT,=H'0'          ANY JCL ENTRIES?
         BE    MG1LOW                  NO, JUST WRITE THE RECORD
         LA    R11,TBLEND-TBLNTRY      COMPUTE LENGTH
         MH    R11,JCLENTS                OF JCL AREA
         AH    R11,MG2LENG                   AND ADD IT
         STH   R11,MG2LENG                      TO TOTAL RECORD LENGTH
         L     R12,JCLTBLA             FORMAT JCL TO OUTPUT RECORD
         LH    R11,JCLENTS             GET NUMBER ENTRIES TO COPY
         LA    R10,MG2JCL              POINT TO RECORD JCL AREA
JCLLOOP  MVC   0(TBLEND-TBLNTRY,R10),0(R12) MOVE A JCL ENTRY
         LA    R10,TBLEND-TBLNTRY(R10) STEP TO NEXT ENTRY AREA
         LA    R12,TBLEND-TBLNTRY(R12) STEP TO NEXT JCL TABLE EN
         BCT   R11,JCLLOOP             LOOP TO MOVE ALL ENTRIES
MG1LOW   BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         BAS   R9,GETMG1               READ NEXT MERGE1 RECORD
         B     MAINLINE                CONTINUE
JCLLOW   BAS   R9,GETJCL               READ NEXT JCLSCAN2 RECORD
         B     MAINLINE                CONTINUE
***********************************************************************
*        READ MERGE1 RECORD                                           *
***********************************************************************
GETMG1   GET   MERGE1,MG1REC           READ MERGE1 RECORD
         CLC   MG1CSECT,=CL8'$PRIVATE' VALID CSECT NAME?
         BNH   GETMG1                  NO, SKIP IT
         LA    R1,MG2JCL-MG2REC        GET BASE LENGTH
         STH   R1,MG2LENG              SET RDW LENGTH
         XC    MG2JCLCT,MG2JCLCT       SHOW ZERO JCL ENTRIES
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        READ JCLSCAN2 RECORD                                         *
***********************************************************************
GETJCL   L     R12,JCLTBLA             POINT TO JCL TABLE
         USING JCLNTRY,R12
         LA    R11,0                   ENTRY COUNTER
         MVC   STEPLIB,JCLSTLB         SAVE STEPLIB DSNAME
         MVC   PROGRAM,JCLPGM          SAVE PROGRAM NAME
JCLENTMV MVC   TBLDSN,JCLDSN           PROCLIB DSNAME TO TABLE ENTRY
         MVC   TBLMEMB,JCLMEMB         PROCLIB MEMBER TO TABLE ENTRY
         MVC   TBLSTEP,JCLSTEP         JCL STEPNAME TO TABLE ENTRY
         MVC   TBLIMS,JCLIMS           IMS TYPE TO TABLE ENTRY
         LA    R11,1(R11)              INCREMENT ENTRY COUNTER
         LA    R12,TBLEND-TBLNTRY(R12) STEP TO NEXT TABLE ENTRY
         GET   JCLSCAN2,JCLREC         READ FIRST JCLSCAN2 RECORD
         CH    R11,=H'100'             MORE THAN 100 ENTRIES?
         BNL   JCLTHRU                 YES, QUIT
         CLC   STEPLIB,JCLSTLB         SAME STEPLIB DSNAME?
         BNE   JCLTHRU                 NO
         CLC   PROGRAM,JCLPGM          SAME PROGRAM NAME?
         BE    JCLENTMV                YES, GO STORE IT
JCLTHRU  STH   R11,JCLENTS             SAVE NBR JCL ENTRIES
         BR    R9                      RETURN TO CALLER
EOFJCL   CLI   MG1REC,X'FF'            EOF MERGE1 FILE?
         BE    EOJ                     YES
         MVC   JCLREC,HIVAL            NO, SET HEX FF'S IN JCLSCAN2
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PUTOUT   PUT   OUTPUT1,MG2REC          READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,JCLSCAN2,,MERGE1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
BLANKS   DC    CL107' '                BLANKS
HIVAL    DC    80X'FF'                 FIELD OF HEX FF'S
STEPLIB  DS    CL44                    CURRENT JCLSCAN2 STEPLIB DSNAME
PROGRAM  DS    CL8                     CURRENT JCLSCAN2 PROGRAM NAME
JCLTBLA  DC    A(JCLTBL)               ADDRESS OF JCL TABLE START
JCLENTS  DC    H'0'                    NUMBER JCL ENTRIES STORED
*
JCLREC   DS    0CL120                  JCLSCAN2 OUTPUT RECORD
JCLSTLB  DS    CL44                     STEPLIB NAME
         DS    CL1
JCLPGM   DS    CL8                      PROGRAM NAME
         DS    CL1
JCLDSN   DS    CL44                     PROCLIB DSNAME
         DS    CL1
JCLMEMB  DS    CL8                      PROCLIB MEMBER NAME
         DS    CL1
JCLSTEP  DS    CL8                      STEP NAME IN PROC
         DS    CL1
JCLIMS   DS    CL3                      IMS TYPE
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=VB,LRECL=6413
JCLSCAN2 DCB   DSORG=PS,MACRF=GM,DDNAME=JCLSCAN2,EODAD=EOFJCL,         X
               RECFM=FB,LRECL=120
MERGE1   DCB   DSORG=PS,MACRF=GM,DDNAME=MERGE1,EODAD=EOJ,              X
               RECFM=FB,LRECL=107
         LTORG
*
MG2REC   DS    0F                      MERGE2 OUTPUT RECORD
MG2RDW   DS    0F                       MERGE2 RDW
MG2LENG  DS    H                        MERGE2 RECORD LENGTH
MG2ZZ    DC    H'0'                     RDW ZEROES
MG1REC   DS    0CL107                  MERGE1 OUTPUT RECORD
MG1DSNAM DC    CL44' '                   LOAD LIBRARY DSNAME
MG1MEMB  DC    CL8' '                    LOAD LIBRARY MEMBER NAME
MG1CSECT DC    CL8' '                    CSECT NAME WITHIN MEMBER
MG1LANG  DC    CL10' '                   LANGUAGE TRANSLATOR NAME
MG1VVMM  DC    CL4' '                    VERSION AND MOD LEVEL
MG1EPIND DC    CL1' '                    * IF ENTRY POINT FOR MODULE
MG1SYSTM DC    CL8' '                  SYSTEM NAME
MG1SUBSY DC    CL8' '                  SUBSYSTEM NAME
MG1OWNR  DS    CL16                    SYSTEM OWNER NAME
MG2JCLCT DS    XL2                     NUMBER JCL ENTRIES IN RECORD
MG2JCL   DS    100CL(TBLEND-TBLNTRY)   JCL ENTRY AREA
*
JCLNTRY  DSECT ,                      JCL TABLE ENTRY DESCRIPTOR
TBLNTRY  DS    0C                       ENTRY START
TBLDSN   DS    CL44                     PROCLIB DSNAME
TBLMEMB  DS    CL8                      PROCLIB MEMBER NAME
TBLSTEP  DS    CL8                      STEP NAME IN PROC
TBLIMS   DS    CL3                      IMS TYPE
TBLEND   EQU   *                        JCL ENTRY TABLE LENGTH
MERGE2   CSECT
JCLTBL   DS    101CL(TBLEND-TBLNTRY) JCL TABLE AREA
         END

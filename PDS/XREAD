XREAD    CSECT
         STM   14,12,12(13)        SAVE CALLER'S REGS
         LR    3,15                COPY EPA ADDRESS
         USING XREAD,3             ESTABLISH ADDRESSABILITY
         LA    15,SAVE             POINT TO MY SAVE AREA
         ST    13,SAVE+4           SET LSA IN CALLER'S SAVE
         ST    15,4(13)            SET HSA IN MY SAVE AREA
         LR    13,15               POINT R13 TO MY SAVE AREA
         L     12,0(1)             POINT TO PARAMETER (RECORDIN)
         NOP   AROUND              1ST TIME IS A NOP
         MVI   *-3,C'0'            MAKE NOP A BRANCH
         OPEN  SYSIN               OPEN SYSIN DATASET
         B     AROUND              BRANCH AROUND SAVE AREA
SAVE     DS    18F                 REGISTER SAVE AREA
AROUND   GET   SYSIN,0(12)         READ RECORD INTO CALLER'S AREA
SETRET   SR    15,15               SET RETURN CODE TO ZERO
         ZAP   CODE,ZERO           SET CONDITION CODE
         L     13,4(13)            POINT TO CALLER'S SAVE AREA
         L     14,12(3)            RESTORE CALLER'S RETURN REG
         LM    0,12,20(13)         RESTORE CALLER'S REGS 0-12
         BR    14                  RETURN TO CALLER
EOF      ZAP   ZERO,NONZERO        CHANGE CONDITION CODE
         B     SETRET              GO RETURN
CODE     DC    P'0'                TO SET COND CODE
ZERO     DC    P'0'                TO SET COND CODE
NONZERO  DC    P'1'                TO SET COND CODE
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOF
         END

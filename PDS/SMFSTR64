         TITLE '***** STRIP TYPE 64 RECORDS FROM AN SMF FILE *****'
         PRINT NOGEN
SMFSTR64 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFSTR64,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFSTR64'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
* STRIPS ALL TYPE 64 RECORDS: VSAM COMPONENT OR CLUSTER STATUS       *
*                                                                    *
*  AUTHOR R. THORNTON JAN 1988                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
         USING SMFRECD,R6
MAINLINE GET   INPUT1,SMFRCD+4         GET INPUT AREA ADDRESS
         LA    R6,SMFRCD               POINT TO INPUT AREA
         MVC   SMF64LEN,INPUT1+82      MOVE LENGTH TO OUTPUT
         XC    SMF64SEG,SMF64SEG       CLEAR SEGMENT DESCRIPTOR
         CLI   SMF64RTY,64             TYPE 64 RECORD?
         BNE   MAINLINE                NO, SKIP IT
         CLC   SMF64DNM,=CL44'SUBSCRIB.DXH0040.BCACT'
         BE    WRITEREC
         CLC   SMF64DNM,=CL44'SUBSCRIB.DXH0095.MTTACT'
         BE    WRITEREC
         CLC   SMF64DNM,=CL44'MAJMED.DJC0140.ACTIVITY'
         BNE   MAINLINE
WRITEREC PUT   OUTPUT1,SMFRCD          WRITE OUTPUT RECORD
         B     MAINLINE                GO READ AGAIN
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
*
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
RETCODE  DC    F'0'                    RETURN CODE
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
*
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
*
*
*        SMF RECORD DSECT
*
SMFRECD  DSECT ,                       *** S M F D A T A R E C O R D S
SMF64LEN DS    H                       RECORD LENGTH
SMF64SEG DS    H                       SEGMENT DESCRIPTOR
SMF64FLG DS    XL1                     0=MVT, 2=MVS
SMF64RTY DS    XL1                     RECORD TYPE
SMF64TME DS    XL4                     TIME 1/100 SEC
SMF64DTE DS    PL4                     DATE 00YYDDD
SMF64SID DS    CL4                     SYSTEM ID
SMF64JBN DS    CL8                     JOB NAME
SMF64RST DS    XL4                     READER START TIME
SMF64RSD DS    PL4                     READER START DATE
SMF64UIF DS    CL8                     USER ID
SMF64RIN DS    XL1                     SITUATION INDICATOR (0=CLOSED,
*                                       1=VOL SWCH, 2=NO SPACE, 3=CRA,
*                                       4=CLOSED TYPE T)
SMF64DTY DS    XL1                     COMPONENT ID (0=DATA, 1=INDEX)
SMF64CNM DS    CL44                    DEFINING CATALOG NAME
SMF64DNM DS    CL44                    COMPONENT OR CLUSTER NAME
SMF64NTR DS    XL2                     TRACKS REQ, NOT ALLOC
SMF64CHR DS    XL4                     HIGHEST RBA
SMF64ESL DS    XL2                     LENGTH OF EXTENT ENTRY PORTION
*                                       EXCLUDING THIS FIELDECTION
*
*
*
SMF64XXX DSECT ,                       EXTENT ENTRY, ONE PER EXTENT,
*                                       FOLLOWS FIXED PORTION,
*                                       26 BYTES EACH
SMF64FCC DS    XL4                     BEGINNING CCHH
SMF64TCC DS    XL4                     ENDING CCHH
SMF64VSN DS    CL6                     VOLUME SERIAL
SMF64CUU DS    XL1                     CHANNEL ADDRESS
         DS    XL1                     UNIT ADDRESS
SMF64IND DS    XL2                     SPINDLE ID
SMF64UTY DS    XL4                     UNIT TYPE
SMF64RV1 DS    XL4                     RESERVED
*
*
*
SMF64SSS DSECT ,                       STATISTICS SECTION:
*                                       FOLLOWS LAST EXTENT ENTRY
*                                       114 BYTES (FIXED)
*                                      (1) ACCUMULATION SINCE CREATION
*                                          TO THIS OPEN
SMF64SLN DS    XL4                     LENGTH OF STATISTICS SECTION
SMF64NIL DS    XL4                     NBR INDEX LEVELS
SMF64NEX DS    XL4                     TOTAL EXTENTS
SMF64NLR DS    XL4                     TOTL RECORDS
SMF64NDE DS    XL4                     RECORDS DELETED
SMF64NIN DS    XL4                     RECORDS INSERTED
SMF64NUP DS    XL4                     RECORDS UPDATED
SMF64NRE DS    XL4                     RECORDS RETRIEVED
SMF64NFS DS    XL4                     UNUSED CI'S
SMF64NCS DS    XL4                     CI SPLITS
SMF64NAS DS    XL4                     CA SPLITS
SMF64NEP DS    XL4                     EXCP'S
*
*                                      (2) CHANGES FROM OPEN TO TIME
*                                          OF EOV AND CLOSE
SMF64DIL DS    XL4                     NBR INDEX LEVELS
SMF64DEX DS    XL4                     TOTAL EXTENTS
SMF64DLR DS    XL4                     TOTL RECORDS
SMF64DDE DS    XL4                     RECORDS DELETED
SMF64DIN DS    XL4                     RECORDS INSERTED
SMF64DUP DS    XL4                     RECORDS UPDATED
SMF64DRE DS    XL4                     RECORDS RETRIEVED
SMF64DFS DS    XL4                     UNUSED CI'S
SMF64DCS DS    XL4                     CI SPLITS
SMF64DAS DS    XL4                     CA SPLITS
SMF64DEP DS    XL4                     EXCP'S
*
*
*                                      (3) DATASET CHARACTERISTICS
*                                          SECTION
SMF64DBS DS    XL4                     PHYSICAL BLOCK SIZE
SMF64DCI DS    XL4                     CI SIZE
SMF64DLS DS    XL4                     MAX LRECL
SMF64DKL DS    XL2                     KEY LENGTH
SMF64DDN DS    CL8                     DDNAME
*
SMFSTR64 CSECT
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
SMFRCD   DS    CL32768                 SMF RECORD INPUT AREA
         END

         PRINT NOGEN
         TITLE '*****ASCBLIST: LIST ACTIVE ASVT ENTRIES *****'
ASCBLIST SVLNK R3
***********************************************************************
* RUNS THE ASVT TABLE AND PRINTS DATA FOR EACH ENTRY. IF THE A.S. IS  *
* ACTIVE, SHOWS ASID AND JOBNAME                                      *
*                                                                     *
* AUTHOR R THORNTON NOV 1990                                          *
***********************************************************************
         OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         PUT   SYSPRINT,HEADER         WRITE HEADER LINE
         SR    R11,R11                 CLEAR ASVT ENTRY COUNTER
         L     R12,16                  POINT TO CVT (NUC < 16M)
         L     R12,556(R12)            POINT TO ASVT (SQA < 16M)
         LA    R12,528(R12)            POINT TO 1ST ASVT ENTRY
CKUSE    TM    0(R12),X'80'            THIS ASID IN USE?
         BZ    ACTIVE                  YES
         CLC   0(4,R12),ASVTEND        END OF ASVT TABLE?
         BE    EOJ                     YES
STEPASVT LA    R12,4(R12)              TO NEXT ASVT ENTRY
         LA    R11,1(R11)              MAINTAIN ASVT ENTRY COUNTER
         B     CKUSE                   CONTINUE PROCESSING
ACTIVE   STH   R11,DBLWD               MAKE
         UNPK  PNBR(5),DBLWD(3)           ASVT
         TR    PNBR,TRCHAR-C'0'              ENTRY NUMBER
         MVI   PNBR+4,C' '                      PRINTABLE
         UNPK  PASCBA(9),0(5,R12)      MAME ASCB
         TR    PASCBA,TRCHAR-C'0'            ADDRESS
         MVI   PASCBA+8,C' '                    PRINTABLE
         L     R8,0(R12)               POINT TO ASCB
         ICM   R10,15,172(R8)          POINT
         BNZ   GOTNAM                     TO
         ICM   R10,15,176(R8)                JOBNAME
GOTNAM   MVC   PJOBNM,0(R10)           JOBNAME TO PRINT
         UNPK  PASID(5),36(3,R8)       MAKE
         TR    PASID,TRCHAR-C'0'          ASCBASID
         MVI   PASID+4,C' '                  PRINTABLE
         UNPK  PASXB(9),108(5,R8)      MAKE
         TR    PASXB,TRCHAR-C'0'          ASCBASXB
         MVI   PASXB+8,C' '                  PRINTABLE
         UNPK  PJSTCB(9),284(5,R8)      MAKE
         TR    PJSTCB,TRCHAR-C'0'          ASCBXTCB
         MVI   PJSTCB+8,C' '                  PRINTABLE
         BAL   R9,PRINT                PRINT MESSAGE
         B     STEPASVT                CONTINUE RUNNING ASVT TABLE
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE SYSPRINT                CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        ROUTINE TO PRINT A LINE                                      *
***********************************************************************
PRINT    PUT   SYSPRINT,PRT            PRINT A LINE
         MVC   PRT,BLANKS              CLEAR PRINT LINE
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
DBLWD    DS    D
NEXTTCB  DS    A                       ADDRESS OF NEXT TCB
ASVTEND  DC    X'80000000'             END OF ASVT
TRCHAR   DC    C'0123456789ABCDEF'     HEX NUMERICS
BLANKS   DC    CL80' '                 BLANKS
*
PRT      DS    0CL80                   PRINT LINE
PNBR     DS    CL4                     ASVT ENTRY NBR
         DC    CL1' '
PASCBA   DS    CL8                     ASCB ADDR
         DC    CL1' '
PJOBNM   DS    CL8                     JOB NAME
         DC    CL1' '
PASID    DS    CL4                     ASCBASID
         DC    CL1' '
PASXB    DS    CL8                     ASCBASXB
         DC    CL1' '
PJSTCB   DS    CL8                     ASCBXTCB
         DC    CL35' '
*
HEADER   DS    0CL80                   PRINT LINE
         DC    CL4'ENT#'               ASVT ENTRY NBR
         DC    CL1' '
         DC    CL8'ASCBADDR'           ASCB ADDR
         DC    CL1' '
         DC    CL8'JOBNAME'            JOB NAME
         DC    CL1' '
         DC    CL4'ASID'               ASCBASID
         DC    CL1' '
         DC    CL8'ASXBADDR'           ASCBASXB
         DC    CL1' '
         DC    CL8'JSTCBADR'           ASCBXTCB
         DC    CL35' '
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
         LTORG
         END

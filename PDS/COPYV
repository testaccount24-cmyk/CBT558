         TITLE '*** COPYV: COPY RECFM=V.. FILES     ***'
COPYV CSECT
*********************************************************************
* COPIES RECORDS FROM ONE VARIABLE FILE TO ANOTHER. RESPECIALIZE    *
* FOR EACH USE.                                                     *
*                                                                   *
* AUTHOR R THORNTON JAN 2002                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING COPYV,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'COPYV'              PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    OPEN  INPUT1                  OPEN INPUT FILE
         MVC   OUTPUT1+62(2),INPUT1+62 COPY INPUT BLOCKSIZE
         MVC   OUTPUT1+82(2),INPUT1+82 COPY INPUT LRECL
         MVC   OUTPUT1+36(1),INPUT1+36 COPY INPUT RECFM
         OPEN  (OUTPUT1,OUTPUT)        OPEN OUTPUT FILE
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,INDATA           READ INPUT RECORD
         MVC   OUTRDW,INPUT1+82        MOVE LRECL TO RDW
         AP    INRECS,P1               MAINTAIN RECORD COUNTER
         PUT   OUTPUT1,OUTREC          WRITE IT TO OUTPUT
         B     MAINLINE                NO, CONTINUE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
RETCODE  DC    F'0'                    PROGRAM RETURN CODE
INRECS   DC    PL5'0'                  INPUT RECORD COUNTER
P1       DC    P'1'                    CONSTANT 1
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
         LTORG
COPYV CSECT
OUTREC   DS    0F              OUTPUT RECORD AREA
OUTRDW   DS    H
         DC    H'00'
INDATA   DS    CL32768
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

        TITLE '*** MERGE1A: MERGE ENDVMTCH FILE INTO COBLTYPE FILE ***'
***********************************************************************
* MERGE ENDEVOR SYSTEM AND SUBSYSTEM INFORMATION AS WELL AS SYSTEM    *
* OWNER INTO THE PROGRAM INFORMATION FILE FROM PROGRAM COBLTYPE.      *
* THE ENDVMTCH FILE IS SORTED ON ENDEVOR ELEMENT NAME AND THE COBLTYPE*
* FILE ON MEMBER NAME BEFORE PROCESSING BY MERGE1A.                   *
* A MATCH OCCURS WHEN THE MERGE1A RECORD IS FOR THE MODULE ENTRY POINT*
* AND MEMBER NAME IS EQUAL TO THE ENDEVOR ELEMENT NAME.               *
*   THE COBLTYPE FILE IS THE SORTED COBLTYPE OUTPUT FILE              *
*   THE ENDVMTCH FILE IS THE SORTED ENDVMTCH FILE                     *
* AUTHOR R THORNTON DEC 1994                                          *
***********************************************************************
*
MERGE1A SVLNK R3,R4
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT) OPEN OUTPUT FILES
         OPEN  (ENDVMTCH,,COBLTYPE)    OPEN INPUT FILES
         BAS   R9,GETEND               READ FIRST ENDVMTCH RECORD
         BAS   R9,GETTYP               READ FIRST COBLTYPE RECORD
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLC   ENDELEM,TYPMEMB         ELEMENT = MEMBER NAME?
         BL    ENDLOW                  NO, ENDVMTCH RECORD LOW
         BH    TYPLOW                  NO, COBLTYPE RECORD LOW
         MVI   MATCH,1                 SHOW MATCH FOUND
         MVC   MG1DSNAM,TYPDSNAM       LOAD LIBRARY DSNAME
         MVC   MG1MEMB,TYPMEMB         LOAD LIBRARY MEMBER NAME
         MVC   MG1CSECT,TYPCSECT       CSECT NAME WITHIN MEMBER
         MVC   MG1LANG,TYPLANG         LANGUAGE TRANSLATOR NAME
         MVC   MG1VVMM,TYPVVMM         VERSION AND MOD LEVEL
         MVC   MG1EPIND,TYPEPIND       * IF ENTRY POINT FOR MODULE
         MVC   MG1SYSTM,ENDSYSTM       ENDEVOR SYSTEM TO OUTPUT
         MVC   MG1SUBSY,ENDSUBSY       ENDEVOR SUBSYSTEM TO OUTPUT
         MVC   MG1OWNR,ENDOWNR         SYSTEM OWNER TO OUTPUT
         BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         BAS   R9,GETTYP               READ NEXT COBLTYPE RECORD
         B     MAINLINE                CONTINUE
TYPLOW   MVC   PRTPGM,=CL8'COBLTYPE'   MISMATCH IN COBLTYPE FILE
         MVC   PRTCSECT,TYPCSECT       SHOW CSECT
         MVC   PRTMEMB,TYPMEMB         AND MEMBER
         MVC   PRTDSNAM,TYPDSNAM       AND LIBRARY DSNAME
         BAS   R9,PRINT                PRINT THE ERROR LINE
         MVC   MG1DSNAM,TYPDSNAM       LOAD LIBRARY DSNAME
         MVC   MG1MEMB,TYPMEMB         LOAD LIBRARY MEMBER NAME
         MVC   MG1CSECT,TYPCSECT       CSECT NAME WITHIN MEMBER
         MVC   MG1LANG,TYPLANG         LANGUAGE TRANSLATOR NAME
         MVC   MG1VVMM,TYPVVMM         VERSION AND MOD LEVEL
         MVC   MG1EPIND,TYPEPIND       * IF ENTRY POINT FOR MODULE
         MVC   MG1SYSTM,BLANKS         ENDEVOR SYSTEM TO OUTPUT
         MVC   MG1SUBSY,BLANKS         ENDEVOR SUBSYSTEM TO OUTPUT
         MVC   MG1OWNR,BLANKS          SYSTEM OWNER TO OUTPUT
         BAS   R9,PUTOUT               WRITE OUTPUT RECORD
         BAS   R9,GETTYP               READ NEXT COBLTYPE RECORD
         B     MAINLINE                CONTINUE
ENDLOW   CLI   MATCH,1                 WAS A MATCH FOUND?
         BE    READMTC                 YES
         MVC   PRTPGM,=CL8'ENDVMTCH'   MISMATCH IN ENDVMTCH FILE
         MVC   PRTCSECT,ENDELEM        SHOW ELEMENT NAME
         MVC   PRTMEMB,BLANKS          CLEAR MEMBER
         MVC   PRTDSNAM,BLANKS         AND LIBRARY DSNAME
         BAS   R9,PRINT                PRINT THE ERROR LINE
READMTC  BAS   R9,GETEND               READ NEXT ENDVMTCH RECORD
         MVI   MATCH,0                 RESET MATCH INDICATOR
         B     MAINLINE                CONTINUE
***********************************************************************
*        READ COBLTYPE RECORD                                         *
***********************************************************************
GETTYP   GET   COBLTYPE,TYPREC         READ COBLTYPE RECORD
         BR    R9                      RETURN TO CALLER
EOFTYP   CLI   ENDREC,X'FF'            EOF ENDVMTCH FILE?
         BE    EOJ                     YES
         MVC   TYPREC,HIVAL            NO, SET HEX FF'S IN TYPREC
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        READ ENDVMTCH RECORD                                         *
***********************************************************************
GETEND   GET   ENDVMTCH,ENDREC         READ ENDVMTCH RECORD
         BR    R9                      RETURN TO CALLER
EOFEND   CLI   TYPREC,X'FF'            EOF COBLTYPE FILE?
         BE    EOJ                     YES
         MVC   ENDREC,HIVAL            NO, SET HEX FF'S IN ENDVMTCH
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
PUTOUT   PUT   OUTPUT1,MG1REC          READ NEXT RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             PRINT SUBROUTINE                                        *
***********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         BR    R9                      RETURN
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,PRINT1,,ENDVMTCH,,COBLTYPE) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
MATCH    DC    XL1'0'                  MATCH FOUND
BLANKS   DC    CL107' '                BLANKS
HIVAL    DC    80X'FF'                 FIELD OF HEX FF'S
*
PRTLINE  DS    0CL133                  PRINT LINE
         DC    CL11' UNMATCHED '
PRTPGM   DC    CL8' '
         DC    CL15' RECORD: CSECT='
PRTCSECT DC    CL8' '
         DC    CL9', MEMBER='
PRTMEMB  DC    CL8' '
         DC    CL9', DSNAME='
PRTDSNAM DC    CL44' '
         DC    CL21' '
*
MG1REC   DS    0CL107                  MERGE1A OUTPUT RECORD
MG1DSNAM DC    CL44' '                   LOAD LIBRARY DSNAME
MG1MEMB  DC    CL8' '                    LOAD LIBRARY MEMBER NAME
MG1CSECT DC    CL8' '                    CSECT NAME WITHIN MEMBER
MG1LANG  DC    CL10' '                   LANGUAGE TRANSLATOR NAME
MG1VVMM  DC    CL4' '                    VERSION AND MOD LEVEL
MG1EPIND DC    CL1' '                    * IF ENTRY POINT FOR MODULE
MG1SYSTM DC    CL8' '                  SYSTEM NAME
MG1SUBSY DC    CL8' '                  SUBSYSTEM NAME
MG1OWNR  DS    CL16                    SYSTEM OWNER NAME
*
TYPREC   DS    0CL80                   COBLTYPE OUTPUT RECORD
TYPDSNAM DC    CL44' '                   LOAD LIBRARY DSNAME
TYPMEMB  DC    CL8' '                    LOAD LIBRARY MEMBER NAME
TYPCSECT DC    CL8' '                    CSECT NAME WITHIN MEMBER
TYPLANG  DC    CL10' '                   LANGUAGE TRANSLATOR NAME
TYPVVMM  DC    CL4' '                    VERSION AND MOD LEVEL
TYPEPIND DC    CL1' '                    COMPILE DATE
         DC    CL5' '
*
ENDREC   DS    0CL80                   OUTPUT RECORD
ENDELEM  DS    CL8                     ELEMENT NAME
         DC    CL1' '
ENDTYPE  DC    CL8' '                  ELEMENT TYPE
         DC    CL1' '
ENDENV   DC    CL8' '                  ENVIRONMENT NAME
         DC    CL1' '
ENDSYSTM DC    CL8' '                  SYSTEM NAME
         DC    CL1' '
ENDSUBSY DC    CL8' '                  SUBSYSTEM NAME
         DC    CL1' '
ENDOWNR  DS    CL16                    SYSTEM OWNER NAME
         DC    CL19' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=107
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
ENDVMTCH DCB   DSORG=PS,MACRF=GM,DDNAME=ENDVMTCH,EODAD=EOFEND,         X
               RECFM=FB,LRECL=80
COBLTYPE DCB   DSORG=PS,MACRF=GM,DDNAME=COBLTYPE,EODAD=EOFTYP,         X
               RECFM=FB,LRECL=80
         LTORG
MERGE1A  CSECT
         END

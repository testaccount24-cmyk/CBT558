         TITLE '   T G E T    I N T E R F A C E'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                   *
*    TGET - OBTAINS A MESSAGE FROM THE TERMINAL.                    *
*     INVOCATION:  CALL TGET(VSTR,RC);                              *
*       WHERE  VSTR IS A VARYING CHARACTER STRING USED TO RECEIVE   *
*                   THE INPUT MESSAGE.                              *
*       AND    RC   IS A HALFWORD BINARY VARIABLE.  IT WILL CONTAIN *
*                   THE TGET RETURN CODE.                           *
*     ENTRY DECLARATION:                                            *
*       DCL  TGET ENTRY(CHAR(X) VAR, FIXED BIN(15));                *
*       THE MAXIMUM LENGTH OF VSTR IS USED AS THE TGET BUFFER       *
*           LENGTH:  AFTER THE EXECUTION OF TGET, LENGTH(VSTR) =    *
*           NUMBER OF CHARACTERS READ.                              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                   *
*        R  E  G  I  S  T  E  R  S                                  *
*                                                                   *
R0       EQU   0
R1       EQU   1
RW2      EQU   6
RW3      EQU   7
RSDV     EQU   8
RPARM    EQU   9
RBSE     EQU   10
RW1      EQU   11
R13      EQU   13
R15      EQU   15
TGET     CSECT
         SAVE (14,12),,*
         LR    RBSE,R15
         USING TGET,RBSE
         ST    R13,SA+4            SAVE REG 13
         LR    RW1,R13
         LA    R13,SA
         ST    R13,8(RW1)
         LR    RPARM,R1
         L     RSDV,0(RPARM)
         USING DSDV,RSDV
         SR    R0,R0               CLEAR OUT REG ZERO
         LH    R0,SDVMSL
         LA    RW2,128             FLAGS - TGET, WAIT, EDIT
         L     RW3,SDVADDR         ADDR OF STRING (OR LENGTH FIELD)
TG1A     SLL   RW3,8               DROP HIGH ORDER BYTE
         SLDL  RW2,24              FLAGS AND ADDR INTO ONE REG
         LR    R1,RW2
         TGET  (1),(0),R
TG1B     STH   R1,SDVCSL              SET CURRENT STRING LENGTH IN SDV
TG1C     L     RW1,4(RPARM)
         STH   R15,0(RW1)          SET RETURN CODE
         B     RET                 GET OUT
RET      L     R13,4(R13)
         RETURN (2,12)
DPARM    DSECT
PL1      DS    F
PL2      DS    F
PL3      DS    F
DSDV     DSECT
SDVADDR  DS    F
SDVMSL   DS    H
SDVCSL   DS    H
TGET     CSECT
SA       DS    18F                 SAVE AREA
HWHLD    DS    H
         END

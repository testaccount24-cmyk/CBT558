         PRINT NOGEN
**********************************************************************
* READS MEMBERS FROM A PDS AND WRITES THEM TO A PHYSICALLY SEQUENTIAL*
* OUTPUT DATASET WITH A ./ ADD CARD SEPARATING EACH MEMBER.          *
*                                                                    *
* THE OUTPUT DATASET WILL BE WRITTEN AS RECFM=FB,LRECL=80, AS THAT IS*
* REQUIRED BY IEBUPDTE. INPUT RECORDS WILL BE TRUNCATED TO 80 BYTES  *
* AND SHOULD NEVER EXCEED 1688 BYTES, AS THAT IS THE AREA SIZE FOR   *
* INPUT RECORDS.                                                     *
*                                                                    *
* AUTHOR R THORNTON AUG 1998                                         *
**********************************************************************
PDSCPYMB SVLNK R3
         OPEN  (SYSOUT,OUTPUT)         OPEN PUNCH
         OPEN  (SYSPRINT,OUTPUT)       OPEN SYSPRINT
         OPEN  SYSIN                   YES, OPEN CARD FILE
         CALL  READMEM,OPEN            OPEN MEMBER FILE
         MVC   LRECL,82(R1)            SAVE LRECL (R1 = DCB ADDR)
         MVC   RECFM,36(R1)            SAVE RECFM
NEWMEM   GET   SYSIN,CARD              READ NEXT MEMBER NAME
         MVC   FINDNAM,CARD            NAME TO FIND LIST
         CALL  READMEM,FIND            LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BNE   PUNHDR                  YES
         MVC   NOTNAM,FINDNAM          NAME TO MESSAGE
         MVC   OUTREC,NOTFND           MESSAGE TO USER
         PUT   SYSPRINT,OUTCC          WRITE PRINT LINE
         B     NEWMEM                  TRY NEXT MEMBER
PUNHDR   MVC   ADDNAME,FINDNAM         MEMBER NAME TO ./ ADD
         PUT   SYSOUT,ADDREC           NO, WRITE VARIABLE ADD REC
READMEM  CALL  READMEM,(READ,MEMREC)   READ NEXT RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         TM    RECFM,X'40'             INPUT1 RECORDS ARE VARIABLE?
         BO    VRBLRECS                YES
         PUT   SYSOUT,MEMREC           WRITE THE RECORD TO OUTPUT
         B     READMEM                 GO READ AGAIN
VRBLRECS CLC   MEMREC(2),=H'84'        INPUT1 RECORD OVER 80 BYTES?
         BH    OVER80                  YES
         MVC   OUTREC,BLANKS           CLEAR IT TO BLANKS
         LH    R1,MEMREC               NO, GET RECORD LENGTH
         SH    R1,=H'5'                SUBTRACT 5 FOR RDW, LENG CODE
         EX    R1,VRMOVE               MOVE IT TO OUTPUT
         PUT   SYSOUT,OUTREC           WRITE TO OUTPUT
         B     READMEM                 GO READ AGAIN
OVER80   PUT   SYSOUT,MEMREC+4         WRITE 1ST 80 BYTES
         CLC   LNGNAME,FINDNAM         ALREADY WROTE MSG FOR MEMBER?
         BE    READMEM                 YES, GO READ AGAIN
         MVC   LNGNAME,FINDNAM         NAME TO MESSAGE LINE
         PUT   SYSPRINT,LONGMSG        WRITE MESSAGE
         B     READMEM                 GO READ AGAIN
VRMOVE   MVC   OUTREC(1),MEMREC+4      EXECUTED MOVE
******************************************************************
* END OF JOB. CLOSE FILES AND TERMINATE                          *
******************************************************************
EOJ      CLOSE (SYSIN,,SYSOUT,,SYSPRINT) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
******************************************************************
*                      *** STORAGE AND FILES ***                 *
******************************************************************
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
LONGMSG  DC    CL1' '                  ERROR MESSAGE
LNGNAME  DC    CL8' '                  MEMBER NAME
         DC    CL72' CONTAINS LINES TRUNCATED TO 80 BYTES.'
NOTFND   DS    0CL80
         DC    CL7'MEMBER'
NOTNAM   DS    CL8
         DC    CL65' '
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
         DS    0F
MEMREC   DS    CL1688                  MEMBER RECORD
LRECL    DS    H                       LRECL FROM INPUT1 DCB
RECFM    DS    XL1                     RECFM FROM INPUT1 DCB
BLANKS   DC    CL80' '                 BLANKS
*
OUTCC    DS    CL1                     CONTROL CHAR FOR PRINTING
OUTREC   DC    CL80' '                 OUTPUT RECORD
*
CARD     DC    CL80' '                 CARD WITH MEMBER NAMES
         DC    C' '                    SCAN STOPPER
EOF      EQU   X'FF'
*
ADDREC   DC    CL80' '
         ORG   ADDREC
         DC    C'./ ADD NAME='
ADDNAME  DC    CL8' '
         ORG
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,LRECL=81,BLKSIZE=81
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,RECFM=FB,LRECL=80
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,                         X
               RECFM=FB,LRECL=80,EODAD=EOJ
         END

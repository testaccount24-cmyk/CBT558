         PRINT NOGEN
         TITLE '*****EXAMPLE OF 31-BIT ADDRESS MODE USE****'
A31BIT CSECT
A31BIT RMODE 24
A31BIT AMODE 24
***********************************************************************
* DOES A GETMAIN FOR STORAGE ABOVE THE 16M LINE, LOADS IT WITH DATA   *
* FROM A FILE USING 31-BIT MODE TO ACCESS THE STORAGE AND REVERTING   *
* TO 24-BIT MODE FOR OTHER OPERATIONS.                                *
* USEFUL AS AN EXAMPLE OF STORAGE USE ABOVE 16M.                      *
* AUTHOR R THORNTON NOV 1991                                          *
***********************************************************************
         STM   R14,R12,12(R13)         SAVE CALLERS REGS
         LR    R3,R15                  COPY BASE REG
         USING A31BIT,R3
         LA    R15,SAVE                POINT TO SAVE
         ST    R15,8(R13)              SET LSA
         ST    R13,SAVE+4              SET FSA
         LR    R13,R15                 SET UP SAVE POINTER
         B     START                   AROUND SAVE AREA
SAVE     DS    18F                     SAVE AREA
START    LA    R1,OPEN                 INSURE PSW
         N     R1,HIBITOF                 IS IN 24-BIT
         BSM   0,R1                          ADDRESSING MODE
OPEN     OPEN  INPUT1                  OPEN FILE IN 24-BIT MODE
         GETMAIN RU,LV=524288,LOC=ANY  GET STRG ABOVE 16M
         ST    R1,STRGADDR             SAVE GOTTEN STORAGE ADDRESS
         LR    R12,R1                  POINT TO TABLE
         USING UTENTRY,R12
*
MAINLINE LA    R1,GET                  INSURE PSW
         N     R1,HIBITOF                 IS IN 24-BIT
         BSM   0,R1                          ADDRESSING MODE
GET      GET   INPUT1,INRECD           READ NEXT RECORD IN 24-BIT MODE
         LA    R1,LOADTBL              PUT PSW
         O     R1,HIBITON                 INTO 31-BIT
         BSM   0,R1                          ADDRESSING MODE
LOADTBL  MVC   UTENTRY(UTBLEND-UTENTRY),INRECD MOVE RECORD TO TABLE
         LA    R12,UTBLEND-UTENTRY(R12) ADVANCE TO NEXT ENTRY
         B     MAINLINE                LOOP TO LOAD TABLE
*
EOF      ST    R12,TBLEND              SAVE TABLE END ADDRESS
         LA    R1,SETEND               PUT PSW
         O     R1,HIBITON                 INTO 31-BIT
         BSM   0,R1                          ADDRESSING MODE
SETEND   MVC   UTUSER,HIVALS           SET USER ID HIGH AT END
         DROP  R12
         LA    R1,CLOSE                INSURE PSW
         N     R1,HIBITOF                 IS IN 24-BIT
         BSM   0,R1                          ADDRESSING MODE
CLOSE    CLOSE INPUT1                  CLOSE FILE IN 24-BIT MODE
         L     R1,STRGADDR             POINT TO GOTTEN STORAGE
         FREEMAIN RU,LV=524288,A=(1)   FREE THE GOTTEN STORAGE
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
STRGADDR DC    A(0)                    ADDRESS OF TABLE STORAGE
TBLEND   DC    A(0)                    ADDRESS OF GOTTEN STORAGE
HIBITON  DC    X'80000000'             TO TURN ON 31-BIT ADDRESSING
HIBITOF  DC    X'7FFFFFFF'             TO TURN OFF 31-BIT ADDRESSING
HIVALS   DC    8X'FF'                  HIGH VALUES
INRECD   DS    CL36                    USER TABLE RECORD
INPUT1   DCB   DSORG=PS,DDNAME=INPUT1,MACRF=GM,EODAD=EOF,              X
               RECFM=FB,LRECL=36,BLKSIZE=36
DCBEND   EQU   *
         REQU
*
UTENTRY  DSECT ,                       DEFINE USER TABLE ENTRY
UTUSER   DS    CL8                     USER ID
UTBITS   DS    CL28                    BIT STRING FOR TRANSACTION AUTH
UTBLEND  EQU   *
A31BIT   CSECT
         END

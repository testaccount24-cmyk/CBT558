         TITLE '***** PRINT MEMBER, CSECT NAMES W/COBOL VERSION *****'
COBLTYP2 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  ESTABLISH BASE REGISTER
         USING COBLTYP2,R3
         LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R13,4(R15)              SAVE BACK POINTER (HSA)
         ST    R15,8(R13)              SAVE FORWARD POINTER (LSA)
         LR    R13,R15                 SET UP SAVE REGISTER
         B     START                   CONTINUE
         DC    CL8'COBLTYP2'           EYECATCHER: PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
*
***********************************************************************
* READS THE FILE PRODUCED BY PROGRAM COBLTYPE AFTER IT IS SORTED IN   *
* MEMBER NAME, TRANSLATOR NAME, VERSION AND MOD LEVEL, AND COMPILED   *
* DATE ORDER. FILES USED:                                             *
*     DDNAME INPUT1: RECFM=FB,LRECL=80 SORTED COBLTYPE FILE           *
*     DDNAME OUTPUT1: RECFM=FB,LRECL=80  STRIPPED OUTPUT FILE         *
* OUTPUT FILE IS STRIPPED OF ILB..., IGZ... CSECTS                    *
* AUTHOR R THORNTON - MAY 1994                                        *
*                                                                     *
***********************************************************************
* OPEN FILES                                                   *
****************************************************************
*
START    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
*
****************************************************************
* MAINLINE PROCESSING FOR THE LOAD MODULE.                     *
****************************************************************
*
MAINLINE GET   INPUT1,INREC            GET COBLTYPE RECORD
         AP    INRECS,P1               INPUT RECORD COUNT
         CLC   ISYM(3),ILBMOD          IS IT AN ILB MODULE?
         BE    DROPPED                 YES, DROP IT
         CLC   ISYM(3),IGZMOD          IS IT AN IGZ MODULE?
         BNE   OUTPUT                  NO, WRITE IT
DROPPED  AP    DROPRECS,P1             DROPPED RECORD COUNT
         B     MAINLINE                YES, GO TO NEXT MEMBER
OUTPUT   PUT   OUTPUT1,INREC           WRITE RECORD TO OUTPUT
         AP    OUTRECS,P1              OUTPUT RECORD COUNT
         B     MAINLINE                YES, GO TO NEXT MEMBER
****************************************************************
* END OF JOB.                                                  *
****************************************************************
EOJ      MVI   EOJSW,X'FF'             SET EOF/EOJ SWITCH
         CLOSE (INPUT1,,OUTPUT1) CLOSE FILES
         L     R13,4(R13)              GET POINTER TO CALLER'S SAVE ARE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
****************************************************************
*                     DATA AND WORK AREAS
****************************************************************
P1       DC    P'1'                    CONSTANT 1
INRECS   DC    PL5'0'                  INPUT RECORD COUNT
DROPRECS DC    PL5'0'                  DROPPED RECORD COUNT
OUTRECS  DC    PL5'0'                  OUTPUT RECORD COUNT
EOJSW    DC    X'00'                   SET TO FF AT EOJ
ILBMOD   DC    CL3'ILB'
IGZMOD   DC    CL3'IGZ'
*
INREC    DS    0CL80                   RECORD FROM COBLTYPE
IMEM     DC    CL8' '
         DC    CL1' '
ISYM     DC    CL8' '
         DC    CL1' '
ITRAN    DC    CL10' '
         DC    CL1' '
IVERML   DC    CL4' '
         DC    CL1' '
IPCPLDT  DC    CL6' '
         DC    CL1' '
         DC    CL65' '
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
OUTPUT1 DCB    DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FBA,LRECL=80
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

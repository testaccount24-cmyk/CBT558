         TITLE '*** CALLPDSR: TEST THE PDSREAD SUBROUTINE ***'
CALLPDSR CSECT
*********************************************************************
* CALLS THE PDSREAD SUBROUTINE TO TEST ITS FUNCTION.                *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CALLPDSR,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'CALLPDSR'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT)         OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE CALL  PDSREAD,(DDNAME,DATAADDR) CALL PDSREAD SUBROUTINE
         L     R5,DATAADDR               POINT TO RETURNED DATA AREA
         USING RETHDR,R5
         MVC   PRTNBR,EDITWORD           MOVE EDIT WORD
         ED    PRTNBR,DIRBLKS            EDIT NUMERIC DATA
         MVC   PRTDESC(L'MSG1),MSG1      MOVE IN COUNT DESCRIPTOR
         BAS   R9,PRINT                  GO PRINT THE LINE
         MVC   PRTNBR,EDITWORD           MOVE EDIT WORD
         ED    PRTNBR,USDBLKS            EDIT NUMERIC DATA
         MVC   PRTDESC(L'MSG2),MSG2      MOVE IN COUNT DESCRIPTOR
         BAS   R9,PRINT                  GO PRINT THE LINE
         MVC   PRTNBR,EDITWORD           MOVE EDIT WORD
         ED    PRTNBR,ACTVMEM            EDIT NUMERIC DATA
         MVC   PRTDESC(L'MSG3),MSG3      MOVE IN COUNT DESCRIPTOR
         BAS   R9,PRINT                  GO PRINT THE LINE
         MVC   PRTNBR,EDITWORD           MOVE EDIT WORD
         ED    PRTNBR,DELEMEM            EDIT NUMERIC DATA
         MVC   PRTDESC(L'MSG4),MSG4      MOVE IN COUNT DESCRIPTOR
         BAS   R9,PRINT                  GO PRINT THE LINE
         MVC   PRTNBR,EDITWORD           MOVE EDIT WORD
         ED    PRTNBR,TABLMEM            EDIT NUMERIC DATA
         MVC   PRTDESC(L'MSG5),MSG5      MOVE IN COUNT DESCRIPTOR
         BAS   R9,PRINT                  GO PRINT THE LINE
         ZAP   TABLMEM,TABLMEM           ANY TABLE ENTRIES?
         BZ    FREEAREA                  NO, SKIP PRINTING
         LA    R6,MBRNTRY                POINT TO START OF TABLE
         USING MBRNTRY,R6
PRTNEXT  CLI   MBRNAME,X'FF'             END OF TABLE?
         BE    FREEAREA                  YES, ALL THROUGH
         MVC   PRTNAME,MBRNAME           MEMBER NAME TO PRINT
         UNPK  PRTTTR,MBRTTR(4)          UNPACK TTR FOR PRINTING
         TR    PRTTTR,TRCHAR-C'0'        MAKE PRINTABLE
         MVI   PRTTTR+6,C' '             CLEAR TRASH
         CLI   MBRALIAS,C'A'             IS IT AN ALIAS?
         BNE   CKDELE                    NO
         MVC   PRTIND,ALIAS              SHOW ALIAS
         B     PRTNTRY                   GO PRINT
CKDELE   CLI   MBRALIAS,C'D'             IS IT DELETED?
         BNE   PRTNTRY                   NO
         MVC   PRTIND,DELETED            SHOW DELETED
PRTNTRY  BAS   R9,PRINT                  PRINT THE ENTRY
         LA    R6,MBREND-MBRNTRY(R6)     STEP TO NEXT ENTRY
         B     PRTNEXT                   LOOP TO PRINT TABLE
FREEAREA LM    R11,R12,AREASTRT          GET AREA ADDRESS AND LENGTH
         STORAGE RELEASE,LENGTH=(R12),ADDR=(R11) FREE PDSREAD DATA
         B     EOJ                       EXIT PROGRAM
*
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      EXIT
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE PRINT1                  CLOSE FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
DATAADDR DC    A(0)                    ADDRESS OF PDSREAD DATA AREA
*
BLANKS   DC    CL133' '                BLANKS
DDNAME   DC    CL8'LIBRARY'  DDNAME FOR THE PDS TO BE ANALYZED
ALIAS    DC    CL7'ALIAS'                LITERAL
DELETED  DC    CL7'DELETED'              LITERAL
TRCHAR   DC    C'0123456789ABCDEF'     TRANSLATE TABLE (MAKE PRINTABLE)
EDITWORD DC    X'40206B2020206B202120' EDIT WORD FOR COUNTS
MSG1     DC    C'NUMBER DIRECTORY BLOCKS TOTAL'
MSG2     DC    C'NUMBER DIRECTORY BLOCKS USED'
MSG3     DC    C'NUMBER ACTIVE MEMBERS IN PDS'
MSG4     DC    C'NUMBER DELETED MEMBERS IN PDS'
MSG5     DC    C'NUMBER TABLE ENTRIES TOTAL'
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DC    CL1' '                  CARRIAGE CONTROL
PRT      DS    CL132' '                PRINT DATA
         ORG   PRT
PRTNBR   DS    CL10                       NUMBER FIELD
         DS    CL1
PRTDESC  DS    CL121                      DESCRIPTION FIELD
         ORG   PRT
PRTNAME  DS    CL8                        MEMBER NAME
         DS    CL1
PRTTTR   DS    CL7                        TTR
         DS    CL1
PRTIND   DS    CL7                        INDICATOR
         ORG
*
RETHDR   DSECT ,             DATA AREA RETURNED BY PDSREAD
AREASTRT DC    A(0)             ADDRESS OF RETURNED DATA (SUBPOOL 1)
AREALEN  DC    A(0)             LENGTH OF GOTTEN STORAGE
HDRLENG  DC    AL2(TABLSTRT-RETHDR) OFFSET TO TABLE FROM AREA START
NTRYLENG DC    AL2(MBREND-MBRNTRY) LENGTH OF EACH TABLE ENTRY
DIRBLKS  DC    PL4'0'           NUMBER DIRECTORY BLOCKS TOTAL
USDBLKS  DC    PL4'0'           NUMBER DIRECTORY BLOCKS USED
ACTVMEM  DC    PL4'0'           NUMBER ACTIVE MEMBERS IN PDS
DELEMEM  DC    PL4'0'           NUMBER DELETED MEMBERS IN PDS
TABLMEM  DC    PL4'0'           NUMBER TABLE ENTRIES TOTAL
TABLSTRT EQU   *                BEGINNING OF NAME-TTR-ALIAS TABLE
MBRNTRY  DS    0CL12            TABLE ENTRY DESCRIPTION
MBRNAME  DS    CL8                 MEMBER NAME (BLANK IF DELETED)
MBRTTR   DS    XL3                 TTR TO START OF MEMBER
MBRALIAS DS    CL1                 A = ALIAS, D = DELETED, ELSE BLANK
MBREND   EQU   *                   END OF ENTRY
CALLPDSR CSECT
*
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
         LTORG
CALLPDSR CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

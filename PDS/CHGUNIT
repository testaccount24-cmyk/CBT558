         PRINT NOGEN
         TITLE '*** CHGUNIT: CHANGE UNIT=SYSTS TO UNIT=DISK ***'
CHGUNIT SVLNK R3
*
***********************************************************************
* SCAN A JCL PDS AND MODIFY JCL DD STATEMENTS TO REPLACE UNIT=SYSTS   *
* WITH UNIT=DISK.                                                     *
*                                                                     *
* THE INPUT PDS IS IDENTIFIED BY DDNAME INPUT1, AND HAS RECFM=FB,     *
* AND LRECL=80 HARD-CODED IN THE PROGRAM.                             *
* THE INPUT PDS IS MODIFIED DIRECTLY WITH THE NEW UNIT, SO A COPY     *
* OF THE PDS SHOULD BE MADE BEFORE THE RUN TO INSURE AGAINST LOSS OF  *
* DATA.                                                               *
*                                                                     *
* AUTHOR R THORNTON OCT 1990                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: PRINT FILE                                          *
***********************************************************************
         OPEN  (SYSPRINT,OUTPUT)       OPEN LISTING FILE
***********************************************************************
* INITIALIZATION: OPEN THE JCL PDS FOR DIRECTORY AND MEMBER RECORD    *
*                 PROCESSING BY READDIR AND UPDMEM.                   *
***********************************************************************
         CALL  READDIR,OPEN            OPEN PDS FOR DIRECTORY READS
         CALL  UPDMEM,OPEN             OPEN PDS FOR MEMBER PROCESSING
***********************************************************************
* MAINLINE PROCESSING: LOCATE THE NEXT MEMBER OF THE INPUT1 PDS,      *
* POSITION TO IT, AND READ THE DATA RECORDS. SEARCH FOR THE STRING    *
* UNIT=SYSTS. WHEN FOUND, REPLACE IT WITH THE STRING UNIT=DISK AND    *
* COPY REMAINING DATA ON THE RECORD 1 SPACE LEFT.                     *
***********************************************************************
*
NEWMEM   CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
         CALL  UPDMEM,FIND             LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BE    NEWMEM                  NO, TRY NEXT MEMBER
CALLREAD CALL  UPDMEM,(READ,MEMREC)    READ NEXT RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BE    NEWMEM                  YES, GO TO NEXT MEMBER
         CLC   MEMREC(2),=CL2'//'      IS IT A JCL RECORD?
         BNE   CALLREAD                NO
         LA    R1,MEMREC+2             POINT TO INPUT RECORD
RETRT    TRT   1(80,R1),FINDU          FOUND THE LETTER U?
         BZ    CALLREAD                NO
         CLC   1(9,R1),=CL9'NIT=SYSTS' FOUND UNIT=SYSTS?
         BNE   RETRT                   NO
         MVC   5(4,R1),=CL4'DISK'      YES, CHANGE TO UNIT=DISK
         LA    R1,9(R1)                POINT PAST UNIT=DISK
         LA    R15,MEMREC+69           POINT TO INPUT COLUMN 70
         SR    R15,R1                  COMPUTE MOVE LENGTH CODE
         EX    R15,SHFTLEFT            SHIFT REMAINDER LEFT 1 BYTE
         MVI   MEMREC+70,C' '          INSURE COL 71 BLANK
         CALL  UPDMEM,(UPDATE,MEMREC)  UPDATE THE RECORD
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVC   PRTIMAG,MEMREC          MEMBER RECORD TO PRINT REC
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
         B     CALLREAD                GO GET NEXT RECORD
SHFTLEFT MVC   0(1,R1),1(R1)           EXECUTED MOVE
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE SYSPRINT                CLOSE FILES
         CALL  UPDMEM,CLOSE            CLOSE PDS MEMBER ACCESS
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
FINDU    DC    XL256'00'               TRT TABLE TO FIND 'U'
         ORG   FINDU+C'U'
         DC    X'FF'                   STOP ON 'U'
         ORG
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ REQUEST CODE
UPDATE   DC    C'U'                    UPDATE REQUEST CODE
CLOSE    DC    C'C'                    CLOSE REQUEST CODE
EOF      EQU   X'FF'                   END OF FILE CODE
*
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
*
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
*
MEMREC   DS    CL80                    MEMBER RECORD
         DC    CL81'00'                OVERFLOW FOR TRT
*
PRTREC   DS    0CL80                   OUTPUT RECORD
PRTMEM   DC    CL8' '                  MEMBER NAME
PRTIMAG  DC    CL72' '                 IMAGE OF INPUT RECORD
*
TRTSCH   DC    256X'00'                 TRT TABLE FOR SCANNING
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
*
         LTORG
         END

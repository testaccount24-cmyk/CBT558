         TITLE '*** SHUFFLE: INSERT DATA EVERY-OTHER LINE ***'
***********************************************************************
* READS INPUT1 FILE (UP TO 5 LINES) AND WRITES THE RESULT AFTER EACH  *
* SET OF LINES IN THE INPUT2 FILE, WRITING THE RESULT TO THE OUTPUT1  *
* FILE. RECFM, LRECL, AND BLKSIZE MUST BE SPECIFIED IN THE JCL OR FILE*
* LABELS. THE PARM FIELD MUST CONTAIN A NUMBER BETWEEN 1 AND 9, WHICH *
* SPECIFIES THE SIZE OF EACH SET OF LINES IN THE INPUT2 FILE.         *
* AUTHOR R THORNTON MAY 1993                                          *
***********************************************************************
*
SHUFFLE SVLNK R3
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),H1              VALID PARM DATA?
         BE    PARMOK                  YES, LENGTH IS O.K.
BADPARM  WTO   'SHUFFLE: INVALID PARM: MUST BE 1 - 9',ROUTCDE=11
         ABEND 111,DUMP
PARMOK   CLI   2(R2),C'1'              VALID PARM DATA?
         BL    BADPARM                 NO
         CLI   2(R2),C'9'              VALID PARM DATA?
         BH    BADPARM                 NO
         MVN   IN2RECS+1(1),2(R2)      VALID PARM
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
         LH    R12,INPUT1+82           GET INPUT1'S LRECL
         STH   R12,INLRECL             SAVE INPUT1 LRECL
         MH    R12,H5                  COMPUTE 5 X LRECL
         GETMAIN RU,LV=(12),SP=0,LOC=BELOW GET STORAGE
         ST    R1,INRECDA              SAVE INPUT AREA ADDRESS
LOADIN1  L     R12,INRECDA             POINT TO INPUT1 AREA
         SR    R11,R11                 INITIALIZE COUNTER
CHKEND1  CH    R11,H5                  ALREADY HAVE 5 INPUT1 RECDS?
         BE    IN1END                  YES
         GET   INPUT1,0(R12)           NO, READ NEXT INPUT1 RECORD
         LA    R11,1(R11)              INCREMENT COUNTER
         AH    R12,INLRECL             STEP TO NEXT RECORD AREA
         B     CHKEND1                 LOOP TO LOAD FILE
IN1END   STH   R11,IN1CNT              SAVE NBR INPUT1 RECORDS
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE LH    R11,IN2RECS             GET NBR RECORDS PER GROUP
COPY2    GET   INPUT2                  READ INPUT RECORD
         LR    R12,R1                  COPY RECORD ADDRESS
         PUT   OUTPUT1,0(R12)          WRITE OUTPUT RECORD
         BCT   R11,COPY2               LOOP THROUGH INPUT1 RECORDS
         LH    R11,IN1CNT              NBR RECORDS TO WRITE
         L     R12,INRECDA             POINT TO 1ST RECORD
COPY1    PUT   OUTPUT1,0(R12)          WRITE OUTPUT RECORD
         AH    R12,INLRECL             STEP TO NEXT RECORD
         BCT   R11,COPY1               LOOP THROUGH INPUT1 RECORDS
         B     MAINLINE                LOOP THROUGH INPUT2 FILE
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS 14-12
         SR    R15,R15                 SET RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
INLRECL  DC    H'0'                    LRECL FROM INPUT1 DCB
IN1CNT   DC    H'0'                    NBR INPUT1 RECORDS
H5       DC    H'5'                    CONSTANT 5
INRECDA  DC    A(0)                    ADDRESS OF INPUT1 AREA
H1       DC    H'1'                    CONSTANT 1
IN2RECS  DC    H'0'                    INPUT2 RECORD GROUP SIZE
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=IN1END
INPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT2,EODAD=EOJ
         LTORG
         END

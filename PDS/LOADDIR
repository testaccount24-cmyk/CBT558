         TITLE 'LOADDIR - PRINT LOAD MODULE DIRECTORY WITH LENGTHS'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* PROGRAM TO READ THE DIRECTORY IN AN INPUT PDS AND PRINT A LISTING
* SHOWING THE MODULE NAME AND LENGTH IN HEX, ONE 80-BYTE LINE PER
* MODULE. INPUT PDS DDNAME IS INPUT1, PRINT DDNAME IS PRINT1.
* RECFM=FB,LRECL=80 IS HARD-CODED FOR PRINT1.
*
* AUTHOR R. THORNTON   JAN 1982
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
LOADDIR  SVLNK R3
         OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
*
*
*
READDIR  CLC   RECADDR,ENDADDR         IS BLOCK EXHAUSTED?
         BL    GETADR                  NO, MORE ENTRIES IN BLOCK
NEXREC   GET   INPUT1                  READ DIRECTORY BLOCK
         LR    R12,R1                  GET BLOCK ADDRESS
         LH    R1,0(R12)               GET BLOCK SIZE
         LTR   R1,R1                   ANY DATA IN BLOCK?
         BZ    EOJ                     NO, END OF DIRECTORY
         AR    R1,R12                  COMPUTE END OF BLOCK ADDRESS
         ST    R1,ENDADDR              SAVE IT
         LA    R12,2(R12)              STEP PAST BLOCK SIZE FIELD
         ST    R12,RECADDR             STORE RECORD ADDRESS
GETADR   L     R12,RECADDR             GET NEXT ENTRY ADDRESS
         USING DIRNTRY,R12
         CLI   DIRNAME,X'FF'           END OF BLOCK?
         BE    NEXREC                  YES
         CLI   DIRNAME,C' '            POSSIBLY VALID NAME?
         BNH   NEXREC                  NO
         MVC   PNAME,DIRNAME           MOVE MEMBER NAME TO PRINT
         NI    DIRALIAS,X'0F'          CLEAR INDICATOR BITS
         CLI   DIRALIAS,8              AT LEAST 8 USER HALFWORDS?
         BNL   PRTLENG                 YES
         MVC   PLENG(6),=C'??????'     NO, LENGTH INVALID
         B     PRTMEM                  CONTINUE
PRTLENG  UNPK  PLENG(7),DIRLGTH(4)     UNPACK MODULE LENGTH
         MVI   PLENG+6,C' '            CLEAR OUT TRASH
         TR    PLENG,TRTBL-C'0'        MAKE HEX PRINTABLE
PRTMEM   BAL   R9,PRINT                LINE FULL, PRINT IT
         SR    R11,R11                 CLEAR FOR INSERT
         IC    R11,DIRALIAS            PICK UP NBR USER HALFWORDS
         SLL   R11,1                   COMPUTE NBR USER BYTES
         LA    R11,12(R11)             COMPUTE TOTAL ENTRY LENGTH
         A     R11,RECADDR             CALCULATE NEXT ENTRY ADDRESS
         ST    R11,RECADDR             STORE NEXT ENTRY ADDRESS
         B     READDIR                 GO READ AGAIN
*
*
*
PRINT    PUT   PRINT1,PRTLINE          PRINT A LINE
         BR    R9                      RETURN
*
*
*
EOJ      CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              RESTORE RETURN POINTER
         LM    R14,R12,12(R13)         RESTORE CALLING PGM REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*
*
*
TRTBL    DC    C'0123456789ABCDEF'
ENDADDR  DC    F'0'                    END OF BLOCK ADDRESS
RECADDR  DC    F'1'                    CURRENT DIRECTORY ENTRY ADDRESS
PRTLINE  DS    0CL80
PNAME    DC    CL8' '                  MODULE NAME
         DC    CL2' '
PLENG    DC    CL6' '                  MODULE LENGTH
         DC    CL80' '
*
*
*
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,RECFM=F,                X
               LRECL=256,BLKSIZE=256,EODAD=EOJ
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,RECFM=FB,               X
               LRECL=80
*
*
*
DIRNTRY  DSECT
DIRNAME  DS    CL8
DIRTTR   DS    XL3
DIRALIAS DS    XL1
DIRFRST  DS    XL3
         DS    XL1
DIRNOTE  DS    XL3
DIRNBRN  DS    XL1
DIRATTR1 DS    XL2
DIRLGTH  DS    XL3
DIR1TXT  DS    XL2
DIREPA   DS    XL3
DIRATTR2 DS    XL1
         DS    XL2
DIRACDL  DS    XL1
DIRAUTH  DS    XL1
         END

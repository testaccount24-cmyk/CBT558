         TITLE '*** VOLFILE: VOLSER COLLECTION SUBROUTINE *****'
VOLFILE  CSECT
VOLFILE  AMODE 24
VOLFILE  RMODE 24
*********************************************************************
* USES THE CALLTSSR MACRO TO INVOKE IKJEHCIR TO RETRIEVE SYSTEM     *
* CATALOG INFORMATION FOR A DSNAME SPECIFIED BY THE CALLER AND WRITE*
* A FILE CONTAINING THE VOLSERS CONTAINING THE FILE.                *
*                                                                   *
* STANDARD LINKAGE CONVENTIONS ARE USED WITH THE CALLER PASSING TWO *
* PARAMETERS:                                                       *
*  1) THE DSNAME WHOSE VOLSERS ARE TO BE RETURNED. THE DSNAME MUST  *
*     BE LEFT JUSTIFIED WITH TRAILING SPACES IN A 44-BYTE FIELD.    *
*  2) A RETURN CODE FIELD THAT WILL CONTAIN THE ZONED DECIMAL VALUE *
*     '00' IF THE CALL WAS SUCCESSFUL, OR '12' IF AN ERROR OCCURRED.*
* IF THE CALL WAS SUCCESSFUL, A DATASET CONTAINING FIXED LENGTH     *
* 6-BYTE RECORDS IS WRITTEN TO THE VOLSERFL FILE. THERE WILL BE ONE *
* RECORD FOR EACH VOLUME SERIAL ON WHICH THE SPECIFIED DSNAME IS    *
* WRITTEN. THE CALLER MUST INSURE THAT A DD STATEMENT IS INCLUDED   *
* AT EXECUTION TIME FOR DDNAME VOLSERFL. DSORG=PS,RECFM=FB,LRECL=6  *
* IS HARD-CODED FOR THIS FILE.                                      *
*                                                                   *
* IF THE CALL IS NOT SUCCESSFUL, THE RETURN CODE WILL CONTAIN '12', *
* THE VOLSERFL FILE WILL BE A NULL FILE, AND A DESCRIPTION OF THE   *
* ERROR WILL BE WRITTEN TO THE SYSOUT FILE. THE CALLER MUST INSURE  *
* THAT A DD STATEMENT IS INCLUDED FOR THE SYSOUT FILE AT EXECUTION  *
* TIME. DSORG=PS,RECFM=FB IS REQUIRED FOR THIS FILE, AND LRECL IS   *
* AT THE DISCRETION OF THE USER, BUT SHOULD BE AT LEAST 120 BYTES   *
* TO CONTAIN ALL THE DATA.                                          *
*                                                                   *
* AUTHOR R THORNTON DEC 1998                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING VOLFILE,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     PARMS                   BRANCH AROUND CONSTANTS
         DC    CL8'VOLFILE'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        GET PARAMETER FIELD ADDRESSES.                             *
*********************************************************************
PARMS    L     R5,0(R1)                POINT TO CALLER SPECIFIED DSNAME
         L     R6,4(R1)                POINT TO CALLER'S RETCODE FIELD
         MVC   0(2,R6),=CL2'00'        INITIALIZE RETCODE
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (SYSOUT,OUTPUT,VOLSERFL,OUTPUT) OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE ST    R5,CIRSRCH              PUT DSNAME IN PARMS FOR IKJEHCIR
         CALLTSSR EP=IKJEHCIR,MF=(E,CIRPARM) EXECUTE IKJEHCIR
         LTR   R15,R15                 ANY ERROR?
         BZ    MLWRT                   NO
         ST    R15,SAVER15             YES, SAVE R15
         MVC   0(2,R6),=CL2'12'        SET ERROR RETCODE FOR CALLER
         MVC   PRT(36),=CL36'ERROR IN CATALOG SEARCH FOR DSNAME: '
         MVC   PRT+36(44),0(R5)        DSNAME TO PRINT
         BAS   R9,PRINT                PRINT IT TO SYSOUT
         MVC   PRT(L'FORINFO),FORINFO  MESSAGE TO PRINT
         BAS   R9,PRINT                PRINT IT TO SYSOUT
         L     R1,SAVER15              GET IKJEHCIR RETCODE
         CVD   R1,DBLWD                CONVERT TO PACKED
         MVC   PRT(41),=CL41'IKJEHCIR RETURN CODE IN R15 IS (DECIMAL):'
         UNPK  PRT+42(3),DBLWD+5(3)    UNPACK THE RETCODE
         OI    PRT+44,C'0'             CLEAR THE SIGN
         MVC   PRT+46(21),=CL21'ERROR DESCRIPTION IS:'
         BAS   R9,PRINT                PRINT IT TO SYSOUT
         LA    R1,ERRORTBL             POINT TO 1ST ERROR TABLE ENTRY
MLRELOOK CLC   CIRLOCRC,4(R1)          FOUND THE ENTRY?
         BE    MOVETEXT                YES
         CLI   4(R1),X'FF'             LAST ENTRY?
         BNE   MLADVNC                 NO
         SR    R2,R2                   CLEAR FOR INSERT
         IC    R2,CIRLOCRC             PICK UP LOCATE RETURN CODE
         CVD   R2,DBLWD                CONVERT TO DECIMAL
         UNPK  ERRORFFC,DBLWD+5(3)     UNPACK TO MESSAGE
         OI    ERRORFFC+2,C'0'         AND CLEAR SIGN
         B     MOVETEXT                YES
MLADVNC  L     R1,0(R1)                NO, POINT TO NEXT ENTRY
         B     MLRELOOK                LOOP TO FIND ENTRY
MOVETEXT IC    R2,5(R1)                GET TEXT LENGTH
         EX    R2,EXMOVE               MOVE TEXT TO CALLER'S AREA
         BAS   R9,PRINT                PRINT IT TO SYSOUT
         B     RETURN                  RETURN TO CALLER
MLWRT    LA    R12,CIRVOLNT            POINT TO FIRST VOLUME ENTRY
MLCKEND  CLI   0(R12),X'FF'            END OF LIST?
         BE    RETURN                  YES, GO READ AGAIN
         PUT   VOLSERFL,CIRVOLSR-CIRVOLNT(R12) NO, WRITE VOLSER RECORD
         LA    R12,12(R12)             ADVANCE TO NEXT ENTRY
         B     MLCKEND                 LOOP THRU THE VOLUME LIST
EXMOVE   MVC   PRT(1),6(R1)            EXECUTED MOVE
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    PUT   SYSOUT,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
RETURN   CLOSE (SYSOUT,,VOLSERFL)      CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                      DOUBLEWORD WORK AREA
*
RETCODE  DS    F                      RETURN CODE
SAVER15  DS    F                      SAVE AREA FOR R15 FROM IKJEHCIR
BLANKS   DC    CL133' '                BLANKS
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DC    CL160' '
*
FORINFO  DC    C'FOR INFO SEE IBM MANUAL SC28-1875 "IBM TSO EXTENSIONS X
               FOR MVS PROGRAMMING SERVICES" CHAPTERS 4 AND 18.'
*********************************************************************
*   TABLE OF ERROR DESCRIPTIONS                                     *
*********************************************************************
ERRORTBL DS    0F                   LOCATE ERROR DESCRIPTIONS
ERROR04  DC    A(ERROR08),AL1(04),AL1(L'ERROR04D-1)
ERROR04D DC    C'THE REQUIRED CATALOG DOES NOT EXIST OR IT CANNOT BE OPX
               ENED.'
ERROR08  DC    A(ERROR0C),AL1(08),AL1(L'ERROR08D-1)
ERROR08D DC    C'EITHER THE ENTRY WAS NOT FOUND IN THE CATALOG, THE USEX
               R IS NOT AUTHORIZED, OR A GDG ALIAS WAS FOUND.'
ERROR0C  DC    A(ERROR10),AL1(12),AL1(L'ERROR0CD-1)
ERROR0CD DC    C'EITHER AN INDEX OR GDG BASE WAS FOUND, AN ALIAS WAS FOX
               UND, OR AN INCORRECT LOW-LEVEL GDG NAME WAS FOUND.'
ERROR10  DC    A(ERROR14),AL1(16),AL1(L'ERROR10D-1)
ERROR10D DC    C'A DATASET EXISTS AT OTHER THAN THE LOWEST INDEX LEVEL X
               SPECIFIED.'
ERROR14  DC    A(ERROR18),AL1(20),AL1(L'ERROR14D-1)
ERROR14D DC    C'A SYNTAX ERROR EXISTS IN THE NAME.'
ERROR18  DC    A(ERROR1C),AL1(24),AL1(L'ERROR18D-1)
ERROR18D DC    C'EITHER A PERMANENT I/O ERROR OCCURRED, THERE WAS A NONX
               -ZERO ESTAE RETURN CODE WAS FOUND OR THERE WAS A PARAMETX
               ER LIST ERROR.'
ERROR1C  DC    A(ERRORFF),AL1(28),AL1(L'ERROR1CD-1)
ERROR1CD DC    C'THE RELATIVE ADDRESS SUPPLIED TO THE LOCATE ROUTINE ISX
                OUTSIDE OF THE SYSCTLG DATASET EXTENTS.'
ERRORFF  DC    A(0),AL1(255),AL1(L'ERRORFFD+2)
ERRORFFD DC    C'SEE DESCRIPTION OF MESSAGE IDC3009I IN THE SYSTEM MESSX
               AGES MANUAL FOR THE LOCATE RETURN CODE '
ERRORFFC DC    CL3'  '
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,RECFM=FB
VOLSERFL DCB   DSORG=PS,MACRF=PM,DDNAME=VOLSERFL,                      X
               RECFM=FB,LRECL=6
         LTORG
*
CIRPARM  DS    0F                     PARAMETER LIST FOR IKJEHCIR
CIROPT   DC    XL1'04'                   REQUEST FOR VOLUME INFO
         DC    XL2'00'                   RESERVED
CIRLOCRC DC    XL1'00'                   LOCATE RETURN CODE
CIRSRCH  DC    A(0)                      POINTER TO DSNAME
CIRVOL   DC    A(0)                      POINTER TO USERCAT DSNAME
CIRWA    DC    A(CIRWORK)                POINTER TO VOLUME WORK AREA
CIRSAVE  DC    A(CIRREGS)                POINTER TO REGISTER SAVE AREA
CIRPSWD  DC    A(0)                      POINTER TO CATALOG PASSWORD
CIRREGS  DS    16F                    REGISTER SAVE AREA FOR IKJEHCIR
CIRWORK  DS    0F                     VOLUME INFORMATION FROM IKJEHCIR
         DC    AL2(CIRWEND-CIRWORK)      LENGTH OF AREA
DATALIN  DC    AL2(0)                    LENGTH OF DATA RETURNED
CIRNUMVL DS    XL1                       NUMBER OF RESIDENT VOLUMES
CIRVOLNT DS    0CL12                     VOLUME ENTRY
CIRDEVTY DS    XL4                         DEVICE TYPE
CIRVOLSR DS    CL6                         VOLUME SERIAL
CIRFILSQ DS    XL2                         FILE SEQUENCE NUMBER
         DS    254CL12                   REMAINING 254 ENTRIES
CIRWEND  DC    X'FFFFFFFFFFFFFFFF'    INSURE END OF LIST FOUND
VOLFILE  CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*********************************************************************
*  CVT DESCRIPTION (REQUIRED BY THE CALLTSSR MACRO)                 *
*********************************************************************
         CVT   DSECT=YES
         END

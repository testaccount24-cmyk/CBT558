         TITLE '***** STRIP DATASET RECORDS FROM AN SMF FILE *****'
         PRINT NOGEN
SMFSTR14 SVLNK R3,R4
**********************************************************************
*  STRIP ALL TYPE 14 (INPUT DATASET), TYPE 15 (OUTPUT DATASET),      *
*  TYPE 17 (SCRATCH DATASET), AND TYPE 18 (RENAME DATASET) SMF       *
*  TO THE OUTPUT FILE. INPUT1 DEFINES THE SMF INPUT FILE, WHICH IS   *
*  HARD-CODED TO BE RECFM=VBS.                                       *
*                                                                    *
*  THE EXEC STATEMENT PARM CAN BE "PARM=JOBLOG" TO PRODUCE AN OUTPUT *
*  FILE CONTAINING DSNAME, JOBLOG NUMBER, AND SMF TAPE VOLSER,       *
*  OTHERWISE THE OUTPUT FILE WILL CONTAIN THE ENTIRE INPUT SMF RECORD*
*  FOR PARM=JOBLOG, THE OUTPUT1 DCB INFORMATION SHOULD BE RECFM=FB,  *
*  LRECL=100, BLKSIZE=100 MULTIPLE, OTHERWISE THE OUTPUT1 DCB SHOULD *
*  BE RECFM=VB,LRECL=32716,BLKSIZE=32720.                            *
*                                                                    *
*  THE DSNAMES OF THE DATASETS TO BE FOUND ARE ENTERED IN THE FIRST  *
*  44 BYTES OF THE 80 COLUMN RECORDS IN THE READER1 FILE. SINCE THE  *
*  COMPARISON IS ONLY MADE ON NAME UP TO, BUT NOT INCLUDING, THE     *
*  FIRST BLANK, GENERIC NAMES CAN BE USED, SUCH AS SYS1.PLI TO GET   *
*  ANY DATASET BEGINNING WITH SYS1.PLI. UP TO 50 DATASET NAMES CAN BE*
*  ENTERED VIA READER1.                                              *
*                                                                    *
*  AT END OF JOB, A LISTING IS PRINTED, SHOWING THE NUMBER OF RECORDS*
*  WRITTEN FOR EACH DSNAME IN THE READER1 FILE. FOR READER1, THE     *
*  RECFM IS HARD CODED AS FB, AND LRECL IS 80.                       *
*                                                                    *
*                                                                    *
*                                                                    *
*  AUTHOR R. THORNTON JUL 1990                                       *
**********************************************************************
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
         L     R2,0(R1)                 POINT TO PARM DATA
         CLC   2(6,R2),=C'JOBLOG'       JOBLOG STRIP OUTPUT?
         BNE   OPENS                    NO
         MVI   JLOGSW,X'F0'             SET JOBLOG SWITCH
OPENS    OPEN  (PRINT1,OUTPUT,OUTPUT1,OUTPUT,INPUT1,,READER1)
         PUT   OUTPUT1,HEADER1          WRITE OUTPUT HEADER1
         PUT   OUTPUT1,HEADER2          WRITE OUTPUT HEADER2
         PUT   OUTPUT1,HEADER3          WRITE OUTPUT HEADER3
         PUT   OUTPUT1,HEADER4          WRITE OUTPUT HEADER3
         L     R12,DSNTBLA              POINT TO DSNAME TABLE
         USING DSNAMTBL,R12
         LA    R11,50                   MAX TABLE ENTRIES
TBLOAD   GET   READER1                  READ NEXT DSNAME CARD
         CLI   0(R1),C' '               ANY DSNAME?
         BE    TBLOAD                   NO, IGNORE RECORD
         ZAP   DSNCNT,P0                CLEAR COUNTER
         MVC   DSNNAME,0(R1)            MOVE NAME TO TABLE ENTRY
         LA    R1,DSNNAME+44            POINT TO RIGHT END OF ENTRY+1
         TRT   DSNNAME,FINDBLNK         FIND RIGHT END OF NAME+1
         LA    R9,DSNNAME+1             POINT TO LEFT END OF ENTRY+1
         SR    R1,R9                    COMPUTE NAME LENGTH CODE
         STH   R1,DSNLEN                SAVE LENGTH CODE
         LA    R12,L'DSNENTRY(R12)      POINT TO NEXT ENTRY
         BCT   R11,TBLOAD               CONTINUE LOAD
ENDCTL   MVI   0(R12),X'FF'             SET TABLE STOPPER
         L     R1,16                    POINT TO CVT
         L     R1,0(R1)                 POINT TO NEW-OLD WORDS
         L     R1,0(R1)                 POINT TO MY TCB
         L     R1,12(R1)                POINT TO MY TIOT
         AH    R1,INPUT1+40             POINT TO ENTRY FOR INPUT1
         ST    R1,TIOTDD                SAVE INPUT1 TIOT DD ENTRY ADDR
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
MAINLINE GET   INPUT1,SMF14HDR          GET INPUT AREA ADDRESS
         SR    R2,R2                    CLEAR FOR TRT
         TRT   SMF14RTY,SELECTYP        SCAN FOR DESIRED RECORD TYPES
         BZ    MAINLINE                 NOT A DESIRED TYPE, SKIP IT
         L     R12,DSNTBLA              POINT TO DSNAME TABLE
BRTBL    B     BRTBL(R2)                BRANCH TO APPROPRIATE POINT
         B     GOT1415                  BRANCH IF TYPES 14, 15
         B     GOT17                    BRANCH IF TYPE 17
         B     GOT18                    BRANCH IF TYPE 18
GOT1415  LA    R9,SMF14DSN              POINT TO TYPE 14-15 DSNAME
         B     SRCHTBL                  GO SEARCH DSNAME TABLE
GOT17    LA    R9,SMF17DSN              POINT TO TYPE 17 DSNAME
         B     SRCHTBL                  GO SEARCH DSNAME TABLE
GOT18    LA    R9,SMF18DSN              POINT TO TYPE 18 DSNAME
SRCHTBL  LH    R2,DSNLEN                GET ENTRY LENGTH CODE
         EX    R2,COMPARE               FOUND THE DSN?
         BE    JLOGBR                   GOT SCRATCH DATASET
         LA    R12,L'DSNENTRY(R12)      POINT TO NEXT ENTRY
         CLI   DSNENTRY,X'FF'           END OF TABLE?
         BNE   SRCHTBL                  NO
         B     MAINLINE                 YES, GO READ SMF FILE
JLOGBR   NOP   JLOGOUT                  BRANCH IF PARM=JOBLOG
JLOGSW   EQU   JLOGBR+1,1
         MVC   SMF14LEN,INPUT1+82       LENGTH TO OUTPUT
         XC    SMF14SEG,SMF14SEG        CLEAR REST OF RDW
         PUT   OUTPUT1,SMF14REC         WRITE THE ENTIRE RECORD
         AP    DSNCNT,P1                INCREMENT OUTPUT COUNT
         B     MAINLINE                 GO READ SMF
JLOGOUT  MVC   JLOGJBN,SMF14JBN         JOBNAME TO OUTPUT
         UNPK  JLOGRSD,SMF14RSD         READER START DATE TO OUTPUT
         L     R1,SMF14RST              GET READER START TIME
         CVD   R1,DBLWD                 CONVERT TO DECIMAL
         UNPK  JLOGRST,DBLWD            READER START TIME TO OUTPUT
         OI    JLOGRST+7,C'0'           CLEAR SIGN
         UNPK  JLOGCRDT,SMF14DTE        RECORD CREATION DATE TO OUTPUT
         L     R1,SMF14TME              RECORD CREATION TIME TO OUTPUT
         CVD   R1,DBLWD                 CONVERT TO DECIMAL
         UNPK  JLOGCRTM,DBLWD           RECORD CREATION TIME TO OUTPUT
         OI    JLOGCRTM+7,C'0'          CLEAR SIGN
         MVC   JLOGSID,SMF14SID         SYSTEM ID TO OUTPUT
         SR    R1,R1                    CLEAR FOR INSERT
         IC    R1,SMF14RTY              GET SMF RECORD TYPE
         CVD   R1,DBLWD                 CONVERT TO DECIMAL
         UNPK  JLOGTYPE,DBLWD           SMF TYPE TO OUTPUT
         OI    JLOGTYPE+1,C'0'          CLEAR SIGN
         L     R1,TIOTDD                TIOT DD ENTRY FOR INPUT1
         L     R1,16(R1)                UCB FOR INPUT1
         MVC   JLOGVOLS,28(R1)          VOLSER TO OUTPUT
         MVC   JLOGDDNM,SMFTIOE5        DDNAME TO OUTPUT
         MVC   JLOGDSN,0(R9)            DSNAME TO OUTPUT
         CLI   SMF14RTY,16              TYPE 14 OR 15
         BL    WRTJLOG                  YES
         MVC   JLOGDDNM,BLANKS          NO, BLANK DDNAME
WRTJLOG  PUT   OUTPUT1,JLOGRECD         WRITE JOBLOG EXTRACT RECORD
         AP    DSNCNT,P1                INCREMENT OUTPUT COUNT
         B     MAINLINE                 GO READ SMF
*
COMPARE  CLC   DSNNAME(1),0(R9)         FOUND THE DSN?
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      L     R12,DSNTBLA              POINT TO DSNAME TABLE
PRTNEXT  CLI   DSNENTRY,X'FF'          END OF TABLE?
         BE    EOJ1                    NO
         MVC   PRTCNT,EDITWRD          EDIT WORD
         ED    PRTCNT,DSNCNT           EDIT COUNT TO PRINT
         MVC   PRTDSN,DSNNAME          DSNAME TO PRINT
         PUT   PRINT1,PRT              WRITE PRINT LINE
         LA    R12,L'DSNENTRY(R12)     ADVANCE TO NEXT ENTRY
         B     PRTNEXT                 YES, GO READ SMF FILE
EOJ1     CLOSE (OUTPUT1,,INPUT1,,PRINT1,,READER1) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             PICK UP RETURN ADDRESS
         L     R15,RETCODE             GET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
DBLWD    DS    D                       WORK AREA
RETCODE  DC    F'0'                    RETURN CODE
DSNTBLA  DC    A(NAMETBL)              DSNAME TABLE AREA ADDRESS
TIOTDD   DC    A(0)                    @ TIOT DD ENTRY FOR INPUT1
BLANKS   DC    CL8' '                  BLANKS
FINDBLNK DC    XL256'00'               TABLE TO SCAN FOR BLANK
         ORG   FINDBLNK+C' '
         DC    X'FF'                   STOP ON C' '
         ORG
SELECTYP DC    XL256'00'                SCAN FOR DESIRED RECORD TYPE
         ORG   SELECTYP+14
         DC    X'0404'                  TYPE 14, 15
         ORG   SELECTYP+17
         DC    X'080C'                  TYPES 17, 18
         ORG
*
JLOGRECD DS    0CL100                   JOBLOG EXTRACT RECORD
JLOGJLOG DS    0CL16                      JOBLOG NUMBER
JLOGJBN  DS    CL8                          JOBNAME
JLOGRSD  DS    CL6                          READER START DATE
JLOGRST  DS    CL8                          READER START TIME
JLOGCRDT DS    CL6                        CREATION DATE
JLOGCRTM DS    CL8                        CREATION TIME
JLOGSID  DS    CL4                        SYSTEM ID
JLOGTYPE DS    CL2                        SMF RECORD TYPE
JLOGVOLS DS    CL6                        VOLSER OF SMF TAPE
JLOGDDNM DS    CL8                        DDNAME
JLOGDSN  DS    CL44                       DSNAME
*
HEADER1  DS    0CL100                   JOBLOG EXTRACT RECORD
         DC    CL8' '                       JOBNAME
         DC    CL6' RDR  '                  READER START DATE
         DC    CL8' RDR  '                  READER START TIME
         DC    CL6' '                     CREATION DATE
         DC    CL8' '                     CREATION TIME
         DC    CL4'SMF '                  SYSTEM ID
         DC    CL2'RT'                    SMF RECORD TYPE
         DC    CL6' SMF  '                VOLSER OF SMF TAPE
         DC    CL8' '                     DDNAME
         DC    CL44' '                    DSNAME
*
HEADER2  DS    0CL100                   JOBLOG EXTRACT RECORD
         DC    CL8'  JOB   '                JOBNAME
         DC    CL6' STRT '                  READER START DATE
         DC    CL8' STRT '                  READER START TIME
         DC    CL6' CREA '                CREATION DATE
         DC    CL8' CREA '                CREATION TIME
         DC    CL4'SYS '                  SYSTEM ID
         DC    CL2'EY'                    SMF RECORD TYPE
         DC    CL6' TAPE '                VOLSER OF SMF TAPE
         DC    CL8' '                     DDNAME
         DC    CL44' '                    DSNAME
*
HEADER3  DS    0CL100                   JOBLOG EXTRACT RECORD
         DC    CL8'  NAME  '                JOBNAME
         DC    CL6' DATE '                  READER START DATE
         DC    CL8' TIME   '                READER START TIME
         DC    CL6' DATE '                CREATION DATE
         DC    CL8' TIME   '              CREATION TIME
         DC    CL4'ID  '                  SYSTEM ID
         DC    CL2'CP'                    SMF RECORD TYPE
         DC    CL6' VOL  '                VOLSER OF SMF TAPE
         DC    CL8' DDNAME '              DDNAME
         DC    CL44'DSNAME'               DSNAME
*
HEADER4  DS    0CL100                   JOBLOG EXTRACT RECORD
         DC    8C'.'                        JOBNAME
         DC    6C'_'                        READER START DATE
         DC    8C'.'                        READER START TIME
         DC    6C'_'                      CREATION DATE
         DC    8C'.'                      CREATION TIME
         DC    4C'_'                      SYSTEM ID
         DC    2C'.'                      SMF RECORD TYPE
         DC    6C'_'                      VOLSER OF SMF TAPE
         DC    8C'.'                      DDNAME
         DC    44C'_'                     DSNAME
*
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
EDITWRD  DC    XL10'40206B2020206B202121'
*
PRT      DS    0CL80
PRTCNT   DC    CL10' '                 COUNT
         DC    CL1' '
PRTDSN   DC    CL44' '                 DSNAME
         DC    CL25' '
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
DSNAMTBL DSECT ,                       DSNAME TABLE DESCRIPTION
DSNENTRY DS    0CL50                   DSNAME TABLE ENTRY
DSNCNT   DS    PL4                       COUNT OF RECORDS FOUND
DSNLEN   DS    XL2                       LENGTH OF DSNAME
DSNNAME  DS    CL44                      VALUE OF DSNAME
SMFSTR14 CSECT
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
READER1  DCB   DSORG=PS,MACRF=GL,DDNAME=READER1,EODAD=ENDCTL
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GD,DDNAME=INPUT1,EODAD=EOJ
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1
         LTORG
*
*        INPUT AREA FOR SMF RECORD
*
SMF14REC DS    0CL32768                SMF RECORD INPUT AREA
SMF14LEN DS    H                       RECORD DESCRIPTOR WORD
SMF14SEG DS    H                       LAST 2 BYTES OF RDW
SMF14HDR DS    0X                      START OF SMF HEADER DATA
SMF14FLG DS    XL1                     4=XA, 8=ESA
SMF14RTY DS    XL1                     RECORD TYPE
SMF14TME DS    XL4                     TIME 1/100 SEC
SMF14DTE DS    PL4                     DATE 00YYDDDF
SMF14SID DS    CL4                     SYSTEM ID
SMF14JBN DS    CL8                     JOB NAME
SMF14RST DS    XL4                     READER START TIME
SMF14RSD DS    PL4                     READER START DATE
SMF17DSN EQU   SMF14REC+44,44          TYPE 17,18 DATASET NAME
SMFTIOE5 EQU   SMF14REC+56,8           TYPE 14 DDNAME
SMF18DSN EQU   SMF14REC+88,44          TYPE 18 NEW DSN
SMF14DSN EQU   SMF14REC+68,44          TYPE 14,15 DATASET NAME
         ORG
         DS    CL32768
*
*        DSNAME TABLE AREA
*
NAMETBL  DS    50CL50                   DSNAME TABLE AREA
         DC    X'FF'                    TABLE STOPPER
         END

         PRINT NOGEN
         TITLE '***** 31-BIT ASSEMBLER SUBROUTINE *****'
ASM31SUB CSECT
ASM31SUB AMODE 31
ASM31SUB RMODE ANY
***********************************************************************
* ENTERED FROM A CALLING PROGRAM WHICH MAY RESIDE ABOVE OR BELOW THE  *
* 16M LINE. PASSES BACK A DATA FIELD CONTAINING A LITERAL THAT        *
* PROVIDES THE ENTRY POINT OF THIS SUBROUTINE. USED TO TEST THE       *
* ABILITY TO CALL AND RETURN SUCCESSFULLY TO A SUBROUTINE RESIDING    *
* ABOVE THE LINE WHEN ITS CALLER RESIDES BELOW THE LINE, AND ALSO TO  *
* TEST THE SITUATION WHEN THIS SUBROUTINE IS STATICALLY LINKED INTO A *
* PROGRAM RESIDING BELOW THE LINE.                                    *
* CALLING PROGRAM MUST PASS THE ADDRESS OF A 34-BYTE AREA IN WHICH THE*
* LITERAL FIELD WILL BE RETURNED.                                     *
* AUTHOR R THORNTON NOV 1991                                          *
***********************************************************************
         STM   R14,R12,12(R13)         SAVE CALLERS REGS
         LR    R3,R15                  COPY BASE REG
         USING ASM31SUB,R3
         LA    R15,SAVE                POINT TO SAVE
         ST    R15,8(R13)              SET LSA
         ST    R13,SAVE+4              SET FSA
         LR    R13,R15                 SET UP SAVE POINTER
         B     MAINLINE                AROUND SAVE AREA
SAVE     DS    18F                     SAVE AREA
*
MAINLINE ST    R3,SAVEPA               STORE ENTRY POINT ADDRESS
         UNPK  LITADDR(9),SAVEPA(5)    MAKE ENTRY
         TR    LITADDR,TRCHAR              POINT ADDRESS PRINTABLE
         L     R12,0(R1)               POINT TO CALLERS PARM FIELD
         MVC   0(L'LITERAL,R12),LITERAL RETURN LITERAL TO CALLER
*
RETURN   L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BSM   0,R14                   RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
SAVEPA   DC    A(0)                    ENTRY POINT ADDRESS
LITERAL  DS    0CL34
         DC    CL26'ASM31SUB''S ENTRY POINT IS '
LITADDR  DS    CL8,CL1                 PRINTABLE EPA ADDRESS
TRCHAR   DC    XL240'00'               MAKE HEX
         DC    CL16'0123456789ABCDEF'      PRINTABLE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

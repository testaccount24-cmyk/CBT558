         TITLE '*** UCBSCAN: SAMPLE PROGRAM USING UCBSCAN MACRO ***'
UCBSCAN CSECT
*********************************************************************
* ISSUES UCBSCAN IN A LOOP TO LOCATE ALL DASD UCBS INCLUDING THOSE  *
* LOCATED ABOVE THE LINE AND THOSE WITH BOTH 3-DIGIT AND 4-DIGIT    *
* DEVICE NUMBERS.                                                   *
* AUTHOR R THORNTON APR 1999                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING UCBSCAN,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'UCBSCAN'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT)       OPEN FILES
         XC    WORKAREA,WORKAREA       CLEAR WORKAREA FOR SCAN
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE UCBSCAN COPY,                                                 X
               DEVCLASS=DASD,          ONLY DASD UCB'S RETURNED        X
               DYNAMIC=YES,            BOTH STATIC AND DYNAMIC         X
               RANGE=ALL,              BOTH 3-DIGIT AND 4-DIGIT DEVICESX
               WORKAREA=WORKAREA,      UCBSCAN'S WORKAREA              X
               UCBAREA=UCBAREA,        RET AREA FOR UCB COMMON SEGMENT X
               UCBPAREA=UCBPAREA,      RET AREA FOR UCB PREFIX AREA    X
               CMXTAREA=CMXTAREA,      RET AREA FOR UCB COMMON EXTNSN  X
               DCEAREA=DCEAREA,        RET AREA FOR DEVICE CLASS EXTNSNX
               DCELEN=256,             LENGTH OF DCEAREA               X
               DEVNCHAR=DEVNCHAR       RET AREA FOR DEVICE NUMBER
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   EOJ                     NO
         LA    R6,UCBAREA              POINT TO UCB COMMON SEGMENT
         USING UCBOB,R6
         LA    R7,UCBPAREA             POINT TO UCB PREFIX
         USING UCBPXST,R7
         LA    R8,CMXTAREA             POINT TO UCB COMMON EXTENSION
         USING UCBCMEXT,R8
         MVC   PRTVOL,UCBVOLI          MOVE VOLSER
         MVC   PRTNBR,DEVNCHAR         MOVE DEVICE NUMBER
         MVC   PRTWHERE,=CL5'?????'    ASSUME UNKNOWN UCB ADDRESS
         MVC   PRTADDR,=CL8'????????'  ASSUME UNKNOWN UCB ADDRESS
         SMODE KEY=0,STATE=S           GET SUPVR STATE, PROT KEY=0
         UCBLOOK DEVNCHAR=DEVNCHAR,    LOOK UP UCB ADDRESS             X
               UCBPTR=UCBADDRA,        UCB ADDRESS RETURN AREA         X
               DYNAMIC=YES,            INCLUDE DYNAMIC UCB'S           X
               RANGE=ALL,              INCLUDE ALL DEVICE NUMBERS      X
               LOC=ANY,                INCLUDE UCBS AT ALL ADDRESSES   X
               LINKAGE=SYSTEM,         INVOKE USING PC                 X
               RETCODE=RETCODE,        R15 IS RETURN CODE              X
               RSNCODE=RSNCODE,        R0 IS REASON CODE RETURNED      X
               NOPIN                   DON'T PIN THE UCB
         SMODE KEY=NZERO,STATE=P       BACK TO PROBLEM STATE AND KEY
         L     R15,RETCODE             GET RETURN CODE FROM UCBLOOK
         LTR   R15,R15                 DID UCBLOOK WORK?
         BNZ   BADLKUP                 NO
         MVC   PRTWHERE,=CL5'BELOW'    ASSUME UCB BELOW THE LINE
         TM    UCBADDRA,X'80'          IS IT BELOW THE LINE?
         BZ    DOADDR                  YES
         MVC   PRTWHERE,=CL5'ABOVE'    NO, SHOW ABOVE THE LINE
DOADDR   UNPK  PRTADDR(9),UCBADDRA(5)  UNPACK UCB ADDRESS
         MVI   PRTADDR+8,C' '          CLEAR TRASH
         TR    PRTADDR,TRCHAR-C'0'     MAKE PRINTABLE
PUTPRT   PUT   SYSPRINT,PRTLINE        PRINT DEVICE LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         B     MAINLINE                GO GET NEXT UCB
BADLKUP  MVC   PRTR15L,=CL8'RETCODE='  DELIMIT
         UNPK  PRTR15V(9),RETCODE(5)   UNPACK
         MVI   PRTR15V+8,C' '          CLEAR TRASH
         TR    PRTR15V,TRCHAR-C'0'     MAKE PRINTABLE
         MVC   PRTR0L,=CL8'RSNCODE='   DELIMIT
         UNPK  PRTR0V(9),RSNCODE(5)    UNPACK
         MVI   PRTR0V+8,C' '           CLEAR TRASH
         TR    PRTR0V,TRCHAR-C'0'      MAKE PRINTABLE
         B     PUTPRT                  GO PRINT THE LINE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE SYSPRINT                CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS 14-12
         SR    R15,R15                 SET RETURN CODE = 0
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
FZERO    DC    F'0'                    CONSTANT ZEROS
UCBADDRA DC    A(0)                    UCB ADDRESS FROM UCBLOOK
RETCODE  DC    F'0'                    RETURN CODE FROM UCBLOOK
RSNCODE  DC    F'0'                    REASON CODE FROM UCBLOOK
*
WORKAREA DS    CL100                   UCBSCAN WORK AREA
UCBAREA  DS    CL48                    UCB COMMON AND DEV DEP SEGMENTS
DEVNCHAR DS    CL4                     UCB DEVICE NUMBER
CMXTAREA DS    CL32                    UCB COMMON EXTENSION
UCBPAREA DS    CL48                    UCB PREFIX
DCEAREA  DS    CL256                   UCB DEVICE CLASS EXTENSION
*
BLANKS   DC    CL133' '                BLANKS
TRCHAR   DC    CL16'0123456789ABCDEF'  CHARACTER TRANSLATION TABLE
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DC    CL1' '                  CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
PRTNBR   DC    CL4' '                  DEVICE NUMBER
         DC    CL1' '
PRTVOL   DC    CL6' '                  VOLUME SERIAL
         DC    CL1' '
PRTWHERE DC    CL5' '                  UCB LOCATION
         DC    CL1' '
PRTADDR  DC    CL8' '                  UCB EXTENSION ADDRESS
         DC    CL1' '
PRTR15L  DC    CL8' '                  LITERAL "RETCODE="
PRTR15V  DC    CL8' '                  R15 VALUE FROM UCBLOOK
         DC    CL1' '
PRTR0L   DC    CL8' '                  LITERAL "RSNCODE="
PRTR0V   DC    CL8' '                  R0 VALUE FROM UCBLOOK
         DC    CL106' '
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
         LTORG
*********************************************************************
*             MAPPING DSECTS                                        *
*********************************************************************
        DSECT
        IEFUCBOB LIST=YES,PREFIX=YES,DEVCLAS=DA
UCBSCAN CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** TEST CALLS TO THE IEFSD095 SUBROUTINE ***'
CALL095 CSECT
*********************************************************************
* SETS UP AND EXECUTES CALLS TO IEFSD095 TO PERMIT ANALYSIS OF THE  *
* IEFSD095 SUBROUTINE IN AN XPEDITER ENVIRONMENT.                   *
*                                                                   *
* AUTHOR R THORNTON NOV 2001                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING CALL095,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'CALL095'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES, PRINT HEADING                                  *
*********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT)         OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE CALL  IEFSD095,(STRING,LINENO,PRT,STRGLEN) CALL IEFSD095
         L     R1,LINENO               PICK UP LINE NUMBER
         CH    R1,H12                  FINISHED 12 LINES?
         BH    EOJ                     YES, ALL THROUGH
         LA    R1,1(R1)                ADD 1 TO LINE NUMBER
         ST    R1,LINENO               UPDATE LINE NUMBER
         BAS   R9,PRINT                PRINT THE LINE FROM IEFSD095
         B     MAINLINE                LOOP 12 TIMES
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      RETURN
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE PRINT1                  CLOSE FILE
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 RETURN CODE = 0
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
H12      DC    H'12'                   CONSTANT 12
STRING   DC    CL8'BC0THORA'           STRING TO BE TRANSLATED
LINENO   DC    F'1'                    LINE NUMBER TO BE RETURNED
STRGLEN  DC    A(L'STRING)             LENGTH OF STRING TO TRANSLATE
BLANKS   DC    CL133' '                BLANKS
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DS    CL132
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
         LTORG
CALL095 CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** CTLGLIST: CONCISE CATALOG LISTING  ***'
***********************************************************************
* READS THE SYSPRINT OUTPUT DATASET CREATED BY AN IDCAMS RUN USING A  *
* A CONTROL CARD SUCH AS:                                             *
*              LISTCAT CATALOG(SYS1.CAT.SP4RES) VOL                   *
* THE PRINT  FILE CONTAINS ONE RECORD FOR EACH CLUSTER, DATA COMP.,   *
* INDEX COMP., NONVSAM DATASET, GDG BASE, GENERATION DATASET, USER    *
* CATALOG, AND PAGESPACE. THE OUT FILE RECORDS CONTAIN THE ENTRY      *
* TYPE, ENTRY NAME, AND VOLUME SERIAL (WHERE APPROPRIATE).            *
*                                                                     *
* AUTHOR R THORNTON MAY 1993                                          *
***********************************************************************
*
CTLGLIST SVLNK R3
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE GET   INPUT1,INRECD           READ NEXT RECORD
         LH    R2,INLENG               GET RECORD LENGTH
         CH    R2,=H'22'               SHORT RECORD?
         BL    MAINLINE                YES, IGNORE IT
         SH    R2,=H'22'               COMPUTE NAME FIELD LENGTH CODE
         CLI   INCC,C'0'               DOUBLESPACED LINE?
         BNE   TRYVOL                  NO
         MVC   VOLSER,=CL6'      '     CLEAR VOLSER
TRYCLSTR CLC   INREC(16),=CL16'CLUSTER ------- '
         BNE   TRYDATA                 NOT CLUSTER
         MVC   TYPE,=CL8'VSAMCLST'     SET ELEMENT TYPE
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         B     PUTOUT                  GO WRITE A RECORD
TRYDATA  CLC   INREC(16),=CL16'   DATA ------- '
         BNE   TRYINDX                 NOT DATA COMPONENT
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE+4(4),=CL4'DATA'    SET ELEMENT TYPE
         MVI   NEEDVOL,1               SHOW VOLSER NEEDED
         B     MAINLINE                CONTINUE
TRYINDX  CLC   INREC(16),=CL16'   INDEX------- '
         BNE   TRYNONVS                NOT INDEX COMPONENT
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE+4(4),=CL4'INDX'    SET ELEMENT TYPE
         MVI   NEEDVOL,1               SHOW VOLSER NEEDED
         B     MAINLINE                CONTINUE
TRYNONVS CLC   INREC(16),=CL16'NONVSAM ------- '
         BNE   TRYGDGBS                NOT NONVSAM
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE,=CL8'NONVSAM '     SET ELEMENT TYPE
         MVI   NEEDVOL,1               SHOW VOLSER NEEDED
         B     MAINLINE                CONTINUE
TRYGDGBS CLC   INREC(16),=CL16'GDG BASE ------ '
         BNE   TRYGENER                NOT GDG BASE
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE,=CL8'GDGBASE '     SET ELEMENT TYPE
         MVC   VOLSER,=CL6'      '     CLEAR VOLSER
         B     PUTOUT                  GO WRITE A RECORD
TRYGENER CLC   INREC(16),=CL16'   NONVSAM ---- '
         BNE   TRYUCAT                 NOT GENERATION DATASET
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE,=CL8'GENERATN'     SET ELEMENT TYPE
         MVI   NEEDVOL,1               SHOW VOLSER NEEDED
         B     MAINLINE                CONTINUE
TRYUCAT  CLC   INREC(16),=CL16'USERCATALOG --- '
         BNE   TRYPAGE                 NOT USER CATALOG
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE,=CL8'USERCTLG'     SET ELEMENT TYPE
         MVI   NEEDVOL,1               SHOW VOLSER NEEDED
         B     MAINLINE                CONTINUE
TRYPAGE  CLC   INREC(16),=CL16'PAGESPACE ----- '
         BNE   TRYVOL                  NOT PAGESPACE
         MVC   NAME,=CL44' '           CLEAR ELEMENT NAME
         EX    R2,MVCNAME              MOVE THE ELEMENT NAME
         MVC   TYPE,=CL8'USERCTLG'     SET ELEMENT TYPE
         MVC   VOLSER,=CL6'      '     CLEAR VOLSER
         B     PUTOUT                  GO WRITE A RECORD
TRYVOL   CLI   NEEDVOL,1               LOOKING FOR A VOLUME SERIAL?
         BNE   MAINLINE                NO
         CLC   INREC(16),=CL16'       VOLSER---'
         BNE   MAINLINE                NOT VOLUME
         MVI   NEEDVOL,0               CLEAR VOLSER SWITCH
         MVC   VOLSER,INREC+25         MOVE THE ELEMENT NAME
PUTOUT   PUT   PRINT1,PRT              PRINT THE LINE
         B     MAINLINE                CONTINUE
MVCNAME  MVC   NAME(1),INREC+16        MOVE THE ELEMENT NAME (EXECUTED)
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
RETCODE  DC    F'0'                    RETURN CODE
NEEDVOL  DC    XL1'0'                  NEED VOLSER SWITCH
*
PRT      DS    0CL132                  PRINT DATA
TYPE     DC    CL8' '
         DC    CL2' '
NAME     DC    CL44' '
         DC    CL2' '
VOLSER   DC    CL6' '
         DC    CL132' '
*
INRECD   DS    0CL125                   INPUT (SYSPRINT) RECORD
INRDW    DS    0XL4                     RDW FOR VARIABLE RECORD
INLENG   DS    XL2                          RECORD LENGTH
         DS    XL2                          ZEROS
INCC     DS    CL1                      CARRIAGE CONTROL CHARACTER
INREC    DS    0CL120                   INPUT RECORD
         DS    CL120
*
OUTREC   DS    0CL80                    OUTPUT RECORD
         DS    CL80
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=VBA,LRECL=125
         END

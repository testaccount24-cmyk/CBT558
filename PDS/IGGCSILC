*********************************************************************
* THIS PROGRAM WAS INITIALLY OBTAINED FROM SYS1.SAMPLIB(IGGCSILC)   *
* TO USE AS THE BASIS FOR OTHER CATALOG SEARCH PROGRAMS. IGGCSILC   *
* USE IS DOCUMENTED IN THE MANAGING CATALOGS IBM MANUAL.            *
*********************************************************************
*********************************************************************
*                                                                   *
*  PROGRAM NAME: IGGCSILC                                           *
*                                                                   *
*  DESCRIPTION: THIS PROGRAM PRODUCES OUTPUT SIMILAR TO IDCAMS      *
*               LISTCAT NAME. IT IS FASTER THAN IDCAMS LISTCAT.     *
*                                                                   *
*        INPUT: SYSIN DD (80 BYTE RECORD)                           *
*               CATALOG NAME, LEFT JUSTIFIED TO FIRST POSITION      *
*               MORE THAN ONE RECORD CAN BE SUPPLIED                *
*               PROCESSING ENDS AT END OF DATA                      *
*                                                                   *
*       OUTPUT: SYSOUT DD                                           *
*               LIST OF ENTRY NAMES CONTAINED IN THE CATALOG        *
*               SUMMARY OF NUMBER OF EACH ENTRY TYPE FOUND          *
*               TOTAL OF ALL ENTRIES FOUND IN THE CATALOG           *
*                                                                   *
* REQUIREMENTS: DFP 3.1.0 OR A LATER VERSION IS NECESSARY IN        *
*               ORDER TO RUN THIS PROGRAM                           *
*                                                                   *
*********************************************************************
IGGCSILC CSECT
IGGCSILC AMODE 24
IGGCSILC RMODE 24
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         OPEN  (PUTDCB,OUTPUT)
         OPEN  (INDCB,INPUT)
         ZAP   PAGECNT,ZERO                 INITILIZE THE PAGE COUNT
NEXTCATN EQU   *
         GET   INDCB,DATAREC                GET CATALOG NAME
         ZAP   AIX,ZERO                     INIT DATA
         ZAP   ALIAS,ZERO                   INIT DATA
         ZAP   CLUSTER,ZERO                 INIT DATA
         ZAP   DATA,ZERO                    INIT DATA
         ZAP   GDG,ZERO                     INIT DATA
         ZAP   INDEX,ZERO                   INIT DATA
         ZAP   NONVSAM,ZERO                 INIT DATA
         ZAP   PAGESPAC,ZERO                INIT DATA
         ZAP   PATH,ZERO                    INIT DATA
         ZAP   USERCAT,ZERO                 INIT DATA
         ZAP   TOTAL,ZERO                   INIT DATA
         MVC   CSICATNM(44),DATAREC         MOVE CATALOG NAME
CATLINE  EQU   *
         ZAP   LINECNT,LINELMT              FORCE FIRST PAGE
         MVC   OUTCAT(44),DATAREC           MOVE CATALOG NAME
NEXTRCD  EQU   *
         LA    1,PARMLIST
         CALL  IGGCSI00
         LTR   15,15                        TEST RETURN CODE
         BZ    NORTCODE                     IF ZERO BYPASS CONVERSION
         CLC   PARMRC(4),FENTERR
         BE    NORTCODE
         MVC   RETDATA(4),PARMRC            MOVE CODES TO WORK AREA
         BAL   3,ERRCNVRT
         B     ENDRTN
NORTCODE EQU   *
         USING DATARET,5
         LA    5,DATAAREA                   LOAD DSECT REG
         L     1,DRETCD                     GET RETURN CODE
         LTR   1,1                          TEST RETURN CODE
         BZ    NEXTFLD                      CONTINUE IF NO ERRORS
         MVC   RETDATA(4),DRETCD            MOVE CODES TO WORK AREA
         BAL   3,ERRCNVRT                   BRANCH TO ERROR CONVERT
         B     ENDRTN
NEXTFLD  EQU *
         LA    4,DATAEND                    GET BEGINNING OF INFO
         LA    7,64                         LENGTH OF ENTRY DATA
         USING ENTRY,4
NEXTENT  EQU   *
         TM    EFLAG,X'40'                  DID ERROR OCCUR FOR ENTRY
         BO    ERRDET                        YES
         B     AIXENT                        NO
ERRDET   EQU   *                            ERROR DETECTED
         MVC   RETDATA(4),EERRCOD           MOVE CODES TO WORK AREA
         BAL   3,ERRCNVRT                   BRANCH TO ERROR CONVERT
         LA    1,50                         ADD ENTRY HDR LENGTH
         AR    7,1                          ADD ENTRY DATA LEN
         AR    4,1                          ADD ENTRY DATA LEN
         C     7,DUSEDLEN                   COMPARE USED TO CALC LEN
         BNM   NEXTRESM                     IF GT OR EQ  RESUME TEST
         B     NEXTENT                      NEXT ENTRY
AIXENT   EQU   *
         CLI   ETYPE,C'G'                   IF AIX     TYPE
         BNE   ALIASENT                       NO
         AP    AIX,ONE                       ADD ONE TO COUNT
         MVC   TITLEOUT(14),AIXOUT           MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
ALIASENT EQU   *
         CLI   ETYPE,C'X'                   IF ALIAS   TYPE
         BNE   CLUSTENT                       NO
         AP    ALIAS,ONE                     ADD ONE TO COUNT
         MVC   TITLEOUT(14),ALIASOUT         MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
CLUSTENT EQU   *
         CLI   ETYPE,C'C'                   IF CLUSTER TYPE
         BNE   DATAENT                        NO
         TM    ECATTR,X'01'                 TEST FOR PAGESPACE
         BO    PAGEENT
         AP    CLUSTER,ONE                   ADD ONE TO COUNT
         MVC   TITLEOUT(14),CLUSTOUT         MOVE TYPE INFO
         CLI   ENAME,X'00'                  IF CATALOG NAME
         BNE   NOTCAT                         NO NEXT KEY
         MVI   OUTNAME,C'0'                 FAKE CATALOG NAME
         MVC   OUTNAME+1(43),OUTNAME          OF ZEROES
         B     CONTCAT
NOTCAT   EQU   *
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
CONTCAT  EQU   *
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
DATAENT  EQU   *
         CLI   ETYPE,C'D'                   IF DATA    TYPE
         BNE   GDGENT                         NO
         AP    DATA,ONE                      ADD ONE TO COUNT
         MVC   TITLEOUT(14),DATAOUTX        MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
GDGENT   EQU   *
         CLI   ETYPE,C'B'                   IF GDG     TYPE
         BNE   INDEXENT                       NO
         AP    GDG,ONE                       ADD ONE TO COUNT
         MVC   TITLEOUT(14),GDGOUT          MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
INDEXENT EQU   *
         CLI   ETYPE,C'I'                   IF INDEX   TYPE
         BNE   NVSAMENT                       NO
         AP    INDEX,ONE                     ADD ONE TO COUNT
         MVC   TITLEOUT(14),INDEXOUX        MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
NVSAMENT EQU   *
         CLI   ETYPE,C'A'                   IF NONVSAM TYPE
         BE    NVSMCONT                       YES
         CLI   ETYPE,C'H'                   IF GDS COUNTS AS NONVSAM
         BNE   PATHENT                        NO
NVSMCONT EQU   *
         AP    NONVSAM,ONE                   ADD ONE TO COUNT
         MVC   TITLEOUT(14),NONVOUT         MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
PAGEENT  EQU   *
         AP    PAGESPAC,ONE                  ADD ONE TO COUNT
         MVC   TITLEOUT(14),PAGEOUT          MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
PATHENT  EQU   *
         CLI   ETYPE,C'R'                   IF PATH    TYPE
         BNE   USERENT                        NO
         AP    PATH,ONE                      ADD ONE TO COUNT
         MVC   TITLEOUT(14),PATHOUT          MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
         B     NEXTKEY                      CONTINUE
USERENT  EQU   *
         CLI   ETYPE,C'U'                   IF USERCAT TYPE
         BNE   NEXTKEY                        NO
         AP    USERCAT,ONE                   ADD ONE TO COUNT
         MVC   TITLEOUT(14),USEROUT          MOVE TYPE INFO
         MVC   OUTNAME(44),ENAME             MOVE NAME INFO
         BAL   3,PRINTNAM                   PRINT NAME OF ENTRY
NEXTKEY  EQU   *                            CALCULATE NEXT KEY POSITION
         SR    1,1                          CLEAR REG 1
         LH    1,EDATALN                    INSERT ENTRY DATA LEN
         LA    1,46(1)                      ADD ENTRY HDR LENGTH
         AR    7,1                          ADD ENTRY DATA LEN
         AR    4,1                          ADD ENTRY DATA LEN
         C     7,DUSEDLEN                   COMPARE USED TO CALC LEN
         BNM   NEXTRESM                     IF GT OR EQ  RESUME TEST
         B     NEXTENT                      NEXT ENTRY
NEXTRESM CLI   CSIRESUM,C'Y'                IF MORE ENTRIES
         BE    NEXTRCD                      NEXT RECORD
ENDRTN   EQU   *                            ELSE FINISH
         CP    LINECNT,ENDNUM               COMPARE TO SEE IF SUMMARY
         BL    SUMFITS                        FITS ON PAGE
         ZAP   LINECNT,LINELMT              FORCE NEW PAGE
SUMFITS  EQU   *
         MVI   OUTPUT,C' '
         MVC   OUTPUTP1(131),OUTPUT         CLEAR LINE
         MVC   OUTPUTP1(45),OUTLINE3
         BAL   2,PRINTRTN                   BRANCH & LINK TO PRINT
         LA    10,AIX                       CALCULATE TOTAL ENTRIES
         LA    8,10
TOTLOOP  EQU   *
         AP    TOTAL,0(5,10)                ADD ENTRY COUNT TO TOTAL
         LA    10,5(10)                     ADD INDEX FOR NEXT COUNT
         BCT   8,TOTLOOP
         LA    9,AIXOUT
         LA    10,AIX
         LA    8,11
PRTLOOP  EQU   *                            SET UP ENTRY COUNT
         BAL   3,PRINTCNT                   PRINT COUNT
         LA    9,14(9)                      ADD INDEX FOR NEXT LITERAL
         LA    10,5(10)                     ADD INDEX FOR NEXT COUNT
         BCT   8,PRTLOOP
         B     NEXTCATN
CLOSEEND EQU   *
         CLOSE (PUTDCB,)
         CLOSE (INDCB,)
         L     13,SAVE+4
         L     14,12(13)
         LM    00,12,20(13)
         BR    14
*
*        PRINT THE ENTRY COUNT LINE ROUTINE
*
PRINTCNT EQU   *
         MVI   OUTPUT,C' '
         MVC   OUTPUTP1(131),OUTPUT
         MVC   OUTCNT,PATTERN               SET UP THE PATTERN
         ED    OUTCNT,0(10)                 EDIT AMOUNT
         MVC   TITLECNT(14),0(9)
         MVC   OUTPUTP1(43),OUTLINE4
         BAL   2,PRINTRTN
         BR    3
*
*        PRINT THE ENTRY NAME ROUTINE
*
PRINTNAM EQU   *
         MVI   OUTPUT,C' '
         MVC   OUTPUTP1(131),OUTPUT
         MVC   OUTPUTP1(59),OUTLINE2
         BAL   2,PRINTRTN
         BR    3
*
*        CONVERT RETURN REASON CODES AND PRINT ROUTINE
*
ERRCNVRT EQU *
         MVI   OUTPUT,C' '
         MVC   OUTPUT+1(131),OUTPUT
         SR    2,2
         IC    2,RETDATA+3
         CVD   2,DECIMAL
         UNPK  UNPKAREA(4),DECIMAL+6(2)     CONVERT RETURN CODE
         OI    UNPKAREA+3,X'F0'
         MVC   RTN(3),UNPKAREA+1
         SR    2,2                          CONVERT REASON CODE
         IC    2,RETDATA+2
         CVD   2,DECIMAL
         UNPK  UNPKAREA(4),DECIMAL+6(2)
         OI    UNPKAREA+3,X'F0'
         MVC   RSN(3),UNPKAREA+1
         MVC   MODIDO(2),RETDATA             MOVE MODULE ID
         MVC   ERRENT,ENAME
         MVC   OUTPUTP1(105),ERRORMSG
         BAL   2,PRINTRTN
         BR    3
*
*        PRINT OUTPUT LINE ROUTINE
*
PRINTRTN EQU *
         CP    LINECNT,LINELMT              LINECNT REACHED LINE LIMIT?
         BL    PUTLINE                      IF NOT WRITE LINE
         AP    PAGECNT,ONE                  ADD ONE TO PAGE CNT
         MVC   PAGEPRT,PATTERN2             MOVE PATTERN FOR CONVERSION
         ED    PAGEPRT,PAGECNT              CONVERT PAGE COUNT
         PUT   PUTDCB,PAGELINE              PUT LINE
         MVI   OUTPUT1,C' '
         MVC   OUTPUT11(132),OUTPUT1
         MVC   OUTPUT11(97),OUTLINE1
         PUT   PUTDCB,OUTPUT1               PUT LINE
         ZAP   LINECNT,TWO                  PUT ONE IN LINE COUNT
PUTLINE  EQU   *
         MVI   OUTPUT,C' '                  ADVANCE LINE
         PUT   PUTDCB,OUTPUT                PUT LINE
         AP    LINECNT,ONE                  ADD ONE TO LINE COUNT
         BR    2
*
         DS    0D
ZERO     DC    PL1'0'                    PACKED ZERO
ONE      DC    PL1'1'                    PACKED ONE
TWO      DC    PL1'2'                    PACKED ONE
ENDNUM   DC    PL2'15'                   ENDING NUMBER OF LINES
PAGECNT  DC    PL3'0'                    PAGE COUNT
LINECNT  DC    PL2'0'                    LINE COUNT
LINELMT  DC    PL2'26'                   LINE LIMIT
AIX      DC    PL5'0'                    AIX COUNT
ALIAS    DC    PL5'0'                    ALIAS COUNT
CLUSTER  DC    PL5'0'                    CLUSTER COUNT
DATA     DC    PL5'0'                    DATA COUNT
GDG      DC    PL5'0'                    GDG COUNT
INDEX    DC    PL5'0'                    INDEX COUNT
NONVSAM  DC    PL5'0'                    NONVSAM COUNT
PAGESPAC DC    PL5'0'                    PAGESPACE COUNT
PATH     DC    PL5'0'                    PATH COUNT
USERCAT  DC    PL5'0'                    USER CATALOG COUNT
TOTAL    DC    PL5'0'                    TOTAL COUNT
*
**********************************************************************
*                                                                    *
* PARAMETER LIST FOR IGGCSI00 INVOCATION                             *
*                                                                    *
**********************************************************************
*
PARMLIST DS    0D
         DC    A(MODRSNRT)                MODULE/REASON/RETURN
         DC    A(CSIFIELD)
         DC    A(DATAAREA)
*
*
*
**********************************************************************
*                                                                    *
* MODULE ID/REASON CODE/RETURN CODE                                  *
*                                                                    *
**********************************************************************
*
MODRSNRT    DS 0F
PARMRC      DS 0CL4
MODID       DC XL2'0000'     MODULE ID
RSNCODE     DC XL1'00'       REASON CODE
RTNCODE     DC XL1'00'       RETURN CODE
**********************************************************************
*                                                                    *
* PARAMETER FIELDS FOR CATALOG SEARCH INTERFACE (CSI)                *
*                                                                    *
**********************************************************************
*
CSIFIELD    DS 0F
CSIFILTK    DC CL44'**'  FILTER   KEY
CSICATNM    DC CL44' '        CATALOG NAME OR BLANKS
CSIRESNM    DC CL44' '        RESUME NAME OR BLANKS
CSIDTYPD    DS 0CL16          ENTRY TYPES
CSIDTYPS    DC CL16'                '
CSIOPTS     DS 0CL4           CSI OPTIONS
CSICLDI     DC CL1'Y'         RETURN D&I IF C A MATCH Y OR BLNK
CSIRESUM    DC CL1' '         RESUME FLAG         Y OR BLANK
CSIS1CAT    DC CL1'Y'         SEARCH CATALOG      Y OR BLANK
CSIRESRV    DC XL1'00'        RESERVED
CSINUMEN    DC H'1'           NUMBER OF ENTRIES FOLLOWING
CSIENTS     DS 0CL8           VARIABLE NUMBER OF ENTRIES FOLLOW
CSIFLDNM    DC CL8'CATTR   '  FIELD NAME
*
ERRORMSG DS    0CL105
         DC    CL7' ENTRY '
ERRENT   DS    CL44
         DC    CL22' RECEIVED RETURN CODE '
RTN      DS    CL3
         DC    CL13' REASON CODE '
RSN      DS    CL3
         DC    CL11' MODULE ID '
MODIDO   DS    CL2
OUTLINE1 DS    0CL97
         DC    CL29' '
         DC    CL24'LISTING FROM CATALOG -- '
OUTCAT   DS    CL44
*
OUTLINE2 DS    0CL59
TITLEOUT DS    CL14
         DC    CL1' '
OUTNAME  DS    CL44
*
AIXOUT   DC    CL14'AIX ----------'
ALIASOUT DC    CL14'ALIAS --------'
CLUSTOUT DC    CL14'CLUSTER ------'
DATAOUT  DC    CL14'DATA ---------'
GDGOUT   DC    CL14'GDG ----------'
INDEXOUT DC    CL14'INDEX --------'
NONVOUT  DC    CL14'NONVSAM ------'
PAGEOUT  DC    CL14'PAGESPACE ----'
PATHOUT  DC    CL14'PATH ---------'
USEROUT  DC    CL14'USERCATALOG --'
TOTALOUT DC    CL14'TOTAL --------'
*
DATAOUTX DC    CL14'   DATA ------'
INDEXOUX DC    CL14'  INDEX ------'
CATNAM   DC    CL44'0'
*
OUTLINE3 DS    0CL45
         DC    CL9' '
         DC    CL36'THE NUMBER OF ENTRIES PROCESSED WAS:'
*
OUTLINE4 DS    0CL43
         DC    CL19' '
TITLECNT DS    CL14
OUTCNT   DS    CL10
PAGELINE DS    0CL132
         DC    CL1'1'
         DC    CL100' '
         DC    CL5'PAGE '
PAGEPRT  DS    CL8
         DC    CL19' '
*
DATAREC  DS    CL80
SAVE     DS    18F
PUTDCB   DCB   MACRF=PM,DSORG=PS,DDNAME=SYSOUT,RECFM=FA,               X
               BLKSIZE=133
INDCB    DCB   MACRF=GM,DSORG=PS,DDNAME=SYSIN,LRECL=80,RECFM=FB,       X
               BLKSIZE=800,EODAD=CLOSEEND
SAVE15   DC    F'0'
RETDATA  DC    F'0'
DECIMAL  DC    D'0'
UNPKAREA DC    XL4'0'
UNPK1    DC    XL11'0'
PATTERN  DC    XL10'60202020202020202120'
PATTERN2 DC    XL8'40202020202120'
OUTPUT   DS    CL1
OUTPUTP1 DS    132CL1' '
OUTPUT1  DS    CL1
OUTPUT11 DS    132CL1' '
         DS    0F
*
NOTFND   DC    XL4'C5C72A08'            NOT FOUND CODE FROM CATALOG
FENTERR  DC    XL4'C6E20464'            FIELD ENTRY ERROR CODE FROM CAT
DATAAREA DS    0F
         DC    F'65535'
         DS    XL65535
DATARET  DSECT
DWORKLEN DS    F
DREQLEN  DS    F
DUSEDLEN DS    F
DPFPLS   DS    H
DCATFLGS DS    CL1
DCATTYPE DS    CL1
DCATNAME DS    CL44
DRETCD   DS    0CL1
DMODID   DS    CL2
DRSNCOD  DS    CL1
DRETCOD  DS    CL1
DATAEND  DS    0F
ENTRY    DSECT
EFLAG    DS    XL1
ETYPE    DS    XL1
ENAME    DS    CL44
EERRCOD  DS    0XL4
EDATALN  DS    XL2
EFLD1LN  DS    XL2
EFLD2LN  DS    XL2
EFLD3LN  DS    XL2
ECATTR   DS    XL1
ENTEND   DS    0XL1
         END   IGGCSILC

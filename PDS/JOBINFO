*        PRINT NOGEN
         TITLE '***** PASS JOBNAME AND JOB NUMBER TO CALLER *****'
JOBINFO CSECT
JOBINFO AMODE 31
JOBINFO RMODE ANY
***********************************************************************
* ENTERED FROM A CALLING PROGRAM WHICH MAY RESIDE ABOVE OR BELOW THE  *
* 16M LINE. PASSES BACK THE JOB NAME (8) BYTES FOLLOWED BY THE JES2   *
* JOB NUMBER FIELD (8 BYTES) IN A 16-BYTE FIELD SUPPLIED BY THE       *
* CALLER. THE JOB NUMBER FIELD CONTAINS THE LITERAL "JOB" FOLLOWED BY *
* THE 5 DIGIT JOB NUMBER AS PRINTABLE DECIMAL CHARACTERS.             *
* JOB NAME IS TAKEN FROM THE TIOT, WHILE JOB NUMBER IS FROM THE SSIB. *
*                                                                     *
* CALLING EXAMPLE (FROM A COBOL PROGRAM):                             *
*                                                                     *
*       01  JOB-INFO-AREA.                                            *
*           05  JOB-NAME          PIC X(8).                           *
*           05  JOB-NUMBER        PIC X(8).                           *
*                                                                     *
*       CALL 'JOBINFO' USING JOB-INFO-AREA.                           *
*                                                                     *
* AUTHOR R THORNTON JAN 1996                                          *
***********************************************************************
         USING JOBINFO,R15
         STM   R14,R12,12(R13)         SAVE CALLERS REGS
         L     R12,0(R1)               POINT TO CALLER'S RETURN AREA
         LA    R1,IN31BIT              PUT PSW
         O     R1,HIBITON                 INTO 31-BIT
         BSM   0,R1                          ADDRESSING MODE
IN31BIT  LR    R3,R15                  COPY BASE REG
         DROP  R15
         USING JOBINFO,R3
         STORAGE OBTAIN,LENGTH=72,ADDR=(1),SP=0,LOC=ANY GET SAVE AREA
         ST    R1,8(R13)               SET LSA
         ST    R13,4(R1)               SET FSA
         LR    R13,R1                  SET UP SAVE POINTER
MAINLINE L     R1,16                   POINT TO CVT
         L     R1,0(R1)                POINT TO NEW-OLD WORDS
         L     R1,0(R1)                POINT TO TCB
         L     R2,12(R1)               POINT TO TIOT
         MVC   0(8,R12),0(R2)          MOVE JOB NAME TO CALLER'S AREA
         L     R2,180(R1)              POINT TO JSCB
         L     R2,316(R2)              POINT TO SSIB
         MVC   8(8,R12),12(R2)         MOVE JOB NUMBER TO CALLER AREA
RETURN   L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         STORAGE RELEASE,LENGTH=72,ADDR=8(R13),SP=0 FREE SAVE AREA
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BSM   0,R14                   RETURN TO CALLER
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
HIBITON  DC    A(X'80000000')              WORD WITH HIGH ORDER BIT ON
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** ALIASES: PRINTS REAL NAME, VOLSER FOR ALIAS ***'
ALIASES CSECT
**********************************************************************
* READS THE SYSIN FILE TO OBTAIN A DATASET NAME. FOR EACH DSNAME,    *
* 1) DYNAMICALLY ALLOCATE IT TO DDNAME INPUT1                        *
* 2) ISSUE RDJFCB FOR INPUT1                                         *
* 3) PRINT THE ORIGINAL DSNAME, JFCB DSNAME, AND JFCB VOLSER         *
*                                                                    *
* THE SYSIN DATASET MUST BE PHYSICALLY SEQUENTIAL WITH FIXED LENGTH  *
* 80-BYTE RECORDS. IF COLUMN 1 CONTAINS AN ASTERISK, THE ENTIRE      *
* RECORD IS A COMMENT. DSNAME RECORDS CONTAIN A DSNAME LEFT JUSTIFIED*
* WITH TRAILING SPACES IN COLUMNS 1-44. DATASETS NAMED IN THESE      *
* RECORDS MUST BE PHYSICALLY SEQUENTIAL AND MUST BE CATALOGED.       *
*                                                                    *
* AUTHOR R THORNTON MAR 2000                                         *
**********************************************************************
*
*********************************************************************
* SAVE CALLER'S REGS, SET UP BASE REGS, CHAIN SAVE AREAS            *
*********************************************************************
         USING ALIASES,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'ALIASES'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
* OPEN THE FILES FOR PROCESSING                                     *
*********************************************************************
OPENS    OPEN  (SYSPRINT,OUTPUT,SYSIN) OPEN FILES
*********************************************************************
* MAINLINE PROCESSING: READ THE NEXT SYSIN FILE RECORD. FOR EACH    *
* RECORD READ, CALL ALLOCDYN TO ALLOCATE IT TO DDNAME INPUT1, THEN  *
* ISSUE RDJFCB AND PRINT RESULTS. WHEN THE SYSIN FILE IS EXHAUSTED, *
* GO TO EOJ.                                                        *
*********************************************************************
MAINLINE GET   SYSIN,INREC             READ NEXT INPUT RECORD
         CLI   INREC,C'*'              COMMENT?
         BE    MAINLINE                YES, IGNORE IT
         MVC   PRTDSNAM,INDSNAME       INPUT DSNAME TO PRINT LINE
         MVC   PRTREAL,INDSNAME        INPUT DSNAME FOR LOCATE
         LOCATE BYDSN                  READ CATALOG FOR SPEC DSNAME
         LTR   R15,R15                 SUCCESSFUL CATALOG READ?
         BNZ   LOCFAIL                 NO
         MVC   PRTVOL,LOCAREA+6        VOLSER FROM LOCATE AREA
         PUT   SYSPRINT,PRTLINE        PRINT THE LINE
         B     MAINLINE                GO READ NEXT SYSIN RECORD
LOCFAIL  MVC   PRTREAL(33),=CL33'***LOCATE FAILED FOR THIS DATASET'
         PUT   SYSPRINT,PRTLINE        PRINT ERROR MESSAGE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         B     MAINLINE                GO GET NEXT SYSIN RECORD
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (SYSPRINT,,SYSIN)       CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS 14-12
         SR    R15,R15                 SET RC=0
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
ERROR    DC    X'00'                   ERROR FOUND BYTE
BLANKS   DC    CL133' '                CONSTANT BLANKS
*
INREC    DS    0CL80                   SYSIN RECORD
INDSNAME DS    CL44                       DSNAME
         DS    CL36
*
PRTLINE  DS    0CL133                  SYSPRINT RECORD
PCC      DS    CL1                        CARRIAGE CONTROL BYTE
PRT      DS    0CL132
PRTVOL   DS    CL6                        VOLUME SERIAL NUMBER
         DS    CL1
PRTDSNAM DS    CL44                       DSNAME FROM INREC COL 1-44
         DS    CL1
PRTREAL  DS    CL44                       REAL DATASET NAME
         DS    CL36
*
BYDSN    CAMLST NAME,PRTREAL,,LOCAREA READ CATALOG LIST
LOCAREA  DS    CL265                   CATALOG INFORMATION AREA
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
         LTORG
ALIASES CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

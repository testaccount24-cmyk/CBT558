         TITLE '*** GENDATES: CREATE DATE TEST FILE FOR CALLGND2 *****'
GENDATES CSECT
*********************************************************************
* CREATES TWO FILES OF TEST DATES FOR USE BY THE CALLGND2 PROGRAM   *
* IN TESTING CHANGES TO THE GENLDAT2 SUBROUTINE. RECORDS IN THE GREG*
* FILE HAVE A 'G' IN COLUMN 1, AND THE DATE IN COLUMNS 2-9 IN THE   *
* MMDDYYYY FORMAT. IN THE GREG FILE, YEARS RANGE FROM 1899 TO 2100, *
* MONTHS RANGE FROM 00 THRU 13, AND DAYS RANGE FROM 00 TO 32. IN THE*
* JULIAN FILE RECORDS CONTAIN A 'J' IN COLUMN 1, AND THE DATE IN    *
* COLUMNS 2-8 IN THE FORM YYYYDDD. IN THE JULIAN FILE, YEARS RANGE  *
* FROM 1899 TO 2100 AND DAYS FROM 000 THROUGH 367.                  *
*                                                                   *
* AUTHOR R THORNTON AUG 2000                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING GENDATES,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'GENDATES'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN OUTPUT FILES                                          *
*********************************************************************
OPENS    OPEN  (GREG,OUTPUT,JULIAN,OUTPUT) OPEN FILES
         MVC   OUTREC,BLANKS           CLEAR OUTPUT RECORD
         MVI   OUTTYPE,C'G'            SHOW GREGORIAN DATE
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
TESTYEAR CP    THISYEAR,=P'2101'       ALL DONE?
         BE    STRTJUL                 YES
TESTMO   CP    THISMO,=P'14'           MONTH OVERFLOW?
         BL    TESTDAY                 NO
         AP    THISYEAR,=P'1'          YES, INCREMENT YEAR
         ZAP   THISMO,=P'0'            SET MONTH=00
         ZAP   THISDAY,=P'0'           SET DAY=00
         B     TESTYEAR                GO CHECK END
TESTDAY  CP    THISDAY,=P'33'          DAY OVERFLOW?
         BL    GREGDATE                NO, GO GENERATE RECORD
         AP    THISMO,=P'1'            YES, ADD 1 TO MONTH
         ZAP   THISDAY,=P'0'           SET DAY=00
         B     TESTMO                  GO CHECK MONTH OVERFLOW
GREGDATE UNPK  OUTGYR,THISYEAR         MOVE YEAR
         OI    OUTGYR+3,X'F0'          SET SIGN
         UNPK  OUTGMO,THISMO           MOVE MONTH
         OI    OUTGMO+1,X'F0'          SET SIGN
         UNPK  OUTGDAY,THISDAY         MOVE DAY
         OI    OUTGDAY+1,X'F0'         SET SIGN
         PUT   GREG,OUTREC             WRITE THE RECORD
         AP    THISDAY,=P'1'           ADD 1 TO DAY
         B     TESTDAY                 AND CONTINUE
STRTJUL  ZAP   THISYEAR,=PL3'1899'     SET YEAR TO 1899
         ZAP   THISDAY,=P'0'           SET DAY TO 000
         MVC   OUTREC,BLANKS           CLEAR OUTPUT RECORD
         MVI   OUTTYPE,C'J'            SET TYPE=J
TESTJYR  CP    THISYEAR,=P'2101'       PAST DATE RANGE?
         BE    EOJ                     YES
TESTJDAY CP    THISDAY,=P'368'         DAY OK?
         BL    GENJREC                 YES
         AP    THISYEAR,=P'1'          NO, ADD 1 TO YEAR
         ZAP   THISDAY,=P'0'           AND SET DAY TO ZERO
         B     TESTJYR                 CONTINUE
GENJREC  UNPK  OUTJYR,THISYEAR         MOVE YEAR
         OI    OUTJYR+3,X'F0'          CLEAR SIGN
         UNPK  OUTJDAY,THISDAY         MOVE DAY
         OI    OUTJDAY+2,X'F0'         CLEAR SIGN
         PUT   JULIAN,OUTREC           WRITE JULIAN RECORD
         AP    THISDAY,=P'1'           ADD 1 TO DAY
         B     TESTJDAY                CONTINUE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (GREG,,JULIAN)          CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RC=0
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
THISYEAR DC    PL3'1899'               CURRENT YEAR
THISMO   DC    PL2'0'                  CURRENT MONTH
THISDAY  DC    PL2'0'                  CURRENT DAY
*
OUTREC   DS    0CL80                   OUTPUT RECORD
OUTTYPE  DS    CL1                     G=GREGORIAN, J=JULIAN
OUTGREG  DS    0CL8                    GREGORIAN DATE MMDDYYYY
OUTGMO   DS    CL2                        GREGORIAN MONTH
OUTGDAY  DS    CL2                        GREGORIAN DAY
OUTGYR   DS    CL4                        GREGORIAN YEAR
         ORG   OUTGREG
OUTJUL   DS    0CL7                    JULIAN DATE
OUTJYR   DS    CL4                        JULIAN YEAR
OUTJDAY  DS    CL3                        JULIAN DAY
         ORG
         DS    CL71
BLANKS   DC    CL80' '                 BLANKS
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
GREG     DCB   DSORG=PS,MACRF=PM,DDNAME=GREG,                          X
               RECFM=FB,LRECL=80
JULIAN   DCB   DSORG=PS,MACRF=PM,DDNAME=JULIAN,                        X
               RECFM=FB,LRECL=80
         LTORG
GENDATES CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

         TITLE '*** SVCDREF1: REFORMAT SVCDUMP INFORMATION ***'
         PRINT NOGEN
SVCDREF1 SVLNK R3
***********************************************************************
* REFORMATS THE SPP.SVCDUMP.DATA FILE OF INFORMATION ON SVCDUMPS FOR  *
* PROCESSING BY OTHER SVCD PROGRAMS. SEE PDS.JCL(SVCDJOB).            *
*                                                                     *
* AUTHOR R THORNTON SEP 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION                                                      *
***********************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
* MAINLINE PROCESSING: REFORMAT THE 80-BYTE RECORDS INPUT TO A SINGLE *
* OUTPUT RECORD FOR EACH DUMP. THE HIGHEST DUMP NUMBER FOUND IS KEPT  *
* FOR EOJ PROCESSING.                                                 *
***********************************************************************
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         CLI   INRECD,C'*'             COMMENT?
         BE    MAINLINE                YES, SKIP IT
         CLI   INRECD,C' '             FIRST RECORD OF NEW DUMP?
         BE    SUBSEQ                  NO
         CLI   PROGRESS,0              RECORD IN PROGRESS?
         BE    NEWDUMP                 NO
         PUT   OUTPUT1,OTRECD          WRITE OUTPUT RECORD
         MVI   PROGRESS,0              RESET IN PROGRESS INDICATOR
NEWDUMP  PACK  OTDMPNBR,ITDMPNBR       DUMP NBR
         CP    HIGHNBR,OTDMPNBR        THIS HIGHEST NUMBER SO FAR?
         BNH   MOVEDSN                 NO
         ZAP   HIGHNBR,OTDMPNBR        YES, SAVE IT
         MVC   OTDSNAME,ITDSNAME       DSNAME ABBREVIATION, LAST DUMP
         PACK  OTLSTDAT,ITLSTDAT       MMDD DATE OF LAST DUMP
         PACK  OTFSTDAT,ITFSTDAT       MMDD DATE OF 1ST DUMP
         PACK  OTTOTAL,ITTOTAL         TOTAL DUMPS
         PACK  OTADUMPS,ITADUMPS       TOTAL SYSA DUMPS
         PACK  OTBDUMPS,ITBDUMPS       TOTAL SYSB DUMPS
         PACK  OTREPDAT,ITREPDAT       PROBLEM REPORT DATE MMDD
         MVC   OTIBMNBR,ITIBMNBR       IBM PROBLEM NUMBER
         MVC   OTPTFNBR,ITPTFNBR       FIXING PTF NUMBER
         PACK  OTAPLYA,ITAPLYA         DATE PTF ON SYSA MMDD
         PACK  OTAPLYB,ITAPLYB         DATE PTF ON SYSB MMDD
         MVC   OTTITLE,ITTITLE         DUMP TITLE
         MVC   OTNOTE,ITNOTE           NOTE
         MVI   PROGRESS,1              SHOW PART 1 COMPLETE
         B     MAINLINE                GO READ NEXT RECORD
SUBSEQ   CLI   PROGRESS,0              ANYTHING IN PROGRESS?
         BNE   MAINLINE                NO, SKIP THIS RECORD
         CLI   PROGRESS,1              THIS FIRST LINE OF TITLE?
         BNE   SUBSEQ2                 NO
         MVC   OTTITLE(61),INRECD+5    YES, MOVE START OF TITLE
         MVI   PROGRESS,2              SHOW TITLE BEGUN
         B     MAINLINE                GO READ NEXT RECORD
SUBSEQ2  CLC   INRECD+5(6),=CL6'NOTE: ' THIS A NOTE?
         BE    GOTNOTE                 YES
         LA    R15,OTTITLE+60          NO, POINT TO END OF FIRST PART
         LA    R14,OTTITLE             POINT TO START OF TITLE
CHKLHE   CR    R15,R14                 AT LEFT END OF TITLE?
         BE    MOV2ND                  YES
         CLI   0(R15),C' '             FOUND A SOLID CHARACTER?
         BNE   ATRHE                   YES
         BCT   R15,CHKLHE              LOOP TO FIND RHE
ATRHE    MVC   0(60,R15),INRECD+5      MOVE SECOND PART OF TITLE
         B     MAINLINE                GO READ NEXT RECORD
GOTNOTE  MVC   OTNOTE,INRECD+5         MOVE NOTE
         MVI   PROGRESS,3              SHOW NOTE WRITTEN
         B     MAINLINE                GO READ NEXT RECORD
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      AP    HIGHNBR,P1              INCREMENT HIGHEST DUMP NBR FOUND
         XC    OTRECD,OTRECD           CLEAR OUTPUT RECORD
         ZAP   OTDMPNBR,HIGHNBR        MOVE IN NEXT AVAILABLE DUMP NBR
         PUT   OUTPUT1,OTRECD          WRITE DUMP NUMBER RECORD
         CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
HIGHNBR  DC    PL3'0'                  HIGHEST DUMP NUMBER FOUND
*
INRECD   DS    0CL80                   INPUT RECORD
INDMPNBR DS    CL4                      DUMP NBR IF 1ST CARD OF DUMP
         DS    CL1
INDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
         DS    CL1
INLSTDAT DS    CL4                      MMDD DATE OF LAST DUMP
         DS    CL1
INFSTDAT DS    CL4                      MMDD DATE OF 1ST DUMP
         DS    CL1
INTOTAL  DS    CL4                      TOTAL DUMPS
         DS    CL1
INADUMPS DS    CL4                      TOTAL SYSA DUMPS
         DS    CL1
INBDUMPS DS    CL4                      TOTAL SYSB DUMPS
         DS    CL1
INREPDAT DS    CL4                      PROBLEM REPORT DATE MMDD
         DS    CL1
INIBMNBR DS    CL5                      IBM PROBLEM NUMBER
         DS    CL1
INPTFNBR DS    CL7                      FIXING PTF NUMBER
         DS    CL1
INAPLYA  DS    CL4                      DATE PTF ON SYSA MMDD
         DS    CL1
INAPLYB  DS    CL4                      DATE PTF ON SYSB MMDD
         DS    CL15
*
OTRECD   DS    0CL221                  OUTPUT RECORD
OTDMPNBR DS    PL3                      DUMP NBR
OTDSNAME DS    CL6                      DSNAME ABBREVIATION, LAST DUMP
OTLSTDAT DS    PL3                      MMDD DATE OF LAST DUMP
OTFSTDAT DS    PL3                      MMDD DATE OF 1ST DUMP
OTTOTAL  DS    PL3                      TOTAL DUMPS
OTADUMPS DS    PL3                      TOTAL SYSA DUMPS
OTBDUMPS DS    PL3                      TOTAL SYSB DUMPS
OTREPDAT DS    PL3                      PROBLEM REPORT DATE MMDD
OTIBMNBR DS    CL5                      IBM PROBLEM NUMBER
OTPTFNBR DS    CL7                      FIXING PTF NUMBER
OTAPLYA  DS    PL3                      DATE PTF ON SYSA MMDD
OTAPLYB  DS    PL3                      DATE PTF ON SYSB MMDD
OTTITLE  DS    CL116                    DUMP TITLE
OTNOTE   DS    CL60                     NOTE
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=221
         END

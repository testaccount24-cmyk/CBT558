        TITLE '*** DISPLAY CONTENTS OF TAPECOPY AUDIT FILE ***'
TCREPRT CSECT
*********************************************************************
* READ THE TAPECOPY AUDITLOG DATASET AND PRODUCE A REPORT SHOWING   *
* DATA IN THE RECORDS IN READABLE FORM.                             *
*                                                                   *
* THE AUDITREC MACRO IS REQUIRED TO DESCRIBE THE RECORDS IN THE     *
* AUDITLOG DATASET. THIS MACRO IS IN SPP.OT.TAPECOPY.INSTALL AS     *
* MEMBER TCAUDIT. IT MUST BE COPIED INTO A LIBRARY IN THE SYSLIB    *
* CONCATENATION OF THE ASSEMBLY STEP WIH MEMBER NAME AUDITREC.      *
*                                                                   *
* AUTHOR R THORNTON JAN 2002                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING TCREPRT,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'TCREPRT'            PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        OPEN FILES, PRINT HEADING                                  *
*********************************************************************
START    OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
         BAS   R9,HEADING              PRINT FIRST HEADING
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE GET   INPUT1,AUDITREC         READ NEXT RECORD
         AP    INRECS,=P'1'            COUNT INPUT RECORDS
         BAS   R9,FORMAT               GO FORMAT FOR PRINT
         BAS   R9,PRINT                WRITE TO PRINT
         B     MAINLINE                LOOP THROUGH INPUT FILE
*********************************************************************
*            FORMAT SUBROUTINE                                      *
*********************************************************************
FORMAT   ST    R9,F9                   SAVE RETURN ADDRESS
         MVC   PRT,BLANKS              CLEAR PRINT LINE
         MVC   WORK(9),=XL9'402121212161212121' DATE EDIT WORD
         ED    WORK(9),AUDDATE         EDIT DATE TO WORK
         MVC   PRTDATE,WORK+1          MOVE EDITED DATE
         MVO   WORK(5),AUDTIME         MOVE TIME SHIFTED
         OI    WORK+4,X'0F'            SET VALID SIGN
         MVC   WORK2(13),=XL12'402121217A21217A21214B2121' TIME EDWRD
         ED    WORK2(13),WORK          EDIT TIME TO WORK
         MVC   PRTTIME,WORK2+1         MOVE TIME SHIFTED
         MVC   PRTDSN,AUDDSN           MOVE DATA SET NAME
         MVC   PRTIVOL,AUDIVOL         MOVE INPUT VOLUME SERIAL
         MVC   PRTDIR,=CL3'IN '        ASSUME INPUT TAPE
         TM    AUDSFLAG,X'80'          IS IT INPUT?
         BZ    TESTAUT                 YES
         MVC   PRTDIR,=CL3'OUT'        NO, OUTPUT
TESTAUT  MVC   PRTAUT,=CL3'YES'        ASSUME AUTO RECOVERED
         TM    AUDSFLAG,X'40'          IS IT AUTO RECOVERED?
         BO    FILSQ                   YES
         MVC   PRTAUT,=CL3'NO'         NO
FILSQ    SR    R1,R1                   CLEAR FOR INSERT
         ICM   R1,3,AUDFILSQ           PICK UP FILE SEQUENCE NBR
         CVD   R1,DBLWORD              CONVERT TO PACKED DECIMAL
         UNPK  PRTFILSQ,DBLWORD        UNPACK FILE SEQUENCE NBR
         OI    PRTFILSQ+4,X'F0'        MAKE UNSIGNED
         SR    R1,R1                   CLEAR FOR INSERT
         ICM   R1,3,AUDVOLSQ           PICK UP VOL SEQUENCE NBR
         CVD   R1,DBLWORD              CONVERT TO PACKED DECIMAL
         UNPK  PRTVOLSQ,DBLWORD        UNPACK VOL SEQUENCE NBR
         OI    PRTVOLSQ+2,X'F0'        MAKE UNSIGNED
         MVC   PRTCAT,=CL3'YES'        ASSUME CATALOGED
         MVC   PRTCOMP,=CL3'YES'       ASSUME COMPRESSED
         MVC   PRTATL,=CL3'YES'        ASSUME IN ATL
         MVC   PRTLAST,=CL3'YES'       ASSUME LAST VOL IN DATA SET
         TM    AUDFLAGS,X'F0'          ANY INPUT FLAGS ON?
         BZ    TESTOUTC                NO
TESTINC  TM    AUDFLAGS,X'80'          INPUT CATALOGED?
         BO    TESTINM                 YES
         MVC   PRTCAT,=CL3'NO'         NO
TESTINM  TM    AUDFLAGS,X'40'          INPUT COMPRESSED?
         BO    TESTINA                 YES
         MVC   PRTCOMP,=CL3'NO '       NO
TESTINA  TM    AUDFLAGS,X'20'          INPUT IN ATL?
         BO    TESTINL                 YES
         MVC   PRTATL,=CL3'NO '        NO
TESTINL  TM    AUDFLAGS,X'10'          INPUT LAST VOLUME IN DATA SET?
         BO    TSTBOTH                 YES
         MVC   PRTLAST,=CL3'NO '       NO
TESTOUTC TM    AUDFLAGS,X'08'          OUTPUT CATALOGED?
         BO    TESTOUTM                YES
         MVC   PRTCAT,=CL3'NO'         NO
TESTOUTM TM    AUDFLAGS,X'04'          OUTPUT COMPRESSED?
         BO    TESTOUTA                YES
         MVC   PRTCOMP,=CL3'NO '       NO
TESTOUTA TM    AUDFLAGS,X'02'          OUTPUT IN ATL?
         BO    TESTOUTL                YES
         MVC   PRTATL,=CL3'NO '        NO
TESTOUTL TM    AUDFLAGS,X'01'          LAST OUTPUT VOLUME IN DATA SET?
         BO    TSTBOTH                 YES
         MVC   PRTLAST,=CL3'NO '       NO
TSTBOTH  TM    AUDFLAGS,X'F0'          ARE THERE INPUT FLAGS?
         BZ    COPYFUNC                NO
         TM    AUDFLAGS,X'0F'          YES, ALSO OUTPUT FLAGS?
         BZ    COPYFUNC                NO
         PACK  WORK(1),AUDFLAGS        INVERT
         CLC   AUDFLAGS,WORK           BOTH SETS SAME?
         BE    COPYFUNC                YES, OK
FERR     MVC   PRTBOTHF,=CL9'BOTH-FLAGS' SHOW ERROR
         MVC   RETCODE,=F'4'           SHOW WARNING RETURN CODE
COPYFUNC LA    R1,FUNCTBL              POINT TO DATA COPY FUNCTION TBL
CHKEND   CLI   0(R1),X'FF'             END OF TABLE?
         BE    MOVENAME                YES
         CLC   AUDXFER,0(R1)           FOUND THE CORRECT TABLE ENTRY?
         BE    MOVENAME                YES
         LA    R1,21(R1)               NO, STEP TO NEXT ENTRY
         B     CHKEND                  LOOP TO FIND NAME
MOVENAME MVC   PRTXFER,1(R1)           MOVE DATA COPY FUNCTION NAME
         UNPK  WORK(9),AUDITYPE(5)     UNPACK TO MAKE PRINTABLE
         TR    WORK(8),TRHEX-C'0'      TRANSLATE TO PRINTABLE
         MVC   PRTIUCB,WORK            MOVE TO PRINT LINE
         UNPK  WORK(9),AUDOTYPE(5)     UNPACK TO MAKE PRINTABLE
         TR    WORK(8),TRHEX-C'0'      TRANSLATE TO PRINTABLE
         MVC   PRTOUCB,WORK            MOVE TO PRINT LINE
FMXIT    L     R9,F9                   GET RETURN ADDRESS
         BR    R9                      EXIT
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    ST    R9,P9                   SAVE RETURN ADDRESS
         PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         AP    LINECT,P1               INCREMENT LINE COUNTER
         CP    LINECT,ENDPAGE          PAST END OF PAGE?
         BNH   PREXIT                  NO, RETURN
         BAS   R9,HEADING              PRINT FIRST HEADING
PREXIT   MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         L     R9,P9                   GET RETURN ADDRESS
         BR    R9                      EXIT
*********************************************************************
*             HEADING SUBROUTINE                                    *
*********************************************************************
HEADING  ST    R9,H9                   SAVE RETURN ADDRESS
         ZAP   LINECT,P0               RESET LINE COUNTER
         AP    PAGECT,P1               INCREMENT PAGE COUNTER
         MVC   PAGENBR,PAGEDIT         PAGE NUMBER
         ED    PAGENBR,PAGECT              TO HEADER LINE
         PUT   PRINT1,HEADER1          WRITE PRINT LINE
         PUT   PRINT1,HEADER2          WRITE HEADER LINE 2
         MVI   PCC,C'0'                DOUBLE SPACE NEXT DATA LINE
         L     R9,H9                   GET RETURN ADDRESS
         BR    R9                      RETURN
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWORD  DS    D                       WORK FIELD
H9       DS    A                       HEADING RETURN ADDRESS
F9       DS    A                       FORMAT RETURN ADDRESS
P9       DS    A                       PRINT RETURN ADDRESS
RETCODE  DC    F'0'                    RETURN CODE
P0       DC    P'0'                    PACKED 0
P1       DC    P'1'                    PACKED 1
INRECS   DC    PL5'0'                  INPUT RECORD COUNTER
LINECT   DS    PL2                     LINE COUNTER
PAGECT   DC    PL3'0'                  PAGE COUNTER
ENDPAGE  DC    P'57'                   MAX LINES PER PAGE
PAGEDIT  DC    XL7'4020206B202020'     EDIT MASK FOR PAGE NBR
WORK     DS    CL20                    WORK FIELD
WORK2    DS    CL20                    WORK FIELD
BLANKS   DC    CL156' '                BLANKS
TRHEX    DC    CL16'0123456789ABCDEF'  HEX TO PRINTABLE TRANSLATION
*
FUNCTBL  DC    CL21'AANALYZE (FATS)'   DATA COPY FUNCTION NAMES
         DC    CL21'CASIS/COPY'
         DC    CL21'DDUPLICATE'
         DC    CL21'EASIS/SHADOW COPY'
         DC    CL21'FSTACK/SHADOW COPY'
         DC    CL21'GUNSTACK/SHADOW COPY'
         DC    CL21'HEXTRACT/SHADOW COPY'
         DC    CL21'MASIS/MOVE'
         DC    CL21'RRECOVER (FATAR)'
         DC    CL21'SSTACK/MOVE'
         DC    CL21'TSTACK/COPY'
         DC    CL21'UUNSTACK/MOVE'
         DC    CL21'VUNSTACK/COPY'
         DC    CL21'XCONVERSION RECOVERY'
         DC    CL21'YEXTRACT/MOVE'
         DC    CL21'ZEXTRACT/COPY'
         DC    X'FF',21C'?'
*
HEADER1  DS    0CL156                  HEADER LINE 1
         DC    CL1'1'                  CARRIAGE CONTROL
         DC    CL62' '
         DC    CL25'TAPECOPY AUDITLOG LISTING'
         DC    CL57' '
         DC    CL4'PAGE'
PAGENBR  DC    CL7' '                  PAGE NUMBER
*
HEADER2  DS    0CL156                  HEADER LINE 2
         DC    CL1'0'                  CARRIAGE CONTROL
         DC    CL8'--DATE--'             DATE: YYYY/DDD
         DC    CL1' '
         DC    CL11'---TIME----'         TIME: HH:MM:SS.TH
         DC    CL1' '
         DC    CL44'DATA SET NAME'       DATA SET NAME
         DC    CL1' '
         DC    CL6'IN-VOL'               INPUT TAPE VOLUME SERIAL
         DC    CL1' '
         DC    CL3'DIR'                  DIRECTION: IN OR OUT
         DC    CL1' '
         DC    CL3'AUT'                  AUTO RECOVERED: YES OR NO
         DC    CL1' '
         DC    CL5'FILSN'                FILE SEQUENCE NUMBER
         DC    CL1' '
         DC    CL3'VSN'                  VOLUME SEQUENCE NUMBER
         DC    CL1' '
         DC    CL3'CAT'                    CATALOG STATUS: YES/NO
         DC    CL1' '
         DC    CL3'CMP'                    COMPRESSION: YES/NO
         DC    CL1' '
         DC    CL3'ATL'                    ATL STATUS: YES/NO
         DC    CL1' '
         DC    CL3'LST'                    LAST VOL IN DSET: YES/NO
         DC    CL1' '
         DC    CL20'DATA COPY FUNCTION'  COPY FUNCTION DESCRIPTION
         DC    CL1' '
         DC    CL8'INUCBTYP'             INPUT TAPE UCB TYPE
         DC    CL1' '
         DC    CL8'OUTUCBTY'             OUTPUT TAPE UCB TYPE
         DC    CL10' '
*
PRTLINE  DS    0CL156                  PRINT LINE
PCC      DC    CL1'0'                  CARRIAGE CONTROL
PRT      DS    0CL155
PRTDATE  DC    CL8' '                    DATE: YYYY/DDD
         DC    CL1' '
PRTTIME  DC    CL11' '                   TIME: HH:MM:SS.TH
         DC    CL1' '
PRTDSN   DC    CL44' '                   DATA SET NAME
         DC    CL1' '
PRTIVOL  DC    CL6' '                    INPUT TAPE VOLUME SERIAL
         DC    CL1' '
PRTDIR   DC    CL3' '                    DIRECTION: IN OR OUT
         DC    CL1' '
PRTAUT   DC    CL3' '                    AUTO RECOVERED: YES OR NO
         DC    CL1' '
PRTFILSQ DC    CL5' '                    FILE SEQUENCE NUMBER
         DC    CL1' '
PRTVOLSQ DC    CL3' '                    VOLUME SEQUENCE NUMBER
         DC    CL1' '
PRTCAT   DC    CL3' '                      CATALOG STATUS: YES/NO
         DC    CL1' '
PRTCOMP  DC    CL3' '                      COMPRESSION: YES/NO
         DC    CL1' '
PRTATL   DC    CL3' '                      ATL STATUS: YES/NO
         DC    CL1' '
PRTLAST  DC    CL3' '                      LAST VOL IN DSET: YES/NO
         DC    CL1' '
PRTXFER  DC    CL20' '                   COPY FUNCTION DESCRIPTION
         DC    CL1' '
PRTIUCB  DC    CL8' '                    INPUT TAPE UCB TYPE
         DC    CL1' '
PRTOUCB  DC    CL8' '                    OUTPUT TAPE UCB TYPE
         DC    CL1' '
PRTBOTHF DC    CL9' '                    BOTH-FLAGS' ERROR
*
AUDITREC AUDITREC PREFIX=AUD,DSECT=NO
*
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=1072
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=156
         LTORG
TCREPRT CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

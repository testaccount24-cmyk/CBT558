         TITLE '*** LISTVOLS: LIST ALL VOLUMES FOR A CATALOGED DATASET'
LISTVOLS CSECT
*********************************************************************
* READS THE SYSPRINT OUTPUT FROM AN IDCAMS LISTCAT ENTRIES('DSNAME')*
* ALL EXECUTION AND PRINT THE DSNAME AND ALL VOLUMES FOR THE FILE.  *
*                                                                   *
* AUTHOR R THORNTON                                                 *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING LISTVOLS,R3
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'LISTVOLS'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
START    DS    0H                      BEGIN PROCESSING
*********************************************************************
*        OPEN FILES                                                 *
*********************************************************************
OPENS    OPEN  (PRINT1,OUTPUT,INPUT1)  OPEN FILES
*********************************************************************
*             MAINLINE ROUTINE                                      *
*********************************************************************
MAINLINE BAS   R9,GETIN                READ INPUT RECORD
         CR    R6,R7                   ANY NON-BLANK FOUND?
         BH    MAINLINE                NO, DROP THE RECORD
         CLI   GOTDSN,1                GOT DSNAME YET?
         BE    MLCAT                   YES
         CLC   0(7,R6),=CL7'CLUSTER'   IS IT CLUSTER NAME?
         BE    MLNAME                  NO
         CLC   0(7,R6),=CL7'NONVSAM'   IS IT DSNAME?
         BNE   MAINLINE                NO
MLNAME   LA    R6,7(R6)                STEP PAST IDENTIFIER
         TRT   0(120,R6),FINDNATL      LOOK FOR FIRST NATIONAL CHAR
         BZ    MAINLINE                NOT FOUND, DROP THE RECORD
         CR    R1,R7                   FOUND WITHIN THE RECORD?
         BH    MAINLINE                NO, DROP THE RECORD
         LR    R6,R1                   COPY FIRST BYTE ADDRESS
         MVI   GOTDSN,1                INDICATE DSNAME FOUND
         TRT   0(120,R6),FINDBLNK      SCAN FOR FOLLOWING BLANK
         BZ    MLNMND                  NO BLANK FOUND
         BCTR  R1,0                    FOUND, BACK UP 1
         CR    R1,R7                   FOUND WITHIN THE RECORD?
         BNH   MLNMLN                  YES
MLNMND   LR    R1,R7                   POINT TO END OF LINE
MLNMLN   SR    R1,R6                   COMPUTE LENGTH TO MOVE
         EX    R1,MLMVNAME             MOVE THE NAME TO PRINT
         BAS   R9,PRINT                PRINT THE NAME
         B     MAINLINE                CONTINUE
MLCAT    CLI   GOTCAT,1                GOT THE CATALOG NAME ALREADY?
         BE    MLVOL                   YES
         CLC   0(6,R6),=CL6'IN-CAT'    THIS THE CATALOG NAME RECORD?
         BNE   MAINLINE                NO, DROP THE RECORD
         LA    R6,6(R6)                STEP PAST THE IN-CAT
         TRT   0(120,R6),FINDNATL      LOOK FOR FIRST NATIONAL CHAR
         BZ    MAINLINE                NOT FOUND, DROP THE RECORD
         CR    R1,R7                   FOUND WITHIN THE RECORD?
         BH    MAINLINE                NO, DROP THE RECORD
         LR    R6,R1                   COPY FIRST BYTE ADDRESS
         MVI   GOTCAT,1                INDICATE CATALOG NAME FOUND
         LR    R1,R7                   POINT TO END OF LINE
         TRT   0(120,R6),FINDBLNK      SCAN FOR FOLLOWING BLANK
         BZ    MLCTND                  NO BLANK FOUND
         BCTR  R1,0                    FOUND, BACK UP 1
         CR    R1,R7                   FOUND WITHIN THE RECORD?
         BNH   MLCTLN                  YES
MLCTND   LR    R1,R7                   POINT TO END OF LINE
MLCTLN   SR    R1,R6                   COMPUTE LENGTH TO MOVE
         EX    R1,MLMVNAME             MOVE THE NAME TO PRINT
         BAS   R9,PRINT                PRINT THE NAME
         B     MAINLINE                CONTINUE
MLVOL    CLC   0(6,R6),=CL6'VOLSER'    IS IT A VOLUME SERIAL LINE?
         BNE   MAINLINE                NO, DROP THE RECORD
         LA    R6,6(R6)                YES, STEP PAST THE LITERAL
         TRT   0(120,R6),FINDNATL      STEP TO VOLUME SERIAL
         BZ    MAINLINE                NOT FOUND, DROP THE RECORD
         MVC   PRT(6),0(R1)            FOUND, MOVE IT TO PRINT
         BAS   R9,PRINT                AND PRINT IT
         B     MAINLINE                LOOP THROUGH INPUT FILE
MLMVNAME MVC   PRT(1),0(R6)            EXECUTED MOVE
*********************************************************************
* READ NEXT RECORD FROM THE IDCAMS SYSPRINT DATASET. RETURN TO      *
* CALLER WITH R6 POINTING TO FIRST NON-BLANK CHARACTER IN TEXT LINE *
* AND R7 POINTS TO THE LAST CHARACTER IN THE LINE.                  *
*********************************************************************
GETIN    GET   INPUT1,INREC            READ NEXT RECORD
         LH    R7,INRDW                GET RECORD LENGTH
         LA    R7,INREC(R7)            POINT TO LAST BYTE
         BCTR  R7,0                       OF SYSPRINT TEXT
         LA    R6,INTEXT+130           POINT PAST END OF RECORD
         TRT   INTEXT,FINDNONB         SCAN FOR 1ST NON-BLANK
         BZ    GIXIT                   BRANCH IF NO HIT
         LR    R6,R1                   HIT, SAVE ADDRESS
GIXIT    BR    R9                      RETURN TO CALLER
*********************************************************************
*             PRINT SUBROUTINE                                      *
*********************************************************************
PRINT    PUT   PRINT1,PRTLINE          WRITE PRINT LINE
         MVC   PRTLINE,BLANKS          CLEAR PRINT LINE
         BR    R9                      NO, RETURN
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      CLOSE (PRINT1,,INPUT1)        CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
INREC    DS    0F                      IDCAMS SYSPRINT RECORD
INRDW    DS    H                          RECORD LENGTH
         DS    H                          ZEROS
INCC     DS    CL1                        CARRIAGE CONTROL BYTE
INTEXT   DS    CL120                      SYSPRINT TEXT
*
FINDNONB DC    256X'FF'                TRT SCAN TABLE TO FIND NON-BLANK
         ORG   FINDNONB+C' '
         DC    X'00'
         ORG
*
FINDNATL DC    256X'00'                TRT SCAN TO FIND NATIONAL CHARS
         ORG   FINDNATL+C'#'
         DC    C'#'
         ORG   FINDNATL+C'@'
         DC    C'@'
         ORG   FINDNATL+C'$'
         DC    C'$'
         ORG   FINDNATL+C'A'
         DC    C'ABCDEFGHI'
         ORG   FINDNATL+C'J'
         DC    C'JKLMNOPQR'
         ORG   FINDNATL+C'S'
         DC    C'STUVWXYZ'
         ORG   FINDNATL+C'0'
         DC    C'0123456789'
         ORG
*
FINDBLNK DC    256X'00'
         ORG   FINDBLNK+C' '
         DC    C' '
         ORG
*
RETCODE  DS    F                       RETURN CODE
BLANKS   DC    CL133' '                BLANKS
GOTDSN   DC    X'00'                   GOT DSNAME INDICATOR
GOTCAT   DC    X'00'                   GOT CATALOG NAME INDICATOR
*
PRTLINE  DS    0CL133                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    0CL132                  PRINT DATA
         DS    CL132
*********************************************************************
*             DCB'S                                                 *
*********************************************************************
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=VBA,LRECL=125
         LTORG
LISTVOLS CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

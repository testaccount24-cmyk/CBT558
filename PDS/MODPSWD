MODPSWD   SVLNK R3
*THIS PROGRAM SEARCHES IT'S TIOT FOR DDNAMES BEGINNING WITH
*MODPSWD. WHEN FOUND, THE DDNAME IS INSERTED IN THE VTOC DCB,
*AND THE JFCB IS READ. THE JFCB DSNAME IS SET TO ALL X'04'S
*AND OPEN TYPE J OPENS THE VTOC DATASET. THE DSCB FOR THE
*FILE SPECIFIED BY THE DD CARD IS READ. IF THE PARM FIELD
*ON THE EXEC CARD WAS 'ON', THE PASSWORD PROTECTION BITS IN THE
*DS1DSIND BYTE ARE SET ON. IF 'OFF' WAS SPECIFIED, THE PASSWORD
*PROTECT BITS ARE SET OFF. A MESSAGE IS WRITTEN TO THE SYSPRINT
*DATASET, AND THE DSCB IS REWRITTEN.
*
* AUTHOR R. THORNTON    MAY, 1974
*
*
*
         L     R2,0(R1)                 GET PARM FIELD ADDR
         OPEN  (SYSPRINT,OUTPUT)        OPEN PRINT FILE
         CLC   0(2,R2),ZEROS            ANY PARM FIELD DATA
         BE    INVPARM                    NO
         MVC   PARM,2(R2)                SAVE PARM DATA
         CLC   PARM,=C'OFF'           PARM VALUE IS OFF
         BE    FINDTIOT                   YES
         CLC   PARM(2),=C'ON'               PARM VALUE IS LO
         BE    FINDTIOT                  YES
         CLC   PARM(3),=C'WRT'          PARM VALUE IS WRT
         BE    FINDTIOT                 YES
INVPARM  B     EOJ1                     ERROR IN PARM FIELD
FINDTIOT L    R1,16                        GET CVT ADDR
         L     R1,0(R1)                 GET TCB POINTER ADDR
         L     R1,0(R1)                 GET TCB ADDR
         L     R1,12(R1)                GET TIOT ADDR
         LA    R1,24(R1)                GET 1ST DD ENTRY ADDR
         ST    R1,THISDD                SAVE DD ENTRY ADDR
         B     CKTEND                   GO BEGIN TIOT SEARCH
SCHTIOT  L     R1,THISDD                GET TIOT ADDR ENTRY
STEPTIOT SR    R2,R2                    CLEAR WORK REG
         IC    R2,0(R1)                 GET DD ENTRY LENGTH
         AR    R1,R2                    GET NEXT DD ENTRY ADDR
         ST    R1,THISDD                SAVE IT'S ADDRESS
CKTEND   CLC   0(4,R1),ZEROS            END OF TIOT
         BE    EOJ                      YES, STOP RUN
         CLC   4(7,R1),MODNAM           DDNAME BEGINS MODPSWD+
         BNE   STEPTIOT                 NO, CONTINUE SEARCH
          MVI   EOJ+1,C'0'              SET FOUND SWITCH
         MVC   VTOC+40(8),4(R1)         TIOT DDNAME TO DCB
JFCBRD   RDJFCB VTOC                    READ JFCB FOR INPUT FILE
         LTR   R15,R15                 JFCB READ O.K.
         BZ    SET4                    YES
         MVC   PRTLINE,BADJFCB         MESSAGE TO PRINT LINE
         B     EOJ1                    ERROR END
SET4     MVC   DSNAME,JFCBDSNM         SAVE DSNAME
         MVI   JFCBDSNM,4              SET DSNAME
         MVC   JFCBDSNM+1(43),JFCBDSNM TO ALL HEX 04'S, VTOC DSCB
         MVI   JFCBTSDM,X'08'          SUPPRESS JFCB REWRITE
         OPEN  (VTOC,OUTPUT),TYPE=J    OPEN USING MODIFIED JFCB
         L     R1,VTOC+44              GET DEB ADDRESS
         MVC   VTSRCH(4),38(R1)        BEGINNING CCHH TO IOB SEARCH
         MVI   VTREC,1                 START WITH RECORD 1
          MVI   RWCCW,6                 SET CCW TO READ DATA
         EXCP  VTOCIOB                 LOCATE AND READ FORMAT 1 DSCB
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         CLI   VTECB,X'7F'             NORMAL COMPLETION
         BE    CKOPT                   YES, CONTINUE
         MVC   PRTLINE,READER          MESSAGE TO PRINT LINE
         B     EOJ1                    ERROR EOJ
CKOPT    CLC   PARM,=C'OFF'            PARM IS OFF
         BE    PASSOFF                 YES
         CLC   PARM,=C'WRT'            PROTECT FOR WRITE ONLY
         BE    WRTONLY                 YES
         MVC   PRTLINE,SETON           PRINT MESSAGE
         OI    DS1DSIND,X'10'          SET PASSWORD BITS
         B     REWRITE                 GO REWRITE DSCB
WRTONLY  MVC   PRTLINE,SETWRT          PRINT MESSAGE
         OI    DS1DSIND,X'14'          SET NOPWREAD, PWWRITE
         B     REWRITE
PASSOFF  MVC   PRTLINE,SETOFF          PRINT MESSAGE
         NI    DS1DSIND,X'EB'          SET PASSWORD BITS OFF
REWRITE  MVI   RWCCW,5                 CHANGE READ COMMAND TO WRITE
         EXCP  VTOCIOB                 LOCATE AND REWRITE DATA PORTION
         WAIT  ECB=VTECB               WAIT FOR I/O COMPLETION
         MVC   PRTLINE+30(44),DSNAME   DSNAME TO PRINT LINE
         CLI   VTECB,X'7F'             I/O COMPLETED NORMALLY
         BE    PRINT                YES
         MVC   PRTLINE,REWRTER         MESSAGE TO PRINT LINE
EOJ1     PUT   SYSPRINT,PRTLINE        WRITE ERROR MESSAGE
         ABEND 999
PRINT   PUT   SYSPRINT,PRTLINE        PRINT MESSAGE
         CLOSE VTOC                    CLOSE VTOC FILE
         B     SCHTIOT                 CONTINUE TIOT SEARCH
EOJ       NOP   EOJ2                    BRANCH WHEN ANY MODPSWD DD FND
          MVC   PRTLINE,NONE            ERROR MESSAGE TO PRINT
          B     EOJ1                    ERROR END
EOJ2     CLOSE SYSPRINT        CLOSE FILES
RETURN    L     R13,4(R13)              GET O/S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE O/S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO O/S
PARM     DC    C'   '                  PARM FIELD FROM USER
DSNAME   DS    CL44                    DATASET NAME FROM JFCB
THISDD   DS    F                       CURRENT TIOT DD ENTRY ADDR
MODNAM   DC    CL7'MODPSWD'             DDNAME PREFIX
ZEROS    DC    XL4'00'                 ZEROES
TWO      DC    H'2'                    CONSTANT 2
PRTLINE  DC    CL80'INVALID PARM FIELD' PRINT LINE
READER   DC    CL80'ERROR READING DSCB'
BADJFCB  DC    CL80'UNABLE TO READ JFCB'
SETOFF   DC    CL80'PASSWORD REMOVED'
SETWRT   DC    CL80'PASSWORD SET FOR WRITE PROTECTION ONLY'
SETON    DC    CL80'PASSWORD PROTECTED'
REWRTER  DC    CL80'ERROR UPDATING DSCB'
NONE     DC    CL80'NO MODPSWD+ DDNAMES FOUND, ERROR END'
CMDCHN   DS    0D                      COMMAND CHAIN TO READ/WRITE
         CCW   X'1A',VTSRCH,X'70',5 READ HA TO POSITION TO INDEX PT
         CCW   X'A9',DSNAME,X'40',44 SEARCH DSCB'S FOR THAT NAMED
         CCW   8,*-8,0,0               TIC
RWCCW    CCW   6,DS1FMTID,0,96         READ FORMAT 1 DSCB
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   LAST ENTRY AND RDJFCB EXIT
         DC    AL3(JFCB)               ADDRESS OF AREA FOR JFCB
         JFCBDEF DSECT=NO
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=F,LRECL=80,     X
               BLKSIZE=80
VTOC     DCB   DDNAME=MODPSWD,MACRF=E,EXLST=JFCBADDR
VTOCIOB EXCPIOB CCW=CMDCHN,DCB=VTOC
 DSCBDEF DSECT=NO,FMT2=NO,FMT3=NO,FMT4=NO,FMT5=NO,FMT6=NO
         END

         TITLE '*** MTCHMERG: MATCH-MERGE FILES        ***'
***********************************************************************
* GENERIC MATCH/MERGE PROGRAM THAT MUST BE RESPECIALIZED FOR EACH USE.*
* CURRENT SETUP:                                                      *
*  0. PRE-SORT INPUT1 AND INPUT2 ON COLUMNS 1-8                       *
*  1. MATCH INPUT1 AND INPUT2 ON COLUMNS 1-8                          *
*  2. IF NOMATCH AND INPUT1 IS LOW, WRITE INPUT1 RECORD TO IN1LOW     *
*  3. IF NOMATCH AND INPUT2 IS LOW, WRITE INPUT2 RECORD TO IN2LOW     *
*  4. IF MATCH MOVE INPUT1 RECORD TO MATCHED                          *
* AUTHOR R THORNTON MAR 1999                                          *
***********************************************************************
*
MTCHMERG SVLNK R3,R4
OPENS    OPEN  (IN1LOW,OUTPUT,IN2LOW,OUTPUT,MATCHED,OUTPUT) OPN OUTPUTS
         OPEN  (INPUT1,,INPUT2)        OPEN INPUTS
         BAS   R9,GETIN1               READ INPUT1 RECORD
         BAS   R9,GETIN2               READ INPUT2 RECORD
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLC   INKEY1,INKEY2           COMPARE RECORDS
         BE    MATCH                   BRANCH IF MATCH
         BH    KEY2LO                  NO MATCH: INPUT2 LOW
         PUT   IN1LOW,0(R6)            WRITE INPUT1 TO IN1LOW
         BAS   R9,GETIN1               AND READ NEXT INPUT1
         B     MAINLINE                CONTINUE
KEY2LO   PUT   IN2LOW,0(R7)            WRITE INPUT2 RECORD TO IN2LOW
         BAS   R9,GETIN2               INPUT2 LOW, READ NEXT INPUT2
         B     MAINLINE                CONTINUE
MATCH    PUT   MATCHED,0(R6)           WRITE INPUT1 RECORD TO MATCHED
         BAS   R9,GETIN1               READ NEXT INPUT1
         BAS   R9,GETIN2               READ NEXT INPUT2
         B     MAINLINE                CONTINUE
***********************************************************************
*             READ NEXT INPUT1 RECORD                                 *
***********************************************************************
GETIN1   GET   INPUT1                  READ NEXT RECORD
         LR    R6,R1                   COPY RECORD ADDRESS
         MVC   INKEY1,0(R6)            COPY RECORD KEY
         BR    R9                      RETURN TO CALLER
EOF1     CLC   INKEY2,HIVAL            EOF INPUT2?
         BE    EOJ                     YES
         MVC   INKEY1,HIVAL            NO, SET EOF FLAG
         BR    R9
***********************************************************************
*             READ NEXT INPUT2 RECORD                                 *
***********************************************************************
GETIN2   GET   INPUT2                  READ NEXT RECORD
         LR    R7,R1                   COPY RECORD ADDRESS
         MVC   INKEY2,0(R7)            COPY RECORD KEY
         BR    R9                      RETURN TO CALLER
EOF2     CLC   INKEY1,HIVAL            EOF INPUT1?
         BE    EOJ                     YES
         MVC   INKEY2,HIVAL            NO, SET EOF FLAG
         BR    R9
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (IN1LOW,,IN2LOW,,MATCHED,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RC=0
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
HIVAL    DC    16X'FF'
INKEY1   DC    CL16' '
INKEY2   DC    CL16' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
IN1LOW   DCB   DSORG=PS,MACRF=PM,DDNAME=IN1LOW
IN2LOW   DCB   DSORG=PS,MACRF=PM,DDNAME=IN2LOW
MATCHED  DCB   DSORG=PS,MACRF=PM,DDNAME=MATCHED
INPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT1,EODAD=EOF1
INPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT2,EODAD=EOF2
         END

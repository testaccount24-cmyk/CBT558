         PRINT NOGEN
         TITLE '*** CHGJOBCD: REPLACE JOB CARDS ***'
CHGJOBCD SVLNK R3
*
***********************************************************************
* SCAN A JCL PDS AND REPLACE JOB CARDS WITH A NEW JOB CARD SET        *
*                                                                     *
* THE INPUT PDS IS MODIFIED DIRECTLY WITH THE NEW JOB CARD, SO A COPY *
* OF THE PDS SHOULD BE MADE BEFORE THE RUN TO INSURE AGAINST LOSS OF  *
* DATA.                                                               *
*                                                                     *
* FOR EACH MEMBER, THE FIRST THREE RECORDS ARE READ AND CHECKED TO SEE*
* IF THEY ARE (1) JOB CARD FIRST RECORD, (2) JOB CARD CONTINUATION    *
* BEGINNING WITH NOTIFY, (3) JESOUT CARD, AND (4) COMMENT CARD. THE   *
* FIRST THREE RECORDS IN EACH MEMBER ARE REPLACED WITH A HARD-CODED   *
* JOB CARD SET. IF THERE IS A MISMATCH IN ANY OF THE FIRST THREE      *
* RECORDS, THE MEMBER NAME IS WRITTEN TO THE SYSPRINT FILE.           *
*                                                                     *
* AUTHOR R THORNTON JUL 1998                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN THE PRINT FILE                                 *
***********************************************************************
*
         OPEN  (SYSPRINT,OUTPUT)       OPEN LISTING
*
***********************************************************************
* INITIALIZATION: OPEN THE JCL PDS FOR DIRECTORY AND MEMBER RECORD    *
*                 PROCESSING BY READDIR AND UPDMEM.                   *
***********************************************************************
*
         CALL  READDIR,OPEN            OPEN PDS FOR DIRECTORY READS
         CALL  UPDMEM,OPEN             OPEN PDS FOR MEMBER PROCESSING
*
***********************************************************************
* MAINLINE PROCESSING: LOCATE THE NEXT MEMBER OF THE INPUT1 PDS,      *
* POSITION TO IT, AND READ THE FIRST RECORD. CHECK TO SEE IF IT IS A  *
* JOB CARD BEGINNING RECORD. IF NOT, WRITE THE MEMBER NAME TO SYSPRINT*
* AND ADVANCE TO THE NEXT MEMBER.                                     *
***********************************************************************
*
NEWMEM   CALL  READDIR,(READ,DIRECTRY) READ NEXT DIRECTORY ENTRY
         CLI   MEMNAME,EOF             END OF FILE
         BE    EOJ                     YES, STOP RUN
         MVC   FINDNAM,MEMNAME         NAME TO FIND LIST
         CALL  UPDMEM,FIND             LOCATE MEMBER
         CLI   FINDNAM,0               MEMBER FOUND
         BE    NEWMEM                  NO, TRY NEXT MEMBER
CALLREAD CALL  UPDMEM,(READ,MEMREC)    READ NEXT RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BNE   CHECK1                  NO
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'1'             RECORD NUMBER
         MVC   PRTIMAG,EMPTY           SHOW EMPTY MEMBER
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
         B     NEWMEM                  GO TO NEXT MEMBER
CHECK1   CLC   MEMREC+10(5),=CL5' JOB ' IS IT 1ST JOB CARD RECORD?
         BE    REPLACE1                YES
         CLC   MEMREC+11(5),=CL5' JOB ' IS IT 1ST JOB CARD RECORD?
         BE    REPLACE1                YES
         CLC   MEMREC+9(5),=CL5' JOB ' IS IT 1ST JOB CARD RECORD?
         BE    REPLACE1                YES
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'1'             RECORD NUMBER
         MVC   PRTIMAG,MEMREC          MEMBER RECORD TO PRINT REC
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
REPLACE1 MVC   MEMREC,NEWJOB1          REPLACE WITH NEW JOB START
         TR    JOB8,TRTBL              MAKE IT VALID
         MVC   MEMREC+9(1),JOB8        MOVE IN 8TH CHAR OF JOBNAME
         CALL  UPDMEM,(UPDATE,MEMREC)  UPDATE THE RECORD
         CALL  UPDMEM,(READ,MEMREC)    READ 2ND RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BNE   CHECK2                  NO
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'2'             RECORD NUMBER
         MVC   PRTIMAG,NOREC2          SHOW EMPTY MEMBER
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
         B     NEWMEM                  GO TO NEXT MEMBER
CHECK2   CLC   MEMREC+5(6),=CL6'NOTIFY' IS IT JOB CONTINUATION?
         BE    REPLACE2                YES
         CLC   MEMREC+5(6),=CL6'MSGCLA' IS IT JOB CONTINUATION?
         BE    REPLACE2                YES
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'2'             RECORD NUMBER
         MVC   PRTIMAG,MEMREC          MEMBER RECORD TO PRINT REC
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
REPLACE2 MVC   MEMREC,NEWJOB2          REPLACE WITH NEW JOB CONTIN
         CALL  UPDMEM,(UPDATE,MEMREC)  UPDATE THE RECORD
         CALL  UPDMEM,(READ,MEMREC)    READ 3RD RECORD IN MEMBER
         CLI   MEMREC,EOF              IS IT EOF
         BNE   CHECK3                  NO
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'3'             RECORD NUMBER
         MVC   PRTIMAG,NOREC3          SHOW EMPTY MEMBER
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
         B     NEWMEM                  GO TO NEXT MEMBER
CHECK3   CLC   MEMREC(8),=CL8'//JESOUT' IS IT JOB CONTINUATION?
         BE    REPLACE3                YES
         MVC   PRTMEM,MEMNAME          MEMBER NAME TO PRINT REC
         MVI   PRTNBR,C'3'             RECORD NUMBER
         MVC   PRTIMAG,MEMREC          MEMBER RECORD TO PRINT REC
         PUT   SYSPRINT,PRTREC         WRITE PRINT RECORD
REPLACE3 MVC   MEMREC,NEWJOB3          REPLACE WITH NEW JOB START
         CALL  UPDMEM,(UPDATE,MEMREC)  UPDATE THE RECORD
         B     NEWMEM                  CONTINUE TO NEXT MEMBER
*
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
*
EOJ      CLOSE (SYSPRINT)              CLOSE FILES
         CALL  UPDMEM,CLOSE            CLOSE PDS MEMBER ACCESS
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
NUMCK    DC    CL7'0000000'            NUMERIC CHECK AREA
NUMZROS  DC    CL7'0000000'            NUMERIC CHECK CONSTANT
EOF      EQU   X'FF'                   EOF INDICATOR
*
NEWJOB1 DC    CL40'//BC0THORX JOB (1152002-9800039,,TMVS01-'
        DC    CL40'UR-XXXX),''DTHORNTON'',CLASS=X,           '
NEWJOB2 DC    CL40'//   MSGCLASS=Y,NOTIFY=BC0THOR,REGION=4M'
        DC    CL40'                                        '
NEWJOB3 DC    CL40'//JESOUT OUTPUT GROUPID=HDQTRSF2,DEFAULT'
        DC    CL40'=YES,JESDS=ALL                          '
*
EMPTY    DC    CL80'********* EMPTY MEMBER ************'
NOREC2   DC    CL80'********* ONLY 1 RECORD IN MEMBER *******'
NOREC3   DC    CL80'********* ONLY 2 RECORDS IN MEMBER *******'
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ REQUEST CODE
UPDATE   DC    C'U'                    UPDATE REQUEST CODE
CLOSE    DC    C'C'                    CLOSE REQUEST CODE
*
DIRECTRY DS    0CL11                   DIRECTORY DATA
MEMNAME  DS    CL8                     MEMBER NAME
MEMTTR   DS    XL3                     TTR OF FIRST BLOCK
*
FIND     DS    0CL10                   FIND MEMBER PARAMETER LIST
         DC    C'FN'                   FIND BY NAME
FINDNAM  DS    CL8                     NAME TO BE FOUND
*
MEMREC   DS    CL80                    MEMBER RECORD
*
PRTREC   DS    0CL80                   OUTPUT RECORD
PRTMEM   DC    CL8' '                  MEMBER NAME
         DC    CL1' '                  MEMBER RECORD NUMBER
PRTNBR   DS    CL1                     MEMBER RECORD NUMBER
         DC    CL1' '                  MEMBER RECORD NUMBER
PRTIMAG  DC    CL72' '                 IMAGE OF INPUT RECORD
*
JOB8     DC    CL1'A'                  LAST CHARACTER OF JOB NAME
*
TRTBL    DC    256C'A'                 MAKE JOB CHARACTER VALID
         ORG   TRTBL+C'A'
         DC    C'BCDEFGHIJ'
         ORG   TRTBL+C'J'
         DC    C'KLMNOPQRS'
         ORG   TRTBL+C'J'
         DC    C'TUVWXYZ0'
         ORG   TRTBL+C'0'
         DC    C'123456789A'
         ORG
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
*
         LTORG
         END

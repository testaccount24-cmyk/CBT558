         TITLE '*** ENDVMTCH: REFORMAT SYSTEM OWNER LIST ***'
***********************************************************************
* MATCH SYSTEM OWNER TO THE PROGRAM-TO-ENDEVOR-SYSTEM FILE CREATED BY *
* PROGRAM ENDVRSYS.                                                   *
* AUTHOR R THORNTON NOV 1994                                          *
***********************************************************************
*
ENDVMTCH SVLNK R3,R4
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1,,INPUT2)
         BAS   R9,GETIN1               READ ENDVREFM FILE
         BAS   R9,GETIN2               READ SYSTEM:OWNER FILE
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLC   ENDSYSTM,SYSSYSTM       SAME ENDEVOR SYSTEM?
         BL    READEND                 NO, ENDEVOR RECORD LOW
         BH    READSYS                 NO, SYSTEM:OWNER LOW
         CLC   SYSSUBSY,BLANKS         SYSTEM:OWNER SPECIFIES SUBSY?
         BE    MATCH                   NO
         CLC   ENDSUBSY,SYSSUBSY       YES, SUBSYSTEMS EQUAL?
         BL    READEND                 NO, ENDEVOR RECORD LOW
         BH    READSYS                 NO, SYSTEM:OWNER LOW
MATCH    MVC   ENDOWNR,SYSOWNR         OWNER NAME TO OUTPUT
READEND  BAS   R9,WRTOUT               WRITE OUTPUT RECORD
         BAS   R9,GETIN1               READ ENDVREFM FILE
         B     MAINLINE                CONTINUE
READSYS  BAS   R9,GETIN2               READ SYSTEM:OWNER FILE
         B     MAINLINE                CONTINUE
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN1   GET   INPUT1,ENDVREC          READ NEXT ENDVREFM RECORD
         BR    R9                      EXIT
***********************************************************************
*             READ INPUT FILE SUBROUTINE                              *
***********************************************************************
GETIN2   GET   INPUT2,SYSOWNER         READ NEXT SYSTEM:OWNER RECORD
         BR    R9                      EXIT
EOF2     MVI   SYSSYSTM,X'FF'          NO, SET EOF FLAG
         BR    R9                      AND RETURN TO CALLER
***********************************************************************
*             WRITE OUTPUT RECORD                                     *
***********************************************************************
WRTOUT   PUT   OUTPUT1,ENDVREC         WRITE OUTPUT RECORD
         BR    R9                      EXIT
***********************************************************************
*             END OF JOB ROUTINE                                      *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 RETURN CODE = 0
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
BLANKS   DC    CL80' '                 BLANKS
*
ENDVREC  DS    0CL80                   OUTPUT RECORD
ENDELEM  DS    CL8                     ELEMENT NAME
         DC    CL1' '
ENDTYPE  DC    CL8' '                  ELEMENT TYPE
         DC    CL1' '
ENDENV   DC    CL8' '                  ENVIRONMENT NAME
         DC    CL1' '
ENDSYSTM DC    CL8' '                  SYSTEM NAME
         DC    CL1' '
ENDSUBSY DC    CL8' '                  SUBSYSTEM NAME
         DC    CL1' '
ENDOWNR  DS    CL16                    SYSTEM OWNER NAME
         DC    CL19' '
*
SYSOWNER DS    0CL80
SYSSYSTM DS    CL8                     SYSTEM NAME
         DS    CL1
SYSSUBSY DS    CL8                     SUBSYSTEM NAME
         DS    CL1
SYSOWNR  DS    CL16                    SYSTEM OWNER NAME
         DS    CL46
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=FB,LRECL=80
         LTORG
ENDVMTCH CSECT
         END

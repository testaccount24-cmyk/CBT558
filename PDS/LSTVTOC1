         TITLE '    ***** LSTVTOC1 - STRIP DISK PACK VTOCS *****'
LSTVTOC1 CSECT
         REQU
         USING LSTVTOC1,R3
* THIS PROGRAM SEARCHES IT'S VTOC FOR DD CARDS WHOSE NAMES BEGIN WITH
* VTOC. WHEN FOUND, THE DDNAME IS MOVED TO THE VTOC DCB DDNAME FIELD,
* AND THE JFCB IS READ. THE DSNAME FIELD OF THE JFCB IS SET TO ALL
* HEX 04'S TO PERMIT OPENING THE VTOC, AS THE FORMAT 4 DSCB DEFINES THE
* VTOC EXTENTS. THE VTOC IS THEN READ AS FOLLOWS: THE FORMAT 4 DSCB
* IS READ, THEN THE CHAIN OF FORMAT 6 DSCB'S POINTED TO BY THE FORMAT 4
* DSCB, THEN THE FORMAT 5 DSCB WHICH FOLLOWS THE FORMAT 4 DSCB IS READ
* FOLLOWED BY THE FORMAT 5 DSCB CHAIN. THE FOREGOING RECORDS ARE WRITTN
* OUT AS RECEIVED. AT THE END OF THE FORMAT 5 DSCB CHAIN, THE DATA SET
* DSCB'S ARE READ AND USED TO LOCATE THEIR ASSOCIATED FORMAT 2 AND 3
* DSCB'S. ALL OUTPUT RECORDS ARE FORMATTED WITH A 60 BYTE SORT FIELD.
*
*
* AUTHOR R. THORNTON     FEB, 1972
*
*
         STM   R14,R12,12(R13)         SAVE CALLER'S REGISTERS
         LR    R3,R15                  INITIALIZE BASE REGISTER
         LA    R15,SAVE                GET SAVE AREA ADDRESS
         ST    R15,8(R13)              STORE FORWARD POINTER
         ST    R13,SAVE+4              STORE BACK POINTER
         LR    R13,R15                 INITIALIZE SAVE POINTER
         L     R1,0(R1)                GET PARM FIELD ADDRESS
         CH    R1,ZEROS                ANY PARM FIELD CODED
         BE    OPENS                   NO
         MVC   PARM,2(R1)              SAVE PARM FIELD
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT) OPEN OUTPUT FILES
         L     R1,16                   GET CVT ADDRESS
         L     R1,0(R1)                GET CVTTCBP
         L     R1,0(R1)                GET TCB ADDRESS
         L     R1,12(R1)               GET TIOT ADDRESS
         LA    R1,24(R1)               GET ADDRESS OF 1ST DD ENTRY
         ST    R1,TIOTDD               SAVE TIOT DD ENTRY ADDRESS
*
*
*
NEXVOL   EQU   *                       *** INITIALIZE FOR NEXT VOLUME *
         L     R1,TIOTDD               GET TIOT DD ENTRY ADDRESS
         SR    R2,R2                   CLEAR WORK REG
CKLSTDD  CLI   0(R1),0                 LAST TIOT DD ENTRY
         BE    EOJ                     YES
         CLC   4(4,R1),=C'VTOC'        DDNAME BEGINS WITH VTOC
         BE    USEDD                   YES, USE IT
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         B     CKLSTDD                 GO CK NEXT DD
USEDD    MVC   VTOC+40(8),4(R1)        MOVE DNAME IN TIOT TO DCB
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         ST    R1,TIOTDD               SAVE IT'S ADDRESS
         XC    JFCBDSNM,JFCBDSNM       CLEAR JFCB DSNAME FOR CHECK
         RDJFCB VTOC                   READ THE JFCB
         CLC   JFCBDSNM(8),ZEROS       WAS JFCB READ
         BNE   CKSERNO                 YES, DSNAME NOT ZEROS
         MVC   PRT(16),=C'NO JFCB READ FOR'
MOVDD    MVC   PRT+21(8),VTOC+40       DDNAME TO MESSAGE
         BAL   R9,PRINT                GO PRINT THE MESSAGE
         B     NEXVOL                  GO START NEXT VOLUME
CKSERNO  CLC   JFCBVOLS(8),=CL8' '     SERIAL NBR IN DD CARD OMITTED
         BNE   DS4NAME                 NO, O.K.
         MVC   PRT(20),=C'NO VOL SER IN DD FOR'
         B     MOVDD                   GO MOVE DDNAME
DS4NAME  MVI   JFCBDSNM,4              DATA SET NAME FOR FMT 4 DSCB
         MVC   JFCBDSNM+1(43),JFCBDSNM DATA SET NAME FOR FORMAT 4 DSC
         MVI   JFCBTSDM,X'08'          SUPPRESS JFCB REWRITE
         OPEN  VTOC,TYPE=J             OPEN USING MODIFIED JFCB
         L     R1,VTOC+44              GET DEB ADDRESS
         MVC   NEXCCHH,38(R1)          MOVE BEGIN CCHH TO NEXT RECADR
         MVI   NEXREC,1                BEGIN RECORD IS 1
         MVC   RNAME,JFCBVOLS          RNAME FOR ENQ IS VOLSER
         ENQ   (QNAME,RNAME,S,6,SYSTEM) ENQ ON VTOC
SEQREAD  OI    READCMD+4,X'40'         SET CHAIN FLAG TO READ COUNT
         MVC   VTSRCH,NEXRECA          MOVE IN NEXT REC ADDR
         BAL   R9,EXCPRTN              GO READ THE RECORD
         LA    R4,DSCB                 GET ADDRESS OF RECORD
         USING DSCBFMT1,R4
         CLI   DS1FMTID,C'4'           IS IT FORMAT 4
         BE    FORMAT4                 YES
         CLI   DS1FMTID,C'5'           IS IT A FORMAT 5
F5CHK    BE    FORMAT5                 YES
         CLI   DS1FMTID,C'1'           IS IT FORMAT 1
         BNE   SEQREAD                 NO, GO READ NEXT
DATASETS MVC   DSNAME,DS1DSNAM         MOVE DSNAME TO SORT FIELD
         MVC   INITVOL,DS1DSSN         INITIAL VOL SER TO SORT FIELD
         MVC   VOLSEQ,DS1VOLSQ         VOL SEQ NO TO SORT FIELD
         MVI   SEQ,1                   SORT SEQUENCE NO TO 1
DATAWRIT PUT   OUTPUT1,OUTREC          WRITE THE RECORD
         CLC   DS1PTRDS,ZEROS          POINTER TO NEXT DSCB ZERO
         BE    SEQREAD                 YES, NO FORMAT 2 OR 3
         MVC   VTSRCH,DS1PTRDS         MOVE POINTER TO IOB SEARCH FIELD
         MVI   READCMD+4,0             TURN OFF CHAIN FLAG
         BAL   R9,EXCPRTN              GO READ THE RECORD
         CLI   DS1FMTID,C'2'           IS IT A FORMAT 2
         BNE   CKFMT3                  NO
         MVI   SEQ,0                   SET SEQ TO INDICATE FORMAT 2
         B     DATAWRIT                GO WRITE THE RECORD
CKFMT3   CLI   DS1FMTID,C'3'           IS IT FORMAT 3
         BE    SETFMT3                 YES, O.K.
         MVC   PRT(27),=C'INCORRECT CHAIN POINTER FOR'
         MVC   PRT+28(44),DSNAME       DSNAME TO MESSAGE
         BAL   R9,PRINT                GO PRINT MESSAGE
         B     SEQREAD                 GO START NEXT DSCB
SETFMT3  MVI   SEQ,2                   SET SEQUENCE TO INDICATE FMT 3
         B     DATAWRIT                GO WRITE THE RECORD
FORMAT4  XC    OUTREC(50),OUTREC       CLEAR SORT FIELD
         LA    R8,DSCB                 GET DSCB ADDR
         USING DSCBFMT4,R8
         MVC   CYLVOL,DS4NOCYL         CYLS PER VOL TO OUT RECD
         MVC   TRKCYL,DS4TKLC          CYLS PER TRK TO OUT RECD
         MVC   BYTRK,DS4DEVTK          BYTES PER TRK TO OUT RECD
         XC    KBLKC(4),KBLKC          CLEAR BLOCK CONSTANT FIELDS
         MVC   KBLKC+1(1),DS4DEVI      CONST FOR KEYED BLOCK
         MVC   NKBLKC+1(1),DS4DEVK     CONST FOR UNKEYED BLOCK
         TM    DS4DEVFG,X'08'          2-BYTE KEYED BLK CONST
         BZ    SET4                    NO
         MVC   KBLKC,DS4DEVI           YES, MOVE KEYED BLOCK CONST
         DROP  R8
SET4     MVI   DSNAME,X'04'            SET FORMAT 4 INDICATOR
         MVC   VOLSER,JFCBVOLS         VOL SER NO TO SORT FIELD
         MVI   SEQ,0                   SET FORMAT 6 SEQ TO 0
PUT4     PUT   OUTPUT1,OUTREC          WRITE THE RECORD
         CLC   DS1PTRDS,ZEROS          POINTER TO FORMAT 6 IS ZERO
         BE    SEQREAD                 YES, GO CONTINUE
         MVI   READCMD+4,0             SET CHAIN FLAG OFF
         MVC   VTSRCH,DS1PTRDS         MOVE POINTER TO IOB SEARCH ADDR
         BAL   R9,EXCPRTN              GO READ FORMAT 6 DSCB
         CLI   DS1FMTID,C'6'           IS IT A FORMAT 6
         BE    SETFMT6                 YES, O.K.
         MVI   PRT+35,C'6'             SET FORMAT TYPE IN MESSAGE
CHAINER  MVC   PRT(34),=C'INCORRECT CHAIN POINTER FOR FORMAT'
         BAL   R9,PRINT                GO PRINT MASSAGE
         B     SEQREAD                 GO READ NEXT DSCB
SETFMT6  MVI   DSNAME,6                SET FORMAT 6 INDICATOR
         IC    R1,SEQ                  GET LAST SEQ NBR
         LA    R1,1(R1)                ADD 1
         STC   R1,SEQ                  STORE UPDATED CTR
         B     PUT4                    GO WRITE THE RECORD
FORMAT5  MVI   DSNAME,5                SET FORMAT 5 INDICATOR
         MVI   SEQ,1                   SEQ FORMAT 5 SEQUENCE NO TO 1
         MVI   F5CHK+1,0               NOP FORMAT 5 SWITCH
PUT5     PUT   OUTPUT1,OUTREC          WRITE FORMAT 5 RECORD
         MVI   READCMD+4,0             SET CHAIN FLAG OFF
         CLC   DS1PTRDS,ZEROS          POINTER TO NEXT FORMAT 5 IS ZERO
         BE    SEQREAD                 YES, GO CONTINUE
         MVC   VTSRCH,DS1PTRDS         MOVE POINTER TO IOB SEARCH ADDR
         BAL   R9,EXCPRTN              GO READ THE FORMAT 5 DSCB
         CLI   DS1FMTID,C'5'           IS IT FORMAT 5
         BE    SETFMT5                 YES, O.K.
         MVI   PRT+35,C'5'             INDICATE FORMAT 5 CHAIN ERROR
         B     CHAINER                 GO FINISH MESSAGE
SETFMT5  IC    R1,SEQ                  GET SEQUENCE NUMBER
         LA    R1,1(R1)                ADD 1
         STC   R1,SEQ                  STORE UPDATED COUNTER
         B     PUT5                    GO WRITE THE RECORD
*
*
*
EXCPRTN  EXCP  VTOCIOB                 CALL O/S FOR READ
         WAIT  ECB=VTECB               WAIT FOR READ
         CLI   VTECB,X'7F'             ANY ERROR
         BCR   8,R9                    NO, RETURN
         CLI   VTECB,X'42'             EXTENT VIOLATION
         BNE   IOERR                   NO, MUXT BE AN ERROR
         DEQ   (QNAME,RNAME,6,SYSTEM)  RELEASE VTOC
         CLOSE VTOC                    END OF VTOC, CLOSE
         MVI   F5CHK+1,X'80'           SET FORMAT 5 SWITCH
         B     NEXVOL                  GO OPEN NEXT VOLUME
IOERR    ABEND 104,DUMP
*
*
*
PRINT    EQU   *                       *** PRINT ROUTINE ***
         PUT   PRINT1,PRTLINE
         MVC   PRT,PRTCC               CLEAR PRINT LINE
         L     R1,ERRCOD               GET ERROR INDICATOR
         LA    R1,4(R1)                INCREMENT ERROR
         ST    R1,ERRCOD               STORE UPDATED ERROR INDIC
         CLC   PARM,=C'DUMP'           USER WANTS DUMP
         BCR   8,R9                    NO, RETURN TO CALLER
         ABEND 999,DUMP
*
*
*
EOJ      EQU   *                       *** END OF JOB ***
         CLOSE (OUTPUT1,,PRINT1)       CLOSE OUTPUT FILES
         L     R13,SAVE+4              GET CALLER'S SAVE AREA ADDRESS
         MVC   16(4,R13),ERRCOD        ERROR INDICATOR TO R15 IN SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         BR    R14                     RETURN TO CALLER
*
*
*
*                       *** DATA, WORK, FILE, AND STORAGE AREAS ***
*
SAVE     DS    18F
CMDCHN   DS    0D                      COMMAND CHAIN TO READ VTOC
         CCW   X'31',VTSRCH,X'40',5    SEARCH ID EQUAL
         CCW   8,*-8,0,0               NOT FOUND, REPEAT SEARCH
READCMD  CCW   X'0E',DSCB,X'40',140    READ KEY AND DATA INTO DSCB
         CCW   X'92',NEXCNT,0,8        READ COUNT OF NEXT RECORD, M-T
NEXCNT   DS    0D                      COUNT RECORD OF NEXT RECORD
NEXRECA  DS    0XL5                    CCHHR ADDRESS OF NEXT RECORD
NEXCCHH  DS    XL4                     CCHH ADDRESS OF NEXT RECORD
NEXREC   DS    XL1                     RECORD NBR ON TRACK
         DS    XL3                     KEY LENG AND DATA LENG
ERRCOD   DC    F'0'                    ERROR INDICATOR
QNAME    DC    CL8'SYSVTOC'            QNAME TO ENQ ON VTOC
RNAME    DS    CL6                     RNAME = VOLUME SERIAL NBR
ZEROS    DC    2F'0'
PARM     DC    C'    '                 PARM FIELD ON JOB CARD
OUTREC   DS    0CL210                  OUTPUT RECORD, 210 BYTES
DSNAME   DS    CL44                    DSNAME OF ASSOCIATED FORMAT 1 RE
INITVOL  DS    CL6                     INITIAL VOL SER IN FILE
VOLSEQ   DS    XL2                     VOLUME SEQUENCE NUMBER
SEQ      DS    XL1                     0=FMT2, 1=FMT1, 2=FMT3
VOLSER   DS    CL6                     VOLUME SERIAL NBR
         DC    X'00'
CYLVOL   DS    H                       CYLINDERS PER VOLUME
TRKCYL   DS    H                       TRACKS PER CYLINDER
BYTRK    DS    H                       BYTES PER TRACK
KBLKC    DS    H                       KEYED BLOCK CONSTANT
NKBLKC   DS    H                       DECREMENT IF NO KEY CONST
DSCB     DS    CL140                   DSCB AREA
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
TIOTDD   DS    F                       ADDRESS OF NEXT DD ENTRY IN TIOT
PRTLINE  DS    0CL133                  PRINT LINE OUTPUT
PRTCC    DC    C' '                    CARRIAGE CONTROL CHARACTER
PRT      DC    CL132' '                PRINT LINE
         JFCBDEF DSECT=NO
VTOC     DCB   DDNAME=VTOC,MACRF=E,EXLST=JFCBADDR
*
*
*
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=210
PRINT1   DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=133,BLKSIZE=133,       X
               DDNAME=PRINT1
         DSCBDEF
         END

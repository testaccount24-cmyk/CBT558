      TITLE '**** PRINT THE HEAVY HITTER LIST DETAIL ****'
         PRINT NOGEN
SMFSTEP4 CSECT
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  COPY THE PROGRAM BASE REG
         USING SMFSTEP4,R3
         B     INIT1                   GO TO INITIALIZATION
         DC    CL8'SMFSTEP4'           PROGRAM NAME
SAVE     DS    18F                     REGISTER SAVE AREA
INIT1    LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              CHAIN FORWARD
         ST    R13,SAVE+4              CHAIN BACKWARD
         LR    R13,R15                 POINT R13 TO MY SAVE
*
**********************************************************************
*  READ OUTPUT FROM SMFSTEP3. USE THE PROGRAM NAME AS A SEARCH       *
*  ARGUMENT FOR THE SORTED OUTPUT FROM SMFSTEPT. PRINT ALL THE       *
*  MATCHING PROGRAM RECORDS INCLUDING THE SYSTEM ID, JOB NAME, STEP  *
*  NAME, START DATE AND TIME, STEP CPU TIME, SRB TIME, AND EXCPS.    *
*  AUTHOR R. THORNTON APR 1994                                       *
**********************************************************************
*
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
*
OPENS    OPEN  (PRINT1,OUTPUT,INPUT1,,INPUT2)
         BAS   R9,HEADING              PRINT FIRST HEADING
*
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
*
MAINLINE GET   INPUT1,SMFSTEP3         GET SMFSTEP3 RECORD
GETSTEPT GET   INPUT2,SMFSTEPT         GET SMFSTEPT RECORD
         CLC   INTPGM,IN3PGM           SAME PROGRAM NAME?
         BL    GETSTEPT                NO. SMFSTEPT LOW
         BH    MAINLINE                NO, SMFSTEPT HIGH
         MVC   PRTSID,INTSID           SYSTEM ID TO PRINT
         MVC   PRTJOBN,INTJOBN         JOB NAME TO PRINT
         MVC   PRTSTEP,INTSTEP         STEP NAME TO PRINT
         MVC   PRTPGM,INTPGM           PROGRAM NAME TO PRINT
         MVC   PRTSTDAT,=X'40212061202020' EDIT MASK
         ED    PRTSTDAT,INTSTDAT+1     START DATE TO PRINT
         ICM   R1,15,INTSTTIM          PICK UP START TIME
         CVD   R1,DBLWD                CONVERT TO PASKED DECIMAL
         DP    DBLWD,=PL3'6000'        COMPUTE MINUTES
         ZAP   DBLWD,DBLWD(5)          RIGHT JUSTIFY
         DP    DBLWD,=PL2'60'          COMPUTE HOURS AND MINUTES
         UNPK  PRTSTHR,DBLWD+4(2)      UNPACK HOUR
         OI    PRTSTHR+1,C'0'          CLEAR SIGN
         MVI   PRTSTCOL,C':'           DELIMIT TIME
         UNPK  PRTSTMN,DBLWD+6(2)      UNPACK HOUR
         OI    PRTSTMN+1,C'0'          CLEAR SIGN
         MVC   PRTJBCPU,TIMEDIT        EDIT WORD
         ICM   R1,15,INTJBCPU          GET TASK CPU TIME
         CVD   R1,DBLWD                CONVERT TO PACKED
         ED    PRTJBCPU,DBLWD          TASK CPU TIME TO PRINT
         MVC   PRTSRCPU,TIMEDIT        EDIT WORD
         ICM   R1,15,INTSRCPU          GET SRB CPU TIME
         CVD   R1,DBLWD                CONVERT TO PACKED
         ED    PRTSRCPU,DBLWD          SRB CPU TIME TO PRINT
         MVC   PRTIOS,IOSEDIT          EDIT WORD
         ICM   R1,15,INTIOS            GET TOTAL EXCPS
         CVD   R1,DBLWD                CONVERT TO PACKED
         ED    PRTIOS,DBLWD            TOTAL EXCPS TO PRINT
         PUT   PRINT1,PRTREC           WRITE PRINT LINE
         AP    LINECT,P1               INCREMENT LINE COUNTER
         CP    LINECT,ENDPAGE          PAGE IS FULL?
         BL    MAINLINE                NO
         BAS   R9,HEADING              YES, PRINT A HEADING
         B     GETSTEPT                GO READ AGAIN
*
***********************************************************************
*        PRINT A HEADING                                              *
***********************************************************************
*
HEADING  AP    PAGENO,P1               INCREMENT PAGE COUNTER
         MVC   HDGPAGE,PAGEDIT         MOVE EDIT MASK
         ED    HDGPAGE,PAGENO          MOVE PAGE NUMBER
         PUT   PRINT1,HDGLINE          WRITE HEADING LINE
         ZAP   LINECT,P1               RESET LINE COUNTER
         BR    R9                      EXIT
*
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
*
EOJ      CLOSE (PRINT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         GET CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE = 0
         BR    R14                     RETURN TO CALLER
*
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
P0       DC    P'0'                    CONSTANT 0
P1       DC    P'1'                    CONSTANT 1
PAGENO   DC    PL2'0'                  PAGE COUNTER
PAGEDIT  DC    XL4'40202021'           PAGE EDIT MASK
ENDPAGE  DC    P'58'                   NUMBER LINES PER PAGE
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
TIMEDIT  DC    XL20'40202020206B2020206B2020206B2020214B2020'
IOSEDIT  DC    XL20'402020206B2020206B2020206B2020206B202020'
*
SMFSTEP3 DS    0CL80           SMFSTEP3 RECORD
IN3PGM   DC    CL8' '          PROGRAM NAME
         DC    CL1' '
IN3TCPU  DC    CL25' '         TOTAL CPU TIME
         DC    CL1' '
IN3RUNS  DC    CL5' '          TOTAL RUNS
         DC    CL1' '
IN3AVG   DC    CL19' '         AVERAGE CPU PER RUN
         DC    CL20' '
*
SMFSTEPT DS    0CL80
INTSID   DS    CL4             SMF30SID: SYSTEM ID
INTJOBN  DS    CL8             SMF30JBN: JOB NAME
INTSTEP  DS    CL8             SMF30STM: STEP NAME
INTPGM   DS    CL8             SMF30PGM: PROGRAM NAME
INTSTDAT DS    PL4             SMF30RSD: START DATE = 0CYYDDDF
INTSTTIM DS    XL4             SMF30RST: START TIME = SEC/100
INTJBCPU DS    XL4             SMF30CPT: TASK CPU TIME = SEC/100
INTSRCPU DS    XL4             SMF30CPS: SRB CPU TIME = SEC/100
INTIOS   DS    XL4             SMF30TEP: TOTAL EXCPS
         DS    CL32
*
PRTREC   DS    0CL133
         DC    CL1' '
PRTSID   DC    CL4' '          SYSTEM ID
         DC    CL1' '
PRTJOBN  DC    CL8' '          JOB NAME
         DC    CL1' '
PRTSTEP  DC    CL8' '          STEP NAME
         DC    CL1' '
PRTPGM   DC    CL8' '          PROGRAM NAME
PRTSTDAT DC    CL7' '          START DATE = YY/DDD
         DC    CL1' '
PRTSTHR  DC    CL2' '          START HOUR
PRTSTCOL DC    CL1' '          COLON
PRTSTMN  DC    CL2' '          START MINUTE
         DC    CL1' '
PRTJBCPU DC    CL20' '         TASK CPU TIME IN SEC/100
         DC    CL1' '
PRTSRCPU DC    CL20' '         SRB CPU TIME IN SEC/100
         DC    CL1' '
PRTIOS   DC    CL20' '         TOTAL EXCPS
         DC    CL25' '
*
HDGLINE  DS    0CL133
         DC    CL1'1'
         DC    CL5' SID '      SYSTEM ID
         DC    CL9' JOBNAME '  JOB NAME
         DC    CL9'STEPNAME '  STEP NAME
         DC    CL8' PGMNAME'   PROGRAM NAME
         DC    CL14' **STARTED*** ' START DATE AND TIME
         DC    CL21'    TASK CPU SECONDS ' TASK CPU TIME
         DC    CL21'     SRB CPU SECONDS ' SRB CPU TIME
         DC    CL21'         TOTAL EXCPS ' TOTAL EXCPS
         DC    CL8'   PAGE '
HDGPAGE  DS    CL4
         DC    CL12' '
         LTORG
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
*
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOJ,              X
               RECFM=FB,LRECL=80
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FBA,LRECL=133
*
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

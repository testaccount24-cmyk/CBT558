         TITLE '*** CLIST STATISTICS COLLECTION PROGRAM ***'
CLISTMON CSECT
CLISTMON AMODE 24
CLISTMON RMODE 24
***********************************************************************
* CALLED BY A CLIST BEING EXECUTED. THE NAME OF THE CLIST IS PASSED AS*
* A PARAMETER. THE CLIST STATS FILE IS DYNAMICALLY ALLOCATED, OPENED, *
* AND A RECORD IS FORMATTED AND WRITTEN TO IT. THE STATS FILE IS THEN *
* CLOSED AND DEALLOCATED.                                             *
*                                                                     *
* RECORD WRITTEN CONTAINS CLIST NAME AND JOB/TSOSERID NAME            *
*                                                                     *
* AUTHOR R THORNTON AUG 1998                                          *
***********************************************************************
*
***********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS           *
***********************************************************************
         USING CLISTMON,R3
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     GETPARM                 BRANCH AROUND CONSTANTS
         DC    CL8'CLISTMON'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
***********************************************************************
* GET CLIST NAME FROM THE PASSED PARM AND MOVE IT TO THE STAT RECORD  *
***********************************************************************
GETPARM  L     R1,0(R1)                POINT TO FIRST (ONLY) PARM
         MVC   CLSTNAME,2(R1)          CLIST NAME TO STAT RECORD
***********************************************************************
* DYNAMICALLY ALLOCATE THE CLIST STATS DATASET                        *
***********************************************************************
         LA    R1,ALLOC                POINT TO ALLOCATION PARMS
         DYNALLOC ,                    ALLOCATE DYNAMICALLY
         LTR   R15,R15                 SUCCESSFUL ALLOCATION?
         BNZ   EOJ                     NO, EXIT
***********************************************************************
* OPEN THE STATS DATASET, BUILD THE STATS RECORD, AND WRITE IT        *
***********************************************************************
         MVC   STATS+40(8),DDNAME      MOVE DDNAME TO DCB
         OPEN  (STATS,OUTPUT)          OPEN CLIST STATS FILE
         TM    STATS+48,X'10'          SUCCESSFUL OPEN?
         BZ    EOJ                     NO, EXIT
         L     R1,16                   POINT TO CVT
         L     R1,0(R1)                POINT TO NEW/OLD WORDS
         L     R1,0(R1)                POINT TO MY TCB
         L     R1,12(R1)               POINT TO MY TIOT
         MVC   USERNAME,0(R1)          JOB/TSOUSER NAME
         PUT   STATS,STATREC           WRITE THE STATS RECORD
***********************************************************************
* END OF PROCESSING, CLOSE AND FREE THE STATS DATASET AND RETURN      *
***********************************************************************
EOJ      CLOSE STATS                   CLOSE STATS FILE
         LA    R1,DEALLOC              POINT TO DEALLOCATION PARMS
         DYNALLOC ,                    DEALLOCATE DSNAME
         L     R13,4(R13)              POINT TO CALLER'S SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*       DATA, WORK, AND STORAGE AREAS                                 *
***********************************************************************
STATREC  DS    0CL16                   CLIST STATS RECORD
CLSTNAME DS    CL8                     CLIST NAME
USERNAME DS    CL8                     JOB/TSOUSER NAME
*
*****************************************************************
* DYNALLOC PARAMETERS TO DEALLOCATE THE CLIST STATS DATASET     *
*****************************************************************
         DS    0F
DEALLOC  DC    X'80',AL3(DEALLPB)      POINTER TO PARAMETER BLOCK
DEALLPB  DS    0XL20                   DDDYNAM PARAMETER BLOCK
         DC    AL1(20)                 LENGTH OF BLOCK
         DC    AL1(2)                  REQUEST CODE = 02 (DEALLOC)
         DC    XL2'0000'               FLAGS
DEALLERR DC    AL2(0)                  ERROR RETURN CODE
DEALLINF DC    AL2(0)                  INFORMATION REASON CODE
         DC    A(DEALLTU)              POINTER TO POINTER LIST
         DC    A(0)                    RESERVED
         DC    A(0)                    FLAGS2
DEALLTU  DS    0F                      LIST OF TEXT UNIT POINTERS
         DC    XL1'80',AL3(DSNKEY)     DSNAME IS ONLY TEXT UNIT
********************************************************************
* DYNALLOC PARAMETERS TO ALLOCATE THE CLIST STATS DATASET DISP=MOD *
********************************************************************
         DS    0F
ALLOC    DC    X'80',AL3(ALLOCPB)      POINTER TO PARAMETER BLOCK
ALLOCPB  DS    0XL20                   DDDYNAM PARAMETER BLOCK
         DC    AL1(20)                 LENGTH OF BLOCK
         DC    AL1(1)                  REQUEST CODE = 01 (ALLOC DSN)
         DC    XL2'4000'               FLAGS: MAKE NEW ALLOC
ALLOCERR DC    AL2(0)                  ERROR RETURN CODE
ALLOCINF DC    AL2(0)                  INFORMATION REASON CODE
         DC    A(ALLOCTU)              POINTER TO POINTER LIST
         DC    A(0)                    RESERVED
         DC    A(0)                    FLAGS2
ALLOCTU  DS    0F                      LIST OF TEXT UNIT POINTERS
         DC    A(DSNKEY)               DSNAME
         DC    A(STATKEY)              STATUS KEY
         DC    A(NDISPKEY)             NORMAL DISP KEY
         DC    A(CDISPKEY)             CONDITIONAL DISP KEY
         DC    XL1'80',AL3(DALRTKEY)   RETURN DDNAME IS LAST TEXT UNIT
         DS    0F
DSNKEY   DC    AL2(2),AL2(1),AL2(44)
DSNAME   DC    CL44'TEST.CLIST.STATS'  DATASET NAME
         DS    0F
STATKEY  DC    AL2(4),AL2(1),AL2(1)
STATUS   DC    XL1'02'                 DATASET STATUS: MOD
*                                       01=OLD, 02=MOD, 04=NEW, 08=SHR
         DS    0F
NDISPKEY DC    AL2(5),AL2(1),AL2(1)
NORMDISP DC    XL1'08'                 NORMAL DISPOSITION: KEEP
*                                      01=UNCATLG, 02=CATLG, 04=DELETE,
*                                      08=KEEP
         DS    0F
CDISPKEY DC    AL2(6),AL2(1),AL2(1)
CONDDISP DC    XL1'08'                 CONDITIONAL DISPOSITION: KEEP
*                                      (SAME AS NORMDISP)
         DS    0F
DALRTKEY DC    AL2(85),AL2(1),AL2(8)
DDNAME   DC    CL8' '                     RETURN DDNAME AREA
********************************************************************
* DEFINE THE CLIST STATS DATASET                                   *
********************************************************************
STATS    DCB   DSORG=PS,MACRF=PM,DDNAME=STATS,RECFM=F,LRECL=16
********************************************************************
* REGISTER EQUATES                                                 *
********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

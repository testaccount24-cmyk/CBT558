         PRINT NOGEN
         TITLE '*****ABENDJOB: CAUSE VARIOUS ABENDS *****'
ABENDJOB SVLNK R3
*
***********************************************************************
* CAUSES ABEND SPECIFIED IN PARM FIELD. VALID VALUES ARE:             *
*    S0C1, S0C4, S0C6, S0C7, S0C9, S0CB, S001, S002, SD37             *
*                                                                     *
*                                                                     *
* AUTHOR R THORNTON MAY 1989                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: CHECK PARM FIELD                                    *
***********************************************************************
*
         L     R2,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R2),H4              VALID PARM FIELD SIZE?
         BE    CHKPARM                 YES
BADPARM  WTO   'VALID PARM VALUES: S0C1, S0C4, S0C6, S0C7, S0C9, S0CB, X
               S001, S002, AND SD37',ROUTCDE=11
         ABEND 777,DUMP                TERMINATE ABNORMALLY
CHKPARM  CLC   2(4,R2),=C'S0C1'        SOC1 ABEND REQUESTED?
         BE    S0C1                    YES
         CLC   2(4,R2),=C'S0C4'        SOC4 ABEND REQUESTED?
         BE    S0C4                    YES
         CLC   2(4,R2),=C'S0C6'        SOC6 ABEND REQUESTED?
         BE    S0C6                    YES
         CLC   2(4,R2),=C'S0C7'        SOC7 ABEND REQUESTED?
         BE    S0C7                    YES
         CLC   2(4,R2),=C'S0C9'        SOC9 ABEND REQUESTED?
         BE    S0C9                    YES
         CLC   2(4,R2),=C'S0CB'        SOCB ABEND REQUESTED?
         BE    S0CB                    YES
         CLC   2(4,R2),=C'S001'        S001 ABEND REQUESTED?
         BE    S001                    YES
         CLC   2(4,R2),=C'S002'        S001 ABEND REQUESTED?
         BE    S002                    YES
         CLC   2(4,R2),=C'SD37'        S001 ABEND REQUESTED?
         BE    SD37                    YES
         B     BADPARM                 NONE OF THESE, ABEND
S0C1     DC    H'0'                    CAUSE S0C1 ABEND
S0C4     MVI   0,0                     CAUSE S0C4 ABEND
S0C6     LA    R12,S0C6+1              POINT TO ODD BOUNDARY
         BR    R12                     CAUSE S0C6 ABEND
S0C7     AP    S0C1,S0C4               CAUSE S0C7 ABEND
S0C9     SR    R10,R10                 SET
         LA    R11,100                   UP REGS
         SR    R12,R12                      FOR
         DR    R10,R12                        S0C9 ABEND
S0CB     DP    PKNBR,PKZERO            CAUSE S0CB ABEND
S001     OPEN  INPUT1                  OPEN FILE
         GET   INPUT1,INREC            CAUSE S001 ABEND
S002     OPEN  INPUT2                  OPEN FILE
         GET   INPUT2,INREC            CAUSE S002 ABEND
SD37     OPEN  (OUTPUT1,OUTPUT)        OPEN FILE
WRTLOOP  PUT   OUTPUT1,INREC           CAUSE SD37 ABEND
         B     WRTLOOP                   BY LOOPING
*
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
H4       DC    H'4'                    CONSTANT 4
PKNBR    DC    PL5'100'                VALID PACKED NUMBER
PKZERO   DC    PL1'0'                  VALID PACKED ZERO
INREC    DS    CL67                    INPUT RECORD AREA
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,                        X
               RECFM=FB,LRECL=67,BLKSIZE=67
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,                        X
               RECFM=VB,LRECL=67,BLKSIZE=23440
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=67,BLKSIZE=67
         END

         TITLE '*** PRINT EXTERNAL SYMBOL DICTIONARIES ***'
EXTSYMS SVLNK R3
*******************************************************************
* EACH DIRECTORY ENTRY OF THE INPUT PDS FILE IS READ USING THE    *
* READDIR SUBROUTINE. A FIND MACRO LOCATES THE DATA MEMBER, WHICH *
* IS THEN READ USING BPAM READS. THE CESD RECORDS ARE PROCESSED TO*
* FIND ALL EXTERNAL SYMBOL DICTIONARY (ESD) ENTRIES. EASH ESD     *
* ENTRY IS FORMATTED AND WRITTEN TO THE OUTPUT1 FILE. EACH OUTPUT *
* RECORD CONTAINS MEMBER NAME, ESD SYMBOL NAME, ESD TYPE, LINKEDIT*
* ASSIGNED ENTRY POINT ADDRESS, SEGMENT NUMBER, AND ID/LENGTH.    *
* AUTHOR R THORNTON (BASED ON FINDSUBR) SEP 1998                  *
*******************************************************************
*
*******************************************************************
* INITIALIZATION: OPEN FILES AND LOAD THE CONTROL TABLE           *
*******************************************************************
         OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
         CALL  READDIR,OPEN            OPEN DIRECTORY FILE
*******************************************************************
* MAINLINE: READ DIRECTORY ENTRY, AT END OF DIRECTORY, TERMINATE  *
*           PROCESSING. FOR EACH DIRECTORY, ISSUE A FIND MACRO TO *
*           POSITION TO THE MEMBER, THEN READ IT USING BPAM READS.*
*           FOR EACH CESD RECORD READ, SEARCH FOR EXTERNAL REFER- *
*           ENCES.
*******************************************************************
MAINLINE CALL  READDIR,(READ,DIRENTRY) READ DIRECTORY ENTRY
         CLI   DIRENTRY,X'FF'          IS IT EOF
         BE    EOJ                     YES
         FIND  INPUT1,DIRTTR,C         FIND THE MEMBER
         LTR   R15,R15                 FOUND O.K.
         BZ    READMEM                 YES
         WTO   'EXTSYMS: ERROR RETURNED BY FIND MACRO',ROUTCDE=11
         ABEND  111,DUMP               TERMINATE IN ERROR
READMEM  READ  DECB,SF,INPUT1,INREC,248 READ LOAD MODULE RECORD
         CHECK DECB                    WAIT FOR COMPLETION
         CLI   RECDID,X'20'            IS IT A CESD RECORD
         BE    GETSIZE                 YES, GO PROCESS
         TM    RECDID,X'01'            IS IT A CONTROL RECORD
         BO    MAINLINE                YES, GO GET NEXT MEMBER
         B     READMEM                 NO, READ NEXT LOAD MODULE RECORD
GETSIZE  LA    R5,ESDATA               GET ADDR OF 1ST ESD ENTRY
         LH    R2,COUNT                GET LENGTH OF ESD DATA
         SRL   R2,4                    DIVIDE BY 16 BYTES PER ENTRY
         USING ESDNTRY,R5
FORMAT   MVC   OUTREC,BLANKS           CLEAR OUTPUT RECORD AREA
         MVC   OUTMEM,DIRNAME          MEMBER NAME TO OUTPUT RECORD
         MVC   OUTSYM,ESDSYM           SYMBOL NAME
         MVC   WORK,ESDTYPE            COPY ESD TYPE/SUBCLASS
         NI    WORK,X'0F'              ISOLATE ESD TYPE
         LA    R1,TYPETBL              POINT TO ESD TYPE TABLE
FMCKND1  CLI   0(R1),X'FF'             END OF TABLE?
         BE    MOVTYPE                 YES
         CLC   0(1,R1),WORK            FOUND TYPE ENTRY?
         BE    MOVTYPE                 YES
         LA    R1,15(R1)               STEP TO NEXT ENTRY
         B     FMCKND1                 CONTINUE SEARCH
MOVTYPE  MVC   OUTTYPE,1(R1)           MOVE TYPE FROM TABLE
         TM    ESDTYPE,X'10'           DELETE OR REPLACE?
         BZ    FMCK20                  NO
         MVC   OUTDEL,=CL3'DEL'        YES
FMCK20   TM    ESDTYPE,X'20'           INSERT?
         BZ    FMCK40                  NO
         MVC   OUTINS,=CL3'INS'        YES
FMCK40   TM    ESDTYPE,X'40'           CHAIN?
         BZ    FMCK80                  NO
         MVC   OUTCHN,=CL3'CHN'        YES
FMCK80   TM    ESDTYPE,X'80'           MAP?
         BZ    MOVADDR                 NO
         MVC   OUTMAP,=CL3'MAP'        YES
MOVADDR  UNPK  OUTADDR(7),ESDADDR(4)   UNPACK ADDRESS
         MVI   OUTADDR+6,C' '          CLEAR TRASH
         TR    OUTADDR,TRCHAR-C'0'     MAKE PRINTABLE
         UNPK  OUTSEG(3),ESDSEGNO(2)      UNPACK SEGMENT NBR
         MVI   OUTSEG+2,C' '           CLEAR TRASH
         TR    OUTSEG,TRCHAR-C'0'      MAKE PRINTABLE
         UNPK  OUTIDLEN(7),ESDIDLEN(4) UNPACK ID/LENGTH
         MVI   OUTIDLEN+6,C' '         CLEAR TRASH
         TR    OUTIDLEN,TRCHAR-C'0'    MAKE PRINTABLE
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT
NEXTESD  BCT   R2,STEPESD              LOOP THRU RECORD
         B     READMEM                 AT END GO READ AGAIN
STEPESD  LA    R5,16(R5)               STEP TO NEXT ESD ENTRY
         B     FORMAT                  CONTINUE PROCESSING
         DROP  R5
*******************************************************************
* END OF JOB: CLOSE FILES AND TERMINATE PROCESSING                *
*******************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       YES,CLOSE FILES
         CALL  READDIR,CLOSE           CLOSE DIRECTORY FILE
         L     R13,4(R13)              GET CALLERS SAVE ADDR
         LM    R14,R12,12(R13)         RESTORE HIS REGISTERS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
*******************************************************************
* DATA, CONSTANT, AND FILE AREAS                                  *
*******************************************************************
DBLWD    DS    D                       DOUBLEWORD
*
OPEN     DC    CL9'OINPUT1'            OPEN PARAMETER LIST
READ     DC    C'R'                    READ PARAMETER
CLOSE    DC    C'C'                    CLOSE PARAMETER
DIRENTRY DS    0CL11                   *** DIRECTORY ENTRY FROM READDIR
DIRNAME  DS    CL8                     MEMBER NAME
DIRTTR   DS    XL3                     TTR OF MEMBER ON LIBRARY
         DC    X'00'
*
WORK     DS    CL1                     WORK FIELD
TRCHAR   DC    CL16'0123456789ABCDEF'  TRANSLATE HEX TO PRINTABLE
BLANKS   DC    CL80' '                 BLANKS
TYPETBL  DS    0CL15                   ESD TYPE TABLE
         DC    X'00',CL14'SD-CSECT      '   SECTION DEFINITION
         DC    X'03',CL14'LR-LBL REF    '   LABEL REFERENCE
         DC    X'04',CL14'PC-PRIVATE    '   PRIVATE CODE
         DC    X'05',CL14'CM-COMMON     '   COMMON
         DC    X'06',CL14'PR-PSEUDO REG '   PSEUDO REGISTER
         DC    X'07',CL14'  -NULL       '   NULL
         DC    X'02',CL14'ER-EXT REF    '   EXTERNAL REFERENCE
         DC    X'0A',CL14'WX-WEAK EXTREF'   WEAK EXTERNAL REFERENCE
         DC    X'FF',CL14'??-UNIDENT    '   UNIDENTIFIABLE TYPE
*
OUTREC   DS    0CL80                   *** OUTPUT RECORD ***
OUTMEM   DC    CL8' '                    MEMBER NAME
         DC    CL1' '
OUTSYM   DC    CL8' '                    ESD SYMBOL NAME
         DC    CL1' '
OUTTYPE  DC    CL14' '                   ESD TYPE
         DC    CL1' '
OUTSUBCL DS    0CL16                     SUBCLASSIFICATIONS
OUTDEL   DC    CL3' '                       REPLACE/DELETE
         DC    CL1' '
OUTINS   DC    CL3' '                       INSERT
         DC    CL1' '
OUTCHN   DC    CL3' '                       CHAIN
         DC    CL1' '
OUTMAP   DC    CL3' '                       MAP
         DC    CL1' '
OUTADDR  DC    CL6' '                    LINKEDIT ASSIGNED ADDRESS
         DC    CL1' '
OUTSEG   DC    CL2' '                    SEGMENT NUMBER
         DC    CL1' '
OUTIDLEN DC    CL6' '                    ID/LENGTH
         DC    CL39' '
*
         DS    0H
INREC    DS    0CL248                  *** LOAD MODULE RECORD ***
RECDID   DS    XL1                     RECORD ID: (IN HEX)
*                20=CESD RECORD, 40=SYM RECORD, 10=SCATTER TRANSLATION,
*                01=CONTROL RECORD, 05=CONTROL RECORD BEFORE LAST TEXT
*                RECORD OF OVERLAY SEGMENT, 0D=CONTROL RECORD BEFORE
*                LAST TEXT RECORD OF MODULE (EOM), 02=RLD RECORD,
*                06=LAST RECORD OF SEGMENT, 0E=LAST RECORD OF MODULE
*                03=CONTROL AND RLD RECORD (FOLLOWED BY TEXT RECORD),
*                07=CONTROL AND RLD RECORD FOLLOWED BY LAST TEXT
*                RECORD OF SEGMENT (EOS), 0F=CONTROL AND RLD RECORD
*                FOLLOWED BY LAST TEXT RECORD OF MODULE (EOM),
*                80=CSECT IDENTIFICATION RECORD
         DS    XL3                     RESERVED, ZEROS
ESDID1   DS    XL2                     ESD ID OF 1ST ESD ITEM
COUNT    DS    CL2                     NBR BYTES OF ESD DATA
ESDATA   DS    0CL16                   *** ESD DATA ELEMENT ***
SYMBOL   DS    CL8                     EXTERNAL NAME (ZEROS IF NULL)
TYPE     DS    XL1                     SYMBOL TYPE: (X MAY BE ANY BITS)
*                X0=SD, X3=LR, X4=PC, X5=CM, X6=PR,
*                07=NULL, X2=ER, XA=WX, 1X=DELETE,
*                1X=REPLACE, 2X=INSERT, 4X=CHAIN, 8X=MAP
ADDRESS  DS    XL3                     ADDRESS OF SYMBOL IN MODULE. ZER
SEGNO    DS    XL1                     SEGMENT NBR IN WHICH SYMBOL APPE
*                                        ZERO FOR ER WX NULL
LENGTH   DS    0XL3                    LENGTH IF SD PC CM OR PR
*                                        ZERO IF WX ER NULL
*                                        X'06'=NEVER CALL
ID       DS    XL2                     ID IF TYPE IS LR
         DS    XL1
         DS    14CL16                  ROOM FOR 14 ADDITIONAL ESD ENTRI
*******************************************************************
* FILE DEFINITIONS                                                *
*******************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PO,MACRF=R,DDNAME=INPUT1,EODAD=MAINLINE,          X
               RECFM=U
*******************************************************************
* DESD ENTRY DESCRIPTION                                          *
*******************************************************************
ESDNTRY  DSECT ,                       *** ESD DATA ELEMENT ***
ESDSYM   DS    CL8                     EXTERNAL NAME (ZEROS IF NULL)
ESDTYPE  DS    XL1                     SYMBOL TYPE AND SUBCLASS
ESDADDR  DS    XL3                     ADDRESS OF SYMBOL IN MODULE OR 0
ESDSEGNO DS    XL1                     SEGMENT NBR WHERE SYMBOL APPEARS
ESDIDLEN DS    XL3                     LENGTH IF SD PC CM OR PR
EXTSYMS  CSECT
         LTORG
         END

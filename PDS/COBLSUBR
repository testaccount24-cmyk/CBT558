         TITLE '*** COBLSUBR: GET COBOL SUBROUTINE INFO ***'
***********************************************************************
* READS FILE OF LOAD LIBRARY CSECT DATA CREATED BY PROGRAM CSECTLST   *
* AND WRITES A FILE WHICH CAN BE USED TO PRINT THE SUBROUTINE INFO    *
* LISTING. OUTPUT FROM THIS PROGRAM IS ONE RECORD FOR EACH CSECT WHOSE*
* NAME DOES NOT BEGIN WITH "ILB". EACH RECORD CONTAINS THE DSNAME, THE*
* THE CSECT NAME, THE MEMBER NAME, AND TWO INDICATORS THAT SHOW       *
* WHETHER THE MEMBER CONTAINS ANY "ILB" CSECTS AND WHETHER THIS IS A  *
* MAIN ENTRY POINT OR A SUBROUTINE WITHIN THE MODULE. THE OUTPUT FILE *
* IS INTENDED TO BE SORTED IN CSECT ORDER AND WILL THEN BE INPUT TO   *
* PROGRAM COBSUBPT.                                                   *
*                                                                     *
* AUTHOR R THORNTON JUN 1992                                          *
***********************************************************************
*
COBLSUBR CSECT
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R3,R15                  SET CSECT BASE GPR
         USING COBLSUBR,R3
         LA    R15,SAVE                POINT TO MY SAVE AREA
         ST    R15,8(R13)              SET LSA
         ST    R13,SAVE+4              SET HSA
         LR    R13,R15                 POINT R13 TO MY SAVE AREA
***********************************************************************
*        OPEN FILES                                                   *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE GET   INPUT1,INREC            READ INPUT RECORD
         CLC   INMEMBR,CURRMEM         SAME MEMBER?
         BE    SAMEMEM                 YES
         BAS   R9,NEWMEM               TERMINATE OLD MEMBER
SAMEMEM  CLC   INCSECT(3),=CL3'ILB'    IS THIS AN ILB MODULE?
         BE    GOTILB                  YES
         L     R12,TBLA                POINT TO TABLE
         MVC   0(8,R12),INCSECT        SAVE CSECT NAME
         MVC   8(1,R12),INENTPT           AND ENTRY POINT INDIC
         LA    R12,9(R12)              STEP OVER 1 ENTRY
         ST    R12,TBLA                UPDATE ADDRESS
         B     MAINLINE                LOOP THROUGH INPUT FILE
GOTILB   MVI   CURRILB,C'*'            INDICATE MEMBER CONTAINS ILB'S
         B     MAINLINE                CONTINUE
***********************************************************************
*             TERMINATE OLD MEMBER, INITIALIZE FOR NEW                *
***********************************************************************
NEWMEM   CLC   CURRMEM,BLANK           ANY MEMBER IN PROGRESS?
         BE    INITNEW                 NO
         L     R12,TBLSTRT             POINT TO TAB
TBCKND   C     R12,TBLA                AT END OF TABLE?
         BE    INITNEW                 YES
         MVC   OMEMBR,CURRMEM          MEMBER NAME TO OUTPUT
         MVC   OILB,CURRILB            ILB INDICATOR TO OUTPUT
         MVC   ODSNAME,INDSNAME        MOVE DSNAME
         MVC   OCSECT,0(R12)           ENTRY POINT INDIC TO OUTPUT
         MVC   OMAIN,8(R12)            MAIN ENTRY INDIC TO OUTPUT
         PUT   OUTPUT1,OUTREC          WRITE OUTPUT RECORD
         LA    R12,9(R12)              ADVANCE TO NEXT ENTRY
         B     TBCKND                  LOOP TO WRITE ALL CSECTS
INITNEW  MVC   CURRMEM,INMEMBR         SAVE INCOMING MEMBER NAME
         MVI   CURRILB,C' '            CLEAR ILB INDICATOR
         MVC   TBLA,TBLSTRT            RESET TABLE POINTER
         BR    R9                      RETURN
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      BAS   R9,NEWMEM               FINISH CURRENT MEMBER
         CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,SAVE+4              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
*
SAVE     DS    18F                     SAVE AREA FOR CALLED PGMS
*
RETCODE  DS    F                       RETURN CODE
TBLSTRT  DC    A(TABLE)                ADDRESS OF TABLE
TBLA     DC    A(TABLE)                ADDRESS OF TABLE
CURRMEM  DC    CL8' '                  CURRENT MEMBER NAME
CURRILB  DC    CL1' '                  CURRENT ILB STATUS
BLANK    DC    CL80' '                 BLANKS
*
INREC    DS    0CL80                   OUTPUT RECORD
INMEMBR  DC    CL8' '                    MEMBER NAME
         DC    CL1' '
INENTPT  DC    CL1' '                    * IF ENTRY POINT FOR MODULE
INCSECT  DC    CL8' '                    CSECT NAME
         DC    CL1' '
INLANG   DC    CL3' '                    ASM OR COB
         DC    CL1' '
INADDR   DC    CL6' '                    CSECT OFFSET, PRINTABLE HEX
         DC    CL1' '
INSIZE   DC    CL6' '                    CSECT SIZE IN PRINTABLE HEX
         DC    CL1' '
INDSNAME DC    CL43' '                   DSNAME CONTAINING THE MEMBER
*
OUTREC   DS    0CL80                    OUTPUT RECORD
OCSECT   DS    CL8                        CSECT NAME
OMAIN    DS    CL1                        * = CSECT IS MAIN E.P.
OMEMBR   DS    CL8                        MEMBER NAME
OILB     DS    CL1                        * = MEMBER CONTAINS ILB'S
         DC    CL1' '
ODSNAME  DC    CL43' '
         DC    CL18' '
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FBA,LRECL=80
         LTORG
TABLE    DS    3000CL9
COBLSUBR CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

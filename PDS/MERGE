         TITLE '*** MERGE: MERGE DATA FROM ONE FILE INTO ANOTHER ***'
***********************************************************************
* MERGE DATA FROM TWO INPUT FILES BOTH SORTED ON THE SAME FIELD       *
* PROGRAM MUST BE REBUILT FOR EACH RUN. PRESENT SPECS ARE:            *
*  1) INPUT SORT KEYS IN COL 1-8, BOTH FILES.                         *
*  2) DATA FROM INPUT2 FILE COLS 10-16 OR 18-24 IS MOVED TO COLUMNS   *
*     10-16 OF THE INPUT1 RECORDS WHICH ARE THEN WRITTEN TO THE       *
*     OUTPUT1 FILE.                                                   *
*  3) UNMATCHED INPUT1 RECORDS ARE WRITTEN TO THE OUTPUT1 FILE WITH   *
*     BLANKS IN COL 10-16. UNMATCHED INPUT2 RECORDS ARE DROPPED. A    *
*     MESSAGE IS WRITTEN TO THE PRINT1 FILE FOR ALL UNMATCHED RECORDS.*
* AUTHOR R THORNTON DEC 1998                                          *
***********************************************************************
*
MERGE SVLNK R3,R4
***********************************************************************
*        OPEN FILES, PRINT HEADING                                    *
***********************************************************************
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT,INPUT1,,INPUT2) OPEN FILES
         BAS   R9,GETIN1               READ FIRST INPUT1 RECORD
         BAS   R9,GETIN2               READ FIRST INPUT2 RECORD
***********************************************************************
*             MAINLINE ROUTINE                                        *
***********************************************************************
MAINLINE CLC   REC1(8),REC2            SAME MEMBER NAME?
         BL    REC1LOW                 NO, INPUT1 RECORD LOW
         BH    REC2LOW                 NO, INPUT2 RECORD LOW
         MVC   REC1+9(7),REC2+9        MOVE COL 10-16
         CLC   REC1+9(7),=CL7'UNKNOWN' IS IT "UNKNOWN"?
         BE    MOV18                   YES
         CLC   REC1+9(7),=CL7'       ' IS IT BLANKS?
         BNE   MLWRT1                  YES
MOV18    MVC   REC1+9(7),REC2+17       MOVE COL 18-24
MLWRT1   BAS   R9,WRTOUT1              GO WRITE TO OUTPUT FILE
         BAS   R9,GETIN1               READ NEXT INPUT1 RECORD
         BAS   R9,GETIN2               READ NEXT INPUT2 RECORD
         B     MAINLINE                CONTINUE
REC1LOW  MVC   REC1+9(7),=CL7'      '  COL 10-16 BLANK
         BAS   R9,WRTOUT1              GO WRITE TO OUTPUT FILE
         MVC   PRT,REC1                UNMATCHED RECORD TO PRINT
         BAS   R9,PRINT                PRINT IT
         BAS   R9,GETIN1               READ NEXT INPUT1 RECORD
         B     MAINLINE                CONTINUE
REC2LOW  MVC   PRT,REC2                UNMATCHED RECORD TO PRINT
         BAS   R9,PRINT                PRINT IT
         BAS   R9,GETIN2               READ NEXT INPUT2 RECORD
         B     MAINLINE                CONTINUE
***********************************************************************
*        READ INPUT1 RECORD                                           *
***********************************************************************
GETIN1   GET   INPUT1,REC1             READ INPUT1 RECORD
         BR    R9                      RETURN TO CALLER
EOF1     MVC   REC1(7),HIVAL           SET RECORD TO HEX FF'S
         CLC   REC1(7),REC2            BOTH AT END?
         BE    EOJ                     YES, END OF JOB
         BR    R9                      NO, RETURN
***********************************************************************
*        READ INPUT2 RECORD                                           *
***********************************************************************
GETIN2   GET   INPUT2,REC2             READ INPUT2 RECORD
         BR    R9                      RETURN TO CALLER
EOF2     MVC   REC2(7),HIVAL           SET RECORD TO HEX FF'S
         CLC   REC1(7),REC2            BOTH AT END?
         BE    EOJ                     YES, END OF JOB
         BR    R9                      NO, RETURN
***********************************************************************
*             WRITE OUTPUT RECORD SUBROUTINE                          *
***********************************************************************
WRTOUT1  CLC   REC1+9(7),=CL7'UNKNOWN' UNKNOWN OWNER?
         BNE   WOWRT                   NO
         MVC   REC1+9(7),BLANKS        YES, RESET TO BLANKS
WOWRT    PUT   OUTPUT1,REC1            WRITE OUTPUT1 RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             WRITE PRINT LINE                                        *
***********************************************************************
PRINT    PUT   PRINT1,PRT              WRITE PRINT1 RECORD
         BR    R9                      RETURN TO CALLER
***********************************************************************
*             END OF JOB                                              *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,PRINT1,,INPUT1,,INPUT2) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
BLANKS   DC    CL107' '                BLANKS
HIVAL    DC    80X'FF'                 FIELD OF HEX FF'S
*
PRT      DC    CL80' '                 PRINT LINE
*
REC1     DS    CL80                    INPUT1 RECORD
*
REC2     DS    CL80                    INPUT2 RECORD
*
***********************************************************************
*             DCB'S                                                   *
***********************************************************************
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FB,LRECL=80
PRINT1   DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT1,                        X
               RECFM=FB,LRECL=80
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOF1,             X
               RECFM=FB,LRECL=80
INPUT2   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT2,EODAD=EOF2,             X
               RECFM=FB,LRECL=80
         LTORG
MERGE    CSECT
         END

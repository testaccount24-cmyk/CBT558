         TITLE '***** EXTRACT DATA FOR A DSNAME FROM AN SMF FILE *****'
         PRINT NOGEN
SMFDSNAM CSECT
**********************************************************************
*  WRITE AN EXTRACT REPORT CONTAINING ONE LINE FOR EACH SELECTED     *
*  SMF RECORD OF TYPE:                                               *
*    14 - INPUT OR RDBACK DATASET ACTIVITY                           *
*    15 - OUTPUT, UPDAT, INOUT, OR OUTIN DATASET ACTIVITY            *
*    17 - SCRATCH DATASET STATUS                                     *
*    18 - RENAME DATASET STATUS                                      *
*    30 - COMMON ADDRESS SPACE WORK (NOT PRESENTLY INCLUDED)         *
*    35 - LOGOFF (OUTPUT TO USERID FILE)                             *
*    61 - INTEGRATED CATALOG FACILITY DEFINE ACTIVITY                *
*    64 - VSAM COMPONENT OR CLUSTER STATUS                           *
*    65 - INTEGRATED CATALOG FACILITY DELETE ACTIVITY                *
*    66 - INTEGRATED CATALOG FACILITY ALTER ACTIVITY                 *
*  WHEN ONE OF THE ABOVE SMF RECORDS IS READ FOR A QUALIFYING DSNAME,*
*  AN OUT RECORD IS FORMATTED WHICH CONTAINS THE DSNAME, DATE AND    *
*  TIME OF REFERENCE, JOBNAME, DEVICE TYPE, AND AN INDICATOR SHOWING *
*  ACTION PERFORMED (CREATE, DELETE, UPDATE, READ, ETC).             *
*                                                                    *
*  DDNAME RECFM LRECL IN/OUT CONTENT                                 *
*  RAWSMF    VBS 32756  IN   RAW SMF DATA                            *
*  DSNEXTR   FB     65 OUT   DATASET INFO EXTRACT RECORDS            *
*  USREXTR   FB      8 OUT   TSO LOGONID RECORDS                     *
*  LISTING   FBA   133 OUT   TOTAL COUNTS                            *
*                                                                    *
*  AUTHOR R. THORNTON MAY 1998                                       *
**********************************************************************
*
***********************************************************************
*        INITIALIZATION                                               *
***********************************************************************
         USING SMFDSNAM,R3,R4
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R4,4095(R3)             SET UP SECOND
         LA    R4,1(R4)                    BASE REGISTER
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     OPENS                   BRANCH AROUND CONSTANTS
         DC    CL8'SMFDSNAM'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
OPENS    OPEN  (DSNEXTR,OUTPUT,USREXTR,OUTPUT,LISTING,OUTPUT,RAWSMF)
***********************************************************************
*        MAINLINE PROCESSING                                          *
***********************************************************************
MAINLINE L     R10,SMFRECDA             POINT TO SMF RECORD AREA
         GET   RAWSMF,4(R10)            GET SMF RECORD INTO SMFRECD
         USING TYPE1415,R10
         CLC   SMF14SID,=CL4'RCH1'      THIS A RCH1 RECORD?
         BNE   MAINLINE                 NO, IGNORE IT
         MVC   DSNRECD,BLANKREC         CLEAR OUTPUT RECORD AREA
         MVC   DSNSMFTY,SMF14RTY        MOVE IN SMF RECORD TYPE
         SR    R1,R1                    CLEAR FOR INSERT
         IC    R1,SMF14RTY              PICK UP RECORD TYPE
         SLL   R1,5                     X32 = OFFSET TO COUNTER
         A     R1,=A(COUNTERS)          POINT TO COUNTER
         L     R2,0(R1)                 GET COUNTER
         LA    R2,1(R2)                 ADD 1 TO COUNT
         ST    R2,0(R1)                 UPDATE COUNTER
         SR    R2,R2                    CLEAR FOR TRT
         TRT   SMF14RTY,SELECTYP        SCAN FOR DESIRED RECORD TYPES
         BZ    MAINLINE                 NOT A DESIRED TYPE, SKIP IT
         DROP  R10
BRTBL    B     BRTBL(R2)                BRANCH TO APPROPRIATE POINT
         B     GOT1415                  BRANCH IF TYPES 14, 15
         B     GOT17                    BRANCH IF TYPE 17
         B     GOT18                    BRANCH IF TYPE 18
         B     GOT30                    BRANCH IF TYPE 30
         B     GOT35                    BRANCH IF TYPE 35
         B     GOT61                    BRANCH IF TYPE 61
         B     GOT64                    BRANCH IF TYPE 64
         B     GOT65                    BRANCH IF TYPE 65
         B     GOT66                    BRANCH IF TYPE 66
***********************************************************************
*  PROCESS TYPE 14 AND 15 SMF RECORDS                                 *
***********************************************************************
         USING TYPE1415,R10
GOT1415  TM    SMF14RIN,X'11'           IGNORE IF TEMPORARY DATASET
         BNZ   MAINLINE                    OR VIO DATASET
         LA    R9,SMFJFCB1              POINT TO JFCB
         USING JFCBDSNM,R9
         TM    JFCBTSDM,JFCSDS          IGNORE IF SYSIN OR
         BNZ   MAINLINE                    SYSOUT DATASET
         MVC   DSNJOB,SMF14JBN          JOB NAME
         ZAP   DSNDATE,SMF14DTE         REFERENCE DATE
         MVC   DSNTIME,SMF14TME         REFERENCE TIME
         MVC   DSNDSNAM,JFCBDSNM        DATASET NAME
         MVC   DSNDSORG,=C'PS'          ASSUME DSORG=PS
         TM    JFCDSRG1,JFCORGPS        IS IT PS?
         BZ    TRY14IS                  NO, TRY ISAM
         TM    JFCBIND1,JFCGDG          IS IT A GDG?
         BZ    TRY14PO                  NO
         MVC   DSNDSORG,=C'GD'          INDICATE GDG
         B     ACTN1415                 CONTINUE
TRY14PO  TM    JFCBIND1,JFCPDS          IS IT A PDS MEMBER?
         BZ    ACTN1415                 NO
         MVC   DSNDSORG,=C'PO'          YES, INDICATE DSORG=PO
         B     ACTN1415                 CONTINUE
TRY14IS  MVC   DSNDSORG,=C'IS'          INDICATE DSORG=IS
         TM    JFCDSRG1,JFCORGIS        IS IT ISAM?
         BO    ACTN1415                 YES
         MVC   DSNDSORG,=C'DA'          INDICATE DSORG=DA
         TM    JFCDSRG1,JFCORGDA        IS IT DIRECT ACCESS?
         BO    ACTN1415                 YES
         MVC   DSNDSORG,=C'PO'          INDICATE DSORG=PO
         TM    JFCDSRG1,JFCORGPO        IS IT PARTITIONED?
         BO    ACTN1415                 YES
         MVC   DSNDSORG,=C'??'          INDICATE DSORG UNKNOWN
ACTN1415 MVI   DSNACTN,C'R'             ASSUME DATASET INPUT ONLY
         CLI   SMF14RTY,14              IS IT INPUT OR RDBACK?
         BE    DEV1415                  YES
         MVI   DSNACTN,C'C'             ASSUME CREATING DATASET
         TM    JFCBIND2,JFCNEW          IS IT DISP=NEW?
         BE    DEV1415                  YES
         MVI   DSNACTN,C'U'             NONE OF THESE, THEN UPDATE
DEV1415  MVI   DSNDEVTY,C'D'            ASSUME DASD FILE
         CLI   SMFUCBTY+2,X'20'         IS IT DASD?
         BE    PUT1415                  YES
         MVI   DSNDEVTY,C'T'            NO, ASSUME IT IS ON TAPE/CART
         CLI   SMFUCBTY+2,X'80'         IS IT TAPE?
         BNE   MAINLINE                 NO, IGNORE THE RECORD
PUT1415  BAS   R9,FINISH                WRITE RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R9,R10
***********************************************************************
*  PROCESS TYPE 17 SMF RECORDS                                        *
***********************************************************************
         USING TYPE17,R10
GOT17    MVC   DSNDSNAM,SMF17DSN        DATASET NAME
         ZAP   DSNDATE,SMF17DTE         REFERENCE DATE
         MVC   DSNTIME,SMF17TME         REFERENCE TIME
         MVC   DSNJOB,SMF17JBN          JOB NAME
         MVI   DSNACTN,C'D'             INDICATE DELETE DATASET
         BAS   R9,FINISH                WRITE SCRATCH RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 18 SMF RECORDS                                        *
***********************************************************************
         USING TYPE18,R10
GOT18    MVC   DSNDSNAM,SMF18ODS        OLD DATASET NAME
         MVC   DSNJOB,SMF18JBN          JOB NAME
         ZAP   DSNDATE,SMF18DTE         REFERENCE DATE
         MVC   DSNTIME,SMF18TME         REFERENCE TIME
         MVI   DSNACTN,C'F'             INDICATE RENAME FROM
         BAS   R9,FINISH                WRITE RENAME FROM RECORD
         MVC   DSNDSNAM,SMF18NDS        NEW DATASET NAME
         MVC   DSNTIME,SMF18TME         REFERENCE TIME
         MVI   DSNACTN,C'T'             INDICATE RENAME TO
         BAS   R9,FINISH                WRITE RENAME TO RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 30 SMF RECORDS (NOT PRESENTLY USED)                   *
***********************************************************************
         USING SMFRCD30,R10
GOT30    EQU   *
GOT30XIT B     MAINLINE     BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 35 SMF RECORDS                                        *
***********************************************************************
         USING TYPE35,R10
GOT35    EQU   *
         CLC   TLGUIF,BLANKS          ANY TSO USER ID PRESENT?
         BE    MAINLINE               NO
         MVC   TSOUSER,TLGUIF         MOVE TSO UID TO OUTPUT REC
         PUT   USREXTR,TSOUSER        WRITE TSO USERID RECORD
GOT35XIT B     MAINLINE     BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 61 SMF RECORDS                                        *
***********************************************************************
         USING TYPE61,R10
GOT61    CLI   SMF61TYP,C'A'            IGNORE IF NOT NONVSAM
         BL    MAINLINE                    DATASET, GDG
         CLI   SMF61TYP,C'D'               BASE, CLUSTER,
         BH    MAINLINE                    OR DATASET
         MVC   DSNDSNAM,SMF61ENM        DATASET NAME
         MVC   DSNDSORG,=C'VS'          INDICATE DSORG=VS
         CLI   SMF61TYP,C'C'            IS IT A CLUSTER?
         BE    SET61JNM                 YES
         CLI   SMF61TYP,C'D'            IS IT A DATASET?
         BE    SET61JNM                 YES
         MVC   DSNDSORG,=C'GD'          INDICATE GDG
         CLI   SMF61TYP,C'B'            IS IT A GDG BASE?
         BE    SET61JNM                 YES
         MVC   DSNDSORG,=C'PS'          ELSE JUST FLAT FILE
SET61JNM MVC   DSNJOB,SMF61JNM          JOB NAME
         ZAP   DSNDATE,SMF61DTE         REFERENCE DATE
         MVC   DSNTIME,SMF61TME         REFERENCE TIME
         MVI   DSNACTN,C'C'             INDICATE DATASET CREATED
         BAS   R9,FINISH                WRITE CREATION RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 64 SMF RECORDS                                        *
***********************************************************************
         USING TYPE64,R10
GOT64    CLI   SMF64RIN,X'80'           THIS RECORD FOR CLOSE?
         BNE   MAINLINE                 NO, IGNORE IT
         MVC   DSNDSNAM,SMF64DNM        DATASET NAME
         MVC   DSNDSORG,=C'VS'          INDICATE DSORG=VS
         MVC   DSNJOB,SMF64JBN          JOB NAME
         ZAP   DSNDATE,SMF64DTE         REFERENCE DATE
         MVC   DSNTIME,SMF64TME         REFERENCE TIME
         MVI   DSNDEVTY,C'D'            ASSUME DASD DATASET
         CLI   SMF64UTY+2,X'20'         IS IT DASD?
         BE    ACTN64                   YES
         MVI   DSNDEVTY,C'T'            ASSUME TAPE/CART DATASET
         CLI   SMF64UTY+2,X'80'         IS IT TAPE?
         BNE   MAINLINE                 NO, IGNORE RECORD
ACTN64   MVI   DSNACTN,C'R'             INDICATE READ ONLY
         CLC   SMF64DLR,XZROS           NBR RECORDS CHANGED?
         BNE   SET64UPD                 YES
         CLC   SMF64DDE,XZROS           NBR RECORDS DELETED CHANGED?
         BNE   SET64UPD                 YES
         CLC   SMF64DIN,XZROS           NBR RECORDS INSERTED CHANGED?
         BNE   SET64UPD                 YES
         CLC   SMF64DUP,XZROS           NBR RECORDS UPDATED CHANGED?
         BNE   SET64UPD                 YES
         B     WRT64                    ELSE JUST READ
SET64UPD MVI   DSNACTN,C'U'             INDICATE UPDATE
WRT64    BAS   R9,FINISH                WRITE THE RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 65 SMF RECORDS                                        *
***********************************************************************
         USING TYPE65,R10
GOT65    CLI   SMF65TYP,C'A'            IGNORE IF NOT FOR
         BL    MAINLINE                    NONVSAM DATASET,
         CLI   SMF65TYP,C'D'               GDG BASE, CLUSTER,
         BH    MAINLINE                    OR DATASET
         MVC   DSNDSNAM,SMF65ENM        DATASET NAME
         ZAP   DSNDATE,SMF65DTE         REFERENCE DATE
         MVC   DSNTIME,SMF65TME         REFERENCE TIME
         MVI   DSNACTN,C'D'             INDICATE DELETE DATASET
         MVC   DSNDSORG,=C'VS'          INDICATE DSORG=VS
         CLI   SMF65TYP,C'C'            IS IT A CLUSTER?
         BE    SET65JNM                 YES
         CLI   SMF65TYP,C'D'            IS IT A DATASET?
         BE    SET65JNM                 YES
         MVC   DSNDSORG,=C'GD'          INDICATE GDG
         CLI   SMF65TYP,C'B'            IS IT A GDG BASE?
         BE    SET65JNM                 YES
         MVC   DSNDSORG,=C'PS'          ELSE JUST FLAT FILE
SET65JNM MVC   DSNJOB,SMF65JNM          JOB NAME
         BAS   R9,FINISH                WRITE DELETE RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*  PROCESS TYPE 66 SMF RECORDS                                        *
***********************************************************************
         USING TYPE66,R10
GOT66    CLI   SMF66FNC,C'R'            THIS A RENAME?
         BNE   MAINLINE                 NO, DROP IT
         CLI   SMF66TYP,C'A'            IGNORE IF NOT NONVSAM
         BL    MAINLINE                    DATASET, GDG
         CLI   SMF66TYP,C'D'               BASE, CLUSTER,
         BH    MAINLINE                    OR DATASET
         MVC   DSNDSNAM,SMF66ENM        OLD DATASET NAME
         MVC   DSNDSORG,=C'VS'          INDICATE DSORG=VS
         CLI   SMF66TYP,C'C'            IS IT A CLUSTER?
         BE    SET66JNM                 YES
         CLI   SMF66TYP,C'D'            IS IT A DATASET?
         BE    SET66JNM                 YES
         MVC   DSNDSORG,=C'GD'          INDICATE GDG
         CLI   SMF66TYP,C'B'            IS IT A GDG BASE?
         BE    SET66JNM                 YES
         MVC   DSNDSORG,=C'PS'          ELSE JUST FLAT FILE
SET66JNM MVC   DSNJOB,SMF66JNM          JOB NAME
         ZAP   DSNDATE,SMF66DTE         REFERENCE DATE
         MVC   DSNTIME,SMF66TME         REFERENCE TIME
         MVI   DSNACTN,C'F'             INDICATE RENAME FROM
         BAS   R9,FINISH                WRITE RENAME FROM RECORD
         MVC   DSNDSNAM,SMF66NNM        NEW DATASET NAME
         MVC   DSNTIME,SMF66TME         REFERENCE TIME
         MVI   DSNACTN,C'T'             INDICATE RENAME TO
         BAS   R9,FINISH                WRITE RENAME TO RECORD
         B     MAINLINE                 BACK TO MAINLINE
         DROP  R10
***********************************************************************
*        FINISH DSNRECS AND WRITE TO OUTPUT                           *
***********************************************************************
FINISH   ST    R9,FIN9                  SAVE RETURN ADDRESS
         MVC   VALTBL+C'0'(10),INVAL09  INVALIDATE 0-9
         TRT   DSNDSNAM(1),VALTBL       1ST BYTE OF NAME IS VALID?
         BNZ   FINXIT                   NO
         MVC   VALTBL+C'0'(10),VAL09    VALIDATE 0-9
         TRT   DSNDSNAM+1(43),VALTBL    REST IS VALID?
         BZ    TESTTEMP                 YES
         CLI   0(R1),C' '               HIT A BLANK?
         BNE   FINXIT                   NO, INVALID DSNAME
TESTTEMP CLC   DSNDSNAM(3),=CL3'SYS'    POSSIBLE TEMP DSNAME?
         BNE   CLRDIV                   NO
         CLC   DSNDSNAM+8(2),=CL2'.T'   POSSIBLE TEMP DSNAME?
         BNE   CLRDIV                   NO
         CLC   DSNDSNAM+16(2),=CL2'.R'  POSSIBLE TEMP DSNAME?
         BNE   CLRDIV                   NO
         MVZ   TSTNUM(5),DSNDSNAM+3     MOVE ZONES FOR TEST
         CLC   TSTNUM(5),ZEROS          NUMERIC, POSSIBLE TEMP DSNAME?
         BNE   CLRDIV                   NO
         MVZ   TSTNUM(6),DSNDSNAM+10    MOVE ZONES FOR TEST
         CLC   TSTNUM(6),ZEROS          NUMERIC, A TEMP DSNAME?
         BE    FINXIT                   YES, SKIP IT
CLRDIV   MVI   DSNDATE,X'19'            ASSUME CENTURY = 19
         CLI   DSNDATE+1,X'50'          IF YEAR > 50
         BH    FINPUT                   THEN CENTURY IS 19
         MVI   DSNDATE,X'20'            ELSE CENTURY IS 20
FINPUT   PUT   DSNEXTR,DSNRECD          WRITE OUTPUT RECORD
FINXIT   L     R9,FIN9                  GET RETURN ADDRESS
         BR    R9                       RETURN
***********************************************************************
*        PRINT ROUTINE                                                *
***********************************************************************
PRINT    PUT   LISTING,PRTLINE          PRINT DATA LINE
CLRPLIN  MVC   PRTLINE,BLANKS           CLEAR PRINT LINE
         BR    R9                       RETURN
***********************************************************************
*        END OF JOB                                                   *
***********************************************************************
EOJ      MVI   PRTCC,C'-'               TRIPLE SPACE
         MVC   PRT,PRTCC                FILL LINE WITH DASHES
         MVC   PRT+58(34),=C' SMF RECORD TYPE STATISTICS       '
         BAS   R9,PRINT                 PRINT SEPARATOR LINE
         MVI   PRTCC,C'-'               TRIPLE SPACE AGAIN
         SR    R11,R11                  SET INITIAL VALUE
EOJCKEND C     R11,=F'256'              END OF COUNTERS?
         BE    EOJ1                     YES
         LR    R12,R11                  POINT R12 TO
         SLL   R12,5                       SMF COUNT/DESCR
         A     R12,=A(COUNTERS)               TABLE ENTRY
         L     R1,0(R12)                GET RECORD COUNT
         LTR   R1,R1                    ANY COUNT?
         BZ    EOJSTEP                  NO
         CVD   R1,DBLWD                 YES, CONVERT COUNT TO DECIMAL
         A     R1,TOTRECS               ADD TO TOTAL RECORDS
         ST    R1,TOTRECS               UPDATE TOTAL RECORD COUNTER
         MVC   PRT+3(12),=XL12'402020206B2020206B202020'
         ED    PRT+3(12),DBLWD+3         EDIT COUNT TO PRINT LINE
         MVC   PRT+16(4),=CL4'TYPE'     LITERAL
         MVC   PRT+21(28),4(R12)        TYPE DESCRIPTION TO PRINT
         BAS   R9,PRINT                 PRINT SMF TYPE LINE
EOJSTEP  LA    R11,1(R11)               TO NEXT SMF TYPE
         B     EOJCKEND                 CONTINUE
EOJ1     MVC   PRT(15),=XL15'4020206B2020206B2020206B202020'
         MVC   PRT+16(18),=CL18'TOTAL RECORDS READ'     LITERAL
         L     R1,TOTRECS               GET TOTAL RECORD COUNT
         CVD   R1,DBLWD                 CONVERT TO DECIMAL
         ED    PRT(15),DBLWD+2          TOTAL RECORD COUNT TO PRINT
         BAS   R9,PRINT                 PRINT SMF TYPE LINE
         CLOSE (DSNEXTR,,USREXTR,,LISTING,,RAWSMF) CLOSE FILES
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 SET RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        DATA DEFINITIONS                                             *
***********************************************************************
DBLWD    DS    D                       WORK AREA
XZROS    DC    F'0'                    CONSTANT HEX ZEROS
FIN9     DS    A                       RETURN ADDRESS FOR FINISH
SMFRECDA DC    A(SMFRECD)              ADDRESS OF SMF RECORD
TOTRECS  DC    F'0'                    TOTAL RECORDS COUNTER
BLANKS   DC    CL133' '                CONSTANT BLANKS
TSOUSER  DS    CL8                     USREXTR FILE RECORD
TSTNUM   DC    CL6'000000'             NUMERIC TEST AREA
ZEROS    DC    CL6'000000'             NUMERIC TEST CONSTANT
*
PRTLINE  DS    0CL133                   PRINT LINE
PRTCC    DC    CL1'1'                    CARRIAGE CONTROL
PRT      DS    0CL132                    PRINT DATA
*
DSNRECD  DS    0CL65                    DSNAME RECORD
DSNDSNAM DS    CL44                         DATASET NAME
DSNDATE  DS    PL4                          REFERENCE DATE YYYYDDD
DSNTIME  DS    XL4                          REFERENCE TIME HHMM
DSNACTN  DS    CL1                          ACTION (C=CREATE, D=DELETE,
*                                             U=UPDATE, R=READ, T&F =
*                                             RENAME TO AND FROM
DSNDSORG DS    CL2                          DSORG (PS, GD, VS, IS, DA)
DSNDEVTY DS    CL1                          DEVICE TYPE
DSNJOB   DS    CL8                          JOB NAME
DSNSMFTY DS    XL1                          SMF RECORD TYPE USED
*
BLANKREC DS    0CL65                    DSNAME RECORD
         DC    CL44' '                      DATASET NAME
         DC    PL4'0'                       REFERENCE DATE
         DC    XL4'00'                      REFERENCE TIME
         DC    CL1' '                       ACTION INDICATOR
         DC    CL2' '                       DSORG
         DC    CL1' '                       DEVICE TYPE
         DC    CL8' '                       JOB NAME
         DC    XL1'00'                      SMF RECORD TYPE USED
*
VALTBL   DC    256X'FF'                 DSNAME VALIDITY TABLE
         ORG   VALTBL+C'.'
         DC    X'00'
         ORG   VALTBL+C'@'
         DC    X'00'
         ORG   VALTBL+C'$'
         DC    X'00'
         ORG   VALTBL+C'#'
         DC    X'00'
         ORG   VALTBL+C'A'
         DC    XL9'00'
         ORG   VALTBL+C'J'
         DC    XL9'00'
         ORG   VALTBL+C'S'
         DC    XL8'00'
         ORG
VAL09    DC    XL10'00'
INVAL09  DC    10X'FF'
*
SELECTYP DC    XL256'00'                SCAN FOR DESIRED RECORD TYPE
         ORG   SELECTYP+14
         DC    X'0404'                  TYPES 14, 15
         ORG   SELECTYP+17
         DC    X'080C'                  TYPES 17, 18
         ORG   SELECTYP+30
         DC    X'10'                    TYPE 30
         ORG   SELECTYP+35
         DC    X'14'                    TYPE 35
         ORG   SELECTYP+61
         DC    X'18'                    TYPE 61
         ORG   SELECTYP+64
         DC    X'1C2024'                TYPES 64-66
         ORG
*
***********************************************************************
*        FILE DEFINITIONS                                             *
***********************************************************************
RAWSMF   DCB   DSORG=PS,MACRF=GD,DDNAME=RAWSMF,EODAD=EOJ
DSNEXTR  DCB   DSORG=PS,MACRF=PM,DDNAME=DSNEXTR,                       X
               RECFM=FB,LRECL=65
USREXTR  DCB   DSORG=PS,MACRF=PM,DDNAME=USREXTR,                       X
               RECFM=FB,LRECL=8
LISTING  DCB   DSORG=PS,MACRF=PM,DDNAME=LISTING,                       X
               RECFM=FBA,LRECL=133
         LTORG
*******************************************************************
*                   DSECTS                                        *
*******************************************************************
         PRINT GEN
*
         TITLE 'SMF TYPE 14/15 RECORDS: DATASET ACTIVITY'
TYPE1415 DSECT
         IFASMFR 14
*
         TITLE 'SMF TYPE 17 RECORDS: SCRATCH DATASET STATUS'
TYPE17   DSECT
         IFASMFR 17
*
         TITLE 'SMF TYPE 18 RECORDS: RENAME DATASET STATUS'
TYPE18   DSECT
         IFASMFR 18
*
         TITLE 'SMF TYPE 30 RECORDS: COMMON ADDRESS SPACE WORK'
*SMFRCD30 DSECT
         IFASMFR 30
*
         TITLE 'SMF TYPE 35 RECORDS: LOGOFF'
TYPE35   DSECT
         IFASMFR 35
*
         TITLE 'SMF TYPE 61 RECORDS: ICF CATALOG DEFINE ACTIVITY'
TYPE61   DSECT
         IFASMFI6 61
*
         TITLE 'SMF TYPE 64 RECORDS: VSAM COMPONENT OR CLUSTER STATUS'
TYPE64   DSECT
         IFASMFR 64
*
         TITLE 'SMF TYPE 65 RECORDS: ICF CATALOG DELETE ACTIVITY'
TYPE65   DSECT
         IFASMFI6 65
*
         TITLE 'SMF TYPE 66 RECORDS: ICF CATALOG ALTER ACTIVITY'
TYPE66   DSECT
         IFASMFI6 66
*
JFCBDEF  DSECT
         IEFJFCBN LIST
*
SMFDSNAM CSECT
*
COUNTERS DS    0F                       SMF RECORD NAMES BY TYPE
         DC    F'0',CL28'  0 IPL                     '
         DC    F'0',CL28'  1 UNKNOWN TYPE            '
         DC    F'0',CL28'  2 DUMP HEADER             '
         DC    F'0',CL28'  3 DUMP TRAILER            '
         DC    F'0',CL28'  4 STEP TERMINATION        '
         DC    F'0',CL28'  5 JOB TERMINATION         '
         DC    F'0',CL28'  6 JES2 OUTPUT WRITER      '
         DC    F'0',CL28'  7 DATA LOST               '
         DC    F'0',CL28'  8 I/O CONFIGURATION       '
         DC    F'0',CL28'  9 VARY DEVICE ONLINE      '
         DC    F'0',CL28' 10 ALLOCATION RECOVERY     '
         DC    F'0',CL28' 11 VARY DEVICE OFFLINE     '
         DC    F'0',CL28' 12 UNKNOWN TYPE            '
         DC    F'0',CL28' 13 UNKNOWN TYPE            '
         DC    F'0',CL28' 14 INPUT OR RDBACK DATASET '
         DC    F'0',CL28' 15 OUTPUT, UPDAT, DATASET  '
         DC    F'0',CL28' 16 DFSORT STATISTICS       '
         DC    F'0',CL28' 17 SCRATCH DATASET STATUS  '
         DC    F'0',CL28' 18 RENAME DATASET STATUS   '
         DC    F'0',CL28' 19 DIRECT ACCESS VOLUME    '
         DC    F'0',CL28' 20 JOB INITIATION          '
         DC    F'0',CL28' 21 ERROR STATS BY VOLUME   '
         DC    F'0',CL28' 22 CONFIGURATION           '
         DC    F'0',CL28' 23 SMF STATUS              '
         DC    F'0',CL28' 24 JES2 SPOOL OFFLOAD      '
         DC    F'0',CL28' 25 JES3 DEVICE ALLOCATION  '
         DC    F'0',CL28' 26 JES2 JOB PURGE          '
         DC    F'0',CL28' 27 UNKNOWN TYPE            '
         DC    F'0',CL28' 28 NPM STATISTICS          '
         DC    F'0',CL28' 29 UNKNOWN TYPE            '
         DC    F'0',CL28' 30 COMMON ADDRESS SPACE WRK'
         DC    F'0',CL28' 31 TIOC INITIALIZATION     '
         DC    F'0',CL28' 32 TSO/E USER WORK ACCOUNTG'
         DC    F'0',CL28' 33 APPC/MVS TP ACCOUNTING  '
         DC    F'0',CL28' 34 TS-STEP TERMINATION     '
         DC    F'0',CL28' 35 LOGOFF                  '
         DC    F'0',CL28' 36 ICF CATALOG             '
         DC    F'0',CL28' 37 NETVIEW HARDWARE MONITOR'
         DC    F'0',CL28' 38 UNKNOWN TYPE            '
         DC    F'0',CL28' 39 NETVIEW (NLDM) RESPONSE '
         DC    F'0',CL28' 40 DYNAMIC DD              '
         DC    F'0',CL28' 41 DIV ACCESS/UNACCESS     '
         DC    F'0',CL28' 42 DFP STATS && CONFIG      '
         DC    F'0',CL28' 43 JES2 START              '
         DC    F'0',CL28' 44 UNKNOWN TYPE            '
         DC    F'0',CL28' 45 JES2 WITHDRAWAL         '
         DC    F'0',CL28' 46 UNKNOWN TYPE            '
         DC    F'0',CL28' 47 JES2 SIGNON/START LINE  '
         DC    F'0',CL28' 48 JES2 SIGNOFF/STOP LINE  '
         DC    F'0',CL28' 49 JES2 SECURITY           '
         DC    F'0',CL28' 50 ACF/VTAM TUNING STATS   '
         DC    F'0',CL28' 51 UNKNOWN TYPE            '
         DC    F'0',CL28' 52 JES2 LOGON/START LINE   '
         DC    F'0',CL28' 53 JES2 LOGOFF/STOP LINE   '
         DC    F'0',CL28' 54 JES2 INTEGRITY (SNA)    '
         DC    F'0',CL28' 55 JES2 NETWORK SIGNON     '
         DC    F'0',CL28' 56 JES2 NETWORK SECURITY   '
         DC    F'0',CL28' 57 JES2 NETWORK SYSOUT TRAN'
         DC    F'0',CL28' 58 JES2 NETWORK SIGNOFF    '
         DC    F'0',CL28' 59 MVS/BDT FILE-TO-FILE TRA'
         DC    F'0',CL28' 60 VSAM VOLUME DATASET UPD '
         DC    F'0',CL28' 61 ICF DEFINE ACTIVITY     '
         DC    F'0',CL28' 62 VSAM OPEN CLUSTER/COMPON'
         DC    F'0',CL28' 63 VSAM CATLG ENTRY DEFINED'
         DC    F'0',CL28' 64 VSAM COMPON/CLUSTER STAT'
         DC    F'0',CL28' 65 ICF DELETE ACTIVITY     '
         DC    F'0',CL28' 66 ICF ALTER ACTIVITY      '
         DC    F'0',CL28' 67 VSAM CATLOG ENTRY DELETE'
         DC    F'0',CL28' 68 VSAM CATLG ENTRY RENAMED'
         DC    F'0',CL28' 69 VSAM DATASPACE DEFEXTDEL'
         DC    F'0',CL28' 70 RMF CPU ACTIVITY        '
         DC    F'0',CL28' 71 RMF PAGING ACTIVITY     '
         DC    F'0',CL28' 72 RMF WKLD ACTVTY && STRG D'
         DC    F'0',CL28' 73 RMF CHANNEL PATH ACTIVTY'
         DC    F'0',CL28' 74 RMF DEVICE && XCF ACTIVTY'
         DC    F'0',CL28' 75 RMF PAGE/SWAP DS ACTIVTY'
         DC    F'0',CL28' 76 RMF TRACE ACTIVITY      '
         DC    F'0',CL28' 77 RMF ENQUEUE ACTIVITY    '
         DC    F'0',CL28' 78 RMF MONITOR I ACTIVITY  '
         DC    F'0',CL28' 79 RMF MONITOR II ACTIVITY '
         DC    F'0',CL28' 80 RACF PROCESSING         '
         DC    F'0',CL28' 81 RACF INITIALIZATION     '
         DC    F'0',CL28' 82 PCF/CUSP/ICSF/MVS RECORD'
         DC    F'0',CL28' 83 RACF AUDIT RECD, DSETS  '
         DC    F'0',CL28' 84 JES3 JMF DATA           '
         DC    F'0',CL28' 85 UNKNOWN TYPE            '
         DC    F'0',CL28' 86 UNKNOWN TYPE            '
         DC    F'0',CL28' 87 UNKNOWN TYPE            '
         DC    F'0',CL28' 88 UNKNOWN TYPE            '
         DC    F'0',CL28' 89 USAGE DATA              '
         DC    F'0',CL28' 90 SYSTEM STATUS           '
         DC    F'0',CL28' 91 BATCHPIPES/MVS STATISTIC'
         DC    F'0',CL28' 92 UNKNOWN TYPE            '
         DC    F'0',CL28' 93 UNKNOWN TYPE            '
         DC    F'0',CL28' 94 IBM TAPE LIBR DSRV STATS'
         DC    F'0',CL28' 95 UNKNOWN TYPE            '
         DC    F'0',CL28' 96 TIRS SERVER             '
         DC    F'0',CL28' 97 UNKNOWN TYPE            '
         DC    F'0',CL28' 98 UNKNOWN TYPE            '
         DC    F'0',CL28' 99 SRM DECISIONS           '
         DC    F'0',CL28'100 DB2 STATISTICS          '
         DC    F'0',CL28'101 DB2 ACCOUNTING          '
         DC    F'0',CL28'102 DB2 PERFORMANCE         '
         DC    F'0',CL28'103 UNKNOWN TYPE            '
         DC    F'0',CL28'104 UNKNOWN TYPE            '
         DC    F'0',CL28'105 UNKNOWN TYPE            '
         DC    F'0',CL28'106 UNKNOWN TYPE            '
         DC    F'0',CL28'107 UNKNOWN TYPE            '
         DC    F'0',CL28'108 UNKNOWN TYPE            '
         DC    F'0',CL28'109 UNKNOWN TYPE            '
         DC    F'0',CL28'110 CICS/ESA STATISTICS     '
         DC    F'0',CL28'111 UNKNOWN TYPE            '
         DC    F'0',CL28'112 UNKNOWN TYPE            '
         DC    F'0',CL28'113 UNKNOWN TYPE            '
         DC    F'0',CL28'114 UNKNOWN TYPE            '
         DC    F'0',CL28'115 UNKNOWN TYPE            '
         DC    F'0',CL28'116 UNKNOWN TYPE            '
         DC    F'0',CL28'117 UNKNOWN TYPE            '
         DC    F'0',CL28'118 TCP/IP STATISTICS       '
         DC    F'0',CL28'119 UNKNOWN TYPE            '
         DC    F'0',CL28'121 UNKNOWN TYPE            '
         DC    F'0',CL28'122 UNKNOWN TYPE            '
         DC    F'0',CL28'123 UNKNOWN TYPE            '
         DC    F'0',CL28'124 UNKNOWN TYPE            '
         DC    F'0',CL28'125 UNKNOWN TYPE            '
         DC    F'0',CL28'126 UNKNOWN TYPE            '
         DC    F'0',CL28'127 UNKNOWN TYPE            '
         DC    F'0',CL28'128 UNKNOWN TYPE            '
         DC    F'0',CL28'129 UNKNOWN TYPE            '
         DC    F'0',CL28'130 UNKNOWN TYPE            '
         DC    F'0',CL28'131 UNKNOWN TYPE            '
         DC    F'0',CL28'132 UNKNOWN TYPE            '
         DC    F'0',CL28'133 UNKNOWN TYPE            '
         DC    F'0',CL28'134 UNKNOWN TYPE            '
         DC    F'0',CL28'135 UNKNOWN TYPE            '
         DC    F'0',CL28'136 UNKNOWN TYPE            '
         DC    F'0',CL28'137 UNKNOWN TYPE            '
         DC    F'0',CL28'138 UNKNOWN TYPE            '
         DC    F'0',CL28'139 UNKNOWN TYPE            '
         DC    F'0',CL28'140 UNKNOWN TYPE            '
         DC    F'0',CL28'141 UNKNOWN TYPE            '
         DC    F'0',CL28'142 UNKNOWN TYPE            '
         DC    F'0',CL28'143 UNKNOWN TYPE            '
         DC    F'0',CL28'144 UNKNOWN TYPE            '
         DC    F'0',CL28'145 UNKNOWN TYPE            '
         DC    F'0',CL28'146 UNKNOWN TYPE            '
         DC    F'0',CL28'147 UNKNOWN TYPE            '
         DC    F'0',CL28'148 UNKNOWN TYPE            '
         DC    F'0',CL28'149 UNKNOWN TYPE            '
         DC    F'0',CL28'150 UNKNOWN TYPE            '
         DC    F'0',CL28'151 UNKNOWN TYPE            '
         DC    F'0',CL28'152 UNKNOWN TYPE            '
         DC    F'0',CL28'153 UNKNOWN TYPE            '
         DC    F'0',CL28'154 UNKNOWN TYPE            '
         DC    F'0',CL28'155 UNKNOWN TYPE            '
         DC    F'0',CL28'156 UNKNOWN TYPE            '
         DC    F'0',CL28'157 UNKNOWN TYPE            '
         DC    F'0',CL28'158 UNKNOWN TYPE            '
         DC    F'0',CL28'159 UNKNOWN TYPE            '
         DC    F'0',CL28'160 UNKNOWN TYPE            '
         DC    F'0',CL28'161 UNKNOWN TYPE            '
         DC    F'0',CL28'162 UNKNOWN TYPE            '
         DC    F'0',CL28'163 UNKNOWN TYPE            '
         DC    F'0',CL28'164 UNKNOWN TYPE            '
         DC    F'0',CL28'165 UNKNOWN TYPE            '
         DC    F'0',CL28'166 UNKNOWN TYPE            '
         DC    F'0',CL28'167 UNKNOWN TYPE            '
         DC    F'0',CL28'168 UNKNOWN TYPE            '
         DC    F'0',CL28'169 UNKNOWN TYPE            '
         DC    F'0',CL28'170 UNKNOWN TYPE            '
         DC    F'0',CL28'171 UNKNOWN TYPE            '
         DC    F'0',CL28'172 UNKNOWN TYPE            '
         DC    F'0',CL28'173 UNKNOWN TYPE            '
         DC    F'0',CL28'174 UNKNOWN TYPE            '
         DC    F'0',CL28'175 UNKNOWN TYPE            '
         DC    F'0',CL28'176 UNKNOWN TYPE            '
         DC    F'0',CL28'177 UNKNOWN TYPE            '
         DC    F'0',CL28'178 UNKNOWN TYPE            '
         DC    F'0',CL28'179 UNKNOWN TYPE            '
         DC    F'0',CL28'180 UNKNOWN TYPE            '
         DC    F'0',CL28'181 UNKNOWN TYPE            '
         DC    F'0',CL28'182 UNKNOWN TYPE            '
         DC    F'0',CL28'183 UNKNOWN TYPE            '
         DC    F'0',CL28'184 UNKNOWN TYPE            '
         DC    F'0',CL28'185 UNKNOWN TYPE            '
         DC    F'0',CL28'186 UNKNOWN TYPE            '
         DC    F'0',CL28'187 UNKNOWN TYPE            '
         DC    F'0',CL28'188 UNKNOWN TYPE            '
         DC    F'0',CL28'189 UNKNOWN TYPE            '
         DC    F'0',CL28'190 UNKNOWN TYPE            '
         DC    F'0',CL28'191 UNKNOWN TYPE            '
         DC    F'0',CL28'192 UNKNOWN TYPE            '
         DC    F'0',CL28'193 UNKNOWN TYPE            '
         DC    F'0',CL28'194 UNKNOWN TYPE            '
         DC    F'0',CL28'195 UNKNOWN TYPE            '
         DC    F'0',CL28'196 UNKNOWN TYPE            '
         DC    F'0',CL28'197 UNKNOWN TYPE            '
         DC    F'0',CL28'198 UNKNOWN TYPE            '
         DC    F'0',CL28'199 UNKNOWN TYPE            '
         DC    F'0',CL28'200 UNKNOWN TYPE            '
         DC    F'0',CL28'201 UNKNOWN TYPE            '
         DC    F'0',CL28'202 UNKNOWN TYPE            '
         DC    F'0',CL28'203 UNKNOWN TYPE            '
         DC    F'0',CL28'204 UNKNOWN TYPE            '
         DC    F'0',CL28'205 UNKNOWN TYPE            '
         DC    F'0',CL28'206 UNKNOWN TYPE            '
         DC    F'0',CL28'207 UNKNOWN TYPE            '
         DC    F'0',CL28'208 UNKNOWN TYPE            '
         DC    F'0',CL28'209 UNKNOWN TYPE            '
         DC    F'0',CL28'210 UNKNOWN TYPE            '
         DC    F'0',CL28'211 UNKNOWN TYPE            '
         DC    F'0',CL28'212 UNKNOWN TYPE            '
         DC    F'0',CL28'213 UNKNOWN TYPE            '
         DC    F'0',CL28'214 UNKNOWN TYPE            '
         DC    F'0',CL28'215 UNKNOWN TYPE            '
         DC    F'0',CL28'216 UNKNOWN TYPE            '
         DC    F'0',CL28'217 UNKNOWN TYPE            '
         DC    F'0',CL28'218 UNKNOWN TYPE            '
         DC    F'0',CL28'219 UNKNOWN TYPE            '
         DC    F'0',CL28'220 UNKNOWN TYPE            '
         DC    F'0',CL28'221 UNKNOWN TYPE            '
         DC    F'0',CL28'222 UNKNOWN TYPE            '
         DC    F'0',CL28'223 UNKNOWN TYPE            '
         DC    F'0',CL28'224 UNKNOWN TYPE            '
         DC    F'0',CL28'225 UNKNOWN TYPE            '
         DC    F'0',CL28'226 UNKNOWN TYPE            '
         DC    F'0',CL28'227 UNKNOWN TYPE            '
         DC    F'0',CL28'228 UNKNOWN TYPE            '
         DC    F'0',CL28'229 UNKNOWN TYPE            '
         DC    F'0',CL28'230 UNKNOWN TYPE            '
         DC    F'0',CL28'231 UNKNOWN TYPE            '
         DC    F'0',CL28'232 UNKNOWN TYPE            '
         DC    F'0',CL28'233 UNKNOWN TYPE            '
         DC    F'0',CL28'234 UNKNOWN TYPE            '
         DC    F'0',CL28'235 UNKNOWN TYPE            '
         DC    F'0',CL28'236 UNKNOWN TYPE            '
         DC    F'0',CL28'237 UNKNOWN TYPE            '
         DC    F'0',CL28'238 UNKNOWN TYPE            '
         DC    F'0',CL28'239 UNKNOWN TYPE            '
         DC    F'0',CL28'240 UNKNOWN TYPE            '
         DC    F'0',CL28'241 UNKNOWN TYPE            '
         DC    F'0',CL28'242 UNKNOWN TYPE            '
         DC    F'0',CL28'243 UNKNOWN TYPE            '
         DC    F'0',CL28'244 UNKNOWN TYPE            '
         DC    F'0',CL28'245 UNKNOWN TYPE            '
         DC    F'0',CL28'246 UNKNOWN TYPE            '
         DC    F'0',CL28'247 UNKNOWN TYPE            '
         DC    F'0',CL28'248 UNKNOWN TYPE            '
         DC    F'0',CL28'249 UNKNOWN TYPE            '
         DC    F'0',CL28'250 UNKNOWN TYPE            '
         DC    F'0',CL28'251 UNKNOWN TYPE            '
         DC    F'0',CL28'252 UNKNOWN TYPE            '
         DC    F'0',CL28'253 UNKNOWN TYPE            '
         DC    F'0',CL28'254 UNKNOWN TYPE            '
         DC    F'0',CL28'255 UNKNOWN TYPE            '
SMFRECD  DS    CL32760
         DS    CL6000
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

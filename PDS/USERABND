         TITLE '*** USERABND: TERMINATE UNNNN OR RC=NN ***'
USERABND CSECT
*********************************************************************
* THIS PROGRAM USES THE PARM FIELD ON THE EXEC STATEMENT TO CONTROL *
* ITS TERMINATION:                                                  *
*   .IF THERE IS NO PARM FIELD, THE PROGRAM TERMINATES NORMALLY WITH*
*    RC=0.                                                          *
*   .IF THE PARM FIELD CONTAINS UNNNN, WHERE NNNN IS A 4-DIGIT DECI-*
*    MAL NUMBER BETWEEN 0 AND 4095, THE PROGRAM ABENDS WITH THE USER*
*    CODE SUPPLIED.                                                 *
*   .IF THE PARM FIELD CONTAINS RNNNN, WHERE NNNN IS A 4-DIGIT DECI-*
*    MAL NUMBER BETWEEN 0 AND 4095, THE PROGRAM WILL TERMINATE NORM-*
*    ALLY WITH THE RETURN CODE FOLLOWING RNNNN.                     *
*   .IF THE FOUR DIGITS FOLLOWING THE LETTER U OR R ARE NOT NUMERIC *
*    THE PROGRAM WILL ABEND S0C6.                                   *
*   .IF THE FOUR DIGITS FOLLOWING THE LETTER U OR R ARE GREATER THAN*
*    4095 THE PROGRAM WILL ABEND WITH S0C1.                         *
*   .IF THE PARM FIELD DOES NOT CONFIRM TO THE ABOVE, THE PROGRAM   *
*    WILL ABEND S0C3.                                               *
* AUTHOR R THORNTON MAY 2000                                        *
*********************************************************************
*
*********************************************************************
*        SAVE REGISTERS, SET UP BASE REGS, CHAIN SAVE AREAS         *
*********************************************************************
         USING USERABND,R3
         STM   R14,R12,12(R13)         SAVE CALLER'S REGS
         LR    R3,R15                  SET UP BASE FIRST BASE REG
         LA    R15,MYSAVE              POINT TO MY REGISTER SAVE AREA
         ST    R15,8(R13)              SET LSA IN CALLER'S SAVE
         ST    R13,4(R15)              SET HSA IN MY SAVE AREA
         LR    R13,R15                 SET UP SAVE AREA POINTER
         B     START                   BRANCH AROUND CONSTANTS
         DC    CL8'USERABND'           PROGRAM NAME IDENTIFIER
         DC    CL8'&SYSDATE'           ASSEMBLY DATE
         DC    CL8'&SYSTIME'           ASSEMBLY TIME
MYSAVE   DS    18F                     MY REGISTER SAVE AREA
*********************************************************************
*        PROCESS PARM FIELD                                         *
*********************************************************************
START    L     R6,0(R1)                GET PARM FIELD ADDR
         CLC   0(2,R6),=H'0'           ANY PARM DATA?
         BE    EOJ                     NO, TERMINATE WITH RC=0
         CLC   0(2,R6),=H'5'           VALID PARM LENGTH?
         BE    CHECKNUM                YES
S0C3     EX    R1,S0C3                 INVALID PARM, ABEND S0C3
S0C1     DC    X'00'                   NNNN > 4095, ABEND S0C1
S0C6     DC    X'00'                   NON-NUMERIC NNNN, ABEND S0C6
CHECKNUM TRT   3(4,R6),CHKNUM          NNNN IS NUMERIC?
         BNZ   S0C6                    NO, ABEND S0C6
         PACK  DBLWD,3(4,R6)           PACK ABEND CODE
         CVB   R1,DBLWD                CONVERT TO BINARY
         C     R1,=F'4095'             VALID NNNN?
         BH    S0C1                    NO, ABEND S0C1
CHECKTYP CLI   2(R6),C'U'              THIS A UNNNN PARM?
         BE    UPARM                   YES
         CLI   2(R6),C'R'              THIS A RNNNN PARM?
         BNE   S0C3                    NEITHER, ABEND S0C3
         ST    R1,RETCODE              SET RETURN CODE
         B     EOJ                     GO TERMINATE WITH RC
UPARM    ABEND (1)                     TERMINATE WITH UNNNN ABEND CODE
*********************************************************************
*             END OF JOB                                            *
*********************************************************************
EOJ      L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)             RESTORE RETURN REG: R14
         L     R15,RETCODE             SET RETURN CODE
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGS 0-12
         BR    R14                     RETURN
*********************************************************************
*        STORAGE AND FILE AREAS                                     *
*********************************************************************
*
DBLWD    DS    D                       DOUBLEWORD WORK AREA
RETCODE  DC    F'0'                    RETURN CODE
CHKNUM   DC    256X'FF'                NUMERIC CHECK TABLE
         ORG   CHKNUM+C'0'
         DC    10X'00'
         LTORG
USERABND CSECT
*********************************************************************
*             REGISTER EQUATES                                      *
*********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END

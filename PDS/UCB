TERMID   SVLNK R12
* TERMID WRITES 'YOU ARE ON TERMINAL CUU' TO THE TSO TERMINAL,
* WHERE 'CUU' IS THE UNIT ADDRESS OF THE TERMINAL
         USING PSA,R0                  SET PSA ADDRESS
         L     R4,PSAAOLD              GET MY ASCB ADDRESS
         USING ASCB,R4                 GET SYMBOLS
         L     R4,ASCBTSB              GET TSB ADDRESS
         USING TSB,R4                  GET TSB SYMBOLS
         SMODE KEY=0,STATE=P     ,     NEED KEY0 FOR MOVE
         MVC   MYLINE,TSBLINE          GET TERMINAL UNIT ADDRESS
         SMODE KEY=N,STATE=P     ,     BACK TO NORMAL
         UNPK  MSGA(5),MYDATA(3)       FORMAT HEX DATA
         MVI   MSGA,C' '               CLEAR LEADING ZERO
         MVI   MSGA+4,C'.'             CLEAR TRAILING BYTE
         TR    MSGA+1(3),HEXCODES      XLATE TO PRINTABLE HEX
         TPUT  MSG,MSGLEN        ,     DISPLAY LINE
         UNLNK 0                 ,     EXIT
MSG      DC    C'YOU ARE ON TERMINAL'  1ST PART OF DISPLAY LINE
MSGA     DS    XL5                     ADDRESS AREA
MSGLEN   EQU   *-MSG               ,   SYMBOL FOR TPUT
MYDATA   DS    0CL3                    AREA TO MOVE LINE ID TO
MYLINE   DS    XL2                     LINE ID GOES HERE
         DS    X                       PAD BYTE
         DC    100X'FF'                KEEPS SYMBOL HEXCODES POSITIVE
J        DC    C'0123456789ABCDEF'     HEX TRANSLATE TABLE
HEXCODES EQU   J-240                   SYMBOL FOR TR INSTR
         PRINT NOGEN                   FOR MACROS
         IHAPSA                ,       PREFIXED STORAGE AREA
         IHAASCB               ,       ADDRESS SPACE CONTROL BLOCK
         IKJTSB                ,       TERMINAL STATUS BLOCK
         END   TERMID

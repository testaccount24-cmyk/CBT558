         TITLE '*** PRINTDAE: PRINT CONTENTS OF SYS1.DAE ***'
         PRINT NOGEN
PRINTDAE SVLNK R3
***********************************************************************
* PRINTS SYS1.DAE DATASET. FOR EACH RECORD, SHOW DATE OF ORIGINAL     *
* RECORD CREATION, DATE OF LAST OCCURRENCE, NUMBER OF OCCURRENCES,    *
* AND THE SYMPTOM STRING TITLE                                        *
*                                                                     *
* AUTHOR R THORNTON JUL 1991                                          *
***********************************************************************
*
***********************************************************************
* INITIALIZATION: OPEN FILES                                          *
***********************************************************************
*
         L     R1,0(R1)                POINT TO PARM FIELD
         MVC   OTSYSTM,2(R1)           SYSTEM NAME TO OUTPUT FIELD
         L     R1,16                   POINT TO CVT
         ZAP   OLDDATE,57(3,R1)        SAVE CURRENT DATE
         CP    OLDDATE+1(2),=P'90'     PAST DAY 90?
         BH    SUB90                   YES
         SP    OLDDATE,=P'725'         NO BACK UP 90 DAYS(-1000+365-90)
         B     INIT1                   CONTINUE
SUB90    SP    OLDDATE,=P'90'          BACK UP 90 DAYS
INIT1    OPEN  (OUTPUT1,OUTPUT,INPUT1) OPEN FILES
***********************************************************************
* MAINLINE PROCESSING:                                                *
***********************************************************************
MAINLINE GET   INPUT1,INRECD           READ NEXT INPUT RECORD
         TRT   INLAST,VALPACK          IS INLAST VALID PACKED DECIMAL?
         BC    13,MAINLINE             NO, DROP THE RECORD
         CLM   R2,1,=X'01'             LAST BYTE VALID SIGNED?
         BNE   MAINLINE                NO
         CP    INLAST,OLDDATE          CURRENT RECORD?
         BL    MAINLINE                NO, DROP IT
         MVC   OTLAST-1(7),=X'40212061202020' EDIT DATE OF
         ED    OTLAST-1(7),INLAST+1              LAST OCCURRENCE
         MVC   OTTOTAL-1(7),=X'4020206B202120' EDIT
         SR    R1,R1                              TOTAL
         ICM   R1,3,INTOTAL                          NUMBER
         CVD   R1,DBLWD                                 OF
         ED    OTTOTAL-1(7),DBLWD+5                        OCCURRENCES
         MVC   OTSYMPTM,INSYMPTM               SYMPTOM STRING
         PUT   OUTPUT1,OTLINE          WRITE OUTPUT RECORD
         B     MAINLINE                GO READ NEXT RECORD
***********************************************************************
*        END OF INPUT FILE AND END OF JOB                             *
***********************************************************************
EOJ      CLOSE (OUTPUT1,,INPUT1)       CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE ADDRESS
         LM    R14,R12,12(R13)         GET CALLER'S REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        STORAGE AND FILE AREAS                                       *
***********************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
OLDDATE  DC    PL3'0'                  OLDEST DATE TO USE
LASTLINE DC    PL2'56'                 END OF PAGE LINE COUNT
VALPACK  DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'000000000000000000000A0B010D0E01'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
*
OTLINE   DS    0CL150                  OUTPUT RECORD
         DC    CL1'0'                  CARR CTL BYTE
         DC    CL1' '
OTLAST   DC    CL6' LAST '
         DC    CL1' '
OTSYSTM  DC    CL4'SYSA'
         DC    CL1' '
OTTOTAL  DC    CL6' '
         DC    CL1' '
OTSYMPTM DC    CL129'SYMPTOMS'
*
INRECD   DS    0CL255                  INPUT RECORD
         DS    CL41
INCREATE DS    PL4                      RECORD CREATION DATE
         DS    CL11
INLAST   DS    PL4                      DATE OF LAST OCCURRENCE
INTOTAL  DS    XL2                      TOTAL NUMBER OCCURRENCES
         DS    CL6
INSYMPTM DS    CL129                    SYMPTOM STRING
         DS    CL58
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,                       X
               RECFM=FBA,LRECL=150
INPUT1   DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT1,EODAD=EOJ,              X
               RECFM=FB,LRECL=255
         END

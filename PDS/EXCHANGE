         TITLE '***** EXCHANGE: DISK FILE EXCHANGE UTILITY *****'
         PRINT NOGEN
EXCHANGE SVLNK R3,R4
         LA    R5,4095(R4)
         LA    R5,1(R5)
         USING EXCHANGE+8192,R5
**********************************************************************
*  EXCHANGES (INTERCHANGES) TWO DATASETS ON THE SAME VOLUME SO THAT  *
*  THE FIRST DSNAME WILL POINT TO THE SECOND DATASET, AND THE SECOND *
*  DSNAME WILL POINT TO THE FIRST DATASET. BOTH DATASETS MUST EXIST  *
*  IN THE VTOC AND NEITHER CAN BE A VSAM DATASET.                    *
*                                                                    *
*  A VOLUME CONTROL CARD MUST PRECEDE THE FIRST EXCHANGE CONTROL     *
*  CARD. IT SPECIFIES THE VOLUME TO BE USED FOR THE EXCHANGES THAT   *
*  FOLLOW UNTIL THE NEXT VOLUME CONTROL CARD IS FOUND. THE VOLUME    *
*  CONTAINS THE WORD VOLUME IN COLUMNS 1-6, IS BLANK IN COLUMN 7,    *
*  AND HAS THE VOLUME SERIAL IN COLUMNS 8-13 LEFT JUSTIFIED WITH     *
*  TRAILING BLANKS. A TWO-CARD SET IS REQUIRED FOR EACH DATASET PAIR *
*  TO BE EXCHANGED. THE FIRST CARD CONTAINS THE WORD EXCHANGE IN     *
*  COLUMNS 1-8, A BLANK IN COLUMN 9, THE WORD DSN= IN COLUMNS 10-13, *
*  AND THE DATASET NAME IN COLUMNS 14-57 LEFT JUSTIFIED WITH TRAILING*
*  SPACES. THE CARD INNEDIATELY FOLLOWING THE EXCHANGE CARD IS BLANK *
*  IN COLUMNS 1-9, HAS THE WORD DSN= IN COLUMNS 10-13, AND THE NAME  *
*  OF THE SECOND DATASET IN COLUMNS 14-57. A CARD WITH AN ASTERISK IN*
*  COLUMN 1 IS TREATED AS A COMMENT. EXAMPLE:                        *
*                                                                    *
*  VOLUME SYSTST                                                     *
*  EXCHANGE DSN=SYS1.LPALIB                                          *
*           DSN=SYS9.LPALIB                                          *
*  EXCHANGE DSN=SYS1.LINKLIB                                         *
*           DSN=SYS9.LINKLIB                                         *
*  VOLUME MVS220                                                     *
*  EXCHANGE DSN=SYS1.MONITOR                                         *
*           DSN=SYS9.MONITOR                                         *
*                                                                    *
*  A LOG FILE IS WRITTEN THAT CONTAINS ALL MESSAGES WRITTEN TO THE   *
*  SYSPRINT FILE AS WELL AS THE CONTENT OF ALL VTOC RECORDS READ     *
*  AND WRITTEN. THE LOG FILE CAN BE USED TO AID IN VTOC RECOVERY IN  *
*  CASE OF A CATASTROPHIC ERROR DURING PROCESSING, AS IT CONTAINS    *
*  VTOC DETAILS.                                                     *
*                                                                    *
*  AUTHOR R. THORNTON DEC 1992                                       *
**********************************************************************
*
***********************************************************************
* OPEN SYSPRINT, SYSIN AND SYSLOG                                     *
***********************************************************************
         OPEN  (SYSPRINT,OUTPUT,SYSLOG,OUTPUT,SYSIN)
***********************************************************************
* READ THE NEXT CONTROL STATEMENT AND CALL THE APPROPRIATE ROUTINES   *
* TO PROCESS IT.                                                      *
***********************************************************************
MAINLINE GET   SYSIN,CONTROL           READ NEXT CONTROL
         MVC   PRT,CONTROL             PRINT THE
         BAS   R9,PRINT                    CONTROL CARD
         CLI   CONTROL,C'*'            IS IT A COMMENT CARD?
         BE    MAINLINE                YES
         CLC   CONTROL(7),=CL7'VOLUME ' IS IT A VOLUME CARD?
         BNE   CHKEXCH                 NO
         BAS   R9,VOLUME               YES, PROCESS
         B     MAINLINE                CONTINUE
CHKEXCH  CLC   CONTROL(13),=CL13'EXCHANGE DSN=' IS IT AN EXCHANGE CARD?
         BNE   CHK2ND                  NO
         BAS   R9,EXCHCARD             PROCESS EXCHANGE CARD
         B     MAINLINE                CONTINUE
CHK2ND   CLC   CONTROL(13),=CL13'         DSN=' IS IT SECOND DSN CARD?
         BNE   BADCARD                 NO, ERROR
         BAS   R9,DSN2CARD             PROCESS SECOND DSN= CARD
         BAS   R9,SWITCH               GO INTERCHANGE DATASETS
         B     MAINLINE                CONTINUE
BADCARD  MVC   PRT,MSGZ1               PRINT INVALID REQUEST
         BAS   R9,PRINT                    CODE MESSAGE
         ABEND 111,DUMP                TERMINATE IN ERROR
***********************************************************************
* PROCESS VOLUME CONTROL CARD: CALL EXCPIO TO ALLOCATE THE VOLUME     *
***********************************************************************
VOLUME   ST    R9,VL9                  SAVE RETURN ADDRESS
         TM    CTLSW,X'40'             ANY DSN= PAIR IN PROGRESS?
         BZ    SETUPV                  NO
         MVC   PRT,MSGZ2               PRINT INCOMPLETE DSN=
         BAS   R9,PRINT                    PAIR MESSAGE
         ABEND 122,DUMP                TERMINATE IN ERROR
SETUPV   MVI   COMMAND,C'V'            ALLOCATE VOLUME REQUEST CODE
         MVC   VOLSER,CONTROL+7        SET VOLUME SERIAL
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    VLXIT                   NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    VLXIT                   YES, NOT AN ERROR
         MVC   PRT,MSGZ7               PRINT UNABLE TO ALLOCATE
         BAS   R9,PRINT                    VOLUME MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 133,DUMP                TERMINATE IN ERROR
VLXIT    OI    CTLSW,X'80'             SHOW VOLUME RECORD PROCESSED
         MVC   VTOCXTNT,CURRXTNT       SAVE VTOC EXTENTS
         MVC   PRT,MSGY0               SEND VOLUME ALLOCATED
         BAS   R9,PRINT                    SUCCESSFULLY MESSAGE
         L     R9,VL9                  RESTORE RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
* PROCESS EXCHANGE CONTROL CARD: INSURE A VOLUME CARD HAS BEEN        *
* RECEIVED, THEN CALL EXCPIO TO ALLOCATE THE DATASET. ON RETURN, SAVE *
* THE FORMAT 1 DSCB AND ITS CCHHR ADDRESS.                            *
***********************************************************************
***********************************************************************
EXCHCARD ST    R9,EX9                  SAVE RETURN ADDRESS
         TM    CTLSW,X'80'             VOLUME ALLOCATED?
         BO    TSTINCMP                YES
         MVC   PRT,MSGZ3               PRINT MISSING VOLUME
         BAS   R9,PRINT                    CARD MESSAGE
         ABEND 144,DUMP                TERMINATE IN ERROR
TSTINCMP TM    CTLSW,X'40'             AWAITING A DSN2 CARD?
         BZ    SET1ON                  NO
         MVC   PRT,MSGZ2               PRINT INCOMPLETE DSN=
         BAS   R9,PRINT                    PAIR MESSAGE
         ABEND 155,DUMP                TERMINATE IN ERROR
SET1ON   OI    CTLSW,X'40'             SHOW AWAITING DSN2 CARD
         MVI   COMMAND,C'D'            ALLOCATE DSNAME REQUEST CODE
         MVC   DSNAME,CONTROL+13       SET DSNAME
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    SAVDSCB1                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    SAVDSCB1                YES, NOT AN ERROR
         MVC   PRT,MSGZ8               PRINT UNABLE TO ALLOCATE
         BAS   R9,PRINT                    DSNAME MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 166,DUMP                TERMINATE IN ERROR
SAVDSCB1 L     R1,FMT1ADDR             SAVE THE
         MVC   DSCB1CKD,0(R1)              FORMAT 1 DSCB
         MVC   PRT,MSGY1               LOG MESSAGE TELLING
         BAS   R9,WRITELOG                 FILE 1 FORMAT1 DSCB READ
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         MVC   PRT+12(8),DSCB1CNT      MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 LOG COUNT RECORD
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         MVC   PRT+12(44),DSCB1KEY     MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 LOG KEY RECORD
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         MVC   PRT+12(96),DSCB1DTA     MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 LOG DATA RECORD
EXXIT    L     R9,EX9                  RESTORE RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
* PROCESS SECOND DSN CARD: INSURE AN EXCHANGE CARD HAS BEEN RECEIVED, *
* THEN CALL EXCPIO TO ALLOCATE THE DATASET. ON RETURN, SAVE THE FORMAT*
* 1 DSCB AND ITS CCHHR ADDRESS.                                       *
***********************************************************************
DSN2CARD ST    R9,DS9                  SAVE RETURN ADDRESS
         TM    CTLSW,X'40'             AWAITING A DSN2 CARD?
         BO    SET1OFF                 YES
         MVC   PRT,MSGZ4               PRINT MISSING EXCHANGE
         BAS   R9,PRINT                    CARD MESSAGE
         ABEND 177,DUMP                TERMINATE IN ERROR
SET1OFF  NI    CTLSW,X'BF'             SHOW DSN2 CARD FOUND
         MVI   COMMAND,C'D'            ALLOCATE DSNAME REQUEST CODE
         MVC   DSNAME,CONTROL+13       SET DSNAME
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    SAVDSCB2                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    SAVDSCB2                YES, NOT AN ERROR
         MVC   PRT,MSGZ8               PRINT UNABLE TO ALLOCATE
         BAS   R9,PRINT                    DSNAME MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 188,DUMP                TERMINATE IN ERROR
SAVDSCB2 L     R1,FMT1ADDR             SAVE THE
         MVC   DSCB2CKD,0(R1)              FORMAT 1 DSCB
         MVC   PRT,MSGY2               LOG MESSAGE TELLING
         BAS   R9,WRITELOG                 FILE 2 FORMAT1 DSCB READ
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         MVC   PRT+12(8),DSCB2CNT      MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 LOG COUNT RECORD
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         MVC   PRT+12(44),DSCB2KEY     MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 LOG KEY RECORD
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         MVC   PRT+12(96),DSCB2DTA     MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 LOG DATA RECORD
DSXIT    L     R9,DS9                  RESTORE RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
* SWITCH THE DATASETS IN THE VTOC. EACH FORMAT 1 RECORD IN THE VTOC IS*
* A 44-BYTE KEY (WHICH IS DATASET NAME) FOLLOWED BY A 96-BYTE DATA    *
* RECORD THAT CONTAINS FILE CHARACTERISTICS AND LOCATION ON THE PACK. *
* THE SWITCH IS EFFECTED AS FOLLOWS:                                  *
* 1. CALL EXCPIO TO READ THE FORMAT 1 DSCB FOR THE FIRST FILE.        *
* 2. IF THE DSCB READ IS IDENTICAL TO THAT SAVED, MOVE THE DATA       *
*    RECORD FROM THE SAVED SECOND FILE TO THE DATA PORTION OF THE     *
*    AREA RETURNED BY EXCPIO, THEN CALL EXCPIO TO REWRITE THE DSCB.   *
* 3. CALL EXCPIO TO READ THE FORMAT 1 DSCB FOR THE SECOND FILE.       *
* 4. IF THE DSCB READ IS IDENTICAL TO THAT SAVED, MOVE THE DATA       *
*    RECORD FROM THE SAVED FIRST FILE TO THE DATA PORTION OF THE      *
*    AREA RETURNED BY EXCPIO, THEN CALL EXCPIO TO REWRITE THE DSCB.   *
***********************************************************************
SWITCH   ST    R9,SW9                  SAVE RETURN ADDRESS
         MVI   XTNTS,1                 RESET EXTENTS
         MVC   CURRXTNT,VTOCXTNT           FOR THE VTOC
         MVI   COMMAND,C'N'            READ NEXT BLOCK REQUEST CODE
         MVC   THISCHR,DSCB1CNT        CCHHR FOR DSN1'S FORMAT1 DSCB
         IC    R1,THISCHR+4            BACK
         BCTR  R1,0                       UP TO
         STC   R1,THISCHR+4                    PREVIOUS DSCB
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    LOGRR1                  NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    LOGRR1                  YES, NOT AN ERROR
         MVC   PRT,MSGZ9               PRINT UNABLE TO REREAD
         BAS   R9,PRINT                    DSCB FOR FILE 1 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 199,DUMP                TERMINATE IN ERROR
LOGRR1   MVC   PRT,MSGY3               LOG MESSAGE TELLING
         BAS   R9,WRITELOG                 FILE 1 FORMAT1 DSCB RE-READ
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(8),0(R1)         MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 LOG COUNT RECORD
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(44),8(R1)        MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 LOG KEY RECORD
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(96),52(R1)       MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 LOG DATA RECORD
CHKDTA1  L     R1,CKDADDR              POINT TO C-K-D AREA READ
         CLC   DSCB1CKD,0(R1)          READ THE SAME FORMAT 1 DSCB?
         BE    MOVDTA2                 YES
         MVC   PRT,MSGZ5               PRINT CHANGED FORMAT 1
         BAS   R9,PRINT                    DSCB MESSAGE
         ABEND 211,DUMP                TERMINATE IN ERROR
MOVDTA2  MVC   DSCB1DTA-DSCB1CKD(96,R1),DSCB2DTA DATA2 TO DSCB1
         MVI   COMMAND,C'R'            REWRITE LAST RECORD READ CODE
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    REREAD12                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    REREAD12                YES, NOT AN ERROR
         MVC   PRT,MSGZB               PRINT UNABLE TO REWRITE
         BAS   R9,PRINT                    DSCB FOR FILE 1 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 222,DUMP                TERMINATE IN ERROR
REREAD12 MVI   COMMAND,C'N'            READ NEXT BLOCK REQUEST CODE
         MVC   THISCHR,DSCB1CNT        CCHHR FOR DSN1'S FORMAT1 DSCB
         IC    R1,THISCHR+4            BACK
         BCTR  R1,0                       UP TO
         STC   R1,THISCHR+4                    PREVIOUS DSCB
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    PRT1SWCH                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    PRT1SWCH                YES, NOT AN ERROR
         MVC   PRT,MSGZ9               PRINT UNABLE TO REREAD
         BAS   R9,PRINT                    DSCB FOR FILE 1 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 233,DUMP                TERMINATE IN ERROR
PRT1SWCH MVC   PRT,SWCH1OK             PRINT DSCB1 SWITCHED
         BAS   R9,WRITELOG                 O.K. MESSAGE
         MVC   PRT,MSGY4               LOG MESSAGE TELLING FILE 1
         BAS   R9,WRITELOG                 FORMAT1 DSCB REWRITTEN
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(8),0(R1)         MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 FILE 1 FORMAT1 DSCB READ
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(44),8(R1)        MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 FILE 1 FORMAT1 DSCB READ
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(96),52(R1)       MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 FILE 1 FORMAT1 DSCB READ
         MVI   COMMAND,C'N'            READ NEXT BLOCK REQUEST CODE
         MVC   THISCHR,DSCB2CNT        CCHHR FOR DSN2'S FORMAT1 DSCB
         IC    R1,THISCHR+4            BACK
         BCTR  R1,0                       UP TO
         STC   R1,THISCHR+4                    PREVIOUS DSCB
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    LOGRR2                  NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    LOGRR2                  YES, NOT AN ERROR
         MVC   PRT,MSGZA               PRINT UNABLE TO REREAD
         BAS   R9,PRINT                    DSCB FOR FILE 2 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 244,DUMP                TERMINATE IN ERROR
LOGRR2   MVC   PRT,MSGY5               LOG MESSAGE TELLING
         BAS   R9,WRITELOG                 FILE 2 FORMAT1 DSCB RE-READ
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(8),0(R1)         MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 LOG COUNT RECORD
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(44),8(R1)        MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 LOG KEY RECORD
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(96),52(R1)       MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 LOG DATA RECORD
CHKDTA2  L     R1,CKDADDR              POINT TO C-K-D AREA READ
         CLC   DSCB2CKD,0(R1)          READ THE SAME FORMAT 1 DSCB?
         BE    MOVDTA1                 YES
         MVC   PRT,MSGZ6               PRINT CHANGED FORMAT 1
         BAS   R9,PRINT                    DSCB MESSAGE
         ABEND 255,DUMP                TERMINATE IN ERROR
MOVDTA1  MVC   DSCB2DTA-DSCB2CKD(96,R1),DSCB1DTA DATA1 TO DSCB2
         MVI   COMMAND,C'R'            REWRITE LAST RECORD READ CODE
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    REREAD22                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    REREAD22                YES, NOT AN ERROR
         MVC   PRT,MSGZC               PRINT UNABLE TO REWRITE
         BAS   R9,PRINT                    DSCB FOR FILE 1 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 266,DUMP                TERMINATE IN ERROR
REREAD22 MVI   COMMAND,C'N'            READ NEXT BLOCK REQUEST CODE
         MVC   THISCHR,DSCB2CNT        CCHHR FOR DSN2'S FORMAT1 DSCB
         IC    R1,THISCHR+4            BACK
         BCTR  R1,0                       UP TO
         STC   R1,THISCHR+4                    PREVIOUS DSCB
         CALL  EXCPIO,(EXCPARMS,ERRMSGS),VL CALL EXCPIO SUBROUTINE
         CLI   RETCODE,C' '            ANY ERROR?
         BE    PRT2SWCH                NO
         CLC   RETCODE,=C'V4'          ZERO EXTENT FILE?
         BE    PRT2SWCH                YES, NOT AN ERROR
         MVC   PRT,MSGZ9               PRINT UNABLE TO REREAD
         BAS   R9,PRINT                    DSCB FOR FILE 1 MESSAGE
         BAS   R9,PRNTERRS             PRINT ERRORS FROM EXCPIO
         ABEND 277,DUMP                TERMINATE IN ERROR
PRT2SWCH MVC   PRT,SWCH2OK             PRINT DSCB2 SWITCHED
         BAS   R9,WRITELOG                 O.K. MESSAGE
         MVC   PRT,MSGY6               LOG MESSAGE TELLING FILE 2
         BAS   R9,WRITELOG                 FORMAT1 DSCB REWRITTEN
         MVC   PRT+5(7),=C'COUNT: '    IDENTIFY COUNT RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(8),0(R1)         MOVE COUNT TO LOG RECORD
         BAS   R9,WRITELOG                 LOG COUNT RECORD
         MVC   PRT+5(7),=C'KEY:   '    IDENTIFY KEY RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(44),8(R1)        MOVE KEY TO LOG RECORD
         BAS   R9,WRITELOG                 LOG KEY RECORD
         MVC   PRT+5(7),=C'DATA:  '    IDENTIFY DATA RECORD
         L     R1,CKDADDR              POINT TO C-K-D AREA
         MVC   PRT+12(96),52(R1)       MOVE DATA TO LOG RECORD
         BAS   R9,WRITELOG                 LOG DATA RECORD
         MVC   PRT,SWITCHOK            PRINT SWITCH
         BAS   R9,PRINT                    O.K. MESSAGE
SWXIT    L     R9,SW9                  RESTORE RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        PRINT ERROR MESSAGES FROM EXCPIO                             *
***********************************************************************
PRNTERRS ST    R9,PE9                  SAVE RETURN ADDRESS
         LA    R12,ERRMSGS             POINT TO ERROR MESSAGE AREA
         LH    R11,ERRNLINE            GET NBR LINES TO PRINT
PRTERRS  MVC   PRT,0(R12)              MOVE ERROR MESSAGE.
         BAS   R9,PRINT                PRINT IT
         LA    R12,80(R12)             STEP TO NEXT MESSAGE LINE
         BCT   R11,PRTERRS             LOOP TO PRINT ALL LINES
         BAS   R9,PRINT                PRINT BLANK LINE
         L     R9,PE9                  RESTORE RETURN ADDRESS
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        PRINT ROUTINE                                                *
***********************************************************************
PRINT    PUT   SYSPRINT,PRT            PRINT THE LINE
         PUT   SYSLOG,PRT              ALSO WRITE TO LOG FILE
         MVC   PRT(133),BLANKS         CLEAR PRINT
         BR    R9                      RETURN TO CALLER
***********************************************************************
*        WRITE TO LOG FILE                                            *
***********************************************************************
WRITELOG PUT   SYSLOG,PRT              WRITE TO LOG FILE
         MVC   PRT(133),BLANKS         CLEAR RECORD AREA
         BR    R9                      RETURN TO CALLER
***********************************************************************
*           END OF JOB ROUTINE                                        *
***********************************************************************
EOJ      TM    CTLSW,X'40'             PARTIAL CONTROL CARD SET READ?
         BZ    EOJ1                    NO
         MVC   PRT,MSGZ2               PRINT INCOMPLETE DSN=
         BAS   R9,PRINT                    PAIR MESSAGE
         ABEND 288,DUMP                TERMINATE IN ERROR
EOJ1     CLOSE (SYSIN,,SYSPRINT,,SYSLOG) CLOSE FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGS
         SR    R15,R15                 CLEAR RETURN CODE
         BR    R14                     RETURN TO CALLER
***********************************************************************
*        EXCPIO PARAMETER AREA (DOUBLEWORD BOUNDARY ALIGNED)          *
***********************************************************************
EXCPARMS DS    0D                      EXCPIO PARAMETER AREA
COMMAND  DS    CL1                     COMMAND (REQUEST) CODE
READNEXT EQU   C'N'                      READ NEXT BLOCK REQUEST
VOLALLOC EQU   C'V'                      ALLOCATE VOLUME REQUEST
DSNALLOC EQU   C'D'                      ALLOCATE DSNAME ON VOLUME
CATALLOC EQU   C'C'                      ALLOCATE DSNAME VIA CATALOG
REWRTLST EQU   C'R'                      REWRITE LAST RECORD READ
SEARCH   EQU   C'S'                      SEARCH USING SRCHKEY
KEYLENG  DS    XL1                     KEY LENGTH IF COMMAND=S
SRCHTYPE DS    CL2                     KEY SEARCH TYPE: EQ OR GE
RETCODE  DS    CL2                     RETURN CODE: BLANK=O.K.,
*                                        BLANK = SUCCESSFUL,
*                                        C1=DATASET NOT IN CATALOG
*                                        D1=DYNALLOC FAIL FOR WORK
*                                        D2=DEB<->DCB PTR ERROR
*                                        E1=END OF ALLOCATED EXTENT
*                                        F1=EOF RECORD READ
*                                        I1=HARD I/O ERROR
*                                        I2=I/O ERROR ON REWRITE
*                                        O1=OPEN FAILED FOR WORK FILE
*                                        O2=EXCP FILE NOT OPEN
*                                        R1=INVALID REQUEST CODE
*                                        R2=VOLUME REQUESTED NOT AVAIL
*                                        R3=UNSUPPORTED DEVICE
*                                        U1=INVALID UCB ADDRESS
*                                        V1=INVALID FORMAT4 DSCB
*                                        V2=DSNAME NOT IN VTOC
*                                        V3=FORMAT3 DSCB I/O ERROR
*                                        V4=DATASET HAS NO EXTENTS
FMT4ADDR DS    A                       ADDRESS OF FORMAT 4 DSCB
FMT1ADDR DS    A                       ADDRESS OF FORMAT 1 DSCB
FMT3ADDR DS    A                       ADDRESS OF FORMAT 3 DSCB
CKDADDR  DS    A                       ADDRESS OF RECORD READ
ERRNLINE DS    H                       NBR LINES OF ERROR MESSAGE
THISCHR  DS    XL5                     CCHHR ADDRESS FOR READ, WRITE
DSNAME   DS    CL44                    DATASET NAME
UCBADDR  DS    AL3                     UCB ADDRESS
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
XTNTS    DS    XL1                     NUMBER OF EXTENTS REMAINING
CURRXTNT DS    0XL10                   CURRENT EXTENT
CURRBIN  DS    XL2                       BIN NUMBER (ZEROS)
CURRBGNC DS    XL2                       BEGINNING CYLINDER
CURRBGNH DS    XL2                       BEGINNING HEAD (TRACK)
CURRENDC DS    XL2                       ENDING CYLINDER
CURRENDH DS    XL2                       ENDING HEAD (TRACK)
EXTADNL  DS    15XL10                  15 ADDITIONAL EXTENTS BBCCHHCCHH
SRCHKEY  DS    CL256                   KEY FOR SEARCH
EXCPEND  EQU   *                       END OF PARAMETER AREA
*
***********************************************************************
*        DATA AREAS                                                   *
***********************************************************************
DBLWD    DS    D                       DOUBLEWORD WORK AREA
*
DS9      DS    A                       RETURN ADDRESS FOR DSN2CARD RTN
PE9      DS    A                       RETURN ADDRESS FOR PRNTERRS RTN
SW9      DS    A                       RETURN ADDRESS FOR SWITCH RTN
VL9      DS    A                       RETURN ADDRESS FOR VOLUME RTN
EX9      DS    A                       RETURN ADDRESS FOR EXCHCARD RTN
CTLSW    DC    X'00'                   CONTROL CARD SWITCH
*
CONTROL  DS    CL80                    FIRST CONTROL CARD
PRT      DC    CL80' '                 PRINT AREA
PRT2     DC    CL53' '                 EXCESS PRINT FOR SYSLOG
BLANKS   DC    CL133' '                BLANKS
VTOCXTNT DC    XL10'00'                VTOC EXTENTS
DSCB1CKD DS    0XL148                  DSN #1 COUNT-KEY-DATA
DSCB1CNT DS    XL8                        COUNT RECORD
DSCB1KEY DS    CL44                       KEY RECORD
DSCB1DTA DS    XL96                       DATA RECORD
DSCB2CKD DS    0XL148                  DSN #2 COUNT-KEY-DATA
DSCB2CNT DS    XL8                        COUNT RECORD
DSCB2KEY DS    CL44                       KEY RECORD
DSCB2DTA DS    XL96                       DATA RECORD
SWITCHOK DC    CL40'EXCHANGE: DATASETS SUCCESSFULLY INTERCHA'
         DC    CL40'NGED ON THE VOLUME                      '
SWCH1OK  DC    CL40'EXCHANGE: FIRST DATASET NOW POINTS TO EX'
         DC    CL40'TENTS AND CHARACTERISTICS OF SECOND FILE'
SWCH2OK  DC    CL40'EXCHANGE: SECOND DATASET NOW POINTS TO E'
         DC    CL40'XTENTS AND CHARACTERISTICS OF FIRST FILE'
MSGY0    DC    CL40'EXCHANGE: VOLUME ALLOCATED SUCCESSFULLY '
         DC    CL40'                                        '
MSGY1    DC    CL40'FORMAT 1 DSCB FOR FIRST FILE READ O.K.: '
         DC    CL40'                                        '
MSGY2    DC    CL40'FORMAT 1 DSCB FOR SECOND FILE READ O.K.:'
         DC    CL40'                                        '
MSGY3    DC    CL40'FORMAT 1 DSCB FOR FIRST FILE RE-READ O.K'
         DC    CL40'.:                                      '
MSGY4    DC    CL40'FORMAT 1 DSCB FOR FIRST FILE REWRITTEN O'
         DC    CL40'K.:                                     '
MSGY5    DC    CL40'FORMAT 1 DSCB FOR SECOND FILE RE-READ O.'
         DC    CL40'K.:                                     '
MSGY6    DC    CL40'FORMAT 1 DSCB FOR SECOND FILE REWRITTEN '
         DC    CL40'O.K.:                                   '
MSGZ1    DC    CL40'EXCHANGE: CONTROL CARD ABOVE IS INVALID '
         DC    CL40'                                        '
MSGZ2    DC    CL40'EXCHANGE: INCOMPLETE DSN= PAIR: EXCHANGE'
         DC    CL40' CARD NOT FOLLOWED BY SECOND DSN= CARD  '
MSGZ3    DC    CL40'EXCHANGE: AN EXCHANGE CARD MUST BE PRECE'
         DC    CL40'DED BY A VOLUME CARD                    '
MSGZ4    DC    CL40'EXCHANGE: A SECOND DSN= CARD MUST BE PRE'
         DC    CL40'CEDED BY AN EXCHANGE CARD               '
MSGZ5    DC    CL40'EXCHANGE: FORMAT 1 DSCB FOR FIRST FILE C'
         DC    CL40'HANGED BETWEEN FIRST AND SECOND READS   '
MSGZ6    DC    CL40'EXCHANGE: FORMAT 1 DSCB FOR SECOND FILE '
         DC    CL40'CHANGED BETWEEN FIRST AND SECOND READS  '
MSGZ7    DC    CL40'EXCHANGE: UNABLE TO ALLOCATE THE VOLUME '
         DC    CL40'                                        '
MSGZ8    DC    CL40'EXCHANGE: UNABLE TO ALLOCATE THE DATASET'
         DC    CL40' ON THIS VOLUME                         '
MSGZ9    DC    CL40'EXCHANGE: UNABLE TO RE-READ FORMAT 1 DSC'
         DC    CL40'B FOR THE FIRST FILE                    '
MSGZA    DC    CL40'EXCHANGE: UNABLE TO RE-READ FORMAT 1 DSC'
         DC    CL40'B FOR THE SECOND FILE                   '
MSGZB    DC    CL40'EXCHANGE: ERROR REWRITING FORMAT 1 DSCB '
         DC    CL40'FOR FILE 1                              '
MSGZC    DC    CL40'EXCHANGE: ERROR REWRITING FORMAT 1 DSCB '
         DC    CL40'FOR FILE 2                              '
***********************************************************************
*        PRINT AND CONTROL FILES                                      *
***********************************************************************
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EOJ,               X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=80
SYSLOG   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSLOG,                        X
               RECFM=FB,LRECL=133
         LTORG
***********************************************************************
*        ERROR MESSAGES FROM EXCPIO                                   *
***********************************************************************
ERRMSGS  DS    8CL80                   ERROR MESSAGE LINES FROM EXCPIO
         END

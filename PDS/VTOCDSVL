         TITLE '    ***** STRIP DISK PACK VTOCS *****'
VTOCDSVL SVLNK R3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* THIS PROGRAM SEARCHES IT'S VTOC FOR DD CARDS WHOSE NAMES BEGIN WITH *
* VTOC. WHEN FOUND, THE DDNAME IS MOVED TO THE VTOC DCB DDNAME FIELD, *
* AND THE JFCB IS READ. THE DSNAME FIELD OF THE JFCB IS SET TO ALL    *
* HEX 04'S TO PERMIT OPENING THE VTOC. THE VTOC IS THEN READ          *
* SEQUENTIALLY. FOR EACH FORMAT 1 DSCB AN OUTPUT RECORD IS WRITTEN    *
* CONTAINING THE DSNAME AND THE VOLSER.                               *
*                                                                     *
* SEP 1999 R THORNTON (BASED ON VTOCSTRP)                             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
OPENS    OPEN  (OUTPUT1,OUTPUT,PRINT1,OUTPUT) OPEN OUTPUT FILES
         L     R1,16                   GET CVT ADDRESS
         L     R1,0(R1)                GET CVTTCBP
         L     R1,0(R1)                GET TCB ADDRESS
         L     R1,12(R1)               GET TIOT ADDRESS
         LA    R1,24(R1)               GET ADDRESS OF 1ST DD ENTRY
         ST    R1,TIOTDD               SAVE TIOT DD ENTRY ADDRESS
NEXVOL   L     R1,TIOTDD               GET TIOT DD ENTRY ADDRESS
         SR    R2,R2                   CLEAR WORK REG
CKLSTDD  CLI   0(R1),0                 LAST TIOT DD ENTRY
         BE    EOJ                     YES
         CLC   4(4,R1),=C'VTOC'        DDNAME BEGINS WITH VTOC
         BE    USEDD                   YES, USE IT
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         B     CKLSTDD                 GO CK NEXT DD
USEDD    MVC   VTOC+40(8),4(R1)        MOVE DNAME IN TIOT TO DCB
         IC    R2,0(R1)                GET DD ENTRY LENGTH
         AR    R1,R2                   STEP TO NEXT DD ENTRY
         ST    R1,TIOTDD               SAVE IT'S ADDRESS
         XC    JFCBDSNM,JFCBDSNM       CLEAR JFCB DSNAME FOR CHECK
         RDJFCB VTOC                   READ THE JFCB
         CLC   JFCBDSNM(8),ZEROS       WAS JFCB READ
         BNE   CKSERNO                 YES, DSNAME NOT ZEROS
         MVC   PRT(16),=C'NO JFCB READ FOR'
MOVDD    MVC   PRT+21(8),VTOC+40       DDNAME TO MESSAGE
         BAL   R9,PRINT                GO PRINT THE MESSAGE
         B     NEXVOL                  GO START NEXT VOLUME
CKSERNO  CLC   JFCBVOLS(8),=CL8' '     SERIAL NBR IN DD CARD OMITTED
         BNE   DS4NAME                 NO, O.K.
         MVC   PRT(20),=C'NO VOL SER IN DD FOR'
         B     MOVDD                   GO MOVE DDNAME
DS4NAME  MVI   JFCBDSNM,4              DATA SET NAME FOR FMT 4 DSCB
         MVC   JFCBDSNM+1(43),JFCBDSNM DATA SET NAME FOR FORMAT 4 DSC
         MVI   JFCBTSDM,X'08'          SUPPRESS JFCB REWRITE
         OPEN  VTOC,TYPE=J             OPEN USING MODIFIED JFCB
         L     R1,VTOC+44              GET DEB ADDRESS
         MVC   NEXCCHH,38(R1)          MOVE BEGIN CCHH TO NEXT RECADR
         MVI   NEXREC,1                BEGIN RECORD IS 1
         MVC   VOLSER,JFCBVOLS         MOVE VOLSER TO OUTPUT
SEQREAD  OI    READCMD+4,X'40'         SET CHAIN FLAG TO READ COUNT
         MVC   VTSRCH,NEXRECA          MOVE IN NEXT REC ADDR
         BAL   R9,EXCPRTN              GO READ THE RECORD
         LA    R4,DSCB                 GET ADDRESS OF RECORD
         USING DSCBFMT1,R4
         CLI   DS1FMTID,C'1'           IS IT FORMAT 1
         BNE   SEQREAD                 NO, GO READ NEXT
         MVC   DSNAME,=44C' '          CLEAR FOR MOVE
         MVC   DSNAME,DS1DSNAM         MOVE TO OUTPUT RECORD
DATAWRIT PUT   OUTPUT1,OUTREC          WRITE THE RECORD
         B     SEQREAD                 GO READ NEXT DSCB
EXCPRTN  EXCP  VTOCIOB                 CALL O/S FOR READ
         WAIT  ECB=VTECB               WAIT FOR READ
         CLI   VTECB,X'7F'             ANY ERROR
         BCR   8,R9                    NO, RETURN
         CLI   VTECB,X'42'             EXTENT VIOLATION
         BNE   IOERR                   NO, MUXT BE AN ERROR
         CLOSE VTOC                    END OF VTOC, CLOSE
         B     NEXVOL                  GO OPEN NEXT VOLUME
IOERR    ABEND 104,DUMP
PRINT    PUT   PRINT1,PRTLINE
         MVC   PRT,PRTCC               CLEAR PRINT LINE
         L     R1,ERRCOD               GET ERROR INDICATOR
         LA    R1,4(R1)                INCREMENT ERROR
         ST    R1,ERRCOD               STORE UPDATED ERROR INDIC
EOJ      CLOSE (OUTPUT1,,PRINT1)       CLOSE OUTPUT FILES
         L     R13,4(R13)              GET CALLER'S SAVE AREA ADDRESS
         MVC   16(4,R13),ERRCOD        ERROR INDICATOR TO R15 IN SAVE
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         BR    R14                     RETURN TO CALLER
**********************************************************************
*            *** DATA, WORK, FILE, AND STORAGE AREAS ***             *
**********************************************************************
         LTORG
CMDCHN   DS    0D                      COMMAND CHAIN TO READ VTOC
         CCW   X'31',VTSRCH,X'40',5    SEARCH ID EQUAL
         CCW   8,*-8,0,0               NOT FOUND, REPEAT SEARCH
READCMD  CCW   X'0E',DSCB,X'40',140    READ KEY AND DATA INTO DSCB
         CCW   X'92',NEXCNT,0,8        READ COUNT OF NEXT RECORD, M-T
NEXCNT   DS    0D                      COUNT RECORD OF NEXT RECORD
NEXRECA  DS    0XL5                    CCHHR ADDRESS OF NEXT RECORD
NEXCCHH  DS    XL4                     CCHH ADDRESS OF NEXT RECORD
NEXREC   DS    XL1                     RECORD NBR ON TRACK
         DS    XL3                     KEY LENG AND DATA LENG
ERRCOD   DC    F'0'                    ERROR INDICATOR
WK1      DS    D                      CVD WORKAREA
ZEROS    DC    2F'0'
*
         DS    0F
OUTREC   DS    0CL80                   OUTPUT RECORD
DSNAME   DC    CL44' '                 DSNAME OF ASSOCIATED
         DC    CL1' '
VOLSER   DC    CL6' '                  VOLUME SERIAL NUMBER
         DC    CL29' '
*
DSCB     DS    CL140                   DSCB AREA
*
JFCBADDR DS    0F                      DCB EXIT LIST
         DC    X'87'                   RDJFCB ENTRY, LAST ENTRY
         DC    AL3(JFCB)               ADDRESS OF JFCB AREA
TIOTDD   DS    F                       ADDRESS OF NEXT DD ENTRY IN TIOT
*
PRTLINE  DS    0CL133                  PRINT LINE OUTPUT
PRTCC    DC    C' '                    CARRIAGE CONTROL CHARACTER
PRT      DC    CL132' '                PRINT LINE
*
         JFCBDEF DSECT=NO
*
VTOC     DCB   DDNAME=VTOC,MACRF=E,EXLST=JFCBADDR
*
VTOCIOB  EXCPIOB CCW=CMDCHN,DCB=VTOC
*
OUTPUT1  DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT1,RECFM=FB
*
PRINT1   DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=133,BLKSIZE=133,       X
               DDNAME=PRINT1
*
         DSCBDEF FMT2=NO,FMT5=NO,FMT6=NO
*
         END
